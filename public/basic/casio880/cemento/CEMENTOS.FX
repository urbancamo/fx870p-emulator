1 CLEAR:DIM C(3),S(3),A(3),F(3),PF(3),K(3):SETN
2 CLS:PRINT "DOSIFICACION CEMENTOS (MODULOS)";
3 LOCATE0,1:PRINT "Consideramos 3 materias primas"
4 CLS
5 FOR A=1 TO 3:PRINT "Porcentaje C ";A;:LOCATE2,1:INPUT ">",C(A):CLS:NEXT
6 FOR A=1 TO 3:PRINT "Porcentaje S ";A;:LOCATE2,1:INPUT ">",S(A):CLS:NEXT
7 FOR A=1 TO 3:PRINT "Porcentaje A ";A;:LOCATE2,1:INPUT ">",A(A):CLS:NEXT
8 FOR A=1 TO 3:PRINT "Porcentaje F ";A;:LOCATE2,1:INPUT ">",F(A):CLS:NEXT
9 FOR A=1 TO 3:PRINT "Porcentaje PF ";A;:LOCATE2,1:INPUT ">",PF(A):CLS:NEXT
10 INPUT "Fijamos G.S.P. (%) ",GSPP:CLS:GSP=GSPP/100
11 INPUT "Fijamos M.F. ",MF:CLS
12 FOR A=1 TO 3:K(A)=2.8*S(A)+1.18*A(A)+0.65*F(A):NEXT A
13 V=MF*F(1)*100-100*A(1)
14 VV=MF*F(3)-MF*F(1)-A(3)+A(1)
15 VVV=A(2)-A(1)-MF*F(2)+MF*F(1)
16 W=GSP*K(1)*100-100*C(1)
17 WW=GSP*K(3)-GSP*K(1)-C(3)+C(1)
18 WWW=C(2)-C(1)-GSP*K(2)+GSP*K(1)
19 P3=(((VVV*W)/WWW)-V)/((-(VVV*WW)/WWW)+VV)
20 P2=(V+VV*P3)/VVV
21 P1=100-P2-P3
22 SETF1:PRINT "P1=";P1;"% P2=";P2;"% P3=";P3;"%"
23 CAL=P1*C(1)+P2*C(2)+P3*C(3)
24 SILICE=P1*S(1)+P2*S(2)+P3*S(3)
25 ALUMINIO=P1*A(1)+P2*A(2)+P3*A(3)
26 OXIFER=P1*F(1)+P2*F(2)+P3*F(3)
27 PF=P1*PF(1)+P2*PF(2)+P3*PF(3)
28 T=CAL+SILICE+ALUMINIO+OXIFER+PF
29 PRINT "Composicion del crudo dl cemento";:CC=CAL*100/T:PRINT "C=";CC;"%"
30 SC=SILICE*100/T:PRINT "S=";SC;"%"
31 AC=ALUMINIO*100/T:PRINT "A=";AC;"%"
32 FC=OXIFER*100/T:PRINT "F=";FC;"%"
33 PFC=PF*100/T:PRINT "PF=";PFC;"%"
34 MH=CAL/(SILICE+OXIFER+ALUMINIO)
35 MS=SILICE/(OXIFER+ALUMINIO)
36 CLS:PRINT "Grado d satura. poten. en cal   G.S.P.=";GSP*100;"%"
37 CLS:PRINT "Modulo de fundentes (MF)=";MF
38 CLS:PRINT "Modulo hidraulico (MH)=";MH
39 CLS:PRINT "Modulo de silicatos (MS)=";MS
40 CLS:PRINT "Composicion del Clinker"
41 CF=CC*(100/(100-PFC)):PRINT "C=";CF;"%"
42 SF=SC*(100/(100-PFC)):PRINT "S=";SF;"%"
43 AF=AC*(100/(100-PFC)):PRINT "A=";AF;"%"
44 FF=FC*(100/(100-PFC)):PRINT "F=";FF;"%"
45 CLS:PRINT "Composicion potencial:":PRINT "FAC4=";3.04*FF;"%"
46 PRINT "AC3=";2.65*AF-1.69*FF;"%"
47 SC3=4.07*CF-7.6*SF-1.43*FF-6.72*AF:PRINT "SC3=";SC3;"%"
48 PRINT "SC2=";2.86*SF-0.75*SC3;"%"
