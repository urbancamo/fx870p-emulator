10 CLS :CLEAR
20 PRINT"TABLAS INPUT-OUTPUT:"
30 PRINT "No.de sectores=";:INPUT NS
40 DIM TB(NS,NS),IN(NS),A(NS,NS),P(NS),A1(NS,NS),UN(NS,NS),LT(NS,NS)
50 DIM LTS(NS,NS),LT1(NS,NS),D(NS),IN1(NS),TB1(NS,NS)
60 PRINT "VALORES DE LA TABLA I-0"
70 FOR I=1 TO NS:FOR J=1 TO NS
80 PRINT "TABLA(";I;",";J;")=";:INPUT TB(I,J)
90 NEXT J,I
100 PRINT
110 PRINT "VALORES DE LOS INPUT:"
120 FOR K=1 TO NS
130 PRINT "INPUT(";K;")=";:INPUT IN(K)
140 NEXT K
150 CLS:PRINT"COEFICIENTES TECNICOS:"
160 FOR I=1 TO NS:FOR J=1 TO NS
170 A(I,J)=TB(I,J)/IN(J)
180 PRINT "A(";I;",";J;")=";A(I,J):GOSUB 980
190 NEXT J,I
200 PRINT
210 PRINT "VARIACION DE PRECIOS (S/N)";:INPUT D$
220 IF D$="S" THEN 230 ELSE IF D$<>"N" THEN 210
230 FOR I=1 TO NS
240 PRINT "VAR.PRECIO SECTOR ";I;"=";:INPUT P(I):P(I)=1+P(I)
250 NEXT I
260 CLS:PRINT"INCREMENTOS PRECIOS:"
270 FOR I=1 TO NS
280 PRINT "P(";I;")=";P(I):GOSUB 980
290 NEXT I 
300 PRINT "NUEVOS COEF-TEC:"
310 FOR I=1 TO NS:FOR J=1 TO NS
320 A1(I,J)=A(I,J)*P(I)/P(J)
330 PRINT "A'(";I;",";J;")=";A1(I,J):GOSUB 980
340 NEXT J,I
350 FOR I=1 TO NS:FOR J=1 TO NS
360 IF I=J THEN UN(I,J)=1 ELSE UN(I,J)=0
370 NEXT J,I
380 PRINT "MATRIZ [I-Ar]:"
390 FOR I=1 TO NS:FOR J=1 TO NS
400 LT(I,J)=UN(I,J)-A1(I,J):LTS(I,J)=LT(I,J)
410 PRINT "LT(";I;",";J;")=";LT(I,J):GOSUB 980
420 NEXT J,I
430 FOR J=1 TO NS:FOR I=1 TO NS
440 NEXT I:LT1(J,J)=1:NEXT J
450 PRINT"Invirtiendo..."
460 FOR J=1 TO NS:FOR I=J TO NS
470 IF LTS(I,J)<>0 THEN 500
480 NEXT I
490 PRINT"MATRIZ SINGULAR":BEEP:END
500 FOR K=1 TO NS
510 S=LTS(J,K):LTS(J,K)=LTS(I,K):LTS(I,K)=S
520 S=LT1(J,K):LT1(J,K)=LT1(I,K):LT1(I,K)=S
530 NEXT K
540 T=1/LTS(J,J)
550 FOR K=1 TO NS
560 LTS(J,K)=T*LTS(J,K)
570 LT1(J,K)=T*LT1(J,K)
580 NEXT K
590 FOR L=1 TO NS
600 IF L=J THEN 660
610 T=-LTS(L,J)
620 FOR K=1 TO NS
630 LTS(L,K)=LTS(L,K)+T*LTS(J,K)
640 LT1(L,K)=LT1(L,K)+T*LT1(J,K)
650 NEXT K
660 NEXT L
670 NEXT J
680 BEEP
690 PRINT"MATRIZ DE LEONTIEF:"
700 FOR I=1 TO NS:FOR J=1 TO NS
710 PRINT"LT1(";I;",";J;")=";LT1(I,J)
720 GOSUB 980
730 NEXT J,I
740 PRINT:PRINT"DEMANDA:"
750 FOR I=1 TO NS
760 PRINT"D(";I;")=";:INPUT D(I)
770 NEXT I
780 PRINT"Multiplicando..."
790 FOR I=1  TO NS
800 IN1(I)=0
810 FOR K=1 TO NS
820 IN1(I)=IN1(I)+LT1(I,K)*D(K)
830 NEXT K,I
840 BEEP
850 PRINT"NUEVOS INPUTS:"
860 FOR I=1 TO NS
870 PRINT"IN1(";I;")=";IN1(I)
880 GOSUB 980
890 NEXT I
900 FOR I=1 TO NS:FOR J=1 TO NS
910 TB1(I,J)=A1(I,J)*IN1(J)
920 NEXT J,I
930 PRINT"NUEVA TABLA:"
940 FOR I=1 TO NS:FOR J=1 TO NS
950 PRINT "TB1(";I;",";J;")=";ROUND(TB1(I,J),-1):GOSUB 980
960 NEXT J,I
970 END
980 IF INKEY$="" THEN 980 ELSE RETURN
