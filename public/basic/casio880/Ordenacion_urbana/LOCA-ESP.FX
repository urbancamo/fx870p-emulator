10 CLEAR
20 CLS
30 PRINT "  LOCALIZACION-ESPECIALIZACION"
40 INPUT "N.FILAS";N
50 INPUT "N.COLUMNAS";M
60 DIM X(N,M),C(M),L(N),Z(N,M),K(N,M),U(N,M),QE(N),QL(M)
70 PRINT "TOTALES"
80 FOR J=1 TO M
90 C(J)=0
100 FOR I=1 TO N
110 PRINT "VAN(";I;",";J;")=";:INPUT X(I,J)
120 C(J)=C(J)+X(I,J)
130 NEXT I
140 PRINT "TOTAL SERV.(";J;")=";C(J)
150 NEXT J
160 T=0
170 FOR I=1 TO N
180 L(I)=0
190 FOR J=1 TO M
200 L(I)=L(I)+X(I,J)
210 NEXT J
220 PRINT "TOTAL MUNICI.(";I;")=";L(I)
230 T=T+L(I)
240 NEXT I
250 PRINT "TOTAL DE TOTALES=";T
260 PRINT "COCIENTES DE LOCALIZACION"
270 FOR J=1 TO M
280 FOR I=1 TO N
290 Z(I,J)=(X(I,J)*T)/(L(I)*C(J))
300 PRINT "Z(";I;",";J;")=";Z(I,J)
310 NEXT I
320 NEXT J
330 PRINT "COEFICIENTE DE ESPECIALIZACION"
340 FOR I=1 TO N
350 QE=0
360 FOR J=1 TO M
370 K(I,J)=ABS((X(I,J)/L(I))-(C(J)/T))
380 QE=QE+K(I,J)
390 PRINT "K(";I;",";J;")=";K(I,J)
400 NEXT J
410 PRINT "QE(";I;")=";QE/2
420 NEXT I
430 PRINT "COEFICIENTE DE LOCALIZACION"
440 FOR J=1 TO M
450 QL=0
460 FOR I=1 TO N
470 U(I,J)=ABS((X(I,J)/C(J))-(L(I)/T))
480 QL=QL+U(I,J)
490 PRINT "U(";I;",";J;")=";U(I,J)
500 NEXT I
510 PRINT "QL(";J;")=";QL/2
520 NEXT J
