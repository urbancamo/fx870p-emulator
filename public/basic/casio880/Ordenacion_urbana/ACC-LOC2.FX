1 CLEAR:' MODIFICADO POR NPZ JUNIO 1993-ESTA BIEN
2 'EN CASO DE QUE D(I,J)<>D(J,I) O T(I,J)<>T(J,I) BORRA LINEAS 220,225 Y 255 Y PON:
3 '225 FOR I=1 TO N:FOR J=1 TO N
5 CLS
15 PRINT "‡‡‡ ACCESIBILIDAD LOCACIONAL ‡‡‡"
20 INPUT "NUM.NUCLEOS O NUDOS =";N
30 DIM X(N),Y(N),T(N),B(N),P(N),D(N,N),V(N,N),K(N),G(N),F(N),O(N),W(N),H(N),L(N),U(N,N),C(N),Q(N),S(N,N)
40 FOR I=1 TO N
50 PRINT "COORD. X NUDO (";I;")";:INPUT "=";X(I)
60 PRINT "COORD. Y NUDO (";I;")";:INPUT "=";Y(I)
70 NEXT I
80 FOR I=1 TO N
85 A=0
90 FOR J=1 TO N
100 A=A+SQR((X(I)-X(J))^2+(Y(I)-Y(J))^2)
110 NEXT J
115 T(I)=A
120 CLS:PRINT "ACCES.LOCAC.ABS.NUDO";I;"=";T(I)
125 IF INKEY$="" THEN 125
128 S=S+A
130 NEXT I
140 CLS:PRINT "‡‡‡‡‡‡ ACCESIBILIDADES LOCACIONALES RELATIVAS ‡‡‡‡‡‡"
145 IF INKEY$="" THEN 145
150 FOR I=1 TO N
160 B(I)=T(I)/S
190 CLS:PRINT "ACCES.RELAT.NUDO ";I;"=";B(I)
195 IF INKEY$="" THEN 195
200 NEXT I
210 CLS:PRINT " CAMINOS,DIST.MIN. Y ACCESIBILIDAD POT. EN DIST.ABS. Y RELAT. "
215 IF INKEY$="" THEN 215
217 CLS
220 FOR I=1 TO N-1:D(I,I)=0:V(I,I)=0
225 FOR J=I+1 TO N
230 CLS:PRINT "DIST.MIN. NUDO";I;"AL NUDO "J;:INPUT D(I,J)
235 IF INKEY$="" THEN 235
236 NEXT J:NEXT I:FOR I=1 TO N:FOR J=I+1 TO N:PRINT "D MIN ("I","J")="D(I,J):NEXT J:NEXT I
237 INPUT "CORREGIR DISTANCIAS ";A$:IF A$="S" OR A$="s" THEN CLS:INPUT "NUM DE LOS NUCLEOS ";I,J:PRINT "DIST MIN "I"A"J;:INPUT D(I,J):D(J,I)=D(I,J):INPUT "CORREGIR MAS";A$:IF A$="S" OR A$="s" THEN 237
240 FOR I=1 TO N-1:FOR J=I+1 TO N:CLS:PRINT "TIEMPO MIN.NUDO";I;"AL NUDO "J;:INPUT V(I,J)
245 IF INKEY$="" THEN 245
246 NEXT J:NEXT I:FOR I=1 TO N:FOR J=I+1 TO N:PRINT "T MIN ("I","J")="V(I,J):NEXT J:NEXT I
247 INPUT "CORREGIR TIEMPOS ";A$:IF A$="S" OR A$="s" THEN CLS:INPUT "NUM DE LOS NUCLEOS ";I,J:PRINT "TIEMPO MIN "I"A"J;:INPUT V(I,J):V(J,I)=V(I,J):INPUT "CORREGIR MAS";A$:IF A$="S" OR A$="s" THEN 247
250 D(N,N)=0:V(N,N)=0
255 FOR I=1 TO N-1:FOR J=I+1 TO N:V(J,I)=V(I,J):D(J,I)=D(I,J):NEXT J:NEXT I
270 FOR I=1 TO N
271 AD=0:AT=0
275 FOR J=1 TO N
280 AD=AD+D(I,J):AT=AT+V(I,J)
290 NEXT J
295 P(I)=AD:F(I)=AT
300 CLS:PRINT "ACCES.POT.ABS. EN DIST. NUDO ";I;"=";P(I)
310 IF INKEY$="" THEN 310
320 NEXT I
325 S=0:Z=0
330 FOR I=1 TO N
340 S=S+P(I):Z=Z+F(I)
350 NEXT I
355 CLS:PRINT "ééé ACCESIBILIDADES POTENCIALES RELATIVAS ééé"
358 IF INKEY$="" THEN 358
360 FOR I=1 TO N
365 K(I)=(P(I))/S:O(I)=(F(I))/Z
370 CLS:PRINT "ACCES.POT.REL.NUDO ";I;"=";K(I)
380 IF INKEY$="" THEN 380
390 NEXT I
400 CLS:PRINT ">> INDICES DE DOTACION/TRAZADO <<"
410 IF INKEY$="" THEN 410
420 FOR I=1 TO N
430 G(I)=K(I)/B(I)
435 CLS
440 PRINT "IND.DOTAC./TRAZ. NUDO ";I;"=";G(I)
450 IF INKEY$="" THEN 450
460 NEXT I
470 CLS:PRINT "ííí ACCESIBILIDAD POTENCIAL ABSOLUTA Y RELATIVA EN TIEMPOS MINIMOS ííí"
471 IF INKEY$="" GOTO471
475 FOR I=1 TO N
480 CLS:PRINT "ACC.POT.ABS. EN TIEM. MINIMOS NUDO ";I;"=";F(I)
485 IF INKEY$="" GOTO 485
490 CLS:PRINT "ACC.POT.REL. EN TIEMP. MINIMOS DEL NUDO ";I;"=";O(I)
495 IF INKEY$="" THEN 495
500 NEXT I
520 CLS:PRINT "‡‡ INDICES DE DISENO/TRAFICO ‡‡"
525 IF INKEY$="" THEN 525
530 FORI=1 TO N
540 W(I)=O(I)/B(I)
550 PRINT "IND.DIS./TRAF. NUDO ";I;"=";W(I)
560 IF INKEY$="" THEN 560
570 NEXT I
590 CLS:PRINT "#### INDICES DE VELOCIDAD ####"
592 IF INKEY$="" THEN 592
595 FOR I=1 TO N
600 H(I)=(K(I))/(O(I))
650 CLS:PRINT "IND. DE VELOCIDAD NUDO ";I;"=";H(I)
660 IF INKEY$="" THEN 660
670 NEXT I
685 BEEP 1:BEEP 0
690 CLS:PRINT ">>> POTENCIALES DEMOGRAFICOS <<<"
700 IF INKEY$="" THEN 700
710 CLS:PRINT "INTRODUCE EL VALOR DE LA FUNCION DE FRICCION"
720 INPUT "COEF. A= ";CA
730 INPUT "COEF. B= ";CB
740 INPUT "COEF. C= ";CC
750 FOR I= 1 TO N
760 CLS:PRINT "POBLACION ";I;:INPUT"=";L(I)
765 S=0
770 NEXT I
775 M=0
780 FOR I=1 TO N
785 S=0
790 FOR J=1 TO N
794 IF I=J THEN U(I,J)=1
796 IF I=J THEN GOTO 806
800 U(I,J)=(CA/(1+CB*EXP(CC*V(I,J)))):PRINT"DESEA VER MATRIZ"
801 K$=INKEY$:IF K$="" THEN 801
802 IF K$="S" THEN GOSUB 2100:GOTO806
803 IF K$="N" THEN 806
804 GOTO801
806 S=S+L(J)*U(I,J)
810 NEXT J
812 Q(I)=S
815 CLS:PRINT "PO.DEMOGR.NUDO ";I;"=";Q(I)
816 IF INKEY$="" THEN 816
817 M=M + Q(I)
820 NEXT I
830 FOR I=1 TO N
840 C(I)=Q(I)/M
850 CLS:PRINT "POT.DEMO.RELAT. DEL NUDO ";I;"=";C(I)
860 IF INKEY$="" THEN 860
870 NEXT I
880 IF INKEY$="" THEN 880
890 CLS:PRINT"‡‡‡ RELACIONES POTENCIALES ‡‡‡"
900 IF INKEY$="" GOTO900
905 PRINT"MATRIZ DE RELACIONES POTENCIALES"
910 FOR I=1 TO N
920 FOR J=1 TO N
925 IF I=J THEN U(I,J)=1
930 S(I,J)=L(I)*L(J)*U(I,J)
950 IF INKEY$="" GOTO950
960 PRINT"ARCO(";I;J")=";S(I,J)
970 NEXTJ
980 NEXTI
2000 END
2100 FOR CAX=1 TO I:FORCAY=1 TO J:PRINT "(";CAX;",";CAY;")=";U(CAX,CAY):NEXT CAY,CAX
2102 IF INKEY$="S" THEN RETURN
2103 GOTO 2102
