10 CLS:CLEAR:ANGLE 1
20 PRINT "Calculo flectores Pretensado:"
50 INPUT "No de puntos trazado="; NPUNT
70 DIM NM$(NPUNT), X(NPUNT), D(NPUNT)
80 PRINT "Consideramos distancias a fibra superior positivas (+).": PRINT
90 INPUT "Yg ="; YG
100 FOR I=1 TO NPUNT
110 PRINT "Nombre(";I;")=";:INPUT NM$(I)
120 PRINT "X(";I;")=";:INPUT X(I)
130 PRINT "d(";I;")=";:INPUT D(I)
140 PRINT
150 NEXT I
160 '
170 NVAN=NPUNT-1
180 DIM L(NVAN),F(NVAN)
190 FOR I=1 TO NVAN
200 L(I)=X(I+1)-X(I)
210 NEXT I
220 FOR I=1 TO NVAN
230 IF I=NVAN THEN F(I)=D(I)-D(I+1):GOTO 270
240 IF D(I+1)>D(I) AND D(I+2)<D(I+1) THEN F(I)=D(I+1)-D(I):GOTO 270
250 IF D(I+1)<D(I) AND D(I+2)>D(I+1) THEN F(I)=D(I+1)-D(I):GOTO 270
260 F(I)=D(I)-D(I+1)
270 NEXT I
280 INPUT "Fza pretensado (Tn)="; P0
290 '
300 DIM FEXT(2,3),W(NVAN)
310 ALFA1=2*F(1)/L(1):FEXT(1,1)=(P0*COS(ALFA1)):FEXT(1,2)=-(P0*SIN(ALFA1)):FEXT(1,3)=(D(1)-YG)*FEXT(1,1)
320 ALFA2=2*F(NVAN)/L(NVAN):FEXT(2,1)=-(P0*COS(ALFA2)):FEXT(2,2)=-(P0*SIN(ALFA2)):FEXT(2,3)=(D(NPUNT)-YG)*FEXT(2,1)
330 FOR I=1 TO NVAN
340 W(I)=P0*2*F(I)/(L(I))^2
350 NEXT I
360 CLS
370 PRINT "Acciones armadura activa sobre  hormigon:":GOSUB 1230 
390 PRINT
410 FOR I=1 TO NVAN
415 PRINT "Tramo Longitud  Flecha      W"
420 PRINT NM$(I);"-";NM$(I + 1);
430 PRINT ROUND (L(I),-3);ROUND(F(I),-3);ROUND(W(I),-3)
440 GOSUB 1230
450 NEXT I
460 '
470 '
480 SH=FEXT(1,1)+FEXT(2,1)
490 '
500 SV=FEXT(1,2)+FEXT(2,2)
510 FOR I=1 TO NVAN
520 SV=SV+W(I)*L(I)
530 NEXT I
540 '
550 SM=FEXT(1,3)+FEXT(2,3)
560 PRINT
570 PRINT "Fuerzas:"
580 PRINT "Fx(1)=";FEXT(1,1)
590 PRINT "Fy(1)=";FEXT(1,2):GOSUB 1230
600 PRINT "Mz(1)=";FEXT(1,3)
610 PRINT "Fx(2)=";FEXT(2,1):GOSUB 1230
620 PRINT "Fy(2)=";FEXT(2,2)
630 PRINT "Mz(2)=";FEXT(2,3):GOSUB 1230
640 PRINT
650 PRINT "Equilibrio?"
660 PRINT "S Fx =";SH
670 PRINT "S Fy =";SV:GOSUB 1230
680 PRINT "S Mz =";SM
690 '
700 PRINT
710 INPUT "X reacc. hiperest =";XH
720 DIM FH(NVAN)
730 PRINT "Calculando ";
740 L=X(NPUNT):X=XH
750 FOR I=1 TO NVAN
760 P=W(I):A=X(I)+L(I)/2:C=L(I):GOSUB 1250:FH(I)=F
770 PRINT ".";
780 NEXT I
790 CLS
800 FTOT=0
810 PRINT
830 FOR I=1 TO NVAN
835 PRINT "Tramo   Flecha/EI"
840 PRINT NM$(I);"-";NM$(I+1);
850 PRINT "";FH(I)
860 FTOT=FTOT+FH(I)
870 GOSUB 1230
880 NEXT I
890 PRINT
900 PRINT "Flecha Total =";FTOT;"/EI":GOSUB 1230
910 FMTO=0
920 L=X(NPUNT):X=XH
930 M=FEXT(1,3):GOSUB 1350:FMTO=FMTO+F
940 PRINT "Flecha Mto =";FMTO;"/EI":GOSUB 1230
950 FTOT=FTOT+FMTO
960 FREAC=0
970 L=X(NPUNT):P=1:X=XH:PR=XH
980 A=PR:GOSUB 1310:FREAC=FREAC+F
990 IF XH=L/2 THEN 1010
1000 A=(L-XH):GOSUB 1310:FREAC=FREAC+F
1010 PRINT "Flecha R =";FREAC;"R/EI":GOSUB 1230
1020 PRINT
1030 RHIP=-(FTOT/FREAC):RISO=-(RHIP+(SV/2))
1040 PRINT "Reaccion R =";RHIP:GOSUB 1230
1050 PRINT "Calculando momentos"
1060 DIM MISOST(NPUNT),MHIPER(NPUNT),MTOT(NPUNT)
1070 FOR I=1 TO NPUNT
1080 MISOST(I)=-P0*(D(I)-YG)
1090 IF X(I)<=XH THEN MHIPER(I)=RISO*X(I):GOTO 1120
1100 IF X(I)>XH AND X(I)<=(X(NPUNT)-XH) THEN MHIPER(I)=RISO*XH:GOTO 1120
1110 IF X(I)>(X(NPUNT)-XH) THEN MHIPER(I)=RISO*(X(NPUNT)-X(I)):GOTO 1120
1120 MTOT(I)=MISOST(I)+MHIPER(I)
1130 NEXT I
1140 CLS
1150 PRINT
1170 FOR I=1 TO NPUNT
1175 PRINT "Pt Isostatic Hiperestatic Total"
1180 PRINT NM$(I);
1190 PRINT ""; ROUND (MISOST(I),-3); ROUND (MHIPER(I),-3); ROUND(MTOT(I),-3)
1200 GOSUB 1230
1210 NEXT I
1220 ANGLE 0:END
1230 IF INKEY$="" THEN 1230 ELSE RETURN
1240 '
1250 B=L-A
1260 IF X<=(A-C/2) THEN F=P*B*C/6/L*X*(-X^2+A*(L+B-C^2/4/A))
1270 IF X>(A-C/2) AND X<=(A+C/2) THEN F=P/24/L*(L*(X-(A-C/2))^4-4*B*C*X^3+4*A*B*C*(L+B-C^2/4/A)*X)
1280 IF X>(A+C/2) AND X<=L THEN F=P*A*C/6/L*(L-X)*(-(L-X)^2+B*(L+A-C^2/4/B))
1290 RETURN
1300 '
1310 B=L-A
1320 IF X<=A THEN F=P*L*B*X/6*(1-B^2/L^2-X^2/L^2) ELSE F=P*L*A*(L-X)/6*(1-A^2/L^2-((L-X)/L)^2)
1330 RETURN
1340 '
1350 F=M*X*(L-X)/2
1360 RETURN
