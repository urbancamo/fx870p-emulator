1 'PRETENSA
5 CLS:CLEAR :PRINT CHR$(9);
10 PRINT " **** HORMIGON  PRETENSADO ****";
15 LOCATE 0,1
20 PRINT " ******************************"
25 REM ***** ENTRADA DATOS ****
30 CLS:INPUT "Fuerza pretensado (tn)";FP
40 INPUT "Eob (m)";EOB
45 INPUT "Dos o Tres tramos (D/T)";TR$
50 IF TR$<>"D" AND TR$<>"T" THEN 45
55 INPUT "Longitud tramo 1 (m)";L:L2=L
60 IF TR$="T" THEN INPUT "Long. tramo intermedio (m)";L2
65 CLS:PRINT "Entrada coeficientes";
70 LOCATE 0,1
75 PRINT CHR$(136);:INPUT A
80 PRINT CHR$(136);"1";:INPUT A1
85 IF TR$="T" THEN PRINT CHR$(136);"2";:INPUT A2
90 PRINT CHR$(137);:INPUT B
100 PRINT CHR$(137);"1";:INPUT B1
110 IF TR$="T" THEN PRINT CHR$(137);"2";:INPUT B2
120 REM ****** CALCULOS ******
130 BI=1-A1*(1-B1)/(1-A)
140 BJ=1-2*A2*(1-B2)
150 REM * FUERZAS  DESVIACION *
160 Q1=2*EOB*(B-B1)*FP/(A*L)^2
170 Q2=2*EOB*(BI-B1)*FP/((1-A-A1)*L)^2
180 Q3=2*EOB*(1-BI)*FP/(A1*L)^2
190 IF TR$="T" THEN 200 ELSE 220
200 Q4=2*EOB*(1-BJ)*FP/(A2*L2)^2
210 Q5=2*EOB*(BJ-B2)*FP/((.5-A2)*L2)^2
220 REM * ISOSTATICOS DE PRETENSADO *
230 E1X$="EOB*(B-2*(B-B1)*X/(A*L)+(B-B1)*X^2/(A*L)^2)"
240 E2X$="EOB*(B1+(BI-B1)*(X/L-A)^2/(1-A-A1)^2)"
250 E3X$="EOB*(1-(1-BI)*(1-X/L)^2/A1^2)"
251 IF TR$="T" THEN 252 ELSE 260
252 E4X$="EOB*(B2+(BJ-B2)*(X-L-L2/2)^2/((.5-A2)*L2)^2)"
255 E5X$="EOB*(1-(1-BJ)*(L-X)^2/(A2*L2)^2)"
260 CLS:BEEP:PRINT "SOLUCIONES";
265 LOCATE 0,1
270 PRINT "Q1=";Q1;"Tn"
280 PRINT "Q2=";Q2;"Tn"
290 PRINT "Q3=";-Q3;"Tn"
300 IF TR$="T" THEN PRINT "Q4=";-Q4;"Tn","Q5=";Q5;"Tn"
500 REM ** CALCULO ESFUERZOS **
505 CLS:PRINT "CALCULO MOMENTOS  ISOSTATICOS";:LOCATE 0,1
510 INPUT "Distancia (m)";X
520 IF X>0 AND (X<=A*L) THEN MTO=-FP*VALF(E1X$):GOTO 580
530 IF X>A*L AND (X<=L*(1-A1)) THEN MTO=-FP*VALF(E2X$):GOTO 580
540 IF X>L*(1-A1) AND X<=L THEN MTO=-FP*VALF(E3X$):GOTO 580
550 IF TR$="D" AND X>L THEN PRINT "Pasaste la mitad":GOTO 600
555 IF TR$<>"T" THEN 590
560 IF X>L AND (X<=L+A2*L2) THEN MTO=-FP*VALF(E5X$):GOTO 580
570 IF (X>L+A2*L2) AND (X<=L+L2/2) THEN MTO=-FP*VALF(E4X$):GOTO 580
575 IF X>L+L2/2 THEN BEEP:PRINT "Pasaste la mitad":GOTO 600
580 PRINT "Momento=";MTO
590 CLS:GOTO 510
600 CLS:END
