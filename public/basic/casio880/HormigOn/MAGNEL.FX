10 '
20 CLS
30 PRINT "Metodo de MAGNEL"
40 '
50 PRINT "1. Caract. mecanicas seccion."
60 PRINT "2. Comprb. seccion y huso paso"
70 '
80 PRINT "3. FIN";
90 A$=INPUT$(1)
100 IF A$="1" THEN 140
110 IF A$="2" THEN 600
120 IF A$="3" THEN END
130 GOTO 90
140 CLS:CLEAR:INPUT "No Zonas forman la seccion =";N
150 FOR K=1 TO N
160 CLS:PRINT "ZONA";K
170 PRINT "1. RECTANGULO  "
190 PRINT "2. TRAPECIO";
200 A$=INKEY$
210 IF A$=CHR$(49) THEN CLS:GOTO 240
220 IF A$=CHR$(50) THEN CLS:GOTO 300
230 GOTO 200
240 INPUT "Base (cm) =";B
250 INPUT "Altura (cm) =";H
260 A=B*H
270 I=B*H^3/12
280 CG=H/2
290 GOTO 400
300 INPUT "Derecho (0) o Inverso (1)";B$
310 INPUT "Base MAYOR (cm) =";B
320 INPUT "Base Menor (cm) =";BM
330 INPUT "Altura (cm) =";H
340 L=B-BM
350 I=H^3*(6*BM^2+6*BM*L+L^2)/36/(2*BM+L)
360 CG=(B+2*BM)/(B+BM)*H/3
370 IF B$="0" THEN CG=H-CG
380 A=(B+BM)/2*H
390 GOTO 400
400 '
410 SA=SA+A
420 D=HT+CG
430 HT=HT+H
440 AD=A*D
450 AD2=A*D^2
460 SAD=SAD+AD
470 IXX=AD2+I
480 SI=SI+IXX
490 NEXT K
500 VG=SAD/SA
510 IG=SI-SA*VG^2
520 A=SA
530 V1=VG
540 I=IG
550 H=HT
560 W1=I/V1:W2=I/(H-V1)
570 CLS:PRINT "A (cm2) =";A,"V1 (cm)=";V1,"I (cm4) =";I:GOSUB 980
580 PRINT "P (t/m) =";2.5*A/10000,"W1 (cm3) =";W1,"|W2| =";W2:GOSUB 980
590 GOTO 20
600 '
610 CLS:PRINT "NUEVOS W1,W2.   1.Si   2.No"
620 K$=INKEY$:IF K$="" THEN 620
630 IF K$="1" THEN 640 ELSE 660
640 CLEAR:CLS
650 INPUT "W1 (cm3) =";W1:INPUT "|W2| =";W2:INPUT "A (cm2) =";A
660 PRINT:INPUT "M max (Tn.m) =";MX:INPUT "M min (Tn.m) =";MI:INPUT "MU estimada =";MU
670 IF ABS(MI)>ABS(MX) THEN MG=-MI ELSE MG=MX
680 IF ABS(MI)>ABS(MX) THEN MP=-MX ELSE MP=MI
690 PRINT:INPUT "åc (Tn/cm2) =";TC:INPUT "åt =";TT:TT=-ABS(TT)
700 W2=-(ABS(W2))
710 CW1=100*(MG-MU*MP)/(TC-MU*TT):CW2=100*(MG-MU*MP)/(MU*TC-TT)
720 PRINT:PRINT "W1(lim) =";CW1,"  <  W1 =";W1:GOSUB 980
730 PRINT:PRINT "|W2(lim)| =";ABS(CW2),"   <   W2 =";ABS(W2):GOSUB 980
740 IF CW1<=W1 AND ABS(CW2)<=ABS(W2) THEN 750 ELSE 760
750 PRINT "SE CUMPLEN LIMITACIONES.":GOSUB 980:GOTO 770
760 PRINT "NO CUMPLEN LIMITACIONES.":GOSUB 980
770 PRINT:PRINT "(1)=";TT*W1-100*MI,"(2)=";TC*W1-(100*MX):GOSUB 980
780 PRINT "(3)=";TT*W2-100*MX,"(4)=";TC*W2-100*MI:GOSUB 980
790 PRINT:PRINT "-W1/A=";-W1/A,"-W2/A=";-W2/A:GOSUB 980
800 PRINT:INPUT "CALCULO HUSO SI=1 NO=0"; KO
810 IF KO=1 THEN 820 ELSE 970
820 INPUT "Po (T) =";PO
830 INPUT "COEF.PERD.TOTAL =";CPT
840 INPUT "COEF.PERD.INIC. =";CPI
850 K1=TT*W1-100*MI:K2=TC*W1-100*MX:K3=TT*W2-100*MX:K4=TC*W2-100*MI
860 PMIN=PO*CPT*.9:PMAX=PO*CPI*1.1
870 E1=-W1/A+K1/PMAX:E2=-W1/A+K1/PMIN:E7=-W2/A+K4/PMAX:E8=-W2/A+K4/PMIN
880 IF E1<=E2 THEN E1=E2
890 IF E7<=E8 THEN E7=E8
900 IF E1<=E8 THEN E1=E8
910 E3=-W1/A+K2/PMAX:E4=-W1/A+K2/PMIN:E5=-W2/A+K3/PMAX:E6=-W2/A+K3/PMIN
920 IF E4<=E3 THEN E3=E4
930 IF E6<=E5 THEN E5=E6
940 IF E5<=E3 THEN E3=E5
950 PRINT:PRINT "HUSO DE PASO"
960 PRINT "";E1;"< e. <";E3:GOSUB 980
970 GOTO 20
980 IF INKEY$="" THEN 980 ELSE RETURN
