1 CLS:PRINT"**********ESTRUCTURAS***********";"
9 CLEAR:CLS:MODE4:INPUT"Cuantos estados necesitas";IPW
10 INPUT"ver matrices (S/N)";Q$
11 INPUT"N. DE NUDOS:";N
25 B=2*N-3
30 DIM P(N,2),PC(B,2),ALF(B),E(B,2*N),NC(3),NA(B),AXILES(IPW,B),DESPLA(IPW,B)
110 FOR S=1 TO N
120 CLS:LOCATE 10,0:PRINT"NUDO";S;
130 LOCATE 0,1:INPUT"COORD.X";P(S,1)
135 LOCATE 0,0
140 LOCATE 16,1:INPUT"COORD.Y=";P(S,2)
150 NEXT
200 CLS:PRINT"CONEXION DE NUDOS:";
205 FOR I=1 TO 200:NEXT I
210 FOR S=1 TO B
220 CLS:PRINT"BARRA";S;"NUDOS QUE UNE:";
225 LOCATE 0,0
230 LOCATE 5,1:INPUT PC(S,1):LOCATE 0,0:LOCATE 10,1:INPUT PC(S,2)
235 IF PC(S,1)<PC(S,2)THEN 240
236 NA=PC(S,1):PC(S,1)=PC(S,2):PC(S,2)=NA
240 NEXT
250 FOR X=1 TO 3
255 CLS:INPUT"NUDO COACCIONADO:";NC(X)
260 INPUT "COACCION X-Y:";N$
265 IF N$="X"THEN NC(X)=NC(X)*2-1 ELSE NC(X)=NC(X)*2
270 NEXT
499 CLS:PRINT"MATRIZ ANGULAR";
500 FOR I=1 TO 200:NEXT I
520 PRINT:FOR S=1 TO B
521 IX=P(PC(S,2),1)-P(PC(S,1),1)
522 IY=P(PC(S,2),2)-P(PC(S,1),2):NA(S)=SQR(IX*IX+IY*IY)
523 IF IY=0 THEN 524 ELSE 526
524 IF IX>0 THEN ALF(S)=0:GOTO 531
525 IF IX<0 THEN ALF(S)=180:GOTO531
526 IF IX=0 THEN 527 ELSE 529
527 IF IY>0 THEN ALF(S)=90:GOTO 531
528 IF IY<0 THEN ALF(S)=-90:GOTO531
529 IF IX<0 THEN ALF(S)=(ATN(IY/IX))+180:BEEP 1:GOTO 531
530 ALF(S)=ATN(IY/IX)
531 IF Q$="S"THEN PRINT"LONGITUD";NA(S):PRINT:PRINT"ANGULO";ROUND(ALF(S),-4)
532 GOSUB 10000
533 NEXT
700 CLS:PRINT"MATRIZ DE EQUILIBRIO";
705 FOR I=1 TO 200:NEXT I
710 J=0:BEEP
720 FOR S=1 TO N:J=J+1:FOR SS=1 TO B
730 IF S=PC(SS,1)THEN E(SS,J)=-COS(ALF(SS)):GOTO 736
735 IF S=PC(SS,2)THEN E(SS,J)=COS(ALF(SS))
736 NEXT SS
737 J=J+1
738 FOR SS=1 TO B
740 IF S=PC(SS,1)THEN E(SS,J)=-SIN(ALF(SS)):GOTO 750
745 IF S=PC(SS,2)THEN E(SS,J)=SIN(ALF(SS))
750 NEXT SS:NEXT S
751 IF Q$<>"S"THEN 830 ELSE PRINT
752 FOR SS=1 TO 2*N:FOR S=1 TO B:PRINT"E(";S;",";SS;")...";E(S,SS):GOSUB 10000:NEXT:NEXT
830 ERASE ALF:DIM AX(B),CC(2*N)
840 PRINT "MAT. E";CHR$(223);"";
850 FOR I=1 TO 200:NEXT I
950 J=0
955 FOR Y=1 TO 2*N
960 IF Y=NC(1)THEN 970
961 IF Y=NC(2)THEN 970
962 IF Y=NC(3)THEN 970
963 J=J+1
965 FOR X=1 TO B:E(X,J)=E(X,Y)
967 NEXT
970 NEXT
999 OK=1:CLS:A$=INKEY$
1000 LOCATE 1,0:PRINT "CARGA";:LOCATE 10,0:PRINT "AXILES";
1001 LOCATE 20,0:PRINT "CORRIMIENTO";
1004 IF A$="1" THEN GOSUB  1275
1006 IF A$="2" THEN GOSUB 6001
1008 IF A$="3" THEN GOSUB 6001
1010 GOTO  999
1275 CLS:PRINT"CARGAS EXTERIORES":FOR S=1 TO 2*N:CC(S)=0:NEXT
1276 INPUT "NUDO CARGADO";X$
1277 IF X$=""THEN GOSUB  1283:RETURN
1278 X=VAL(X$):IF X>0 THEN IF X=<N THEN 1279 ELSE 1276
1279 INPUT "CARGA EN X";CX
1280 INPUT "CARGA EN Y";CY
1281 X=VAL(X$):CC(X*2-1)=CC(X*2-1)+CX:CC(X*2)=CC(X*2)+CY:GOTO 1276
1283 K=0:FOR S=1 TO 2*N:K=K+1
1284 IF S=NC(1)THEN K=K-1:GOTO 1295
1285 IF S=NC(2)THEN K=K-1:GOTO 1295
1286 IF S=NC(3)THEN K=K-1:GOTO 1295
1288 CC(K)=CC(S)
1295 NEXT :RETURN
6001 L=B+1:DIM A(L,B),R(B)
6002 FOR S=1 TO B
6004 IF A$="3"THEN A(B+1,S)=AX(S)*NA(S)ELSE A(B+1,S)=CC(S)
6005 FOR  SS=1 TO B
6007 IF A$="3"THEN A(S,SS)=E(SS,S)ELSE A(SS,S)=E(SS,S)
6008 NEXT :NEXT
6010 FOR X=1 TO B-1
6015 IF A(X,X)=0 THEN GOSUB 7000
6020 FOR Y=X+1 TO B
6025 IF A(X,Y)=0 THEN 6050
6030 C=A(X,X)/A(X,Y)
6035 FOR D=1 TO B+1
6040 A(D,Y)=A(D,Y)*C-A(D,X)
6045 NEXT
6050 NEXT
6055 NEXT
6060 R(B)=A(L,B)/A(B,B)
6065 FOR Y=B-1 TO 1 STEP-1
6070 T=0
6075 FOR X=Y-1 TO B
6080 T=T+A(X,Y)*R(X)
6085 NEXT
6090 R(Y)=(A(L,Y)-T)/A(Y,Y)
6095 NEXT
6099 BEEP:CLS:IF A$="3"THEN NN$="CORRIMIENTO"ELSE NN$="AXIL"
6100 FOR X=1 TO B:PRINT NN$;X;"........";R(X):GOSUB  10000
6104 IF A$="2"THEN AX(X)=R(X)
6105 NEXT :GOSUB 10010:ERASE A,R:RETURN
7000 FOR S=X+1 TO B
7003 IF A(X,S)=0 THEN 8000
7005 FOR U=1 TO L
7010 NA=A(U,S):A(U,S)=A(U,X):A(U,X)=NA
7015 NEXT
7021 RETURN
8000 NEXT
10000 IF INKEY$=""THEN 10000
10001 RETURN
10010 CLS:PRINT "Guardar datos(S/N)";
10020 WA$=INKEY$
10030 IF WA$="N"THEN 10200
10031 IF WA$="S"THEN CLS:GOTO 10050
10035 GOTO 10020
10050 CLS:PRINT "Numero de estado";:FOR SW=1 TO IPW:PRINT SW;:IF SW+1=IPW THEN PRINT "o";
10051 NEXT :PRINT
10055 JHJ$=INKEY$
10056 NUW=VAL(JHJ$)
10057 IF NUW>0 THEN IF NUW<(IPW+1) THEN 10065
10060 GOTO 10055
10065 FOR SW=1 TO B
10070 IF A$="2"THEN AXILES(NUW,SW)=R(SW)
10075 IF A$="3"THEN DESPLA(NUW,SW)=R(SW)
10080 NEXT :PRINT "Realizado!!!"
10090 IF A$="2"THEN PRINT "Guardado en AXILES[";NUW;",?]"
10091 IF A$="3"THEN PRINT "Guardado en DESPLA[";NUW;",?]"
10099 IF INKEY$=""THEN 10099
10100 CLS:PRINT "Quieres operaciones (S/N)?"
10101 IF INKEY$="N"THEN 10200
10102 IF INKEY$="S"THEN 10105
10103 GOTO 10101
10105 INPUT "Producto de cuantos terminos",TER
10106 DIM TIR(TER)
10110 FOR SW=1 TO TER
10115 CLS:PRINT "Registro[";SW;"]......";:INPUT TIR(SW):NEXT
10120 EEEW=0:FOR SW=1 TO B
10122 EEW=1:FOR SSW=1 TO TER
10125 EEW=EEW*AXILES(TIR(SSW),SW)
10126 NEXT :EEW=EEW*NA(SW)
10129 'PRINT "PRODUCTO:";
10130 FOR SSW=1 TO TER:PRINT "N(";TIR(SSW);")*";:NEXT :PRINT "L(";SW;")=";EEW
10139 EEEW=EEEW+EEW
10140 IF INKEY$=""THEN 10140
10150 NEXT
10160 PRINT "SUMATORIO=";EEEW
10170 PRINT "mas operaciones(S/N)"
10200 IF INKEY$="N"THEN ERASE TIR:RETURN
10201 IF INKEY$="S"THEN CLS:GOTO 10105
10205 GOTO 10200
