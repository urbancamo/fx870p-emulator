1 PRINT"    Matriz de rigidez Local","        y general "," Para una sola barra" :GOSUB 600
5 CLS
10 CLEAR
20 PRINT"OBTENCION.Kb"
22 INPUT"EMPARRILLADO(S/N)";A$
25 N=6
30 DIM T(N,N),K(N,N),M(N,N)
35 PRINT "     **SIEMPRE FRONTAL>DORSAL**"
40 PRINT"PTO FRONTAL"
50 INPUT"X=";X2
60 INPUT"Y=";Y2
70 PRINT"PTO DORSAL"
80 INPUT"X";X1,"Y";Y1
100 X=X2-X1:Y=Y2-Y1
110 L=SQR(X^2+Y^2)
120 C=X/L:S=Y/L
123 PRINT"COS=";C:GOSUB 600
125 PRINT"SIN=";S:GOSUB 600
127 IF A$="S" THEN GOTO 1000
130 T(1,1)=C:T(4,4)=C
140 T(1,2)=-S:T(4,5)=-S
150 T(2,1)=S:T(5,4)=S
160 T(2,2)=C:T(5,5)=C
170 T(3,3)=1:T(6,6)=1
180 INPUT"E(Tn/m2)";E
190 INPUT"AREA(m2)";A,"I(m4)";Y
210 K(1,1)=E*A/L:K(4,4)=K(1,1):K(1,4)=-K(1,1)
220 K(2,2)=12*E*Y/L^3:K(5,5)=K(2,2):K(2,5)=-K(2,2)
230 K(2,3)=6*E*Y/L^2:K(5,6)=-K(2,3):K(2,6)=K(2,3):K(3,5)=-K(2,3)
240 K(3,3)=4*E*Y/L:K(6,6)=K(3,3):K(3,6)=0.5*K(3,3)
250 FOR I=1 TO N
260 FOR J=1 TO I
270 K(I,J)=K(J,I)
280 NEXT J
290 NEXT I
294 CLS:BEEP:INPUT"Ver matriz Rigidez LOCAL (S/N)";VMR$:IF VMR$="N" THEN 300
295 FOR G=1 TO N:FOR H=1 TO N
296 PRINT"K(";G;",";H;")=";K(G,H):GOSUB 600
298 NEXT H:NEXT G
299 GOSUB 1140
300 FOR I=1 TO N
310 FOR J=1 TO N
315 F=0
320 FOR Z=1 TO N
330 F=F+T(I,Z)*K(Z,J)
340 NEXT Z
350 M(I,J)=F
360 NEXT J
370 NEXT I
375 ERASE K:DIM K(N,N)
380 FOR I=1 TO N
390 FOR J=1 TO N
395 F=0
400 FOR Z=1 TO N
410 F=F+M(I,Z)*T(J,Z)
420 NEXT Z
430 K(I,J)=F
440 NEXT J
450 NEXT I
455 BEEP
460 FOR I=1 TO N
470 FOR J=1 TO N
480 CLS:PRINT"K(";I;",";J;")=";K(I,J)
490 GOSUB 600
500 NEXT J
510 NEXT I
520 END
600 IF INKEY$="" THEN GOTO 600
610 RETURN
1000 T(1,1)=1:T(4,4)=1
1010 T(2,2)=C:T(5,5)=C
1020 T(3,3)=C:T(6,6)=C
1030 T(2,3)=-S:T(3,2)=S
1040 T(5,6)=-S:T(6,5)=S
1050 INPUT"E(Ton/m2)";E,"G(Tn/m2)";G,"J(m4)";V,"I(m4)";Y
1090 K(1,1)=12*E*Y/L^3:K(1,4)=-K(1,1):K(4,4)=K(1,1)
1100 K(1,3)=-6*E*Y/L^2:K(1,6)=K(1,3):K(3,4)=-K(1,3):K(4,6)=-K(1,3)
1110 K(2,2)=G*V/L:K(2,5)=-K(2,2):K(5,5)=K(2,2)
1120 K(3,3)=4*E*Y/L:K(6,6)=K(3,3):K(3,6)=0.5*K(3,3)
1130 GOTO 250
1140 CLS:INPUT"Calculo de Esfuerzos (S/N)";CE$:IF CE$="N" THEN RETURN
1145 IF A$="S" THEN 2000
1150 CLS:PRINT"Desplaz. extremo Dorsal":GOSUB 600
1155 DIM D(6),ESF(6)
1160 INPUT"dxIj=";D(1),"dyIj=";D(2),"Giro Ij=";D(3)
1170 CLS:PRINT"Desplaz. extremo Frontal"
1180 INPUT"dxiJ=";D(4),"dyiJ=";D(5),"Giro iJ=";D(6)
1190 FOR V=1 TO 6:ESF(V)=0:FOR H=1 TO 6:ESF(V)=ESF(V)+K(V,H)*D(H):NEXT H:NEXT V
1200 CLS:BEEP:PRINT"Esfuerzos en Ejes LOCALES":GOSUB 600:PRINT"Extremo DORSAL":GOSUB 600
1210 PRINT"NxIj=";ESF(1),"NyIj=";ESF(2),"MzIj=";ESF(3):GOSUB 600
1220 CLS:PRINT"Extremo FRONTAL":GOSUB 600
1230 PRINT"NxiJ=";ESF(4),"NyiJ=";ESF(5),"MziJ=";ESF(6):GOSUB 600
1240 RETURN
2000 DIM D(6),ESF(6):INPUT"dzIj=";D(1),"Giro x Ij=";D(2),"Giro y Ij=";D(3),"dziJ=";D(4),"Giro x iJ=";D(5),"Giro y iJ=";D(6)
2010 FOR H=1 TO 6:ESF(H)=0:FOR C=1 TO 6:ESF(H)=ESF(H)+K(H,C)*D(C):NEXT C:NEXT H
2020 CLS:BEEP:PRINT"QzIj=";ESF(1),"MxIj=";ESF(2),"MyIj=";ESF(3):GOSUB 600:PRINT"QziJ=";ESF(4),"MxiJ=";ESF(5),"MyiJ=";ESF(6):GOSUB 600:RETURN
