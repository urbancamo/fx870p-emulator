10 CLS:CLEAR:PRINT"Introduce la estructura en forma CANONICA. Los estados asociados (si los hay) los calcula el     programa";:GOSUB9000
20 PRINT,"Deletrea los nudos segun el     metodo de nudos","Numera las barras":GOSUB9000
30 CLS:INPUT "Num de barras";B,"Num de nudos";N,"Num de reacciones";R:LLAVE=1:ANGLE0
5010 CLS:PRINT "POR NUDOS .............. 1","POR HENNEBERG .......... 2","ESTRUCT HIPERESTATICA .. 3"
5025 I$=INKEY$:IFI$=""THEN5025
5026 ESTRUCT=ASC(I$)-48:IF ESTRUCT>3 THEN 5010
5030 ON ESTRUCT GOTO 5040,5050,7000
5040 GOSUB "ESTRUCT1":IF YO=3 THEN 7030 ELSE 5222
5050 CLS:INPUT "Num de barras cambiadas";AJ
5052 DIMQ(AJ),LL(AJ)
5055 DIM NI(B,AJ+M+1):BEL=0
5060 GOSUB"ESTRUCT1":BEL=BEL+1
5070 FORI=1 TO B:NI(I,BEL)=AX(I):NEXTI
5075 IF BEL>AJ THEN 5155
5076 ERASE AX,C,F:DIMF(N,2)
5080 MO=2:CLS:PRINT "ESTADO ASOCIADO";BEL:GOSUB9020
5085 IF ESTRUCT=3 THEN 5095
5090 CLS:INPUT"Numero barra cambiada";Q(BEL)
5095 CLS:INPUT"Nombre nudo 1 barra suprimida";Q$
5100 INPUT"Nombre nudo 2 barra suprimida";QQ$
5110 Z=ASC(Q$)-64:Z1=ASC(QQ$)-64:LL(BEL)=SQR((N(Z,1)-N(Z1,1))^2+(N(Z,2)-N(Z1,2))^2)
5120 IF N(Z1,2)-N(Z,2)=0 THEN AZIM=90*(2-SGN(N(Z1,1)-N(Z,1))):GOTO5140
5130 AZIM=ATN((N(Z1,1)-N(Z,1))/(N(Z1,2)-N(Z,2)))+90*(1-SGN(N(Z1,2)-N(Z,2)))
5140 F(Z,1)=SINAZIM:F(Z,2)=COSAZIM:AZIM=AZIM+180:F(Z1,1)=SINAZIM:F(Z1,2)=COSAZIM
5150 GOTO5060
5155 IF ESTRUCT=3 THEN IF AJ=1 AND M=0 THENGOSUB 5355:GOTO 7030 ELSE 7030
5160 ERASE A,V,X,B,H:DIM A(AJ,AJ+1),V(AJ),X(AJ),B(AJ,AJ+1),H(2*R+1):T=AJ:O=1
5165 FOR I=1 TO AJ:FOR K=1 TO AJ:A(I,K)=NI(Q(I),K+1):B(I,K)=A(I,K):NEXT K:NEXTI
5170 FOR I=1 TO AJ:A(I,AJ+1)=-NI(Q(I),1):B(I,AJ+1)=A(I,AJ+1):NEXTI
5180 GOSUB"CRAMER":CLS
5190 FOR I=1 TO AJ:PRINT "S(";I;")=N(";Q(I);")=";X(I):NEXTI:GOSUB 9000
5200 PRINT "Axiles de todas las barras:","N=Ni(O)+S*Ni(I)+S'*Ni(II)..."
5210 FOR I=1 TO B:FOR K=2 TO AJ+1:NI(I,1)=NI(I,1)+X(K-1)*NI(I,K):NEXTK
5220 PRINT "A(";I;")=";NI(I,1):GOSUB9000:NEXTI
5222 IF MI=3 AND (AJ+M)<>0 THEN ESTRUCT=3
5225 IF MI=3 THEN GOTO5570
5300 CLS:PRINT "CALCULO DESP., GIROS... (S/N)?":MI=3:MO=0
5310 A$=INKEY$:IF A$=""THEN5310
5320 IF A$<>"S" AND A$<>"N" THEN5300
5330 IFA$="N"THEN CLS:CLEAR:END
5340 DIM NO(B):IF ESTRUCT=1THEN FOR I=1TOB:NO(I)=AX(I):NEXTI:GOTO5355
5350 FORI=1 TOB:NO(I)=NI(I,1):NEXTI:FORI=1TOBEL-1:NO(Q(I))=X(I):NEXTI
5355 DIM K(B)
5360 CLS:PRINT"Conozco EA ...... 1","        K  ...... 2";:INPUTA
5365 IF A<>2 AND A<>1 THEN 5360
5370 IF A=2THEN5450
5380 DIML(B):CLS:FORI=1TOB:Z=ASC(B$(I,1))-64:Z1=ASC(B$(I,2))-64
5390 L(I)=SQR((N(Z,1)-N(Z1,1))^2+(N(Z,2)-N(Z1,2))^2):NEXTI:IF ESTRUCT=2 THEN FOR I=1 TO BEL-1:L(Q(I))=LL(I):NEXTI
5395 FORI=1TOB:PRINT"Longitud barra";I;"=";L(I):GOSUB9000:NEXTI
5400 CLS:PRINT "EA constante (S/N)?"
5410 A$=INKEY$:IF A$=""THEN5410
5420 IF A$<>"S" AND A$<>"N" THEN5400
5430 IF A$="S" THEN INPUT "Valor de EA";EA:FORI=1TOB:K(I)=EA/L(I):PRINT "K(";I;")=";K(I):GOSUB9000:NEXTI:GOTO5495
5440 FORI=1TOB:PRINT "Valor de EA barra";I;"=";:INPUT EA:K(I)=EA/L(I):PRINT"K(";I;")=";K(I):GOSUB9000:NEXTI:GOSUB 8000:GOTO5495
5450 CLS:PRINT "K constante (S/N)?"
5460 A$=INKEY$:IF A$=""THEN5460
5470 IF A$<>"S" AND A$<>"N" THEN5450
5480 IF A$="S"THENINPUT "Valor de K";K:FORI=1TOB:K(I)=K:NEXTI:GOTO5495
5490 FORI=1TOB:PRINT "Valor de Kbarra";I;"=";:INPUTK(I):NEXTI
5495 IF ESTRUCT<>3 THEN 6000 ELSE RETURN
5500 CLS:INPUT"Nombre nudo a desplazar";A$:ZI=ASC(A$)-64
5510 CLS:PRINT "Desplaz. direccion dada .. 1","Desplaz. real ............ 2";:INPUT AF
5520 IF AF<>1AND AF<>2THEN 5510
5540 AZ=0:IF AF=1 THEN INPUT "Azimut desplazamiento";AZ
5545 ERASE F:DIMF(N,2):F(ZI,1)=SINAZ:F(ZI,2)=COSAZ
5550 ERASE NII,A,A$,C,V,X,B,H:DIM C(B),A(R,R+1),A$(R,R)
5555 CLS:PRINT REV"ESTADO ASOCIADO PARA DESPLAZ":PRINTNORM:GOSUB9020
5560 IF ESTRUCT=1THEN5040ELSE ERASENI:GOTO5055
5570 IF BEL1<>0THEN7037
5575 DIM NII(B):IF ESTRUCT=1 THEN FORI=1TOB:NII(I)=AX(I):NEXTI:GOTO5590
5580 FORI=1 TOB:NII(I)=NI(I,1):NEXTI:FORI=1TOBEL-1:NII(Q(I))=X(I):NEXTI
5590 D=0
5600 FORI=1 TOB:D=D+((NO(I)*NII(I))/K(I)):NEXTI
5605 IF OL=2 THENCLS:PRINT"Ang giro rad=((Ni(O)*Ni(I))/Ki)=";D:GOSUB9000:GOTO5640
5606 IF OL=3 THEN CLS:PRINT"Separacion=((Ni(O)*Ni(I))/Ki)=";D:GOSUB9000:GOTO5640
5610 IF AF=1 THENCLS:PRINT"D=((Ni(0)*Ni(I))/Ki)=";D:GOSUB9000:GOTO5640
5620 IF AZ=90 THENCLS:PRINT"Despl X=((Ni(0)*Ni(H))/Ki)=";D,"Despl lineal=SQR(D(X)^2+D(Y)^2)=";SQR(D^2+D1^2);:GOSUB9000:GOTO5640
5630 CLS:PRINT"Despl Y=((Ni(O)*Ni(V))/Ki)=";D:D1=D:GOSUB9000:AZ=90:CLS:PRINTREV"AHORA ESTADO ASOCIADO EN X":PRINTNORM:GOSUB 9020:MO=0:GOTO5545
5640 CLS:PRINT"Otro nudo (S/N)?"
5650 A$=INKEY$:IF A$=""THEN5650
5660 IF A$<>"S" AND A$<>"N" THEN5640
5670 IF A$="S" THEN MO=0:GOTO5495
5680 CLS:CLEAR:END
6000 CLS:PRINT"Desplazamiento nudo ...... 1","Angulo rotacion barras ... 2","Var. Dist. entre 2 punt .. 3";:INPUTOL
6010 IF OL<>1 AND OL<>2 AND OL<>3 THEN 6000
6020 IF OL=1 THEN 5500
6030 IF OL=2 THEN 6100
6040 CLS:INPUT"Nombre nudo 1";A$:Z=ASC(A$)-64:INPUT "Nombre nudo 2";A$:Z1=ASC(A$)-64
6050 ERASEF:DIMF(N,2)
6060 IF N(Z1,2)-N(Z,2)=0 THEN AZIM=90*(2-SGN(N(Z1,1)-N(Z,1))):GOTO 6075
6070 AZIM=ATN((N(Z1,1)-N(Z,1))/(N(Z1,2)-N(Z,2)))+90*(1-SGN(N(Z1,2)-N(Z,2)))
6075 IF OL=2 THEN AZIM=AZIM-90
6080 F(Z1,1)=SINAZIM:F(Z1,2)=COSAZIM:AZIM=AZIM+180:F(Z,1)=SINAZIM:F(Z,2)=COSAZIM
6085 IF OL=2 THEN FORK=1TO2:F(Z1,K)=(1/L(A))*F(Z1,K):F(Z,K)=(1/L(A))*F(Z,K):NEXTK
6090 GOTO 5550
6100 CLS:INPUT"Num barra a calcular giro";A
6110 Z=ASC(B$(A,1))-64:Z1=ASC(B$(A,2))-64:GOTO6050
7000 CLS:INPUT"HIPER-externas";M,"HIPER-interno";AJ
7015 IF AJ=0 THEN ESTRUCT=1:YO=3:DIMNI(B,M+1):GOTO5040
7020 GOTO5052
7030 BEL1=BEL1+1:IF BEL1>M AND ESTRUCT=1 THEN FORI=1 TOB:NI(I,BEL1)=AX(I):NEXTI:GOTO7070
7031 IF BEL1>M THEN7070
7032 MI=3:MO=0:ESTRUCT=1:AF=1
7035 CLS:INPUT"Nombre nudo reac suplimida";A$:ZI=ASC(A$)-64:INPUT"Azimut reaccion suplimida";AZ
7037 ISA=ISA-2:IF ESTRUCT=1 THENISA=0
7038 IF AJ=0 THEN FOR I=1TOB:NI(I,OLE)=AX(I):NEXTI
7040 IF ESTRUCT=1 THEN ESTRUCT=3:GOSUB5355:ESTRUCT=1:GOTO5545
7045 FOR I=1TOB:NI(I,OLE)=AX(I):NEXTI
7050 IF BEL1=M THEN 7070
7060 ISA=ISA+1:GOTO 7030
7070 FOR I=1 TO(1+M+AJ):C=0:FOR J=(I)TO(1+M+AJ):IF I=1 AND J=1 THEN 7095
7080 FORK=1TOB:A=NI(K,I)*NI(K,J)/K(K):CLS:PRINT"N(";K;")^";I-1;"*N(";K;")^";J-1;"/K(";K;")=",A:C=C+A:GOSUB9000:NEXTK
7090 PRINTREV"Suma total=";:PRINTNORM C:GOSUB9000
7095 C=0:NEXTJ:NEXTI
7100 CLS:PRINT "HAY ESTADO DE TEMPERATURA (S/N)? ";
7110 A$=INKEY$: IF A$=""THEN7110
7120 IF A$<>"S" AND A$<>"N" THEN7100
7130 IF A$="N" THEN 7250
7140 CLS:PRINT " L ........L","alfa ..... A ";
7150 A$=INKEY$: IF A$=""THEN 7150
7160 IF A$="A"THEN CLS:INPUT "Alfa";A:DIM T(B),IL(B):FOR I=1 TO B:PRINT ">T(";I;")=";:INPUT T(I):NEXTI
7165 IF A$="A" THEN FOR I=1 TO B: IL(I)=A*T(I)*L(I):PRINT"<L(";I;")=";IL(I):GOSUB 9000:NEXT I:GOTO7180
7170 DIM IL(B):FOR I=1 TO B:CLS:PRINT ">L(";I;")=";:INPUT IL(I):NEXT I
7180 FOR I=2 TO (AJ+M+1):C=0:FORK=1 TO B:A=NI(K,I)*IL(K):CLS:PRINT "N(";K;")^";I-1;"*<L(";K;")",A:C=C+A:GOSUB 9000:NEXT K
7190 PRINT REV"suma total=";:PRINT NORM C:GOSUB 9000
7200 NEXT I
7210 CLS:PRINT "Otro estado de temp(S/N)? ";
7220 A$=INKEY$: IF A$=""THEN 7220
7230 IF A$<>"S" AND A$<>"N"THEN 7210
7240 IF A$="S" THEN ERASE IL,T:GOTO 7140
7250 CLS:PRINT "Borro la memoria (S/N)? ";
7260 A$=INKEY$: IF A$=""THEN 7260
7270 IF A$<>"S" AND A$<>"N" THEN 7250
7280 IF A$="S" THEN CLEAR:CLS:END ELSE CLS:END
8000 CLS:INPUT "Cambio algun K(i)";A$:IF A$<>"S" AND A$<>"N" THEN 8000
8010 IF A$="N" THEN RETURN
8020 INPUT "Num barra a cambiar K(i)";OL:PRINT "Valor K(";OL;")=";:INPUT K(OL):GOTO 8000
9000 J$=INKEY$:IF J$=""THEN9000
9010 RETURN
9020 FORP=1 TO 450:NEXTP:RETURN
