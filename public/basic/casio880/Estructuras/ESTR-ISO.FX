10 PRINT "ARTICULADA ISOSTATICA":CLEAR : MODE 5
20 INPUT "N DE NUDOS",N: M=N*2
30 INPUT "N DE BARRAS",B:A=M-B: SET F0
40 DIM X(N), Y(N),F(B),D(B),G(B),R(M),B$(1)
50 B$(0)=" Rx=": B$(1)=" Ry="
60 FOR I=1 TO N: PRINT "NUDO";I;
70 INPUT "  COORDENADA X=",X(I)
80 INPUT "  COORDENADA Y=",Y(I): NEXT I
90 FOR I=1 TO B:PRINT "BARRA";I;
100 INPUT " ND=",D(I):D=D(I)
110 INPUT " NF=",F(I):F=F(I)
120 X=X(F)-X(D): Y=Y(F)-Y(D):SET F2
130 PRINT "L[M] =";POL(X,Y);" A[I] =";Y*180/PI
140 G(I)=Y: SET F0:NEXT I
165 FOR I=1 TO A:PRINT "APOYO";I;
170 INPUT "S/OX,N DE NUDOS",R:IF R>0 THEN R(I)=2*R-1: I=I+1
180 INPUT "S/OY,N DE NUDO",R:IF R>0 THEN R(I)=2*R
185 IF R=0 THEN I=I-1
190 NEXT I
200 ERASE A,X,Y: PRINT "MATRIZ E   ";: DIM A(M,B), C(M)
205 FOR I=1 TO B: D=D(I): F=F(I): R(A+I)=0
210 S=SIN G(I): Q=COS G(I)
220 A(D*2-1,I)=-Q: A(D*2,I)=-S
230 A(F*2-1,I)=Q: A(F*2,I)=S
240 NEXT I
300 H=H+1: SET F0: PRINT "HIPOTESIS",H
310 FOR I=1 TO N: PRINT ";I;
320 INPUT "CARGA NODAL FX=",C(I*2-1)
330 INPUT "CARGA NODAL FY=",C(I*2): NEXT I
400 PRINT "RESOLVIENDO SISTEMA !ESPERE!";
410 U=A:FOR I=1 TO M: FOR K=1 TO A
420 IF R(K)=I THEN I=I+1 :IF  I>M THEN GOTO 500
430 NEXT K: FOR K=1 TO B
440 IF ABS A(I,K)<1E-8 THEN K=K+1: GOTO 440
450 P=K: FOR J=A+1 TO M:IF K=R(J) THEN J=M: P=0
460 NEXT J: IF P>0 THEN K=B
470 NEXT K: U=U+1: R(U)=P
475 FOR K=1 TO M: IF K=I THEN K=K+1
480 F=A(K,P)/A(I,P)
490 FOR J=1 TO B:A(K,J)=A(K,J)-F*A(I,J): NEXT J
495 C(K)=C(K)-F*C(I): NEXT K: NEXT I
500 PRINT "REACCIONES: ";
510 FOR I=1 TO A:Z=INT(((1+R(I))/2):W=R(I)-Z*2+1
520 SET F0:PRINT "NUDO";Z;B$(W);
530 SET F3: PRINT -C(R(I)): NEXT I
600 PRINT "AXILES: ";: FOR I=1 TO B
610 D=D(I): F=F(I): SET F0: PRINT "BARRA";I;"(";D;"->";F;") N=";
620 SET F3: FOR J=1 TO M: Z=A(J,I)
630 IF  ABS Z>1E-6 THEN PRINT C(J)/Z
640 NEXT J: NEXT I: GOTO 200
