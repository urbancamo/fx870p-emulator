10 PRINT "EMPARRILLADO"
15 INPUT "Borrar datos S/N";A$:IF A$="N" THEN 30
20 CLEAR :DIM B(3,3):PRINT "Entrada de datos":GOSUB 100
30 INPUT "M.BARRA 1,SUMA 2,PRODUCTO 3";R
40 ON R GOSUB 500,1000,1500
45 GOTO 30
50 END
100 INPUT "E y NU ctes S/N";B$:IF B$="S" THEN INPUT "E en kp/cm2";E,"NU";NU
105 INPUT "N.de barras";B:DIM M(B),N(B),V(B),Q(B),T(B),ALFA(B)
110 FOR I=1 TO B:PRINT "barra";I
115 IF B$="S" THEN 130
120 INPUT "E en kp/cm2";E,"NU";NU
130 G=E/2/(1+NU)
140 INPUT "I en cm4";IN,"J en cm4";J,"L en m";L,"ALFA en grados";ALFA(I)
150 IN=E*IN/1E7:J=G*J/1E7
160 M(I)=12*IN/L^3:N(I)=6*IN/L^2:V(I)=J/L:Q(I)=4*IN/L
170 NEXT I
180 RETURN
500 INPUT "II 1,JJ 2,IJ 3,JI 4";R,"de la barra";I
510 ON R GOTO 520,530,540,550
520 M=M(I):N=-N(I):T=N:V=V(I):Q=Q(I):PRINT "K(I,I) de la barra";I:GOTO 555
530 M=M(I):N=N(I):T=N:V=V(I):Q=Q(I):PRINT "K(J,J) de la barra";I:GOTO 555
540 M=-M(I):N=-N(I):T=-N:V=-V(I):Q=Q(I)/2:PRINT "K(I,J) de la barra";I:GOTO 555
550 M=-M(I):N=N(I):T=-N:V=-V(I):Q=Q(I)/2:PRINT "K(J,I) de la barra";I
555 X=COSALFA(I):Y=SINALFA(I)
560 B(1,1)=M:B(1,2)=-N*Y:B(1,3)=N*X:B(2,1)=-T*Y:B(2,2)=V*X^2+Q*Y^2:B(2,3)=(V-Q)*X*Y:B(3,1)=T*X:B(3,2)=B(2,3):B(3,3)=V*Y^2+Q*X^2
565 FOR Z=1 TO 3:FOR W=1 TO 3:PRINT "B(";Z;",";W;")=";B(Z,W):NEXT W:NEXT Z
570 RETURN
1000 ERASE S:DIM S(3,3)
1010 INPUT "N.de sumandos";NS
1020 FOR K=1 TO NS:PRINT "MATRIZ";K:GOSUB 500
1030 FOR L=1 TO 3:FOR V=1 TO 3
1040 S(L,V)=S(L,V)+B(L,V):NEXT V:NEXT L
1050 NEXT K
1060 CLS:PRINT "LA SUMA ES"
1070 FOR I=1 TO 3:FOR J=1 TO 3
1080 PRINT "S(";I;",";J;")=";S(I,J):NEXT J:NEXT I
1090 RETURN
1500 PRINT "Producto":INPUT "Meter desplazamientos S/N";D$:IF D$="N" THEN 1570
1510 INPUT "N.Nudos moviles";H:ERASE D,P:DIM D(3,H),P(3)
1520 FOR Z=1 TO H
1530 PRINT "dz";Z;:INPUT D(1,Z)
1540 PRINT "Giro X";Z;:INPUT D(2,Z)
1550 PRINT "Giro Y";Z;:INPUT D(3,Z)
1560 NEXT Z
1570 GOSUB 500
1580 INPUT "Desplaz.del nudo";F:ERASE P:DIM P(3)
1590 FOR Z=1 TO 3:FOR J=1 TO 3:P(Z)=P(Z)+B(Z,J)*D(J,F):NEXT J:NEXT Z
1600 PRINT "El producto es"
1610 FOR Z=1 TO 3:PRINT "P(";Z;")";P(Z):NEXT Z
1620 INPUT "Otro Producto S/N";PH$:IF PH$="S"THEN GOTO 1570
1630 RETURN
