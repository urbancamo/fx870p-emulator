10 CLEAR :CLS
20 PRINT "**ESTRUCTURAS INTRASLACIONALES**";
30 LOCATE 0,1
40 INPUT "Numero de nudos";NN
50 INPUT "Numero de barras";NB
55 INPUT "Mod.elasticidad(T/m^2)";E
60 INPUT "Hipotesis de carga";HIP
65 DIM X(NN),Y(NN),D(NB),F(NB),IN(NB),K(NN,NN),L(NB),MT(NN,HIP),RC(NN,HIP),MO(2*NB,HIP),S(NN),KB(NB),ARIG(NN)
70 INPUT "Inercia cte (S/N)";A$
80 IF A$="S" THEN INPUT "Inercia";IN ELSE IF A$<>"N" THEN 70
85 CLS
90 PRINT "*** COORDENADAS DE NUDOS ***"
95 LOCATE 0,1
105 FOR I=1 TO NN
110 CLS:PRINT "Nudo";I;
120 LOCATE 0,1
130 INPUT "X";X(I)
140 INPUT "Y";Y(I)
150 NEXT I
155 CLS
160 PRINT "*** NUMERACION DE BARRAS ***"
165 LOCATE 0,1
175 FOR I=1 TO NB
180 CLS:PRINT "Barra";I;
190 LOCATE 0,1
200 INPUT "Nudo dorsal";D(I)
210 INPUT "Nudo frontal";F(I)
220 IF A$="N" THEN INPUT "Inercia(m^4)";IN(I)
230 NEXT I
235 GOSUB 430
240 'CALCULOS GEOMETRICOS
245 CLS:PRINT "Calculando matriz rigidez";
250 FOR I=1 TO NB
260 ND=D(I):NF=F(I)
270 L(I)=SQR((X(NF)-X(ND))^2+(Y(NF)-Y(ND))^2)
280 IF A$="N" THEN IN=IN(I)
290 K(D(I),F(I))=2*E*IN/L(I):KB(I)=K(D(I),F(I))
293 IF ARIG(D(I))=2 OR ARIG(F(I))=2 THEN K(D(I),F(I))=3*K(D(I),F(I))/4:KB(I)=K(D(I),F(I))
295 K(F(I),D(I))=K(D(I),F(I))
300 NEXT I
310 ' FORMACION MATRIZ RIGIDEZ
315 FOR I=1 TO NN
320 S=0
325 FOR J=1 TO NB
330 IF I=D(J) OR I=F(J) THEN S=S+K(D(J),F(J))
360 NEXT J
365 K(I,I)=2*S
375 NEXT I
380 CLS:BEEP:INPUT "Ver matriz rigidez (S/N)";VR$
385 IF VR$="N" THEN GOSUB 1600:GOTO 461 ELSE IF VR$<>"S" THEN 380
390 CLS:PRINT "*** MATRIZ DE RIGIDEZ ***";
395 LOCATE 0,1
405 FOR I=1 TO NN
410 FOR J=I TO NN
415 PRINT "K(";I;J;")=";K(I,J)
420 NEXT J
425 NEXT I :GOSUB 1600:GOTO 461
430 CLS:PRINT "*** ENTRADA APOYOS ***";
435 LOCATE 0,1
440 INPUT "Numero nudos con apoyo";NA
445 FOR I=1 TO NA
450 INPUT "Nudo";ND
455 S=1:N=2:INPUT "RIGIDO (S/N)";ARIG(ND):'K(ND,ND)=0
460 NEXT I:RETURN
461 CONT=0
462 CONT=CONT+1
463 IF CONT>HIP THEN 495
465 CLS:PRINT "*** ENTRADA DE CARGAS ***";
470 LOCATE 0,1
472 PRINT "*** HIPOTESIS";CONT;"***"
475 CLS:PRINT "OPCION 1: B.cargadas";
480 LOCATE 0,1
485 PRINT "OPCION 2: B.descargadas";:INPUT "   Opc";POS
487 IF POS=0 THEN 462
490 ON POS GOSUB 1000,1500
492 GOTO 475
495 CLS:PRINT "Resolviendo sistema";
500 FOR I=1 TO NN-1
510 IF K(I,I)=0 THEN 610
520 FOR J=I+1 TO NN
530 PV=K(I,J)/K(I,I)
540 IF K(J,J)=0 THEN SNL=1 ELSE SNL=0
550 FOR K=J TO NN
560 K(J,K)=K(J,K)-K(I,K)*PV
570 NEXT K
575 FOR L=1 TO HIP
580 MT(J,L)=MT(J,L)-MT(I,L)*PV
585 NEXT L
590 IF SNL=1 THEN K(J,J)=0
600 NEXT J
610 NEXT I
620 FOR I=NN TO 1 STEP -1
630 FOR P=1 TO HIP:RC(I,P)=MT(I,P):NEXT P
640 FOR L=1 TO HIP
645 S(L)=0
650 FOR J=I+1 TO NN
660 S(L)=S(L)+K(I,J)*MT(J,L)
670 NEXT J
680 MT(I,L)=MT(I,L)-S(L)
685 IF K(I,I)<>0 THEN MT(I,L)=MT(I,L)/K(I,I):RC(I,L)=0 ELSE RC(I,L)=-MT(I,L):MT(I,L)=0
687 NEXT L
690 NEXT I
692 BEEP0:BEEP1
695 FOR L=1 TO HIP
700 CLS:PRINT "*** SALIDA GIROS ***";
705 LOCATE 0,1
707 PRINT "HIPOTESIS";L
710 FOR I=1 TO NN
720 IF K(I,I)=0 THEN 750
730 CLS: PRINT "Nudo";I;
735 LOCATE 0,1
740 PRINT "Giro=";
745 PRINT ROUND(MT(I,L),-7)
750 NEXT I
755 PRINT "*** SALIDA REACCIONES*** "
760 FOR I=1 TO NN
770 IF K(I,I)<>0 THEN 800
780 CLS:PRINT "Nudo";I;
785 LOCATE 0,1
790 PRINT "Reaccion=";ROUND(RC(I,L),-4)
800 NEXT I
810 CLS:PRINT "*** ESFUERZOS ***"
820 LOCATE 0,1
840 FOR I=1 TO NB
850 CLS:PRINT "BARRA";D(I);F(I);
860 LOCATE 0,1
862 S=1: IF ARIG(D(I))=2 OR ARIG(F(I))=2 THEN GOSUB 1700:GOTO 900
865 MD=MO(2*I-1,L)+S*KB(I)*(2*MT(D(I),L)+MT(F(I),L))
870 PRINT "Md=";ROUND(MD,-4);
875 MF=MO(2*I,L)+S*KB(I)*(MT(D(I),L)+2*MT(F(I),L))
880 LOCATE 15,1
890 PRINT "Mf=";ROUND(MF,-4)
900 LOCATE 0,2
905 QD=(MD+MF)/L(I)
910 PRINT "Qd=";ROUND(QD,-4);
920 LOCATE 15,2
925 QF=-QD
930 PRINT "Qf=";ROUND(QF,-4)
940 NEXT I
950 NEXT L
960 END
1000 '*** BARRAS CARGADAS ***
1010 INPUT "Numero barras cargadas";BCAR
1015 M1=0:M2=0
1020 FOR I=1 TO BCAR
1030 INPUT "Barra";BR
1040 CLS:PRINT "Momentos empotramiento barra";BR;
1050 LOCATE 0,1
1060 PRINT "Momento nudo dorsal(";D(BR);")";:INPUT M1
1070 PRINT "Momento nudo frontal(";F(BR);")";:INPUT M2
1080 MT(D(BR),CONT)=MT(D(BR),CONT)-M1:MO(2*BR-1,CONT)=MO(2*BR-1,CONT)+M1
1090 MT(F(BR),CONT)=MT(F(BR),CONT)-M2:MO(2*BR,CONT)=MO(2*BR,CONT)+M2
1110 NEXT I
1120 RETURN
1500 '*** BARRAS DESCARGADAS ***
1505 M1=0
1510 INPUT "Numero nudos cargados";NCAR
1520 FOR I=1 TO NCAR
1530 INPUT "Nudo";ND
1540 INPUT "Momento";M1
1550 MT(ND,CONT)=MT(ND,CONT)+M1
1570 NEXT I
1580 RETURN
1600 FOR I=1 TO NN
1610 IF ARIG(I)<>0 THEN K(I,I)=0
1620 NEXT I:RETURN
1700 S=4/3:IF ARIG(D(I))=2 GOTO 1720
1710 GIRO=-(MO(2*I,L)+S*KB(I)*MT(D(I),L))/(2*S*KB(I)):MD=MO(2*I-1,L)+S*KB(I)*(2*MT(D(I),L)+GIRO):MF=0:GOTO 1730
1720 GIRO=-(MO(2*I-1,L)+S*KB(I)*MT(F(I),L))/(2*S*KB(I)):MF=MO(2*I,L)+S*KB(I)*(MT(F(I),L)+2*GIRO):MD=0
1730 PRINT "Giro rotula=";GIRO;"rad":CLS:PRINT "barra";D(I);F(I);
1740 LOCATE 0,1:PRINT "Md=";ROUND(MD,-4);
1750 LOCATE 15,1:PRINT "Mf=";ROUND(MF,-4)
1760 RETURN
