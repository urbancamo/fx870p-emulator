20 CLEAR:CLS
30 PRINT "Estr. Nudos rigidos";
35 LOCATE 0,1
40 INPUT "N de nudos";NN,"N de barras";NB
60 DIM X(NN),Y(NN),D(NB),F(NB),LONG(NB),S(NB),C(NB),K(3*NN,3*NN),KL(3*NN,3*NN)
65 DIM CARGAS(3*NN),REACC(3*NN),CARG(3*NN,NB),CA(NB)
90 DIM AREA(NB),INERCIA(NB)
100 INPUT "M Elasticidad (T/m^2)";MD
130 CLS:PRINT "**COORD DE NUDOS**"
160 FOR I=1 TO NN
180 PRINT "Nudo";I
190 INPUT "X";X(I),"Y";Y(I)
210 NEXT I
230 CLS:PRINT "** Numeracion de barras**"
250 FOR I=1 TO NB
300 PRINT "Barra";I
310 INPUT "Nudo dorsal";D(I)
320 INPUT "Nudo frontal";F(I)
330 NEXT I
340 CLS:PRINT "MODELIZACION BARRAS"
351 INPUT "CUANTOS TIPOS DE BARRAS ";TB
352 FOR I=1 TO TB
353 PRINT "TIPO ";I
354 INPUT "AREA (m2) ";AR
355 INPUT "INERCIA (m4) ";IN
356 PRINT "Ní BARRAS DEL TIPO ";I;:INPUT CT
357 FOR J=1 TO CT
358 INPUT "BARRA Ní ";BA
359 IF BA>NB THEN 358
360 AREA(BA)=AR
361 INERCIA(BA)=IN
362 NEXT J
366 NEXT I
369 REM***CACULOS GEOM BARRAS***
370 FOR I=1 TO NB
390 ND=D(I)
400 NF=F(I)
410 LONG=SQR((X(NF)-X(ND))^2+(Y(NF)-Y(ND))^2)
420 LONG(I)=LONG
440 S(I)=(Y(NF)-Y(ND))/LONG(I)
460 C(I)=(X(NF)-X(ND))/LONG(I)
480 NEXT I
490 REM***Desconexiones***
500 INPUT "Desconexiones S/N";DESC$
510 IF DESC$="S" OR DESC$="s" THEN INPUT "N barras desconectadas";BDESC
515 DIM DESC(NB),TP(NB)
520 FOR I=1 TO BDESC
530 INPUT "Numero de barra";BARRA
540 DESC(BARRA)=BARRA
550 CLS:PRINT "Tipo desconexion";
560 PRINT " Dorsal,Frontal, Ambas (1,2,3)";:INPUT TP(BARRA)
600 NEXT I
610 REM ***CALC MATRIZ RIGIDEZ***
615 CLS:PRINT "Calculando matriz rigidez";
620 FOR I=1 TO NB
630 AREA=AREA(I)
640 INERCIA=INERCIA(I)
650 Ea=AREA*MD/LONG(I)
660 E1=12*MD*INERCIA/LONG(I)^3
670 E2=6*MD*INERCIA/LONG(I)^2
680 E3=2*MD*INERCIA/LONG(I)
690 E4=2*E3
700 D=3*D(I):F=3*F(I)
710 T1=1:T2=1:T3=1:T4=1:T5=1:T6=1:T7=1:T8=1:T9=1:T10=1
720 IF DESC(I)=I THEN 730 ELSE 760
730 IF TP(I)=1 THEN T1=.25:T2=0:T3=.25:T4=.5:T5=0:T6=0:T7=0:T8=.25:T9=.5:T10=.75
740 IF TP(I)=2 THEN T1=.25:T2=.5:T3=.25:T4=0:T5=.75:T6=.5:T7=0:T8=.25:T9=0:T10=0
750 IF TP(I)=3 THEN T1=0:T2=0:T3=0:T4=0:T5=0:T6=0:T7=0:T8=0:T9=0:T10=0
760 K(D-2,F-2)=-Ea*C(I)^2-E1*T3*S(I)^2
770 K(D-2,F-1)=-(Ea-T3*E1)*S(I)*C(I)
780 K(D-2,F)=-E2*T4*S(I)
790 K(D-1,F-2)=-(Ea-T3*E1)*S(I)*C(I)
800 K(D-1,F-1)=-(Ea*S(I)*S(I)+E1*T3*C(I)*C(I))
810 K(D-1,F)=E2*T4*C(I)
820 K(D,F-2)=E2*T6*S(I)
830 K(D,F-1)=-E2*T6*C(I)
840 K(D,F)=E3*T7
850 K(F-2,D-2)=K(D-2,F-2)
860 K(F-1,D-2)=K(D-2,F-1)
870 K(F,D-2)=K(D-2,F)
880 K(F-2,D-1)=K(D-1,F-2)
890 K(F-1,D-1)=K(D-1,F-1)
900 K(F,D-1)=K(D-1,F)
910 K(F-2,D)=K(D,F-2)
920 K(F-1,D)=K(D,F-1)
930 K(F,D)=K(D,F)
940 K(D-2,D-2)=K(D-2,D-2)+Ea*C(I)*C(I)+E1*T1*S(I)*S(I)
950 K(D-2,D-1)=K(D-2,D-1)+(Ea-T1*E1)*S(I)*C(I)
960 K(D-2,D)=K(D-2,D)-E2*T2*S(I)
970 K(D-1,D-2)=K(D-2,D-1)
980 K(D-1,D-1)=K(D-1,D-1)+Ea*S(I)*S(I)+E1*T1*C(I)*C(I)
990 K(D-1,D)=K(D-1,D)+E2*T2*C(I)
1000 K(D,D-2)=K(D-2,D)
1010 K(D,D-1)=K(D-1,D)
1020 K(D,D)=K(D,D)+E4*T5
1030 K(F-2,F-2)=K(F-2,F-2)+Ea*C(I)*C(I)+E1*T8*S(I)*S(I)
1040 K(F-2,F-1)=K(F-2,F-1)+(Ea-T8*E1)*S(I)*C(I)
1050 K(F-2,F)=K(F-2,F)+E2*S(I)*T9
1060 K(F-1,F-2)=K(F-2,F-1)
1070 K(F-1,F-1)=K(F-1,F-1)+Ea*S(I)*S(I)+E1*T8*C(I)*C(I)
1080 K(F-1,F)=K(F-1,F)-E2*C(I)*T9
1090 K(F,F-2)=K(F-2,F)
1100 K(F,F-1)=K(F-1,F)
1110 K(F,F)=K(F,F)+E4*T10
1120 NEXT I
1122 CLS
1125 INPUT "Ver matriz";A$
1127 IF A$="S" OR A$="s" THEN 1131 ELSE 1140
1131 FOR I=1 TO 3*NN:FOR J=1 TO 3*NN
1132 IF (I<J) OR (I=J) THEN PRINT "K(";I;",";J;")=";K(I,J)
1133 NEXT J
1134 NEXT I
1140 PRINT:INPUT "Numero de nudos con apoyos";NAY
1150 FOR I=1 TO NAY
1160 INPUT "Nudo";ND
1170 PRINT "Tipo de apoyo: "
1180 INPUT "Empotram(1),Carro(2),Rotula(3), Deslizad(4), Elastico(5)";TIPO
1190 IF TIPO<>1 AND TIPO<>2 AND TIPO<>3 AND TIPO<>4 AND TIPO<>5 THEN 1180
1220 IF TIPO=1 THEN 1230 ELSE 1260
1230 FOR J=3*ND-2 TO 3*ND
1240 K(J,J)=0
1250 NEXT J
1255 GOTO 1304
1260 IF TIPO=2 THEN 1270 ELSE 1290
1270 INPUT "Desplazamiento impedido (X/Y)";DES$
1280 IF DES$="X" OR DES$="x" THEN K(3*ND-2,3*ND-2)=0:GOTO 1285
1281 IF DES$="Y" OR DES$="y" THEN K(3*ND-1,3*ND-1)=0 ELSE 1270
1285 GOTO 1304
1290 IF TIPO=5 THEN 1291 ELSE 1295
1291 INPUT "Despl coacc (X,Y,Ambos)";COAC$
1292 IF COAC$="X" OR COAC$="x" THEN INPUT "K(x)";M1:K(3*ND-2,3*ND-2)=K(3*ND-2,3*ND-2)+M1: GOTO 1304
1293 IF COAC$="Y" OR COAC$="y" THEN INPUT "K(y)";M2:K(3*ND-1,3*ND-1)=K(3*ND-1,3*ND-1)+M2: GOTO 1304
1294 IF COAC$="A" OR COAC$="a" THEN INPUT "K(x)";M1:K(3*ND-2,3*ND-2)=K(3*ND-2,3*ND-2)+M1:INPUT "K(y)";M2:K(3*ND-1,3*ND-1)=K(3*ND-1,3*ND-1)+M2:GOTO 1304
1295 IF TIPO=3 THEN 1296 ELSE 1299
1296 FOR J=3*ND-2 TO 3*ND-1
1297 K(J,J)=0
1298 NEXT J
1299 IF TIPO=4 THEN 1300 ELSE 1304
1300 K(3*ND,3*ND)=0
1301 INPUT "Desplazamiento impedido X/Y ";DES$
1302 IF DES$="X" OR DES$="x" THEN K(3*ND-2,3*ND-2)=0:GOTO 1304
1303 IF DES$="Y" OR DES$="y" THEN K(3*ND-1,3*ND-1)=0 ELSE 1301
1304 NEXT I
1305 FOR I=1 TO 3*NN:FOR J=1 TO 3*NN
1306 KL(I,J)=K(I,J)
1307 NEXT J:NEXT I
1310 REM **Entrada de cargas**
1311 CLS:PRINT "CARGAS EN BARRAS (Ejes locales)"
1312 PRINT "Pulsar 0 para terminar"
1313 INPUT "BARRA Ní ";I:IF I=0 THEN 1410
1314 INPUT "PUNTUAL O CONTINUA (P/C) ";C$
1315 IF C$="P" OR C$="p" THEN 1335
1316 PRINT "CARGA UNIFORME EN TODA LA BARRA"
1317 INPUT "CARGA Fx (Tn) ";FX:INPUT "CARGA Fy (Tn) ";F:FX=FX*LN(I)/2
1318 IF DESC(I)=I THEN 1320
1319 MI=F*LONG(I)*LONG(I)/12:MJ=MI:FI=F*LONG(I)/2:FJ=FI:GOTO 1327
1320 IF TP(I)=1 THEN 1321 ELSE 1323
1321 FI=3*F*LONG(I)/8:FJ=5*F*LONG(I)/8
1322 MI=0:MJ=F*LONG(I)*LONG(I)/8:GOTO 1327
1323 IF TP(I)=2 THEN 1324 ELSE 1326
1324 FI=5*F*LONG(I)/8:FJ=3*F*LONG(I)/8
1325 MI=F*LONG(I)*LONG(I)/8:MJ=0:GOTO 1327
1326 FI=F*LONG(I)/2:FJ=FI:MI=0:MJ=0
1327 CARGAS(3*D(I)-2)=CARGAS(3*D(I)-2)-S(I)*FI+C(I)*FX:CARG(3*D(I)-2,I)=CARG(3*D(I)-2,I)-S(I)*FI+C(I)*FX
1328 CARGAS(3*D(I)-1)=CARGAS(3*D(I)-1)+C(I)*FI+S(I)*FX:CARG(3*D(I)-1,I)=CARG(3*D(I)-1,I)+C(I)*F(I)+S(I)*FX
1329 CARGAS(3*D(I))=CARGAS(3*D(I))+MI:CARG(3*D(I),I)=CARG(3*D(I),I)+MI
1330 CARGAS(3*F(I)-2)=CARGAS(3*F(I)-2)-S(I)*FJ+C(I)*FX:CARG(3*F(I)-2,I)=CARG(3*F(I)-2,I)-S(I)*FJ+C(I)*FX
1331 CARGAS(3*F(I)-1)=CARGAS(3*F(I)-1)+C(I)*FJ+S(I)*FX:CARG(3*F(I)-1,I)=CARG(3*F(I)-1,I)+C(I)*FJ+S(I)*FX
1332 CARGAS(3*F(I))=CARGAS(3*F(I))-MJ:CARG(3*F(I),I)=CARG(3*F(I),I)-MJ
1333 CA(I)=1
1334 GOTO 1313
1335 PRINT "CARGA PUNTUAL EN LA BARRA"
1336 INPUT "CARGA Fy (Tn) ";F
1337 INPUT "DISTANCIA AL NUDO DORSAL(m) ";DIST
1338 IF DESC(I)=I THEN 1343
1339 F1=F*(LONG(I)-DIST)^2*(LONG(I)+2*DIST)/(LONG(I))^3
1340 F2=F*DIST*DIST*(3*LONG(I)-2*DIST)/(LONG(I))^3
1341 M1=F*DIST*(LONG(I)-DIST)^2/LONG(I)^2
1342 M2=F*DIST*DIST*(LONG(I)-DIST)/(LONG(I))^2:GOTO 1393
1343 IF TP(I)=1 THEN 1344 ELSE 1347
1344 F1=F*(LONG(I)-DIST)^2*(2*LONG(I)+DIST)/2/LONG(I)^3
1345 F2=F*DIST*(3*LONG(I)^2-DIST*DIST)/2/LONG(I)^3
1346 M1=0:M2=F*DIST*(LONG(I)^2-DIST^2)/2/LONG(I)^2:GOTO 1393
1347 IF TP(I)=2 THEN 1348 ELSE 1351
1348 F1=F*(2*LONG(I)^3-3*DIST^2*LONG(I)+DIST^3)/2/LONG(I)^3
1349 F2=F*DIST^2*(3*LONG(I)-DIST)/2/LONG(I)^3
1350 M1=F*(LONG(I)-DIST)*(2*DIST*LONG(I)-DIST^2)/2/LONG(I)^2:M2=0:GOTO 1393
1351 F1=F*(LONG(I)-DIST)/LONG(I)
1352 F2=F*DIST/LONG(I):M1=0:M2=0
1393 CARGAS(3*D(I)-2)=CARGAS(3*D(I)-2)-S(I)*F1:CARG(3*D(I)-2,I)=CARG(3*D(I)-2,I)-S(I)*F1
1394 CARGAS(3*D(I)-1)=CARGAS(3*D(I)-1)+C(I)*F1:CARG(3*D(I)-1,I)=CARG(3*D(I)-1,I)+C(I)*F1
1395 CARGAS(3*D(I))=CARGAS(3*D(I))+M1:CARG(3*D(I),I)=CARG(3*D(I),I)+M1
1396 CARGAS(3*F(I)-2)=CARGAS(3*F(I)-2)-S(I)*F2:CARG(3*F(I)-2,I)=CARG(3*F(I)-2,I)-S(I)*F2
1397 CARGAS(3*F(I)-1)=CARGAS(3*F(I)-1)+C(I)*F2:CARG(3*F(I)-1,I)=CARG(3*F(I)-1,I)+C(I)*F2
1398 CARGAS(3*F(I))=CARGAS(3*F(I))-M2:CARG(3*F(I),I)=CARG(3*F(I),I)-M2
1399 CA(I)=1
1400 GOTO 1313
1410 CLS:INPUT"Cargas en los nudos(S/N)";G$
1411 IF G$="N" OR G$="n" THEN 1520
1420 INPUT "N nudos cargados";NCAR
1430 FOR I=1 TO NCAR
1506 INPUT "Nudo";NUDO
1507 INPUT "Fx";FX:CARGAS(3*NUDO-2)=CARGAS(3*NUDO-2)+FX
1508 INPUT "Fy";FY:CARGAS(3*NUDO-1)=CARGAS(3*NUDO-1)+FY
1509 INPUT "Momento";MOM:CARGAS(3*NUDO)=CARGAS(3*NUDO)+MOM
1510 NEXT I
1520 CLS
1521 FOR I=1 TO 3*NN:FOR J=1 TO 3*NN
1522 K(I,J)=KL(I,J)
1523 NEXT J:NEXT I
1525 PRINT"Resolviendo sistema";
1530 FOR I=1 TO 3*NN-1
1540 IF K(I,I)=0 THEN 1620
1550 FOR J=I+1 TO 3*NN
1560 PV=K(I,J)/K(I,I)
1570 IF K(J,J)=0 THEN SNL=1 ELSE SNL=0
1580 FOR K=J TO 3*NN
1590 K(J,K)=K(J,K)-K(I,K)*PV
1600 NEXT K
1605 CARGAS(J)=CARGAS(J)-CARGAS(I)*PV
1610 IF SNL=1 THEN K(J,J)=0
1615 NEXT J
1620 NEXT I
1630 '***Sust ascendente
1640 FOR I=3*NN TO 1 STEP -1
1650 REACC(I)=CARGAS(I)
1660 D=0
1670 FOR J=I+1 TO 3*NN
1680 D=D+K(I,J)*CARGAS(J)
1690 NEXT J
1700 CARGAS(I)=CARGAS(I)-D
1710 IF K(I,I)<>0 THEN CARGAS(I)=CARGAS(I)/K(I,I):REACC(I)=0 ELSE REACC(I)=-CARGAS(I):CARGAS(I)=0
1715 NEXT I
1720 CLS
1730 PRINT "Salida de desplazamientos       y reacciones   (S/N)";:INPUT S$:IF S$="S" OR S$="s" THEN 1740 ELSE 1920
1740 K=0
1750 FOR I=1 TO 3*NN
1751 K=I MOD 3
1770 H=I/3
1775 IF INT(H)<>H THEN H=INT(H)+1
1780 CLS:PRINT "Nudo";H;:LOCATE 0,1
1790 IF K=1 THEN PRINT "dx(m)= ";
1795 IF K=2 THEN PRINT "dy(m)= ";
1800 IF K=0 THEN PRINT "Giro(rad)= ";
1810 PRINT CARGAS(I)
1815 GOSUB 3000
1820 NEXT I
1830 CLS:PRINT"Reacciones":GOSUB 3000
1835 K=0
1840 FOR I=1 TO 3*NN
1850 K=I MOD 3
1855 IF K(I,I)<>0 THEN 1910
1860 H=I/3
1865 IF INT(H)<>H THEN H=INT(H)+1
1870 CLS:PRINT "Nudo";H;:LOCATE 0,1
1875 IF K=1 THEN PRINT "Rx(Tn)= ";
1880 IF K=2 THEN PRINT "Ry(Tn)= ";
1890 IF K=0 THEN PRINT "Momento(mTn)= ";
1900 PRINT REACC(I)
1905 GOSUB 3000
1910 NEXT I
1920 PRINT "CALCULANDO ESFUERZOS"
1927 FOR I=1 TO NB
1931 EA=AREA(I)*MD/LONG(I)
1932 E1=12*MD*INERCIA(I)/LONG(I)^3
1933 E2=6*MD*INERCIA(I)/LONG(I)^2
1934 E3=2*MD*INERCIA(I)/LONG(I)
1940 DXD=CARGAS(D(I)*3-2)
1950 DYD=CARGAS(D(I)*3-1)
1960 GIROD=CARGAS(3*D(I))
1970 DXF=CARGAS(F(I)*3-2)
1980 DYF=CARGAS(F(I)*3-1)
1990 GIROF=CARGAS(F(I)*3)
1991 IF DESC(I)=I THEN 2100
2000 A11=C(I)*C(I)*EA+S(I)*S(I)*E1
2010 A12=C(I)*S(I)*EA-S(I)*C(I)*E1
2020 A13=S(I)*E2
2030 A22=S(I)*S(I)*EA+C(I)*C(I)*E1
2040 A23=C(I)*E2
2050 A33=2*E3
2060 ND=A11*DXD+A12*DYD-A13*GIROD-A11*DXF-A12*DYF-A13*GIROF:NF=-ND
2070 QD=A12*DXD+A22*DYD+A23*GIROD-A12*DXF-A22*DYF+A23*GIROF:QF=-QD
2080 MDO=-A13*DXD+A23*DYD+A33*GIROD+A13*DXF-A23*DYF+E3*GIROF
2090 MF=-A13*DXD+A23*DYD+E3*GIROD+A13*DXF-A23*DYF+A33*GIROF
2095 GOTO 2630
2100 IF TP(I)=3 THEN 2260
2110 B11=C(I)*C(I)*EA+S(I)*S(I)*E1/4
2120 B12=S(I)*C(I)*EA-S(I)*C(I)*E1/4
2130 B13=-S(I)*E2/2
2140 B22=S(I)*S(I)*EA+C(I)*C(I)*E1/4
2150 B23=C(I)*E2/2
2160 B33=E3*1.5
2170 IF TP(I)=2 THEN 2180 ELSE 2220
2180 ND=B11*DXD+B12*DYD+B13*GIROD-B11*DXF-B12*DYF:NF=-ND
2190 QD=B12*DXD+B22*DYD+B23*GIROD-B12*DXF-B22*DYF:QF=-QD
2200 MDO=B13*DXD+B23*DYD+B33*GIROD-B13*DXF-B23*DYF
2210 MF=0:GOTO 2630
2220 ND=B11*DXD+B12*DYD-B11*DXF-B12*DYF+B13*GIROF:NF=-ND
2230 QD=B12*DXD+B22*DYD-B12*DXF-B22*DYF+B23*GIROF:QF=-QD
2240 MDO=0
2250 MF=B13*DXD+B23*DYD-B13*DXF-B23*DYF+B33*GIROF:GOTO 2630
2260 C11=C(I)*C(I)*EA
2270 C12=C(I)*S(I)*EA
2280 C22=S(I)*S(I)*EA
2290 ND=C11*DXD+C12*DYD-C11*DXF-C12*DYF:NF=-ND
2300 QD=C12*DXD+C22*DYD-C12*DXF-C22*DYF:QF=-QD
2310 MDO=0:MF=0
2630 IF CA(I)<>1 THEN 2700
2640 ND=ND-CARG(3*D(I)-2,I)
2650 QD=QD-CARG(3*D(I)-1,I)
2660 MDO=MDO-CARG(3*D(I),I)
2670 NF=NF-CARG(3*F(I)-2,I)
2680 QF=QF-CARG(3*F(I)-1,I)
2690 MF=MF-CARG(3*F(I),I)
2700 ND1=C(I)*ND+S(I)*QD
2710 QD1=-S(I)*ND+C(I)*QD
2720 MD1=MDO
2730 NF1=C(I)*NF+S(I)*QF
2740 QF1=-S(I)*NF+C(I)*QF
2750 MF1=MF
2760 CLS
2780 PRINT "N(Tn)= ";:PRINT -ND1;:PRINT "    BARRA ";I
2790 PRINT "Q(Tn)= ";:PRINT -QD1;:PRINT "    NUDO  ";D(I)
2800 PRINT "M(mTn)= ";:PRINT -MD1:GOSUB 3000
2820 PRINT "N(Tn)= ";:PRINT NF1;:PRINT "    BARRA ";I
2830 PRINT "Q(Tn)= ";:PRINT QF1;:PRINT "    NUDO  ";F(I)
2840 PRINT "M(mTn)= ";:PRINT MF1:GOSUB 3000
2860 NEXT I
2870 INPUT "OTRA HIPOTESIS DE CARGA(S/N) ";H$
2880 IF H$="N" OR H$="n" THEN END
2890 ERASE CARGAS,CARG,CA:DIM CARGAS(3*NN),CARG(3*NN,NB),CA(NB)
2900 GOTO 1310
3000 IF INKEY$="" THEN 3000 ELSE RETURN
