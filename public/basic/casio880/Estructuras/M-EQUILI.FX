5 'MATRIZ EQUILIRIO Y TABLA
10 MODE4:CLS
15 PRINT "METODO DE MATRIZ EQUILIBRIO":INPUT "EMPEZAR DE NUEVO (S/N)";SN$:IF SN$<>"S" THEN INPUT "AXILES O TABLA (A/T)";AT$:IF AT$="T" GOTO 9000 ELSE C$="S":GOTO 1470
17 STOP:CLEAR:CLS:LOCATE 0,1
20 INPUT "N. nudos";N,"N. barras";B,"N. ESTADOS";EE
30 DIM X(N),Y(N),V(B),S(B),T(B),R(B),N(EE,B),KC(B)
40 H=2*N-B
45 DIM E(2*N,B),Q(B,B),W(H,B),G(B),K(B),F(2*N),C(2*N),O(B,B),L(B)
50 'COORDENADAS
55 CLS:PRINT"COORDENADAS NUDOS"
60 FOR I=1 TO N
65 CLS
70 PRINT "Nudo";I;
80 LOCATE 0,1:INPUT "X";X(I)
90 INPUT "Y";Y(I)
100 NEXT I
102 PRINT "COMPROBACION"
103 FOR I=1 TO N:CLS:PRINT "NUDO ";I;:LOCATE 0,1:PRINT "COORD.X";X(I);:INPUT X(I):PRINT "COOR.Y";Y(I);:INPUT Y(I):NEXT I
119 'NUMERACION DE BARRAS
120 FOR I=1 TO B
125 CLS
130 PRINT "BARRA";I;
140 LOCATE 0,1:PRINT "Punto dorsal";:INPUT V(I)
160 PRINT "Punto frontal";:INPUT S(I)
185 NEXT I
187 CLS
190 'INTRODUCCION COACCIONES
191 INPUT "N. nudos con coacciones";H:CLS
192 FOR I=1 TO H
193 INPUT "Punto";P
194 P1=2*P-1
195 P2=P1+1
196 INPUT "Coaccion en X S/N";A$:IF A$="S" OR A$="s" THEN C(P1)=1
197 INPUT "Coaccion en Y S/N";A$:IF A$="S" OR A$="s" THEN C(P2)=1:CLS
200 NEXT I
205 'CALCULOS
210 FOR I=1 TO B
220 M=S(I)
230 N1=V(I)
240 R=SQR((X(M)-X(N1))^2+(Y(M)-Y(N1))^2)
245 L(I)=R
250 T(I)=(X(M)-X(N1))/R
260 R(I)=(Y(M)-Y(N1))/R
265 NEXT I
270 'FORMAC.MATRIZ EQUILIBRIO
280 FOR I=1 TO B
290 M=2*V(I)-1
300 N1=M+1
310 E(M,I)=-T(I)
320 E(N1,I)=-R(I)
330 M=2*S(I)-1
340 N1=M+1
350 E(M,I)=T(I)
360 E(N1,I)=R(I)
370 NEXT I
380 'ORDEN.MATRIZ EQUILIBRIO
385 P1=0:P2=0
390 FOR I=1 TO 2*N
410 IF C(I)=0 THEN GOSUB 1500 ELSE GOSUB 2000
420 NEXT I
450 ERASE E,X,Y,T,R:DIM E(B,B),X(B),Y(B)
500 BEEP:INPUT "Ver matriz equilibrio S/N";P$
510 IF P$="S" OR P$="s" THEN FOR I=1 TO B:FOR J=1 TO B:PRINT "E(";I;J;")=";Q(I,J):NEXT J:NEXT I ELSE IF P$="N" OR P$="n" THEN GOTO 1000
1000 'CAL.SISTEMA POR PVT MAX
1010 DIM B(B),P(B)
1030 FOR I=1 TO B
1040 P(I)=I
1050 NEXT I
1060 FOR K=1 TO B-1
1070 M=0:KO=B
1080 FOR I=K TO B
1085 S=0
1110 FOR J=K TO B
1120 S=S+ABS Q(P(I),J)
1130 NEXT J
1140 H=ABS (Q(P(I),K)/S)
1150 IF H>=M THEN M=H:KO=I
1160 NEXT I
1166 IF KO=K THEN 1170
1168 IF P(K)<>KO THEN M=P(KO):P(KO)=P(K):P(K)=M
1170 FOR I=K+1 TO B
1172 Q(P(I),K)=Q(P(I),K)/Q(P(K),K)
1174 FOR J=K+1 TO B
1176 Q(P(I),J)=Q(P(I),J)-Q(P(K),J)*Q(P(I),K)
1177 NEXT J
1178 NEXT I
1179 NEXT K
1181 IF DAM=1 THEN 1190
1182 FOR I=1 TO B
1183 FOR J=1 TO B
1184 E(I,J)=Q(I,J):X(I)=P(I)
1185 NEXT J
1186 NEXT I
1187 GOTO 1200
1190 FOR I=1 TO B
1191 FOR J=1 TO B
1192 O(I,J)=Q(I,J):Y(I)=P(I)
1193 NEXT J
1194 NEXT I
1200 IF W$<>"S" THEN GOSUB 6000
1201 'SUSTITUC.INVERSA
1202 FOR K=1 TO B
1204 S=0
1206 FOR J=1 TO K-1
1208 S=S+Q(P(K),J)*B(J)
1210 NEXT J
1212 B(K)=G(P(K))-S
1214 NEXT K
1216 FOR K=B TO 1 STEP -1
1218 S=0
1220 FOR J=B TO K+1 STEP -1
1222 S=S+Q(P(K),J)*G(J)
1224 NEXT J
1226 G(K)=(B(K)-S)/Q(P(K),K)
1228 NEXT K
1261 IF W$="S" THEN 5300
1263 CLS
1265 'SALIDA AXILES
1268 BEEP0:BEEP1
1270 FOR I=1 TO B
1274 PRINT "BARRA";V(I);S(I);
1275 LOCATE 0,1
1280 PRINT "AXIL=";ROUND(G(I),-5);:N(EST,I)=G(I):IF G(I)<0 THEN PRINT "[Comp]" ELSE PRINT "[Trac]"
1290 CLS
1300 NEXT I
1310 INPUT "Reacciones S/N";Y$
1320 IF Y$="S" OR Y$="s" THEN GOSUB 6350
1420 'INPUT "Alargamientos S/N";Z$
1430 IF Z$="S" OR Z$="s" THEN GOSUB 5000
1440 'INPUT "Desplazamientos S/N";W$:IF W$=" " THEN 1440
1450 IF W$="S" OR W$="s" THEN GOSUB 5060:W$=" "
1460 INPUT "Otro sistema cargas S/N";C$
1470 IF C$="S" OR C$="s" THEN ERASE F,B,G:DIM F(2*N),B(B),G(B):CONT=0:GOTO 1200
1490 END
1500 P1=P1+1
1510 FOR J=1 TO B
1520 Q(P1,J)=E(I,J):O(P1,J)=E(I,J)
1535 NEXT J
1540 RETURN
2000 P2=P2+1
2010 FOR J=1 TO B
2020 W(P2,J)=E(I,J)
2040 NEXT J
2045 K(P2)=F(I)
2050 RETURN
4000 S=S+1
4010 IF I MOD 2=0 THEN 4060
4020 Q=INT(I/2+1)
4030 PRINT "NUDO";Q;"Rx=";ROUND(K(S),-4)
4040 RETURN
4060 Q=I/2
4070 PRINT "NUDO";Q;"Ry=";ROUND(K(S),-4)
4090 RETURN
5000 'CAL.ALARGAMIENTOS
5010 IF CONT=0 THEN GOSUB 7000
5030 FOR I=1 TO B
5045 PRINT "Alargamiento";V(I);S(I);"=";ROUND(G(I),-5);"cm":G(I)=100*G(I)
5050 NEXT I
5055 'RETURN
5060 'CALC DESPLAZAMIENTOS
5065 IF CONT=0 THEN GOSUB 7000
5066 IF DAM=1 THEN 5110
5070 FOR I=1 TO B
5080 FOR J=1 TO B
5090 Q(I,J)=O(J,I)
5100 NEXT J:NEXT I
5101 GOSUB 8000
5102 DAM=1
5105 W$="S":GOTO 1000
5110 FOR I=1 TO B
5112 FOR J=1 TO B
5114 Q(I,J)=O(I,J):P(I)=Y(I)
5116 NEXT J:NEXT I
5120 GOSUB 8000
5130 GOTO 1200
5300 'SALIDA DESPLAZAMIENTOS
5301 W$="N":BEEP0:BEEP1
5305 P=0:S=0
5310 FOR I=1 TO 2*N
5320 IF C(I)=0 THEN GOSUB 5500
5325 IF C(I)=0 THEN 5340
5330 IF I MOD 2<>0 THEN P=P+1
5340 NEXT I
5400 GOTO 9100
5500 IF I MOD 2<>0 THEN P=P+1
5505 S=S+1
5510 PRINT "Punto";P;
5520 IF I MOD 2<>0 THEN PRINT "dx=";ELSE PRINT "dy=";
5530 PRINT ROUND(G(S)*1E-2,-5);"cm"
5535 RETURN
6000 'INTRODUCCION CARGAS EXTERNAS
6001 W$="N":BEEP:INPUT "ESTADO";EST,"N. puntos cargados";L:CLS
6002 FOR I=1 TO L
6003 INPUT "Punto aplicacion";P
6004 INPUT "Fx(T)";F(2*P-1)
6005 INPUT "Fy(T)";F(2*P)
6006 CLS
6007 NEXT I
6008 P1=0:P2=0
6009 FOR I=1 TO 2*N
6010 IF C(I)=0 THEN P1=P1+1:G(P1)=F(I) ELSE P2=P2+1:K(P2)=F(I)
6011 NEXT I
6012 FOR I=1 TO B:FOR J=1 TO B
6013 Q(I,J)=E(I,J):P(I)=X(I)
6014 NEXT J:NEXT I
6020 RETURN
6350 'CALCULO REACCIONES
6355 CLS:PRINT "Reacciones";:LOCATE 0,1
6360 FOR I=1 TO 2*N-B
6365 F=0
6370 FOR K=1 TO B
6375 F=F+W(I,K)*G(K)
6380 NEXT K
6385 K(I)=F
6390 NEXT I
6398 'SALIDA REACCIONES
6399 S=0
6400 FOR I=1 TO 2*N
6405 IF C(I)=1 THEN GOSUB 4000
6410 NEXT I
6420 RETURN
7000 'INPUT "E(T/cm2)";E
7010 'INPUT "Area (cm2)";A
7020 FOR I=1 TO B
7030 G(I)=G(I)*KC(I)*1000
7040 NEXT I
7045 CONT=1
7050 RETURN
8000 BEEP
8005 INPUT "Corregir alargamientos S/N";A$
8010 IF A$="N" OR A$="n" THEN RETURN
8020 CLS
8030 INPUT "Cual";S
8050 PRINT "e(";S;")cm";:INPUT G(S):G(S)=100*G(S)
8060 GOTO 8005
9000 'MULTIPLICACION DE ESTADOS
9010 INPUT "INPUT CONSTANTES (S/N)";SN$:IF SN$="S" GOTO 9015 ELSE GOTO 9100
9015 PRINT "SI ES INDEFORMABLE PONER A=1000"
9020 FOR I=1 TO B
9050 PRINT "A(";V(I);",";S(I);")cm ";:INPUT AA:IF AA=1000 THEN KC(I)=0:GOTO 9090
9060 PRINT "E(";V(I);",";S(I);")kg.cm ";:INPUT EE
9065 INPUT "CURVA (S/N)";SN$:IF SN$="S" THEN MODE5:INPUT "I(cm)";II:INPUT "R(cm)";RR:GOTO 9300
9070 KC(I)=100*L(I)/(AA*EE)
9075 PRINT "1/K(";V(I);",";S(I);")=";KC(I)
9090 NEXT I
9100 INPUT "Tabla-Alargamientos (T/A)";TA$:IF TA$="A" THEN GOTO 9500 ELSE INPUT "QUE ESTADOS QUIERES MULTIPLICAR ";E1,E2
9103 INPUT "pasos intermedios S/N";SN$
9105 MM=0
9110 FOR I=1 TO B
9140 IF SN$="S" THEN CLS: PRINT V(I);"-";S(I);
9150 M=1E6*(N(E1,I)*N(E2,I))*KC(I):MM=MM+M
9160 IF SN$="S" THEN LOCATE 0,1:PRINT "M=";ROUND(M,-5)
9175 NEXT I
9180 BEEP:PRINT "SUMA";E1;"-";E2;"=";MM;"cm.kg"
9190 GOTO 9100
9300 ANG=ASN(100*L(I)/(2*RR)):PRINT "ANG=";ANG:KC(I)=(RR^3)*(ANG-(3/2)*SIN(2*ANG)+2*ANG*((COSANG)^2))/(EE*II)+RR*(ANG+(1/2)*SINANG)/(EE*AA):GOTO 9075
9500 INPUT "ESTADO";E0:CONT=0:W$="S"
9505 FOR I=1 TO B:G(I)=N(E0,I):FOR J=1 TO B:Q(I,J)=0:NEXT J:NEXT I
9510 GOTO 5000
9520 GOTO 9100
