10 'CROSS
20 REM CLS:CLEAR:PRINT"CROSS"
25 PRINT "!!ATENCION:NO HAY CLEAR!!       SI NUEVA ESTRUCTURA PONLO"
30 INPUT"N.nudos";N,"N.maximo barras por nudo";MAX
50 DIM B(N,MAX):DIM K(N,MAX):DIM C(N)
60 DIM M(2,N,MAX):DIM T(N,MAX)
100 PRINT"Barras"
105 PRINT"PONER BARRA,NUDO SENALADO,A NUDO ?,Y SINO PONER 0"
110 FOR I=1 TO N:FOR J=1 TO MAX
130 PRINT"Barra";I;",";:INPUT B(I,J)
135 IF B(I,J)=0 THEN J=MAX
150 NEXT J:NEXT I
155 INPUT"Nuevos coef.TRANSMISION(o valen todos 0.5) (S/N)";SN$:IF SN$="S" THEN GOSUB 900 ELSE FOR I=1 TO N:FOR J=1 TO MAX:T(I,J)=0.5:NEXT:NEXT
160 CLS:PRINT"Rigideces";:LOCATE 0,1:INPUT"Introducir K(i,j) o Coef.reparto (K/C)";KC$
165 IF KC$="C" THEN GOSUB 800:GOTO 230
170 FOR I=1 TO N:FOR J=1 TO MAX
190 IF B(I,J)=0 THEN J=MAX:GOTO 220
200 PRINT"K(";I;",";B(I,J);")=";:INPUT K(I,J)
210 C(I)=C(I)+K(I,J)/T(I,J)
220 NEXT J:NEXT I
230 PRINT"Momentos"
240 FOR I=1 TO N:FOR J=1 TO MAX
260 IF B(I,J)=0 THEN J=MAX:GOTO 290
270 PRINT"M(";I;",";B(I,J);")=";:INPUT M(0,I,J)
280 IF KC$="K" THEN K(I,J)=K(I,J)/T(I,J)/C(I):PRINT"Coef.reparto";I;",";B(I,J);"=";ROUND(K(I,J)*100,-3);CHR$(37):GOSUB 2000
285 IF SN$="N" THEN T(I,J)=0.5
290 NEXT J
300 IF KC$="K" THEN PRINT"K(";I;")=";C(I)
310 NEXT I
320 GOSUB 1000
500 FOR I=1 TO N
505 M=0:MM=0
510 FOR J=1 TO MAX
520 IF B(I,J)=0 THEN J=MAX:GOTO 560
530 FOR L=0 TO 1
540 M=M+M(L,I,J)
550 NEXT L
560 NEXT J
570 FOR J=1 TO MAX
580 IF B(I,J)=0 THEN J=MAX:GOTO 640
590 M(2,I,J)=-K(I,J)*M:PRINT"Reparto";I;",";B(I,J);"=";ROUND(M(2,I,J),-4):GOSUB 2000
600 FOR S=1 TO MAX
610 IF B(B(I,J),S)=I THEN M(1,B(I,J),S)=M(2,I,J)*T(I,J):PRINT"Transmision sobre";B(I,J);"=";ROUND(M(1,B(I,J),S),-4):S=MAX:GOSUB 2000
630 NEXT S
640 NEXT J
660 FOR J=1 TO MAX
670 MM=0:IF B(I,J)=0 THEN J=MAX:GOTO 720
680 FOR S=0 TO 2
690 MM=MM+M(S,I,J)
700 NEXT S
710 M(0,I,J)=MM:PRINT"Momento Total";I;",";B(I,J);"=";ROUND(MM,-4):GOSUB 2000
720 NEXT J
730 NEXT I
740 GOTO 500
800 PRINT"Introduccion Coef.Reparto";CHR$(37)
810 FOR I=1 TO N:FOR J=1 TO MAX
830 IF B(I,J)=0 THEN J=MAX:GOTO 850
840 PRINT"C(";I;",";B(I,J);")=";CHR$(37);"=";:INPUT K(I,J):K(I,J)=K(I,J)/100
850 NEXT J:NEXT I
860 RETURN
900 PRINT"Coef.Transmision"
910 FOR I=1 TO N:FOR J=1 TO MAX
930 IF B(I,J)=0 THEN J=MAX:GOTO 950
940 PRINT"T(";I;",";B(I,J);")=";:INPUT T(I,J)
950 NEXT J:NEXT I
960 RETURN
1000 INPUT"Repasar datos (S/N)";R$
1010 IF R$="N" THEN RETURN
1020 FOR I=1 TO N:FOR J=1 TO MAX
1035 IF B(I,J)=0 THEN J=MAX:GOTO 1070
1040 PRINT"Reparto";I;",";B(I,J);"=";ROUND(K(I,J)*100,-3);CHR$(37):GOSUB 2000
1060 PRINT"Momento";I;",";B(I,J);"=";M(0,I,J):GOSUB 2000
1070 NEXT:NEXT
1080 RETURN
2000 IF INKEY$="" THEN 2000 ELSE RETURN
