10 PRINT "MATRIZ DE TRANSFERENCIA SIN CARGAS--VIGA FLOTANTE"
15 ANGLE 1:CLEAR :ERASE
20 INPUT "E T/m2";E
30 INPUT "C T/m3";C
40 INPUT "ANCHO B m";B
50 INPUT "CANTO h m";H
60 I=1/12*B*H^3
70 PRINT "INERCIA I=1/12*B*H^3=";I
80 L=(4*E*I/(B*C))^(1/4)
90 PRINT "L ELASTICA=";L
100 INPUT "COORDENADA X/L";X
110 H1=COSX*HYPCOSX
120 H2=1/2*(COSX*HYPSINX+SINX*HYPCOSX)
130 H3=1/2*SINX*HYPSINX
140 H4=1/4*(SINX*HYPCOSX-COSX*HYPSINX)
150 DIM G(4,4)
152 FOR K=1 TO 4:G(K,K)=H1:NEXT K
154 G(1,2)=L*H2
155 G(1,3)=L*L*H3/E/I
156 G(1,4)=L*L*L*H4/E/I
157 G(2,1)=-4*H4/L
158 G(2,3)=L*H2/E/I
159 G(2,4)=L*L*H3/E/I
160 G(3,1)=-4*E*I*H3/L/L
161 G(3,2)=-4*E*I*H4/L
162 G(3,4)=G(1,2)
163 G(4,1)=-4*E*I*H2/L/L/L
164 G(4,2)=-4*E*I*H3/L/L
165 G(4,3)=-4*H4/L
170 FOR K=1 TO 4
175 FOR J=1 TO 4
180 PRINT "G(";K;",";J;")=";G(K,J)
190 NEXT J:NEXT K:END
