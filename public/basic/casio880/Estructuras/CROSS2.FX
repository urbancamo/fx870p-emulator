10 CLEAR :V=1
20 CLS:PRINT "CROSS 4.10"
30 PRINT :PRINT "N DE VANOS ["V;:DT=V:GOSUB 840:V=INT(ABS(DT)):IF V<1 THEN 10 ELSE NV=2*V+1
40 CLS:PRINT "INERCIA:":PRINT "0=RELATIVA 1=SECCION["IN;:DT=IN:GOSUB 840:IN=DT:IF IN<>0THEN IN=1
50 GOSUB 800:IF RE THEN 20
60 DIM L(V+1),IN(V+1),B(V+1),H(V+1),M(NV),ME(NV),RE(NV),K(NV),V$(V+1)
70 IC=1:V$(0)="V.izq.":V$(V+1)="V.der":FOR I=0 TO V+1:IF I>0 AND I<V+1 THEN V$(I)="VANO"+STR$(I)
80 CLS:PRINT "LONGITUD "V$(I)"["L(I)"m";:DT=L(I):GOSUB 840:L(I)=ABS(DT):IF L(I)=0 THEN IF I>0 AND I<V+1 THEN 80 ELSE PRINT :IN(I)=1:GOTO 120
90 IF RE THEN 100 ELSE IN(I)=IC:B(I)=B:H(I)=H
100 IF IN=0 THEN PRINT "INERCIA RELATIVA ["IN(I);:DT=IN(I):GOSUB 840:IN(I)=DT:IC=IN(I):GOTO 120
110 PRINT "SECCION b*h b["B(I)"cm";:DT=B(I):GOSUB 840:B(I)=DT:PRINT TAB(14)"h["H(I)"cm";:DT=H(I):GOSUB 840:H(I)=DT:IN(I)=(1E-08)B(I)*H(I)^3/12:B=B(I):H=H(I)
120 IF IN(I)<=0 THEN 80 ELSE 130
130 NEXT I:GOSUB 800:IF RE THEN 70
140 FOR I=0 TO V+1:P=I<>0:S=I=V+1:CP=1:CR=1:CM=1:L=L(I):IF L=0 THEN 220
150 VO=0:VD=-1:IF I=0 THEN VO=1:VD=0 ELSE IF I=V+1 THEN VO=1:VD=1
160 QI=0:XI=0:XJ=L:TT=4:CLS:PRINT "1-PUNTUAL ";CP-1;"3-REPARTIDA ";CR-1:PRINT "2-MOMENTO ";CM-1;" 4-O.K
170 PRINT CHR$(11)""V$(I)"                                TIPO CARGA["TT;:DT=TT:GOSUB 840:TT=DT:IF TT<1 OR TT>4 THEN 160
180 IF TT=4 THEN 220 ELSE ON TT GOSUB 500,560,620
190 ME(2*I+P)=ME(2*I+P)+MI:ME(2*I+S)=ME(2*I+S)+MD:RPM=(MI+MD)/L(I)
200 RE(2*I+P)=RE(2*I+P)+TI-RPM:RE(2*I+S)=RE(2*I+S)+TD+RPM
210 GOTO 160
220 NEXT I
230 GOSUB 830
240 K(1)=1:K(2*V)=1
250 FOR I=1 TO V-1:K1=IN(I)/L(I):K2=IN(I+1)/L(I+1):K=K1+K2:K(2*I)=K1/K:K(2*I+1)=K2/K:NEXT I
260 FOR I=0 TO NV:M(I)=ME(I):NEXT I
270 ER=0:FOR I=0 TO NV:IF ABS(M(I))>ER THEN ER=ABS(M(I))
280 NEXT I
290 FOR I=0 TO 2*V STEP 2:SM=M(I)+M(I+1):M(I)=-(SM*K(I)):M(I+1)=-(SM*K(I+1)):ME(I)=ME(I)+M(I):ME(I+1)=ME(I+1)+M(I+1):NEXT I
300 IF ER<=.0001 THEN 390
310 FOR I=0 TO 2*V STEP 2
320 IF I=0 THEN 350
330 IF I=2*V THEN 360
340 M(I-1)=M(I)/2:M(I)=TR:TR=M(I+1)/2:ME(I-1)=ME(I-1)+M(I-1):ME(I+2)=ME(I+2)+TR:GOTO 360
350 M(0)=0:TR=M(1)/2:ME(2)=ME(2)+TR
360 NEXT I
370 M(2*V-1)=M(2*V)/2:M(2*V)=TR:M(NV)=0:ME(2*V-1)=ME(2*V-1)+M(2*V-1)
380 GOTO 270
390 FOR I=1 TO V:CH=(ME(2*I-1)+ME(2*I))/L(I):RE(2*I-1)=RE(2*I-1)+CH:RE(2*I)=RE(2*I)-CH:NEXT I
400 FOR I=0 TO V+1:IF L(I)=0 THEN 450
410 CLS:PRINT "Momentos":PRINT V$(I);:IF I=0 THEN PRINT ":M.vol=";ME(O);"mt":GOSUB 810:GOTO 450
420 IF I=V+1 THEN PRINT ": M.vol=";ME(NV);"mt":GOSUB 810:GOTO 450
430 PRINT ":  M.izq=";ME(2*I-1);"mt"
440 PRINT "  M.der=";ME(2*I);"mt":GOSUB 810
450 NEXT I:GOSUB 800:IF RE THEN 400
460 FOR I=0 TO 2*V STEP 2:CLS:PRINT "REACCIONES":R=RE(I)+RE(I+1):PRINT "APOYO ";(I/2+1);": R=";R:GOSUB 810:NEXT I:GOSUB 800:IF RE THEN 460
470 CLS:PRINT "[0]REPETIR RESULTADOS":PRINT "[1]EMPEZAR PROGRAMA":PRINT "[2]ABANDONAR PROGRAMA";
480 GOSUB 810:KY=VAL(KY$)+1:IF KY<1 OR KY>4 THEN 480 ELSE ON KY GOTO 400,10,490
490 CLS:END
500 CLS:PRINT "CARGA PUNTUAL"CP"DEL"V$(I):PRINT "VALOR DE CARGA ["QI"t";:DT=QI:GOSUB 840:QI=DT:IF QI=0 THEN RETURN
510 PRINT "P. APLICACION ["XI"m";:DT=XI:GOSUB 840:XI=DT:IF XI<0 OR XI>L THEN 500
520 XJ=L-XI:GOSUB 800:IF RE THEN 500 ELSE GOSUB 790
530 MI=(QI*XI*(XJ/L)^2)*(-(VO=0))+(QI*XI)*(-(VD=1)):MD=(-QI*XJ*(XI/L)^2)*(-(VO=0))+(-QI*XJ)*(-(VD=0))
540 TI=(QI*XJ^2*(L+2*XI)/L^3)*(-(VO=0))+(QI)*(-(VD=1)):TD=(QI*XI^2*(L+2*XJ)/L^3)*(-(VO=0))+(QI)*(-(VD=0))
550 CP=CP+1:RETURN
560 CLS:PRINT "MOMENTO PUNTUAL "CM"DEL "V$(I):PRINT "VALOR MOMENTO ["QI"mt";:DT=QI:GOSUB 840:QI=DT:IF QI=0 THEN RETURN
570 PRINT "P. APLICACION ["XI"m";:DT=XI:GOSUB 840:XI=DT:IF XI<0 OR XI>L THEN 560
580 GOSUB 800:IF RE THEN 560 ELSE GOSUB 790
590 MI=(-QI*(L^2-4*XI*L+3*XI^2)/L^2)*(-(VO<>1))+QI*(VD=1):MD=(-QI*(3*XI^2-2*XI*L)/(L^2))*(-(VO<>1))+QI*(VD=0)
600 TI=(6*QI*XI*(L-XI)/L^3)*(-(VO<>1)):TD=-TI
610 CM=CM+1:RETURN
620 CLS:PRINT "CARGA REPARTIDA"CR" DEL "V$(I):PRINT "VALOR CARGA qi ["QI"t/m";:DT=QI:GOSUB 840:QI=DT:IF RE THEN 630 ELSE QJ=QI
630 PRINT "VALOR CARGA qj ["QJ"t/m";:DT=QJ:GOSUB 840:QJ=DT:IF QI=QJ AND QI=0 THEN RETURN
640 PRINT "PUNTO INICIAL ["XI"m";:DT=XI:GOSUB 840:XI=DT:IF XI<0 OR XI>L THEN RE=1:GOTO 620
650 PRINT "PUNTO FINAL ["XJ"m";:DT=XJ:GOSUB 840:XJ=DT:IF XJ>L OR XJ<=XI THEN RE=1:GOTO 620
660 GOSUB 800:IF RE THEN 620 ELSE GOSUB 790
670 QL=(QJ-QI)/(XJ-XI)*(L-XI)+QI
680 RA1=(QI/(2*L^3)*(L-XI)^3*(L+XI)+(QL-QI)/(20*L^3)*(L-XI)^3*(3*L+2*XI))*(-(VO<>1))+((QI+QL)/2*(L-XI))*(-(VD=1))
690 MA1=(QI/(12*L^2)*(L-XI)^3*(L+3*XI)+(QL-QI)/(60*L^2)*(L-XI)^3*(2*L+3*XI))*(-(VO<>1))+(QI/2*(L^2-XI^2)+(QL-QI)/6*(2*L+XI)*(L-XI))*(-(VD=1))
700 RB1=((QI+QL)/2*(L-XI)-RA1)*-(VD<>1)
710 MB1=(RA1*L-MA1-QI/2*(L-XI)^2-(QL-QI)/6*(L-XI)^2)*-(VD<>1)
720 RA2=(QJ/(2*L^3)*(L-XJ)^3*(L+XJ)+(QL-QJ)/(20*L^3)*(L-XJ)^3*(3*L+2*XJ))*(-(VO<>1))+((QJ+QL)/2*(L-XJ))*-(VD=1)
730 MA2=(QJ/(12*L^2)*(L-XJ)^3*(L+3*XJ)+(QL-QJ)/(60*L^2)*(L-XJ)^3*(2*L+3*XJ))*(-(VO<>1))+(QJ/2*(L^2-XJ^2)+(QL-QJ)/6*(2*L+XJ)*(L-XJ))*(-(VD=1))
740 RB2=((QJ+QL)/2*(L-XJ)-RA2)*-(VD<>1)
750 MB2=(RA2*L-MA2-QJ/2*(L-XJ)^2-(QL-QJ)/6*(L-XJ)^2)*-(VD<>1
760 TI=RA1-RA2:TD=RB1-RB2
770 MI=MA1-MA2:MD=MB1-MB2
780 CR=CR+1:RETURN
790 FOR BK=1 TO 30:PRINT CHR$(29);:NEXT BK:PRINT "   PROCESANDO LOS DATOS        ";:RETURN
800 PRINT "[0] REPETIR [SPC] CONTINUAR";
810 RE=0:KY$=INKEY$:IF KY$=""THEN 810 ELSE IF KY$="0" THEN RE=1
820 RETURN
830 CLS:PRINT TAB(9)"CALCULANDO...":RETURN
840 INPUT "]:",CD$:IF CD$=""THEN RETURN ELSE DT$=CD$:DT=VAL(DT$):RETURN
