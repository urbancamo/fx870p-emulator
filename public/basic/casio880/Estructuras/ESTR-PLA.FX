1 CLS
2 PRINT "    EST. ARTICULADAS PLANAS"
3 PRINT "    ***********************"
4 PRINT "NUEVOS DATOS ? "
5 INPUT "1.- SI, 2.- NO  ",I
6 IF I=2 THEN GOTO 200
9 CLEAR :CLS:ANGLE1
20 INPUT "No NUDOS = ",N:M=2*N
30 INPUT "No BARRAS = ",B
40 INPUT "No APOYOS = ",A
41 INPUT "BARRAS DE IGUAL AREA (S/N) ";C$
42 IF C$="S" THEN INPUT "EA ",EA
43 INPUT "L/EA = Cte (S/N) ";V$:IF V$="S" THEN INPUT "L/EA = ",WX
45 CLS
50 DIM X(N),Y(N),F(B),D(B),G(B),K(B),L(B),R(A)
60 FOR I=1 TO N:PRINT "NUDO ";I
70 INPUT "COORDENADA X = ",X(I)
80 INPUT "COORDENADA Y = ",Y(I):CLS:NEXT I:W=1
90 FOR I=1 TO B:PRINT "BARRA ";I;
92 IF C$="S" THEN K(I)=EA:GOTO 100
93 INPUT "EA = ",K(I)
100 INPUT "ND = ",D(I):D=D(I)
110 INPUT "NF = ",F(I):F=F(I)
122 IF F-D>W THEN W=F-D
124 X=X(F)-X(D):L(I)=X:Y=Y(F)-Y(D)
126 U=SQR(X^2+Y^2)
128 IF X<>0 THEN J=ATN(Y/X):IF X<0 THEN J=J+PI:GOTO 135
129 IF X=0 THEN J=PI/2:IF Y<0 THEN J=-J
130 IF Y=0 THEN J=0:IF X<0 THEN J=PI
135 CLS
136 PRINT "BARRA";I;".(";D;"->";F;")"
137 PRINT "L[M]= ";U,"A[0]= ";J*180/PI
138 K(I)=K(I)/U
139 IF V$="S" THEN K(I)=WX
140 G(I)=J:NEXT I
150 W=(W+1)*2
155 IF INKEY$="" THEN 155
165 FOR I=1 TO A:PRINT "APOYO ";I
170 INPUT "S/OX,No de NUDO ",R:IF R>0 THEN R(I)=2*R-1:I=I+1
180 INPUT "S/OY,No de NUDO ",R:IF R>0 THEN R(I)=2*R
185 IF R=0 THEN I=I-1
190 CLS:NEXT I
200 CLS:PRINT "FORMA MATRIZ ":DIM A(M,W),C(M),V(M)
201 FOR I=1 TO M
202 C(I)=0:V(I)=0
203 FOR K=1 TO W
204 A(I,K)=0
205 NEXT K
206 NEXT I
209 FOR I=1 TO B:D=D(I):F=F(I)
210 Z=2*D:V=(F-D+1)*2:S=SING(I):Q=COSG(I)
220 A(Z-1,1)=A(Z-1,1)+K(I)*Q*Q:A(Z-1,2)=A(Z-1,2)+K(I)*S*Q
230 A(Z,1)=A(Z,1)+K(I)*S*S:A(Z-1,V-1)=(-K(I))*Q*Q
240 A(Z-1,V)=(-K(I))*Q*S:A(Z,V-1)=(-K(I))*S*S
250 A(Z,V-2)=(-K(I))*Q*S:Z=Z+V-2:A(Z-1,1)=A(Z-1,1)+K(I)*Q*Q
260 A(Z-1,2)=A(Z-1,2)+K(I)*Q*S:A(Z,1)=A(Z,1)+K(I)*S*S
270 NEXT I
290 FOR I=1 TO A:A(R(I),1)=0:NEXT I:CLS:BEEP0
300 H=H+1:PRINT "HIPOTESIS";H
310 FOR I=1 TO N:PRINT "NUDO ";I
320 INPUT "CARGA NODAL FX = ",C(I*2-1)
330 INPUT "CARGA NODAL FY = ",C(I*2):CLS:NEXT I
331 INPUT "EXISTEN CARGAS SOBRE LAS BARRAS (S/N) ";A$:IF A$="N" THEN GOTO 349
333 INPUT "CUANTAS ?",T:FOR NB=1 TO T
335 INPUT "BARRA ? ",I:INPUT "COORD X PTO APLICACION = ";XF
337 INPUT "FX = ",FX:INPUT "FY = ",FY
338 E=(XF-X(D(I)))/L(I)
339 C(D(I)*2)=C(D(I)*2)+FY*(1-E)
341 C(D(I)*2-1)=C(D(I)*2-1)+FX*(1-E)
343 C(F(I)*2-1)=C(F(I)*2-1)+FX*E
345 C(F(I)*2)=C(F(I)*2)+FY*E
347 NEXT NB
349 INPUT "HAY CARGAS REPARTIDAS (S/N) ";B$:IF B$="N" THEN GOTO 405
351 INPUT "CUANTAS ? ",T:FOR MN=1 TO T
353 INPUT "BARRA = ",I:INPUT "P (Tn/m) ";P
355 C(D(I)*2)=C(D(I)*2)+P*L(I)/2
357 C(F(I)*2)=C(F(I)*2)+P*L(I)/2
359 NEXT MN
405 PRINT "RESOLVIENDO EL SISTEMA"
410 FOR I=1 TO M-1:IF ABSA(I,1)<10^(-7) THEN 470
420 X=M-I:IF X>W-1 THEN X=W-1
430 FOR K=1 TO X:P=A(I,K+1)/A(I,1)
435 O=1:IF A(I+K,1)=0 THEN O=0
440 FOR J=1 TO W-K:A(I+K,J)=A(I+K,J)-A(I,J+K)*P:NEXT J
450 C(I+K)=C(I+K)-C(I)*P:IF O=0 THEN A(I+K,1)=0
460 NEXT K
470 NEXT I:R=0:CLS:PRINT "REACCIONES : ":BEEP0
500 FOR I=M TO 1 STEP-1:P=A(I,1)
510 O=M-I:IF O>=W THEN O=W-1
520 V(I)=C(I):IF O=0 THEN 540
530 FOR J=1 TO O:V(I)=V(I)-A(I,J+1)*V(I+J):NEXT J
540 IF P<>0 THEN V(I)=V(I)/P:C(I)=0:GOTO 560
550 R=R+1:PRINT "R(";A-R+1;")=";
555 PRINT -V(I):V(I)=0
557 IF INKEY$="" THEN 557
560 NEXT I:IF R>A THEN PRINT "ESTRUCTURA FALTA DE APOYOS"
562 CLS
564 INPUT "QUIERES CORRIMIENTOS (S/N) ";D$
566 RT=0
568 FOR I=1 TO B:D=D(I):F=F(I)
570 IF D$="N" THEN GOTO 575
571 PRINT "CORRIMIENTO BARRA ";I,"NF";F;"EJE X = ";V(2*F-1),"EJE Y = ";V(2*F)
572 IF INKEY$="" THEN 572
573 IF D$="S" THEN PRINT "NUDO DORSAL ";D,"EJE X = ";V(2*D-1),"EJE Y = ";V(2*D)
574 IF INKEY$="" THEN 574
575 D=D*2:F=F*2:CV=((V(F)-V(D))*SIN G(I)+(V(F-1)-V(D-1))*COS G(I))
577 PRINT "BARRA ";I;"K(";I;")=";K(I),"AXIL = ";K(I)*CV,"ALARG = ";CV
580 RT=RT+CV^2*K(I)
635 IF INKEY$="" THEN 635
640 NEXT I
645 PRINT "SUMAT N^2/K = ";RT
646 IF INKEY$="" THEN 646
650 CLS:PRINT "ELIGE UNA OPCION"
660 PRINT "RESOLVER CON OTRAS FUERZAS (1)"
670 PRINT "RESOLVER OTRA ESTRUCTURA   (2)"
680 PRINT "SALIR                      (3)":INPUT I
700 IF I=1 THEN 200
710 IF I=2 THEN 9
720 IF I=3 THEN END
730 GOTO 650
