10 CLEAR:ESP=0:CLS:PRINT "ARTIESPA":ED=2100000: 'SET FO
11 NH=0
20 PRINT "DATOS GENERALES"
21 INPUT "Estructura plana o tridimensional P/T? ", RE$
22 IF RE$<> "P" AND RE$<>"T" THEN 21
23 IF RE$="P" THEN ESP=1
30 INPUT "NUM. DE NUDOS...",N:NN=3*N
40 INPUT "NUM. DE BARRAS..",B
50 INPUT "NUM. DE APOYOS..",A
59 PRINT "MODULO E(kg/cm2)";ED;:INPUT " ",E
60 'PRINT CSR18; E; CSRO;:INPUT "Modulo E (Kg/cm2)";E
61 IF E=0 THEN E=ED
62 E=E*10
70 DIM X(N),Y(N),Z(N),ND(B),NF(B),S(B),NC(A)
75 DIM PPROP(B),AX(B),GUAR(B),GUAX(B)
90 PRINT "En adelante, cuando se pare la ejecucion del programa,esta continuara pulsando la tecla C"
92 GOSUB 3000
100 PRINT "Dat. nudos"
110 FOR I=1 TO N:PRINT "NUDO";I;:INPUT "X(m)=",X(I)
120 INPUT "       Y(m)=",Y(I):IF ESP=1 THEN Z(I)=0 ELSE INPUT"       Z(m)=",Z(I)
130 NEXT I
135 INPUT "Corregir algun dato S/N ", CO$:IF CO$="N" THEN 200
140 INPUT "Num. Nudo?",I:IF I>N OR I<0 THEN 140
145 INPUT "X(m)=",X(I):INPUT "Y(m)="; Y(I)
150 IF ESP=0 THEN INPUT "Z(M)=";Z(I)
160 GOTO 135
200 PRINT "Dat. Barras":W=1
210 FOR I=1 TO B:PRINT "barra";I;
211 INPUT "N. dorsal  =", ND(I)
212 IF ND(I)>N-1 OR ND(I)<1 THEN ND(I)=0:GOTO 211
220 INPUT "        N.frontal  =",NF(I)
221 IF NF(I)>N OR NF(I)<2 THEN NF(I)=0:GOTO 220
225 PRINT "    Area (cm2)=";S(I-1)*10000;
230 INPUT " ";S(I):IF S(I)=0 THEN S(I)=S(I-1) ELSE S(I)=S(I)/10000
233 INPUT "PESO PROPIO EN Tn/m?",PPROP(I)
240 DN=NF(I)-ND(I):IF DN>W THEN W=DN
270 NEXT I
271 INPUT "Corregir datos de barras S/N?",CB$:IF CB$="N" THEN 298
273 INPUT "Num. barra?",J
274 INPUT "Nudo dorsal?",ND(J)
275 INPUT "Nudo frontal?",NF(J)
280 INPUT "Area (cm2)=";S(J)
283 INPUT "Peso Propio en tn/m?",PPROP(J)
285 IF NF(J)-ND(J)>W THEN W=NF(J)-ND(J)
286 GOTO 271
298 W=W*3+3:PRINT "Ancho de banda=";W
300 PRINT "Dat. Apoyos"
310 FOR I=1 TO A:PRINT "Coac. de apoyo No?";I;
315 INPUT "NUDO?",NA
317 IF ESP=1 THEN INPUT "     X/Y "; CA$:IF CA$>"Y" OR CA$<"X" THEN 317
320 IF ESP=0 THEN INPUT "      X/Y/Z";CA$:IF CA$>"Z" OR CA$<"X" THEN 320
330 NC(I)=NA*3+ASC(CA$)-90:PRINT "      puntero:";NC(I)
390 NEXT I
391 INPUT "Corregir datos de apoyos S/N?",COAP$:IF COAP$="N" THEN 400
392 INPUT "Num. cond. apoyo?";I
394 INPUT "Nudo?",NA
396 IF ESP=0 THEN INPUT "X/Y/Z ",CA$:IF CA$>"Z" OR CA$<"X" THEN 396
397 IF ESP=1 THEN INPUT "X/Y?",CA$:IF CA$<"X" OR CA$>"Y" THEN 397
398 NC(I)=NA*3+ASC(CA$)-90:PRINT "PUNTERO:";NC(I)
399 GOTO 391
400 PRINT " Calculando Mat. Rigidez"
405 DIM F(NN),D(NN+1),K(NN,W),G(NN)
406 FOR I=1 TO NN+1:D(I)=0:NEXT I
407 FOR I=1 TO NN:FOR J=1 TO W:K(I,J)=0:NEXT J,I
408 FOR I=1 TO NN:F(NN)=0:G(NN)=0:NEXT I
410 FOR I=1 TO B:PRINT "Barra";I:GOSUB 2000
440 I1=ND(I)*3-2:K(I1,1)=K(I1,1)+R*CX*CX
441 K(I1+1,1)=K(I1+1,1)+R*CY*CY
442 K(I1+2,1)=K(I1+2,1)+R*CZ*CZ
443 K(I1,2)=K(I1,2)+R*CX*CY
444 K(I1+1,2)=K(I1+1,2)+R*CY*CZ
445 K(I1,3)=K(I1,3)+R*CX*CZ
450 I1=NF(I)*3-2:K(I1,1)=K(I1,1)+R*CX*CX
451 K(I1+1,1)=K(I1+1,1)+R*CY*CY
452 K(I1+2,1)=K(I1+2,1)+R*CZ*CZ
453 K(I1,2)=K(I1,2)+R*CX*CY
454 K(I1+1,2)=K(I1+1,2)+R*CY*CZ
455 K(I1,3)=K(I1,3)+R*CX*CZ
459 I1=ND(I)*3-2:J=(NF(I)-ND(I))*3+1
460 K(I1,J)=-R*CX*CX
461 K(I1,J+1)=-R*CX*CY
462 K(I1,J+2)=-R*CX*CZ:I1=I1+1:J=J-1
463 K(I1,J)=-R*CY*CX
464 K(I1,J+1)=-R*CY*CY
465 K(I1,J+2)=-R*CY*CZ:I1=I1+1:J=J-1
466 K(I1,J)=-R*CZ*CX
467 K(I1,J+1)=-R*CZ*CY
468 K(I1,J+2)=-R*CZ*CZ
490 NEXT I
500 PRINT " Introduciendo apoyos"
510 FOR I=1 TO A:PRINT "coac. de apoyo No";I
520 K(NC(I),1)=0
530 NEXT I
532 REM "HIP.PESO PROPIO"
533 IF NH>0 THEN GOSUB 4000
535 PRINT "RESOLVIENDO HIPOTESIS DE PESO PROPIO"
540 FOR I=1 TO B
545 PRINT "BARRA ",I
550 GOSUB 2000
555 I1=ND(I)*3-2
560 F(I1+1)=F(I1+1)+PPROP(I)*L/2
562 I2=NF(I)*3-2
564 F(I2+1)=F(I2+1)+PPROP(I)*L/2
566 NEXT I
700 PRINT " resolviendo sistema"
705 FOR I=1 TO NN-1:IF ABS(K(I,1))<0.0000001 THEN 750
706 PRINT "Ecuacion";I
710 Z=NN-I:IF Z>W-1 THEN Z=W-1
715 FOR I1=1 TO Z:AP=K(I,I1+1)/K(I,1)
720 IF K(I+I1,1)=0 THEN PN=0 ELSE PN=1
725 FOR J=1 TO W-I1:K(I+I1,J)=K(I+I1,J)-K(I,J+I1)*AP:NEXT J
730 F(I+I1)=F(I+I1)-F(I)*AP
735 IF PN=0 THEN K(I+I1,1)=0
740 NEXT I1
750 NEXT I:NR=0
760 FOR I=NN TO 1 STEP -1
765 PN=NN-I:IF PN>=W THEN PN=W-1
770 D(I)=F(I)
775 FOR J=1 TO PN:D(I)=D(I)-K(I,J+1)*D(I+J):NEXT J
780 IF K(I,1)<> 0 THEN D(I)=D(I)/K(I,1):F(I)=0
785 IF K(I,1)=0 THEN F(I)=D(I):D(I)=0: NR=NR+1
790 NEXT I:IF NR>A THEN IF ESP=0 THEN PRINT "NO HAY SUFICIENTES APOYOS"
795 GOSUB 3000
800 PRINT " Desplazamientos de nudos (mm) debidos a esta hip."
802 GOSUB 3000
805 PRINT "Nudo      Dx     Dy    Dz"
810 FOR I=1 TO N:I1=I*3-2
820 PRINT I; D(I1)*1000;D(I1+1)*1000;D(I1+2)*1000
825 GOSUB 3000
830 NEXT I
850 PRINT " Acciones sobre los apoyos (T) debidas a esta hip."
852 GOSUB 3000
855 PRINT " NUDO     Fx     Fy    Fz"
860 FOR I=1 TO N:I1=I*3-2
862 IF K(I1,1)*K(I1+1,1)*K(I1+2,1)>0 THEN 880
865 PRINT I,:IF K(I1,1)=0 THEN PRINT F(I1); ELSE PRINT;
870 IF K(I1+1,1)=0 THEN PRINT F(I1+1); ELSE PRINT ;
875 IF K(I1+2,1)=0 THEN PRINT F(I1+2) ELSE PRINT
877 GOSUB 3000
880 NEXT I
900 PRINT "  Esfuerzos axiles (T) debidos a esta hipotesis"
902 GOSUB 3000
905 PRINT "Barra   Axil"
910 FOR I=1 TO B:GOSUB 2000:I1=ND(I)*3-2:I2=NF(I)*3-2
915 DX=D(I2)-D(I1):DY=D(I2+1)-D(I1+1):DZ=D(I2+2)-D(I1+2)
920 AX(I)=R*(DX*CX+DY*CY+DZ*CZ)
940 PRINT I;"(";ND(I);"-";NF(I);")",AX(I)
950 GOSUB 3000
970 NEXT I
980 IF NH=0 THEN FOR I=1 TO B:GUAR(I)=AX(I):NEXT I
981 IF NH=0 THEN PRINT "Hip. CARGAS PERMANENTES":IF NH=0 THEN INPUT "Coef de mayoracion?",COEF:IF NH=0 THEN NH=NH+1
982 IF NHP=0 THEN 406
985 IF NH=1 THEN PRINT "Esfuerzos axiles correspondientes a cargas permanentes+peso propio":GOSUB 3000
987 FOR I=1 TO B
989 IF NH=1 THEN GUAR(I)=GUAR(I)+AX(I)
990 IF NH=1 THEN PRINT "BARRA ";I;"(";ND(I);"-";NF(I);")",GUAR(I)
995 IF NH=1 THEN GOSUB 3000
1000 NEXT I
1010 IF NH=1 THEN PRINT "Cargas accidentales correspondientes a la hipotesis";NH
1015 IF NH=1 THEN INPUT "Coef de mayoracion de cargas?",COEF
1020 IF NH=1 THEN NH=2
1022 IF NHP=1 THEN 406
1025 PRINT "Axiles correspondientes a cargas permanentes+peso propio+hip. num.";NH-1
1030 FOR I=1 TO B:GUAX(I)=GUAR(I)+AX(I)
1040 PRINT "BARRA";I;"(";ND(I);"-":NF(I);")",GUAX(I)
1045 GOSUB 3000:NEXT I
1050 INPUT "DESEA OTRA HIPOTESIS S/N?",HOPOT$:IF HOPOT$="S" THEN NH=NH+1:GOTO 406
1100 END
1999 REM COSENOS DIRECTORES
2000 DX=X(NF(I))-X(ND(I)):DY=Y(NF(I))-Y(ND(I))
2020 DZ=Z(NF(I))-Z(ND(I)):L=SQR(DX*DX+DY*DY+DZ*DZ)
2040 CX=DX/L:CY=DY/L:CZ=DZ/L:R=E*S(I)/L
2050 RETURN
2999 REM PAUSA
3000 C$=INKEY$
3005 IF C$<>CHR$(67) THEN 3000
3010 RETURN
4000 REM CARGAS PUNTUALES
4002 NHP=NHP+1
4005 INPUT "Existen cargas puntuales en barra S/N?",RE$
4010 IF RE$="N" THEN 4109
4015 FOR I=1 TO B
4020 PRINT "Carga en la barra ";I;:INPUT "S/N?",CB$
4025 IF CB$="N" THEN 4080
4028 INPUT "Distancia al nudo dorsal medida en m. de barra?",DIST
4030 INPUT "Coseno director de la carga con eje X?",COX
4035 INPUT "Coseno director con eje Y?",COY
4040 IF ESP=0 THEN INPUT  "Coseno director con eje Z?",COZ
4045 INPUT "Modulo de la fuerza puntual en Tn?",MO
4052 GOSUB 2000
4055 I1=ND(I)*3-2
4060 F(I1)=MO*COX*(L-DIST)*COEF/L+F(I1)
4062 F(I1+1)=F(I1+1)+MO*COY*(L-DIST)*COEF/L
4064 IF ESP=0 THEN F(I1+2)=MO*COZ*(L-DIST)*COEF/L+F(I1+2)
4066 I2=NF(I)*3-2
4068 F(I2)=F(I2)+MO*COX*DIST*COEF/L
4070 F(I2+1)=F(I2+1)+MO*COY*DIST*COEF/L
4072 IF ESP=0 THEN F(I2+2)=F(I2+2)+MO*COEF*COZ*DIST/L
4074 PRINT "Mas cargas puntuales en barra";I;:INPUT "?",CONTE$
4075 IF CONTE$="S" THEN 4030
4080 NEXT I
4109 PRINT "CARGAS EN LOS NUDOS"
4110 FOR I=1 TO N:PRINT "Nudo ";I:I1=I*3-2
4115 INPUT "Fx(T)=",G(I1)
4120 INPUT "Fy(T)=",G(I1+1)
4125 IF ESP=0 THEN INPUT "Fz(T)=",G(I1+2)
4130 F(I1)=F(I1)+G(I1)*COEF
4135 F(I1+1)=F(I1+1)+G(I1+1)*COEF
4140 IF ESP=0 THEN F(I1+2)=F(I1+2)+G(I1+2)*COEF
4190 NEXT I
4200 REM CARGAS REPARTIDAS
4208 INPUT "Existen cargas repartidas en barras?";RESP$
4209 IF RESP$="N" THEN 4300
4210 PRINT "Solo se admiten cargas repartidas por metro de barra"
4215 FOR I=1 TO B
4220 PRINT "Carga repartida en la barra";I;:INPUT "S/N?",COB$
4225 IF COB$="N" THEN 4280
4230 INPUT "Coseno director con eje X?",COX
4235 INPUT "Coseno director con eje Y?",COY
4240 IF ESP=0 THEN INPUT  "Coseno director con eje Z?",COZ
4245 INPUT "Modulo de la fuerza repartida en Tn/m?",MO
4250 GOSUB 2000
4255 I1=NF(I)*3-2
4260 F(I1)=MO*COX*L^2*COEF/12+F(I1)
4262 F(I1+1)=MO*COY*L^2*COEF/12+F(I1+1)
4264 IF ESP=0 THEN F(I1+2)=F(I1+2)+MO*COEF*COZ*L^2/12
4266 I2=ND(I)*3-2
4268 F(I2)=F(I2)+MO*COEF*COX*L^2/12
4270 F(I2+1)=F(I2+1)+MO*COEF*COY*L^2/12
4272 IF ESP=0 THEN F(I2+2)=F(I2+2)+MO*COEF*COZ*L^2/12
4274 PRINT "Mas cargas repartidas en barra";I;:INPUT "?",CONTE$
4276 IF CONTE$="S" THEN 4230
4280 NEXT I
4300 RETURN 700
