1 'MATRIGID
2 CLS
5 PRINT "METODO DE MATRIZ DE RIGIDEZ";:LOCATE 0,1
10 CLEAR
20 INPUT "N. nudos";N,"N. barras";B
30 DIM X(N),Y(N),V(B),S(B),T(B),R(B),N(B),F(2*N),C(2*N),L(B),U(2*N,2*N)
40 CLS
50 'COORDENADAS
55 PRINT "COORDENADAS NUDOS"
60 FOR I=1 TO N
65 CLS
70 PRINT "Nudo";I;
80 LOCATE 0,1:INPUT "X";X(I)
90 INPUT "Y";Y(I)
100 NEXT I
118 'NUMERACION DE BARRAS
120 FOR I=1 TO B
125 CLS
130 PRINT "BARRA";I;
140 LOCATE 0,1:INPUT "Punto dorsal";V(I):IF V(I)=0 THEN 140
160 INPUT "Punto frontal";S(I):IF S(I)=0 THEN 160
185 NEXT I
186 CLS
188 'INTRODUCCION COACCIONES
191 INPUT "N. nudos con coacciones";H:CLS
192 FOR I=1 TO H
193 INPUT "Punto";P:IF P=0 THEN 193
194 P1=2*P-1
195 P2=P1+1
196 INPUT "Coaccion en X S/N";A$:IF A$="S" THEN C(P1)=1 ELSE IF A$<>"N" THEN 196
197 INPUT "Coaccion en Y S/N";A$:IF A$="S" THEN C(P2)=1 ELSE IF A$<>"N" THEN 197
198 CLS:NEXT I
199 GOSUB 9500:COAC=0
200 FOR I=1 TO 2*N:COAC=COAC+C(I):NEXT I
201 ECU=2*N-COAC
208 'CALCULOS
210 FOR I=1 TO B
220 M=S(I)
230 N1=V(I)
240 L(I)=SQR((X(M)-X(N1))^2+(Y(M)-Y(N1))^2)
250 T(I)=(X(M)-X(N1))/L(I)
260 R(I)=(Y(M)-Y(N1))/L(I)
265 NEXT I
269 F=ECU
270 ERASE X,Y
274 DIM A(F),W(COAC,F),K(COAC),Q(F,F)
610 'CALCULO MATRIZ RIGIDEZ
621 BEEP
622 CLS:INPUT "E(T/cm2)=";E,"Area (cm2)=";A:IF E=0 OR A=0 THEN 622
623 GOSUB 9500
625 FOR I=1 TO B
630 P1=2*V(I)-1
655 P2=2*S(I)-1
660 U(P1,P2)=-T(I)*T(I)*E*A/L(I):U(P2,P1)=U(P1,P2)
662 U(P1+1,P2+1)=-R(I)*R(I)*E*A/L(I):U(P2+1,P1+1)=U(P1+1,P2+1)
665 U(P1,P2+1)=-T(I)*R(I)*E*A/L(I):U(P2+1,P1)=U(P1,P2+1)
670 U(P1+1,P2)=U(P1,P2+1)
675 U(P2,P1+1)=U(P1+1,P2)
685 NEXT I
690 FOR I=1 TO 2*N:U(I,I)=0
691 IF I MOD 2=0 THEN 700
692 S1=0:U(I,I+1)=0:FOR J=1 TO (2*N-1) STEP 2
693 S1=S1-U(I,J)
694 NEXT J
695 S2=0
696 FOR J=2 TO 2*N STEP 2:S2=S2-U(I,J)
697 NEXT J
698 U(I,I)=S1:U(I,I+1)=S2:U(I+1,I)=S2
699 GOTO 704
700 S3=0:FOR J=2 TO 2*N STEP 2
701 S3=S3-U(I,J)
702 NEXT J
703 U(I,I)=S3
704 NEXT I
808 'ORD MATRIZ DE RIGIDEZ
810 P1=0:P2=0
820 FOR I=1 TO 2*N
830 IF C(I)=0 THEN GOSUB 8000 ELSE GOSUB 8500
850 NEXT I
855 BEEP
860 CLS:INPUT "Ver matriz de rigidez S/N";H$
870 IF H$="N" THEN 1000 ELSE IF H$<>"S" THEN 860
880 FOR I=1 TO F
890 FOR J=I TO F
900 PRINT "R("I;J;")=";Q(I,J)
910 NEXT J
920 NEXT I
930 ERASE U
1000 'CALCULO DEL SISTEMA
1010 GOSUB 9500
1020 DIM B(F),P(F)
1030 FOR I=1 TO F
1040 P(I)=I
1050 NEXT I
1060 FOR K=1 TO F-1
1065 M=0:KO=F
1070 FOR I=K TO F
1080 S=0
1110 FOR J=K TO F
1120 S=S+ABS(Q(P(I),J))
1130 NEXT J
1152 H=ABS(Q(P(I),K)/S)
1162 IF H>=M THEN M=H:KO=I
1164 NEXT I
1166 IF KO=K THEN 1170
1168 IF P(K)<>KO THEN M=P(KO):P(KO)=P(K):P(K)=M
1170 FOR I=K+1 TO F
1172 Q((P(I),K)=Q(P(I),K)/Q(P(K),K)
1174 FOR J=K+1 TO F
1176 Q(P(I),J)=Q(P(I),J)-Q(P(K),J)*Q(P(I),K)
1178 NEXT J
1180 NEXT I
1182 NEXT K
1200 GOSUB 6000
1202 FOR K=1 TO F
1204 S=0
1206 FOR J=1 TO K-1
1208 S=S+Q(P(K),J)*B(J)
1210 NEXT J
1212 B(K)=A(P(K))-S
1214 NEXT K
1216 FOR K=F TO 1 STEP -1
1218 S=0
1220 FOR J=F TO K+1 STEP -1
1222 S=S+Q(P(K),J)*A(J)
1224 NEXT J
1226 A(K)=(B(K)-S)/Q(P(K),K)
1228 NEXT K
1269 GOSUB 5300
1310 INPUT "Reacciones S/N";Y$
1320 IF Y$="S" THEN GOSUB 6350 ELSE IF Y$<>"N" THEN 1310
1350 INPUT "Axiles S/N";A$:IF A$="S" THEN GOSUB 9000 ELSE IF A$<>"N" THEN 1350
1400 CLS
1420 INPUT "Alargamientos S/N";Z$
1430 IF Z$="S" AND CNT=0 THEN  GOSUB 9000:GOSUB 5000 ELSE IF Z$="N" THEN 1450 ELSE GOSUB 5000
1450 CLS
1460 INPUT "Otro sistema cargas S/N";C$
1470 IF C$="S" THEN  ERASE B,F,A,K,N:DIM F(2*N),A(F),K(COAC),B(F),N(B):GOSUB 6000:GOSUB 9500:GOTO 1202 ELSE IF C$<>"N" THEN 1460
1490 END
3998 'SALIDA DE REACCIONES
4000 S=S+1
4010 IF I MOD 2=0 THEN 4060
4020 Q=INT(I/2+1)
4030 PRINT "NUDO";Q;"Rx=";ROUND(K(S),-4)
4040 RETURN
4060 Q=I/2
4070 PRINT "NUDO";Q;"Ry=";ROUND(K(S),-4)
4090 RETURN
5000 'CALCULO ALARGAMIENTOS
5005 CNT=0
5006 GOSUB 9500
5010 GOSUB 7500
5020 CLS
5030 FOR I=1 TO B
5045 PRINT "Alargamiento";V(I);S(I);"=";ROUND(N(I)*1E3,-4);"mm"
5050 NEXT I
5055 RETURN
5300 'SALIDA DESPLAZAMIENTOS
5303 CLS
5304 BEEP0:BEEP1
5305 P=0:S=0
5310 FOR I=1 TO 2*N
5320 IF C(I)=0 THEN GOSUB 5500
5325 IF C(I)=0 THEN 5340
5330 IF I MOD 2<>0 THEN P=P+1
5340 NEXT I
5400 RETURN
5500 IF I MOD 2<>0 THEN P=P+1
5505 S=S+1
5510 PRINT "Punto";P;
5520 IF I MOD 2<>0 THEN PRINT "dx=";ELSE PRINT "dy=";
5530 PRINT ROUND(A(S)*1E3,-4);"mm"
5535 RETURN
5999 'INTRODUCCION FUERZAS EXTERIORES
6000 CLS:BEEP:INPUT "N. puntos cargados";L:IF L=0 THEN 6000
6002 CLS:FOR I=1 TO L
6003 INPUT "Punto aplicacion";P:IF P=0 THEN 6003
6004 INPUT "Fx(T)";F(2*P-1)
6005 INPUT "Fy(T)";F(2*P)
6006 CLS
6007 NEXT I
6008 GOSUB 9500
6009 P1=0:P2=0
6010 FOR I=1 TO 2*N
6015 IF C(I)=0 THEN P1=P1+1:A(P1)=F(I) ELSE P2=P2+1:K(P2)=F(I)
6020 NEXT I
6050 RETURN
6350 'CALCULO REACCIONES
6355 GOSUB 9500
6360 FOR I=1 TO COAC
6365 S=0
6370 FOR K=1 TO F
6375 S=S+W(I,K)*A(K)
6380 NEXT K
6385 K(I)=S
6390 NEXT I
6397 'SALIDA REACCIONES
6399 S=0
6400 CLS:PRINT "Reacciones";:LOCATE 0,1
6401 FOR I=1 TO 2*N
6405 IF C(I)=1 THEN GOSUB 4000
6410 NEXT I
6420 RETURN
6998 'CALCULO DE ALARGAMIENTOS
7500 FOR I=1 TO B
7510 N(I)=N(I)*L(I)/(E*A)
7520 NEXT I
7530 RETURN
7998 'ELIM FILAS Y COLUM EN K2
8000 P1=P1+1:P3=0
8010 FOR J=1 TO 2*N
8020 IF C(J)=0 THEN P3=P3+1:Q(P1,P3)=U(I,J)
8030 NEXT J
8050 RETURN
8500 P2=P2+1:P3=0
8510 FOR J=1 TO 2*N
8520 IF C(J)=0 THEN P3=P3+1:W(P2,P3)=U(I,J)
8530 NEXT J
8550 RETURN
9000 'CALC DE AXIL EN RIGIDEZ
9001 GOSUB 9500
9005 CNT=1
9010 DIM D(2*N)
9020 P=0
9030 FOR I=1 TO 2*N
9040 IF C(I)=0 THEN P=P+1:D(I)=A(P) ELSE D(I)=0
9050 NEXT I
9060 FOR I=1 TO B
9070 P1=2*V(I)-1:P2=2*S(I)-1
9080 N(I)=E*A*(T(I)*(D(P2)-D(P1))+R(I)*(D(P2+1)-D(P1+1)))/L(I)
9095 NEXT I
9096 ERASE D
9097 IF Z$="S" THEN RETURN
9098 BEEP0
9099 FOR I=1 TO B
9100 CLS
9105 PRINT "Barra";V(I);S(I);:LOCATE 0,1
9110 PRINT "AXIL=";ROUND(N(I),-4);"Tn";
9115 IF N(I)>0 THEN PRINT "  [Trac]" ELSE PRINT "  [Comp]"
9120 NEXT I
9130 RETURN
9500 CLS:PRINT "Operando....";
9510 RETURN
