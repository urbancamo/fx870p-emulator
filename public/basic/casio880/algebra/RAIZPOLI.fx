1 CLS : CLEAR : PRINT "CALCULO RAICES POLINOMIO";
2 PRINT " METODO DE LIN-BAIRSTROW "
4 PRINT "A(N)*X^N+A(N-1)*X^(N-1)+A(N-2)*X^(N-2)+....+ A(0)=0": PRINT
6 REM DEFINT K
7 K = 0
8 REM DEFDBL D
9 P = .0000001
10 REM READ N
12 INPUT "GRADO DEL POLINOMIO (N<=22)="; N
15 DIM A(N + 1), B(N + 1), C(N), Q(N - 1), D(3)
16 REM PRINT "PARTE REAL", "PARTE IMAG."
17 PRINT
18 PRINT "INGRESE LOS COEFICIENTES DE LA ECUACION EN ORDEN DE LAS POTENCIAS DECRECIENTES"
19 REM PRINT : PRINT "A(N)*X^N+A(N-1)*X^(N-1)+A(N-2)*X^(N-2)+....+ A(0)=0"
20 FOR I = 1 TO N + 1
23 REM READ A(I)
25 PRINT "A("; N + 1 - I; ")="; : INPUT A(I)
28 REM  PRINT
32 NEXT I
34 CLS
35 REM PRINT "RAICES DEL POLINOMIO"
36 REM PRINT
37 REM PRINT "PARTE REAL"; "PARTE IMAG."
39 IF N <= 2 THEN GOTO 240
40 U = 0: V = 0
50 B(1) = A(1): B(2) = A(2) + U * B(1)
60 FOR I = 3 TO N + 1
70 B(I) = A(I) + U * B(I - 1) + V * B(I - 2)
75 NEXT I
80 C(1) = B(1): C(2) = B(2) + U * C(1)
90 FOR I = 3 TO N
100 C(I) = B(I) + U * C(I - 1) + V * C(I - 2)
105 NEXT I
110 DELTA = C(N - 1) ^ 2 - C(N) * C(N - 2)
115 REM PRINT "DELTA=";DELTA
120 IF DELTA = 0 THEN GOTO 180
130 DU = (-B(N) * C(N - 1) + B(N + 1) * C(N - 2)) / DELTA
140 DV = (-C(N - 1) * B(N + 1) + B(N) * C(N)) / DELTA
145 REM LOCATE 3, 1: PRINT "DU="; DU, "DV="; DV
150 IF ABS(DU) < P AND ABS(DV) < P THEN GOTO 200
152 rem IF K > 150 THEN P = P * 10
155 K = K + 1
160 U = DU + U: V = DV + V
170 GOTO 50
180 U = U + 1: V = V + 1
190 GOTO 50
200 GOSUB 500
205 REM PRINT
210 GOSUB 1000
215 K = 0: P = .0000001
217 IF P1 < P THEN P1 = P
220 N = N - 2
230 IF N > 2 THEN GOTO 40
240 IF N = 1 THEN GOTO 270
250 GOSUB 500
260 GOTO 275
270 IF A(1) = 0 THEN A(1) = .0000001
271 PRINT "RAIZ="; -A(2) / A(1); : PRINT " +i 0"
275 PRINT : PRINT "PRECISION DE LOS COEFICIENTES DE LAS RAICES="; P1
276 PRINT "CALCULAMOS OTRO POLINOMIO (S/N) "; : INPUT OP$
277 IF OP$ = "S" OR OP$ = "s" THEN GOTO 1
278 IF OP$ = "N" OR OP$ = "n" THEN GOTO 1200
280 END
290 REM DATA 4
300 REM DATA .0774,.00475,5.37E-06,7.53E-08,0
500 REM FORMULA DE BASKARA
502 IF N <> 2 THEN GOTO 510
504 U = -A(2) / A(1): V = -A(3) / A(1)
510 XR = U / 2
520 D = U ^ 2 + 4 * V
530 IF D < 0 THEN GOTO 600
540 IF D = 0 THEN GOTO 580
550 X1 = (SQR(D)) / 2
560 PRINT "RAIZ="; : PRINT (X1 + XR); : PRINT " +i  0"
565 PRINT "RAIZ="; : PRINT (XR - X1); : PRINT " +i  0"
570 RETURN
580 PRINT "RAIZ="; XR, 0
585 PRINT "RAIZ="; XR, 0
590 GOTO 570
600 XI = (SQR(-D)) / 2
610 PRINT "RAIZ="; : PRINT XR; : PRINT " +i "; : PRINT XI
615 PRINT "RAIZ="; : PRINT XR; : PRINT " +i "; : PRINT -XI
620 GOTO 570
1000 REM DIVISION DE POLINOMIOS
1010 D(1) = 1: D(2) = -U: D(3) = -V
1020 FOR I = 1 TO N - 1
1025 Q(I) = A(I)
1030 FOR J = 1 TO 3
1040 A(I + J - 1) = A(I + J - 1) - Q(I) * D(J)
1050 NEXT J
1060 NEXT I
1070 FOR I = 1 TO N - 1
1080 A(I) = Q(I)
1090 NEXT I
1100 RETURN
1200 CLS : PRINT "PROGRAMO MIGLIOSOFT INC."
1260 END

