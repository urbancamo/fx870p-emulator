10 DIM SCELDA(5,5)
40 CLS:PRINT "DISENO FACTORIAL PARA 2 FACTORESFIJOS"
100 CLS
110 PRINT "NUMERO NIVELES FACTOR A";:INPUT a
120 PRINT "NUMERO NIVELES FACTOR B";:INPUT b
130 PRINT "NUMERO DE REPLICAS=";:INPUT n
132 CLS
135 SCUAD=0:SCELDAS=0:SCELDACUAD=0
140 FOR J=1 TO a
150 FOR I=1 TO b
155 CLS:BEEP:PRINT "CELDA(";I;",";J")";
157 SCELDA=0
160 FOR K=1 TO n
170 PRINT "DATO";K;"=";:INPUT D
180 SCUAD=SCUAD+D^2
190 SCELDA=SCELDA+D
230 NEXT K
235 SCELDAS=SCELDAS+SCELDA
237 SCELDACUAD=SCELDACUAD+SCELDA^2
240 PROM=SCELDA/n
245 BEEP 1
250 PRINT "SUMA DE CELDA(";I;",";J;")=";SCELDA
260 PRINT "PROM DE CELDA(";I;",";J;")=";PROM
270 SCELDA(I,J)=SCELDA
280 NEXT I
290 NEXT J
295 SHORCUAD=0
300 FOR J=1 TO a
305 SHOR=0
310 FOR I=1 TO b
350 SHOR=SHOR+SCELDA(I,J)
360 NEXT I
365 PRINT "SHOR";J;"=";SHOR
368 SHORCUAD=SHORCUAD+SHOR^2
370 NEXT J
375 SVERCUAD=0
380 FOR I=1 TO b
385 SVER=0
390 FOR J=1 TO a
400 SVER=SVER+SCELDA(I,J)
410 NEXT J
420 PRINT "SVER";I;"=";SVER
425 SVERCUAD=SVERCUAD+SVER^2
430 NEXT I
432 PRINT "SUMA Yijk’=";SCUAD
435 PRINT "SUMA Y...=";SCELDAS
440 SST=SCUAD-SCELDAS^2/(a*b*n)
450 PRINT "SST=";SST
460 PRINT "SUMA Yi..’=";SHORCUAD
470 SSA=SHORCUAD/(b*n)-SCELDAS^2/(a*b*n)
475 PRINT "SSA=";SSA
480 PRINT "SUMA Y.j.’=";SVERCUAD
490 SSB=SVERCUAD/(a*n)-SCELDAS^2/(a*b*n)
495 PRINT "SSB=";SSB
500 PRINT "SUMA Yij.’=";SCELDACUAD
510 SSAB=SCELDACUAD/n-SCELDAS^2/(a*b*n)-SSA-SSB
520 PRINT "SSAB=";SSAB
530 SSE=SST-SSAB-SSA-SSB
540 PRINT "SSE=";SSE
550 MSA=SSA/(a-1):MSB=SSB/(b-1):MSAB=SSAB/((a-1)*(b-1))
555 MSE=SSE/(a*b*(n-1))
560 PRINT "MSA=";MSA
570 PRINT "MSB=";MSB
580 PRINT "MSAB=";MSAB
585 PRINT "MSE=";MSE
590 PRINT "Fa=";MSA/MSE
600 PRINT "Fb=";MSB/MSE
610 PRINT "Fab=";MSAB/MSE
900 GOTO 40
