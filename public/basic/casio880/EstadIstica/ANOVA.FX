10 DIM N(10),K(10,30),TS(10),M(100),BARIANZA(10),T2(30),M2(30)
90 CLS:PRINT "ANOVA-ANALISIS DE VARIANZA":PRINT CHR$(9);
100 CLS
110 PRINT "NUMERO DE TRATAMIENTOS";:INPUT K
115 N=0:FLAG=0:FLAG2=0
120 FOR I=1 TO K
130 PRINT "n(";I;")=";N(I);:INPUT N(I)
137 N=N+N(I)
140 NEXT I
150 FOR I=1 TO K
160 FOR J=1 TO N(I)
170 PRINT "K(";I;",";J;")=";K(I,J);:INPUT K(I,J)
180 NEXT J
190 NEXT I
300 REM CALCULO DE TOTALES, MEDIAS Y VARIANZAS POBLACIONALES
301 FOR I=1 TO K
305 T=0:SCUAD=0
310 FOR J=1 TO N(I)
320 T=T+K(I,J)
325 SCUAD=SCUAD+K(I,J)^2
330 NEXT J
340 TS(I)=T
350 M(I)=TS(I)/N(I)
355 BARIANZA=(SCUAD-TS(I)^2/N(I))/(N(I)-1):BARIANZA(I)=BARIANZA
360 PRINT "TOTAL TRAT";I;".=";TS(I)
370 PRINT "MEDIA TRAT";I;".=";M(I)
380 PRINT "S’ TRAT";I;".=";BARIANZA(I)
390 NEXT I
400 REM SUMA TOTAL Y MEDIA TOTAL
405 ST=0:MT=0
410 FOR I=1 TO K
420 ST=ST+TS(I)
425 MT=MT+M(I)
430 NEXT I
435 MTT=MT/K
440 PRINT "SUMA TOTAL..=";ST
450 PRINT "MEDIA TOTAL..=";MTT
500 REM VARIANZA TOTAL
502 SCUAD1=0
510 FOR I=1 TO K
520 FOR J=1 TO N(I)
550 SCUAD1=SCUAD1+K(I,J)^2
560 NEXT J
565 NEXT I
570 NUM=(SCUAD1-ST^2/N)
575 BARIANZAT=NUM/(N-1)
580 PRINT "VARIANZA TOTAL..=";BARIANZAT
600 CLS:PRINT "DIGITE LETRA INICIAL";
610 LOCATE 0,1:PRINT "Bart,Coch,1,2,3,4,Tuk,Dun,SNK";:D$=INPUT$(1)
615 D=10
620 IF D$="B" THEN D=1
630 IF D$="C" THEN D=2
640 IF D$="1" THEN D=3
650 IF D$="2" THEN D=4
660 IF D$="3" THEN D=5
670 IF D$="T" THEN D=6
680 IF D$="D" THEN D=7
690 IF D$="S" THEN D=8
695 IF D$="4" THEN D=9
700 ON D GOSUB 1000,2000,3000,4000,5000,6000,7000,8000,9050,600
710 GOTO 600
1000 REM PRUEBA BARTLETT
1010 REM Sp’
1011 CLS
1015 S=0
1020 FOR I=1 TO K
1030 S=S+(N(I)-1)*BARIANZA(I)
1035 NEXT I
1040 SPCUAD=S/(N-K)
1050 PRINT "Sp’=";SPCUAD
1100 REM b
1105 S=1
1110 FOR I=1 TO K
1120 S=S*(BARIANZA(I)^(N(I)-1))^(1/(N-K))
1130 NEXT I
1135 PRINT "NUMERADOR=";S
1140 b=S/SPCUAD
1150 PRINT "b=";b
1200 RETURN
2000 REM COCHRAN
2010 CLS:S=0
2020 FOR I=1 TO K
2030 S=S+BARIANZA(I)
2040 NEXT I
2050 PRINT "SUMA DE VARIANZAS=";S
2060 PRINT "gcalculado=VARIANZA MAYOR/";S
2070 RETURN
3000 CLS:PRINT "ANOVA EN UNA SOLA DIRECCION"
3002 C=0
3005 FOR I=1 TO K-1
3006 IF N(I)=N(I+1) THEN C=C+1
3007 NEXT I
3020 IF C=K-1 THEN 3100
3030 PRINT "N'S DIFERENTES"
3035 CLS
3040 SST=NUM
3045 SSA1=0
3050 FOR I=1 TO K
3060 SSA1=SSA1+TS(I)^2/N(I)
3070 NEXT I
3080 SSA=SSA1-ST^2/N
3085 SSE=SST-SSA
3090 PRINT "SST=";SST:PRINT "SSA=";SSA:PRINT "SSE=";SSE
3092 MSA=SSA/(K-1):MSE=SSE/(N-K)
3095 GOTO 3400
3100 PRINT "N CONSTANTE"
3105 CLS:SSE=0
3110 FOR I=1 TO K
3120 SSE=SSE+(N(I)-1)*BARIANZA(I)
3130 NEXT I
3250 PRINT "SSE=";SSE
3260 SST=(N(1)*K-1)*BARIANZAT
3270 PRINT "SST=";SST
3280 SSA=SST-SSE
3290 PRINT "SSA=";SSA
3300 MSA=SSA/(K-1):MSE=SSE/(K*(N(1)-1)
3400 FCALC=MSA/MSE
3410 PRINT "MSA=";MSA
3420 PRINT "MSE=";MSE
3430 PRINT "Fcalc=";FCALC
3440 v1=K-1:v2=N-K
3450 PRINT "v1=";v1,"v2=";v2
3500 RETURN
4000 CLS:PRINT "BLOQUES COMPLETOS ALEATORIZADOS"
4020 FOR J=1 TO N(1)
4025 T2=0
4030 FOR I=1 TO K
4040 T2=T2+K(I,J)
4050 NEXT I
4060 T2(J)=T2
4070 M2(J)=T2(J)/K
4075 PRINT "TOTAL BLOQ(";J;")=";T2(J)
4077 PRINT "MEDIA BLOQ(";J;")=";M2(J)
4080 NEXT J
4090 v1=K-1:v2=(K-1)*(N(1)-1)
4095 PRINT "v1(TRAT)=";v1,"v2(ERROR)=";v2
4100 SST=NUM
4102 PRINT "SST=";SST
4105 ST2CUAD=0
4110 FOR I=1 TO N(1)
4120 ST2CUAD=ST2CUAD+T2(I)^2
4130 NEXT I
4140 PRINT "SUM T2CUAD=";ST2CUAD
4150 SSB=(ST2CUAD/K)-ST^2/N
4170 PRINT "SSB=";SSB
4180 STCUAD=0
4190 FOR I=1 TO K
4200 STCUAD=STCUAD+TS(I)^2
4210 NEXT I
4220 PRINT "SUM TCUAD=";STCUAD
4230 SSA=(STCUAD/N(1))-ST^2/N
4240 PRINT "SSA=";SSA
4245 IF FLAG=1 THEN 5010
4250 SSE=SST-SSA-SSB
4260 PRINT "SSE=";SSE
4270 MSA=SSA/(K-1)
4280 MSB=SSB/(N(1)-1)
4290 MSE=SSE/((K-1)*(N(1)-1)
4300 F1CALC=MSA/MSE:F2CALC=MSB/MSE
4310 PRINT "MSA=";MSA
4320 PRINT "MSB=";MSB
4330 PRINT "MSE=";MSE
4340 PRINT "Fcalc1=";F1CALC
4350 PRINT "Fcalc2=";F2CALC
4500 RETURN
5000 CLS:PRINT "CUADRADO LATINO"
5002 CLS:PRINT "DEJAR CORRER ESTA PRUEBA HASTA EL FINAL":FLAG=1
5005 GOTO 4020
5010 PRINT "COPIAR DESDE AQUI"
5015 SST=(K^2-1)*BARIANZAT
5020 PRINT "SST=";SST
5100 SSR=SSA
5120 SSC=SSB
5130 PRINT "SSR=";SSR
5140 PRINT "SSC=";SSC
5141 TPT=0
5142 FOR I=1 TO K
5143 TP2=0
5144 FOR J=1 TO K
5145 CAR=64+I
5146 PRINT CHR$(CAR);"(";J;")";:INPUT TP
5148 TP2=TP2+TP
5150 NEXT J
5152 TPT=TPT+TP2^2
5154 NEXT I
5156 TP=TPT/K
5159 PRINT "TERMINO PELUDO=";TP
5160 SSTR=TP-ST^2/N
5170 PRINT "SSTR=";SSTR
5175 IF FLAG2=1 THEN 9100
5180 SSE=SST-SSR-SSC-SSTR
5190 PRINT "SSE=";SSE
5200 MSR=SSR/(K-1)
5210 MSC=SSC/(K-1)
5220 MSTR=SSTR/(K-1)
5230 MSE=SSE/((K-1)*(K-2))
5232 FCALC1=MSR/MSE
5234 FCALC2=MSC/MSE
5236 FCALC3=MSTR/MSE
5250 PRINT "MSR=";MSR
5260 PRINT "MSC=";MSC
5270 PRINT "MSTR=";MSTR
5280 PRINT "MSE=";MSE
5290 PRINT "Fcalc1=";FCALC1
5300 PRINT "Fcalc2=";FCALC2
5310 PRINT "Fcalc3=";FCALC3
5890 FLAG=0
5900 RETURN
6000 REM TUKEY
6005 V=K*(N(1)-1)
6010 CLS:X=9000+K
6015 RESTORE X
6017 FOR I=1 TO V
6019 READ q
6021 NEXT I
6023 PRINT "VALOR DE q(0.05,";K;",";V;")";q
6030 Q=q*SQR(MSE/N(1))
6040 PRINT "Q=";Q
6500 RETURN
7000 REM DUNCAN
7010 CLS
7020 FOR I=1 TO K
7030 PRINT "rp(";I;")=";:INPUT rp
7040 RP=rp*SQR(MSE/N(1))
7045 PRINT "RP";I;"=";RP
7050 NEXT I
7500 RETURN
8000 REM SNK
8002 Sy=SQR(MSE/N(1)):CLS:PRINT "Sy=";Sy
8007 FOR I=K TO 2 STEP -1
8010 X=9000+I
8014 RESTORE X
8016 FOR J=1 TO v2
8018 READ q
8020 NEXT J
8030 PRINT "q(0.05,";I;",";v2;")=";q
8100 Rq=q*Sy
8140 PRINT "R";I;"=";Rq
8150 NEXT I
8200 RETURN
9000 REM TABLA DE RANGOS ESTUDENTIZADOS PARA LAS PRUEBAS DE IGUALDAD DE MEDIAS DE TUKEY Y SNK
9002 DATA 18,6.09,4.5,3.93,3.64,3.46,3.34,3.26,3.2,3.15,3.11,3.08,3.06,3.03,3.01,3,2.98,2.97,2.96,2.95
9003 DATA 27,8.33,5.91,5.04,4.60,4.34,4.16,4.04,3.95,3.88,3.82,3.77,3.73,3.70,3.67,3.65,3.62,3.61,3.59,3.58
9004 DATA 32.8,9.8,6.83,5.76,5.22,4.9,4.68,4.53,4.42,4.33,4.26,4.2,4.15,4.11,4.08,4.05,4.02,4,3.98,3.96
9005 DATA 37.2,10.89,7.51,6.29,5.67,5.31,5.06,4.89,4.76,4.66,4.58,4.51,4.46,4.41,4.37,4.34,4.31,4.28,4.26,4.24
9006 DATA 40.5,11.73,8.04,6.71,6.03,5.63,5.35,5.17,5.02,4.91,7 4.82,4.75,4.69,4.64,4.59,4.56,4.52,4.49,4.47,4.45
9007 DATA 43.1,12.43,8.47,7.06,6.33,5.89,5.59,5.4,5.24,5.12,5.03,4.95,4.88,4.83,4.78,4.74,4.7,4.67,4.64,4.62
9008 DATA 45.4,13.03,8.85,7.35,6.58,6.12,5.8,5.6,5.43,5.3,5.2,5.12,5.05,4.99,4.94,4.9,4.86,4.83,4.79,4.77
9009 DATA 47.3,13.54,9.18,7.6,6.8,6.32,5.99,5.77,5.6,5.46,5.35,5.27,5.19,5.13,5.08,5.03,4.99,4.96,4.92,4.9
9010 FLAG=2
9020 GOTO 5000
9050 CLS:PRINT "CUADRADOS GRECO-LATINOS"
9070 FLAG2=1
9075 GOTO 5002
9100 REM CALCULOS PARA LETRAS GRIEGAS
9141 TPT=0
9142 FOR I=1 TO K
9143 TP2=0
9144 FOR J=1 TO K
9145 CAR=135+I
9146 PRINT CHR$(CAR);"(";J;")";:INPUT TP
9148 TP2=TP2+TP
9150 NEXT J
9152 TPT=TPT+TP2^2
9154 NEXT I
9156 TP=TPT/K
9159 PRINT "TERMINO PELUDO2=";TP
9160 SSTRG=TP-ST^2/N
9170 PRINT "SSTRG=";SSTRG
9180 SSE=SST-SSR-SSC-SSTR-SSTRG
9190 PRINT "SSE=";SSE
9200 MSR=SSR/(K-1)
9210 MSC=SSC/(K-1)
9220 MSTR=SSTR/(K-1)
9225 MSTRG=SSTRG/(K-1)
9230 MSE=SSE/((K-1)*(K-3))
9232 FCALC1=MSR/MSE
9234 FCALC2=MSC/MSE
9236 FCALC3=MSTR/MSE
9246 FCALC4=MSTRG/MSE
9250 PRINT "MSR=";MSR
9260 PRINT "MSC=";MSC
9270 PRINT "MSTR=";MSTR
9280 PRINT "MSTRG=";MSTRG
9290 PRINT "MSE=";MSE
9300 PRINT "Fcalc1=";FCALC1
9310 PRINT "Fcalc2=";FCALC2
9320 PRINT "Fcalc3=";FCALC3
9330 PRINT "Fcalc4=";FCALC4
9400 RETURN
