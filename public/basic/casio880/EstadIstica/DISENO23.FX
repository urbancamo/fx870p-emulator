10 DIM X(7,8),D(5,8),PROM(8),BAR(8),b(7)
50 CLS:PRINT "DISENO 2³"
55 PRINT "SACAR PRIMERO EL VALOR t PARA   0.025 Y v=N-k+1 LIB 6430"
57 INPUT "t=";t
60 FOR J=1 TO 8
70 FOR I=1 TO 3
80 PRINT "X(";I;",";J;")";X(I,J);:INPUT X(I,J)
90 NEXT I
100 NEXT J
105 GOSUB 1000
110 PRINT "NUMERO DE REPLICAS:";:INPUT REP
120 N=REP*8
125 FOR J=1 TO 8
130 FOR I=1 TO REP
140 PRINT "DATO(";I;",";J;")";D(I,J);:INPUT D(I,J)
150 NEXT I
160 NEXT J
170 FOR J=1 TO 8
180 S=0
185 FOR I=1 TO REP
190 S=S+D(I,J)
210 NEXT I
220 PROM(J)=S/REP
230 NEXT J
235 S2=0
240 FOR J=1 TO 8
250 PRINT "PROMEDIO";J;"=";PROM(J)
260 S2=S2+PROM(J)
270 NEXT J
280 PRINT "TOTAL DE PROMEDIOS:";S2
290 PRINT "b0=";S2/8
295 S4=0
300 FOR J=1 TO 8
305 S3=0
310 FOR I=1 TO REP
320 S3=S3+((D(I,J)-PROM(J))^2)/(REP-1)
330 NEXT I
340 BAR(J)=S3
345 PRINT "VAR(";J;")=";BAR(J)
347 S4=S4+BAR(J)
350 NEXT J
360 PRINT "SUMA DE VAR'S=";S4
370 PRINT "Sp²=";S4/8
380 PRINT "S²efecto=";4*(S4/8)/(REP*8)
385 Sefecto=SQR(4*(S4/8)/(8*REP))
390 PRINT "Sefecto=";Sefecto
400 TER=t*Sefecto
410 FOR I=1 TO 7
415 S5=0
420 FOR J=1 TO 8
430 S5=S5+PROM(J)*X(I,J)
440 NEXT J
445 PRINT "E";I;"=";S5/4
447 PRINT "b";I;"=";(S5/4)/2
460 PRINT "LI";I;"=";(S5/4)-TER
470 PRINT "LS";I;"=";(S5/4)+TER
500 NEXT I
990 END
1000 REM RUTINA PARA CALCULAR MATRIZ DE INTERACCIONES
1010 FOR J=1 TO 8
1020 X(4,J)=X(1,J)*X(2,J)
1030 NEXT J
1040 FOR J=1 TO 8
1050 X(5,J)=X(1,J)*X(3,J)
1060 NEXT J
1070 FOR J=1 TO 8
1080 X(6,J)=X(2,J)*X(3,J)
1090 NEXT J
1100 FOR J=1 TO 8
1110 X(7,J)=X(1,J)*X(2,J)*X(3,J)
1120 NEXT J
1130 RETURN
