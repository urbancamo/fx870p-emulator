10 DIM X(30),XPROM(30),R(39),DESVEST(30)
50 CLS:PRINT "GRAFICOS DE CONTROL (VARIABLES)";CHR$(9)
60 PRINT "NUMERO DE SUBGRUPOS";:INPUT m
70 PRINT "NUMERO DE OBSV/SUBGRUPO";:INPUT n
95 CLS
100 FOR I=1 TO m
110 FOR J=1 TO n
120 PRINT "X";J;I;"=";:INPUT X(J)
140 NEXT J
145 MENOR=X(1):MAYOR=X(1)
150 FOR J=2 TO n
170 IF X(J)<MENOR THEN MENOR=X(J)
180 IF X(J)>MAYOR THEN MAYOR=X(J)
190 NEXT J
200 R(I)=MAYOR-MENOR
205 SUMA=0
210 FOR J=1 TO n
220 SUMA=SUMA+X(J)
230 NEXT J
240 XPROM(I)=SUMA/n
255 SUMA2=0
260 FOR J=1 TO n
270 SUMA2=SUMA2+(X(J)-XPROM(I))^2
280 NEXT J
290 DESVEST=SQR(SUMA2/(n-1))
295 DESVEST(I)=DESVEST
320 NEXT I
330 GOSUB 2000
340 CLS:PRINT "DIGITE EL NUMERO DESEADO";
350 LOCATE 0,1:PRINT "1)x,R 2)x,Ž";:D$=INPUT$(1)
360 D=VAL(D$)
370 ON D GOSUB 500,3000
500 CLS:PRINT "GRAFICO x,R"
570 SUMA3=0:SUMA4=0
600 FOR I=1 TO m
610 SUMA3=SUMA3+XPROM(I)
620 SUMA4=SUMA4+R(I)
630 NEXT I
635 XDOBARRA=SUMA3/m
640 RMED=SUMA4/m
645 PRINT "x doble barra INIC=";XDOBARRA
650 PRINT "Rmed INICIAL=";RMED
655 RESTORE 4050
660 FOR I=1 TO n-1
670 READ D4
680 NEXT I
690 PRINT "D4=";D4
700 CONT2=0
720 LSCR=D4*RMED:LCCR=RMED:LICR=0*RMED
730 PRINT "LSCR=";LSCR
740 PRINT "LCCR=";LCCR
750 PRINT "LICR=";LICR
753 CONT=0
760 FOR I=1 TO m
770 IF R(I)>LSCR THEN PRINT "R(";I;")>";LSCR:SUMA4=SUMA4-R(I):R(I)=0:CONT=CONT+1:CONT2=CONT2+1
780 NEXT I
785 IF CONT<>0 THEN PRINT "PUNTOS ELIMINADOS=";CONT2:RMED=SUMA4/(m-CONT2):PRINT "Rmed=";RMED:GOTO 720
800 PRINT "TODOS LOS PTOS ESTAN DENTRO"
805 PRINT "Rmed FINAL=";RMED
810 PRINT "LSCR=";LSCR
820 PRINT "LCCR=";LCCR
830 PRINT "LICR=";LICR
840 PRINT "TOTAL PTOS ELIM=";CONT2;"(";ROUND((CONT2/m)*100,-3);"%)"
850 RESTORE 4010
860 FOR I=1 TO n-1
865 READ A2
870 NEXT I
875 PRINT "A2=";A2
880 CONT2=0
890 LSCX=XDOBARRA+A2*RMED:LCCX=XDOBARRA:LICX=XDOBARRA-A2*RMED
900 PRINT "LSCx=";LSCX
910 PRINT "LCCx=";LCCX
920 PRINT "LICx=";LICX
930 CONT=0
940 FOR I=1 TO m
950 IF XPROM(I)>LSCX THEN PRINT "xprom(";I;")>";LSCX:SUMA3=SUMA3-XPROM(I):XPROM(I)=0:CONT=CONT+1:CONT2=CONT2+1
955 IF XPROM(I)<LICX AND XPROM(I)<>0 THEN PRINT "xprom(";I;")<";LICX:SUMA3=SUMA3-XPROM(I):XPROM(I)=0:CONT=CONT+1:CONT2=CONT2+1
960 NEXT I
970 IF CONT<>0 THEN PRINT "PTOS ELIMINADOS=";CONT2:XDOBARRA=SUMA3/(m-CONT2):PRINT "xdobarra=";XDOBARRA:GOTO 890
980 PRINT "TODOS LOS PTOS ESTAN DENTRO"
985 PRINT "x doble barra final=";XDOBARRA
990 PRINT "TOTAL PTOS ELIM=";CONT2;"(";ROUND((CONT2/m)*100,-3);"%)"
999 END
2000 REM RESULTADOS
2010 FOR I=1 TO m
2020 PRINT "xprom";I;"=";XPROM(I)
2030 PRINT "RANGO";I;"=";R(I)
2040 PRINT "DESV.ESTANDAR";I;"=";DESVEST(I)
2050 NEXT I
2060 RETURN
3000 CLS:PRINT "GRAFICO x,Ž"
3070 SUMA3=0:SUMA4=0
3600 FOR I=1 TO m
3610 SUMA3=SUMA3+XPROM(I)
3620 SUMA4=SUMA4+DESVEST(I)
3630 NEXT I
3635 XDOBARRA=SUMA3/m
3640 DESVMED=SUMA4/m
3645 PRINT "x doble barra inc=";XDOBARRA
3647 PRINT "DESV. ESTANDAR PROM=";DESVMED
3650 RESTORE 4130
3655 FOR I=1 TO n-1
3660 READ B4
3665 NEXT I
3670 PRINT "B4=";B4
3675 RESTORE 4110
3680 FOR I=1 TO n-1
3685 READ B3
3690 NEXT I
3695 PRINT "B3=";B3
3700 CONT2=0
3720 LSCD=B4*DESVMED:LCCD=DESVMED:LICD=B3*DESVMED
3730 PRINT "LSCD=";LSCD
3740 PRINT "LCCD=";LCCD
3750 PRINT "LICD=";LICD
3753 CONT=0
3760 FOR I=1 TO m
3770 IF DESVEST(I)>LSCD THEN PRINT "DESVEST(";I;")>";LSCD:SUMA4=SUMA4-DESVEST(I):DESVEST(I)=0:CONT=CONT+1:CONT2=CONT2+1
3775 REM
3780 NEXT I
3785 IF CONT<>0 THEN PRINT "PTOS ELIMINADOS=";CONT2:DESVMED=SUMA4/(m-CONT2):PRINT "DESVMED=";DESVMED:GOTO 3720
3800 PRINT "TODOS LOS PTOS ESTAN DENTRO"
3805 PRINT "DESV.MEDIA FINAL=";DESVMED
3840 PRINT "TOTAL PTOS ELIMIN=";CONT2;"(";ROUND((CONT2/m)*100,-3);"%)"
3845 RESTORE 4090
3850 FOR I=1 TO n-1
3855 READ A1
3860 NEXT I
3865 PRINT "A1=";A1
3885 CONT2=0
3890 LSCX=XDOBARRA+A1*DESVMED:LCCX=XDOBARRA:LICX=XDOBARRA-A1*DESVMED
3900 PRINT "LSCx=";LSCX
3910 PRINT "LCCx=";LCCX
3920 PRINT "LICx=";LICX
3930 CONT=0
3940 FOR I=1 TO m
3950 IF XPROM(I)>LSCX THEN PRINT "xprom(";I;")>";LSCX:SUMA3=SUMA3-XPROM(I):XPROM(I)=0:CONT=CONT+1:CONT2=CONT2+1
3955 IF XPROM(I)<LICX AND XPROM(I)<>0 THEN PRINT "xprom(";I;")<";LICX:SUMA3=SUMA3-XPROM(I):XPROM(I)=0:CONT=CONT+1:CONT2=CONT2+1
3960 NEXT I
3970 IF CONT<>0 THEN PRINT "PTOS ELIMINADOS=";CONT2:XDOBARRA=SUMA3/(m-CONT2):PRINT "x doble barra=";XDOBARRA:GOTO 3890
3980 PRINT "TODOS LOS PTOS ESTAN DENTRO"
3985 PRINT "x doble barra FINAL=";XDOBARRA
3990 PRINT "TOTAL DE PTOS ELIM=";CONT2;"(";ROUND((CONT2/m)*100,-3);"%)"
3995 END
4000 REM CONSTANTE A2
4010 DATA 1.880,1.023,0.729,0.577,0.483,0.419,0.373,0.337,0.308,0.285
4020 REM CONSTANTE D2
4030 DATA 3.686,4.358,4.698,4.918,5.078,5.203,5.307,5.394,5.469,5.534
4040 REM CONSTANTE D4
4050 DATA 3.267,2.575,2.282,2.115,2.004,1.924,1.864,1.816,1.777,1.744
4060 REM CONSTANTE d2
4070 DATA 1.118,1.693,2.059,2.326,2.534,2.704,2.847,2.970,3.078,3.173
4080 REM CONSTANTE A1
4090 DATA 3.76,2.39,1.88,1.59,1.41,1.28,1.17,1.9,1.03,0.97,0.92,0.88,0.85,0.82,0.79,0.76,0.74,0.72,0.7,0.68,0.66,0.65,0.63,0.62
4100 REM CONSTANTE B3
4110 DATA 0,0,0,0,0.03,0.12,0.18,0.24,0.28,0.32,0.35,0.38,0.41,0.43,0.45,0.47,0.48,0.5,0.51,0.52,0.52,0.55,0.56,0.57
4120 REM CONSTANTE B4
4130 DATA 3.27,2.57,2.27,2.09,1.97,1.88,1.82,1.76,1.72,1.70,1.65,1.62,1.59,1.57,1.55,1.53,1.52,1.5,1.49,1.48,1.47,1.45,1.44,1.43
