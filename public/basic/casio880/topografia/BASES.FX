5 ANGLE2
10 CLEAR :CLS:PRINT "  GEODESIA-REDUCCION DE BASE :":CLS
20 INPUT "CON VISUALES RECIPROCAS (S/N)";A$
21 IF A$="S" THEN GOTO 30
22 IF A$="N" THEN J=1:GOTO 31
23 GOTO 20
30 CLS:PRINT " *** VISUALES RECIPROCAS *** ":CLS
31 CLS:INPUT "QUIERES CORREGIR DE CONDICIONES METEREOLOGICAS (S/N)";B$:CLS
32 IF B$="S" THEN GOTO 35
33 IF B$="N" THEN CON=1:GOTO 49
34 GOTO 31
35 INPUT " - ONDAS LUMINOSAS (L)           - ONDAS DE RADIO  (R) ";F$
36 IF F$="L" THEN LUM=1:GOTO 38
37 IF F$<>"L" AND F$<>"R" THEN GOTO 35
38 IF J=1 THEN GOTO 42
39 IF J=1 THEN GOTO 50
40 IF LUM=1 AND CON=0 THEN CLS:PRINT "        ** VISUAL 1-2 ** ":INPUT "LONGITUD DE ONDA (um)";H1:GOTO 42
41 CLS:PRINT "        ** VISUAL 1-2 ** ":IF CON=1 THEN GOTO 50
42 INPUT "TEMPERATURA SECA (C)";TS1
43 INPUT "TEMPERATURA HUMEDA (C)";TH1
44 INPUT "PRESION en :                     MB(M) ; AT(A) ; mm/Hg(H) ";C$
45 IF C$="H" THEN INPUT " - PRESION EN mm/Hg ";P1:GOTO 48
46 IF C$="M" THEN INPUT " - PRESION EN MB ";P3:P1=(P3*760)/1013:GOTO 48
47 IF C$="A" THEN INPUT " - PRESION EN AT ";P1:P1=P1*760:GOTO 48
48 INPUT "INDICE STANDAR <1.000292>";NS1:IF NS1=0 THEN NS1=1.000292
49 IF CON=1 AND J=0 THEN CLS:PRINT "        ** VISUAL 1-2 ** "
50 INPUT "ALTURA DE INSTRUMENTO (m)";I1
55 INPUT "ALTURA DE MIRA (m)";MI1
60 INPUT "ANGULO CENITAL";VO1
65 INPUT "DISTANCIA MEDIDA (m)";DM1
70 IF J=1 THEN GOTO 130
80 CLS:PRINT "        ** VISUAL 2-1 ** "
81 INPUT "HAY MEDIDA DE DISTANCIA (S/N)";Z$:IF Z$="S" THEN MED=0:GOTO 84
82 IF Z$="N" THEN MED=1:GOTO 100
83 GOTO 81
84 IF CON=1 THEN GOTO 100
89 IF LUM=1 THEN INPUT "LONGITUD DE ONDA (um)";H2
90 INPUT "TEMPERATURA SECA (C)";TS2
91 INPUT "TEMPERATURA HUMEDA (C)";TH2
92 INPUT "PRESION en :                     MB(M) ; AT(A) ; mm/Hg(H) ";D$
94 IF D$="H" THEN INPUT " - PRESION EN mm/Hg ";P2
95 IF D$="M" THEN INPUT " - PRESION EN MB ";P3:P2=(P3*760)/1013:GOTO 97
96 IF D$="A" THEN INPUT " - PRESION EN AT ";P4:P2=P4*760:GOTO 97
97 INPUT "INDICE STANDAR <1.000292>";NS2
98 IF NS2=0 THEN NS2=1.000292
100 INPUT "ALTURA DE INSTRUMENTO (m)";I2
105 INPUT "ALTURA DE MIRA (m)";MI2
110 INPUT "ANGULO CENITAL ";VO2
120 IF MED=0 THEN INPUT "DISTANCIA MEDIDA (m)";DM2:GOTO 130
125 DM2=DM1
130 CLS:PRINT "    ** DATOS GENERALES **    "
131 INPUT "ALTITUD PTO-1 (m)";ALTI
132 INPUT "QUIERES MEDIR EL DESNI. (S/N)";Q$:IF Q$="S" THEN GOTO 135
133 IF Q$="N" THEN INPUT "ALTITUD PTO-2 (m)";ALTI2:FIN=1:POP=(ALTI2-ALTI)/2:GOTO 135
134 GOTO 132
135 INPUT "RADIO APROXIMADO (A)            CALCULADO        (B) ";E$
136 IF E$="A" THEN INPUT "RADIO <6370000 m>";RAD:GOTO 165
137 IF E$="B" THEN GOTO 140
138 GOTO 135
140 INPUT "EJE MAYOR ELIPSOIDE (a) m";a
145 INPUT "EXCENTRICIDAD ELIPSOIDE (e^2)";e:e=SQR(e)
146 INPUT "AZIMUT DE LA DIRECCION (G)";AZIM
147 INPUT "LATITUD PTO-1 (DEG)";LAT1:LAT1=(LAT1*400)/360
148 INPUT "LATITUD PTO-2 (DEG)";LAT2:LAT2=(LAT2*400)/360
149 LAT=(LAT1+LAT2)/2
150 NOR=a/SQR(1-(e^2)*(SINLAT^2))
155 ro=(a*(1-e^2))/((1-(e^2)*(SINLAT^2))^(3/2))
160 RAD=(NOR*ro)/(ro*(SINAZIM^2)+NOR*(COSAZIM^2)):GOTO 168
165 IF RAD=0 THEN RAD=6370000
168 IF FIN=1 THEN GOTO 172
170 IF J=1 THEN INPUT "COEFICIENTE DE REFRACCION <K=0.08>";KR1:GOTO 172
171 GOTO 180
172 IF KR1=0 THEN KR1=0.08
180 CLS:PRINT " ********* CALCULANDO *********"
190 IF CON=1 THEN DCM1=DM1:GOTO 230
200 ep1=10^(26.12612-3049.5/(TH1+273.15)-5.8697*LOG(TH1+273.15))
210 e1=ep1-0.00066*(1+0.0015*TH1*P1*(TS1-TH1))
215 IF LUM=1 GOTO 222
220 n1=1+(((((103.49*(P1-e1))/(TS1+273.15))+(86.26/(TS1+273.15))*(1+(5748/(TS1+273.15)))*e1)*(10^-6)
221 GOTO 225
222 NG1=1+(287.604+(4.8864/(H1^2))+(0.068/(H1^4)))*(10^(-6))
223 n1=1+((0.359474*(NG1-1)*P1)/(273.2+TS1))-((1.5026*e1*(10^(-5)))/(273.2+TS1))
225 DCM1=DM1+DM1*(NS1-n1)
230 c1=((MI1-I1)*SINVO1)/(DCM1*SIN0.0001)
235 DCP1=(DCM1*SINVO1)/SIN(VO1+c1*(10^-4))
240 W=ATN(DCP1/(RAD+ALTI)):DRH1=DCP1*SIN(VO1+c1*(10^-4)-W/2)
250 IF J=1  THEN GOTO 320
255 IF MED=1 THEN DCM2=DCM1:DRH2=DRH1
260 IF CON=1 OR MED=1 THEN DCM2=DM2:GOTO 305
265 ep2=10^(26.12612-3049.5/(TH2+273.15)-5.8697*LOG(TH2+273.15))
270 e2=ep2-0.00066*(1+0.0015*TH2*P2*(TS2-TH2))
275 IF LUM=1 GOTO 290
280 n2=1+(((((103.49*(P2-e2))/(TS2+273.15))+(86.26/(TS2+273.15))*(1+(5748/(TS2+273.15)))*e2)*(10^-6)
285 GOTO 300
290 NG2=1+(287.604+4.8864/(H2^2)+0.068/(H2^4))*(10^-6)
295 n2=1+(0.359474*(NG2-1)*P2)/(273.2+TS2)-(1.5026*e2*(10^-5))/(273.2+TS2)
300 DCM2=DM2+DM2*(NS2-n2)
305 c2=((MI2-I2)*SINVO2)/(DCM2*SIN0.0001)
310 DCP2=(DCM2*SINVO2)/SIN(VO2+c2*(10^-4))
315 DRH2=DCP2*(SIN(VO2+c2*(10^-4)-W/2))
320 REM CALCULO
335 D12=DCM1*COS(VO1+c1*(10^-4)):VC1=VO1+c1*(10^-4)
336 CRL=D12/COSW:CR1=CRL-D12:D12=CRL
337 es1=(DCM1^2)/(2*(RAD+ALTI))
340 IF J=1 THEN GOTO 400
350 D21=DCM2*COS(VO2+c2*(10^-4)):VC2=VO2+c2*(10^-4)
351 CRJ=D21/COSW:CR2=CRJ-D21:D21=CRJ
352 es2=(DCM2^2)/(2*(RAD+ALTI+D12))
360 K=0.5+(((200-(VC1+VC2))*(10^4))/636620)*((RAD+ALTI)/(2*((DCM1+DCM2)/2))
370 re1=(DCM1^2/(RAD+ALTI))*K
380 re2=(DCM2^2/(RAD+ALTI+D12))*K
390 D12C=D12+CR1+es1-re1
391 D21C=D21+CR2+es2-re2
394 IF MED=1 THEN D21C=-D12C
395 DESM=(D12C-D21C)/2
396 HM=DESM/2:IF FIN=1 THEN HM=POP:DESM=POP*2
398 GOTO 430
400 re1=((DCM1^2)/(RAD+ALTI))*KR1
410 D12C=D12+CR1+es1-re1
420 HM=D12C/2:DESM=D12C:IF FIN=1 THEN HM=POP:DESM=POP*2
430 IF J=0 THEN RNM=(RAD*((DRH1+DRH2)/2))/(RAD+HM+ALTI):GOTO 450
440 RNM=(RAD*DRH1)/(RAD+HM+ALTI)
450 IF J=0 THEN CRC=(((DRH1+DRH2)/2)^3)/(24*(RAD^2)):GOTO 460
455 CRC=(DRH1^3)/(24*(RAD^2))
460 CLS:IF FIN=1 THEN PRINT " SOLUCION DEL CALCULO :"
464 PRINT " - REDUCIDA = ";(RNM+CRC);"mts."
465 IF FIN=0 THEN PRINT " - DESNIVEL = ";DESM;"mts."
470 END
