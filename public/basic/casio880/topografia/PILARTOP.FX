1 CLS:CLEAR:PRINT "(C) JuanJu, Caminos 1992";:LOCATE 0,1:PRINT "SuperTopo"
2 CLS:PRINT "1-Interseccion Directa/Triangulacion/Trilateracion":PRINT "2-Diferencias Tabulares":PRINT "3-Varianzas-covarianzas":PRINT "4-Replanteo CaP":PRINT "5-Variacion de Coordenadas"
3 PRINT "7-Poligonal":PRINT "8-Minimos Cuadrados":PRINT "9-Nivelacion Trigonometrica":PRINT "10-Interseccion inversa"
4 CLS:INPUT "Numero del programa ";NUM:ON NUM GOTO 7,59,87,166,197,229,266,432,504,534
6 END
7 '
8 CLEAR :PRINT "TRIANGULACION:";:PRINT "-ENTRADA","*SEXAG (S)","*CENT (C)";:INPUT B$
9 ANGLE 0:IF B$="C" THEN Q=1 ELSE IF B$<>"S"THEN 8
10 CLS:PRINT "-SALIDA","*SEXAG (S)","*CENT (C)";:INPUT B$
11 IF B$="S" THEN 12 ELSE S=1
12 G=180*(1-Q)+200*Q:P=PI/1800
13 DIM N$(3),N(3)
14 '
15 CLS:PRINT "-NORMAS PROGRAMA-"
16 PRINT "PUNTO A CALCULAR ES EL VISADO Y SU NOMBRE ES V";:GOSUB 53
17 CLS:PRINT "-NORMAS PROGRAMA","MIRANDO DESDE PTO V","PTO I IZQUIERDA DE V    ";
18 PRINT "PTO D DERECHA DE V";:GOSUB 53
19 CLS:PRINT "COORDENADAS":INPUT "X PTO D";V,"Y PTO D";W
20 CLS:PRINT "COORDENADAS":INPUT "X PTO I";X,"Y PTO I";Y
21 E=SQR((X-V)^2+(Y-W)^2):D=ATN((X-V)/(Y-W))+90*(1-SGN(Y-W))
22 CLS :PRINT "*INTERSECCION","*TRIANGULACION","*TRILATERACION",TAB (6);"(I/A/L)";
23 INPUT A$:IF A$="I" THEN 29
24 IF A$="L" THEN 35 ELSE IF A$="A" THEN 26 ELSE 22
25 '
26 CLS:PRINT "TRIANGULACION":INPUT "*ANGULO D";H,"*ANGULO I";I:N=H:GOSUB 54
27 H=N:N=I:GOSUB 54:I=N:K=D-H:C=E*SINI/SIN(H+I)
28 CLS:PRINT "TRIANGULACION":GOTO 39
29 CLS:PRINT "INTERSECCION"
30 INPUT "ORIENTACION LADO DV";K,"ORIENTACION LADO IV";L:N=K:GOSUB 54:K=N:N=L:GOSUB 54
31 L=N:I=L-(D+180):I=I-360*INT(I/360)
32 H=D-K:H=H-360*INT(H/360)
33 C=E*SINI/SIN(H+I):CLS:PRINT "INTERSECCION":GOTO 39
34 '
35 CLS:PRINT "TRILATERACION"
36 INPUT "*LADO DV";C,"*LADO IV";O
37 H=ACS((C*C+E*E-O*O)/(2*C*E)):I=ACS((E*E+O*O-C*C)/(2*E*O)):K=D-H
38 PRINT "TRILATERACION"
39 '
40 T=V+C*SINK:U=W+C*COSK
41 PRINT "COOR X";T,"COOR Y";U
42 PRINT "VER TODOS DATOS",TAB(8);"(S/N)";
43 INPUT A$:IF A$="N" THEN 51 ELSE IF A$<>"S" THEN 42
44 O=C*C+E*E-2*C*E*COSH:SU=C*E*SINH/2
45 N=K:GOSUB 56:K=N:L=D+180+I:L=L-360*INT(L/360):Z=SINH/SINI:N=D:GOSUB 56:D=N
46 N=180-H-I:GOSUB 56:R=N:N=L:GOSUB 56:L=N:N=H:GOSUB 56:H=N:N=I:GOSUB 56:I=N
47 PRINT "ORIENTACION LADO DV";K,"ORIENTACION LADO DI";D;:GOSUB 53:CLS
48 PRINT "ORIENTACION LADO IV";L:GOSUB 53:CLS:PRINT "ANG D";H,"ANG I";I
49 PRINT "ANG V";R;:GOSUB 53:CLS:PRINT "LADO DI";E,"LADO DV";C,"LADO IV";C*Z
50 PRINT "SUPER";SU;:GOSUB 53
51 CLS:LOCATE 4,1:PRINT "OTRO PROBLEMA",TAB(8);"(S/N)"
52 A$=INKEY$:IF A$="N" THEN CLS:END ELSE IF A$<>"S" THEN 52 ELSE 14
53 IF INKEY$="" THEN 53 ELSE RETURN
54 F=100*FRACN:N=Q*.9*N+(1-Q)*(N-FRACN+(F-FRACF)/60+FRAC(100*FRACN)/36)
55 RETURN
56 F=60*FRACN:N=S*N/.9+(1-S)*(N-FRACN+(F-FRACF)/100+.006*FRAC(60*FRACN))
57 RETURN
58 '
59 PRINT "*DIFERENCIAS TABULARES*":CLEAR
60 CLS: PRINT "SEX. O CEN.(S/C)";
61 A$=INKEY$:IF A$="" THEN 61
62 IF A$="C" THEN GR=1 ELSE IF A$<>"S" THEN 61
63 G=180*(1-GR)+200*GR:P=PI/180
64 CLS :PRINT "LOG.DEC. O NEP.(D/N)";:BB=1
65 A$=INKEY$:IF A$="" THEN 65
66 IF A$="N" THEN 68
67 BB=1/LN(10)
68 CLS:PRINT "INCR. DE RAD/GRD/MIN/SEG.?";
69 B$=INKEY$:IF B$="" THEN 69
70 IF B$="R" THEN A=1: RO=1:GOTO 76
71 IF B$="G" THEN A=2:GOTO 74
72 IF B$="M" THEN A=3:GOTO 74
73 IF B$="S" THEN A=4 ELSE 69
74 X=10^((2-A)*2):GOSUB 85
75 RO=P*HR
76 CLS:INPUT "ANGULO";AN
77 X=AN:GOSUB 85
78 CLS:PRINT "LOG(SIN(";AN;"))=";LN(SIN(HR))*BB
79 PRINT "DIF. TAB.=";RO*BB/TAN(HR)
80 PRINT "REP.NEW.END.(R/N/E)?"
81 C$=INPUT$(1):IF C$="" THEN 81
82 IF C$="N" THEN CLEAR :GOTO 60
83 IF C$="R" THEN 78
84 IF C$="E" THEN CLEAR :END
85 HR=GR*.9*X+(1-GR)*(INTX+INT(100*FRACX)/60+FRAC(100*FRACX)/36)
86 RETURN
87 '
88 CLEAR
89 CLS:PRINT "CALCULO DE LAS VARIANZAS Y COVARIANZAS"
90 CLS:INPUT "NUMERO DE VISUALES";N
91 CLS:INPUT "NUMERO DE OBSERVACIONES";M
92 DIM A(M,4*N),C(N),D(N),E(N)
93 DIM B$(N+1):CLS
94 FOR I=1 TO N
95 PRINT "NOMBRE DE"I"VISUAL";:INPUT B$(I)
96 CLS:NEXT :B$(N+1)=B$(1)
97 CLS:PRINT "INTRODUCE OBSERVACIONES"
98 CLS:FOR J=1 TO N
99 FOR I=1 TO M
100 PRINT "INTRODUCE LA OBSERVACION"I"DE LA VISUAL "B$(J);:INPUT A(I,J):NEXT I:NEXT J
101 INPUT "CORREGIR DATOS (S/N)";F$
102 IF F$="N" THEN 106
103 INPUT "I";I:INPUT "J";J
104 PRINT "A("I","J")=";:INPUT A(I,J)
105 GOTO 101
106 INPUT "VER DATOS (S/N)";W$
107 IF W$="N" THEN 111
108 FOR J=1 TO N:FOR I=1 TO M
109 PRINT "A("I","J")="A(I,J):NEXT I:NEXT J
110 INPUT "ALGUNA EQUIVOCACION (S/N)";F$:GOTO 102
111 CLS:PRINT "CALCULO DE LOS RESIDUOS"
112 S=0:FOR J=1 TO N
113 FOR I=1 TO M
114 S=S+A(I,J):NEXT I
115 C(J)=S:S=0:NEXT J
116 FOR J=1 TO N
117 C(J)=C(J)/M
118 PRINT "MEDIA DE "B$(J)"="C(J)
119 NEXT J
120 FOR J=1 TO N:FOR I=1 TO M
121 A(I,N+J)=C(J)-A(I,J)
122 NEXT I:NEXT J
123 INPUT "QUIERES LOS RESIDUOS EN UNIDADES DIFERENTES (S/N)";P$
124 IF P$="N" THEN 129
125 INPUT "(A)=NUMERO DE CAMBIO;EJEMPLO 1E4 DE GR A DMGR";R
126 FOR J=1 TO N:FOR I=1 TO M
127 A(I,N+J)=A(I,N+J)*R
128 NEXT I:NEXT J
129 FOR J=1 TO N
130 FOR I=1 TO M
131 PRINT "RESIDUO "B$(J)"("I")="A(I,N+J)
132 NEXT I:NEXT J
133 CLS:PRINT "RESIDUOS AL CUADRADO"
134 CLS:FOR J=1 TO N:FOR I=1 TO M
135 A(I,J+2*N)=A(I,J+N)^2
136 NEXT I:NEXT J
137 FOR J=1 TO N:FOR I=1 TO M
138 PRINT "RESIDUO^2 DE "B$(J)" DE LA OBSEVACION"I"="A(I,2*N+J)
139 NEXT I:NEXT J
140 S=0:FOR J=1 TO N:FOR I=1 TO M
141 S=S+A(I,J+2*N):NEXT I
142 D(J)=S:S=0:NEXT J
143 FOR J=1 TO N
144 D(J)=(D(J)/(M-1)):NEXT
145 CLS:PRINT "VARIANZAS (ERROR STANDAR=RAIZ VARIANZAS)"
146 FOR J=1 TO N
147 PRINT "VARIANZA "B$(J) "="D(J):NEXT
148 CLS:PRINT "CALCULO DE LAS COVARIANZAS"
149 FOR J=1 TO N-1:FOR I=1 TO M
150 A(I,3*N+J)=A(I,J+N)*A(I,J+N+1)
151 NEXT I,J
152 FOR I=1 TO M
153 A(I,4*N)=A(I,N+1)*A(I,2*N):NEXT
154 FOR J=1 TO N:FOR I=1 TO M
155 PRINT "RESIDUO DE "B$(J)"*"B$(J+1)" FILA"I"="A(I,J+3*N)
156 NEXT I,J
157 S=0:FOR J=1 TO N:FOR I=1 TO M
158 S=S+A(I,3*N+J):NEXT I
159 E(J)=S:S=0:NEXT J
160 FOR J=1 TO N
161 E(J)=E(J)/(M-1):NEXT
162 FOR J=1 TO N
163 PRINT "LA COVARIANZA "B$(J)"-"B$(J+1) "="E(J)
164 NEXT
165 CLS:PRINT "FIN"
166 PRINT "REPLANTEO PTOS. POR COORDENADAS",
167 ANGLE 0:CLEAR :PRINT "ENTRADA":INPUT"Sexag o Cent (S/C)";A$
168 IF A$="C"THEN H=1 ELSE IF A$<>"S" THEN 167
169 CLS: PRINT "SALIDA":INPUT "Sexag o Cent (S/C)";A$
170 IF A$="C"THEN J=1 ELSE IF A$<>"S"THEN 169
171 G=180*(1-H)+200*H:P=PI/180
172 CLS: PRINT "N";CHR$(223);:INPUT "de ptos. por coord.=";N:DIM X(N),Y(N),Z(N),N$(N)
173 FOR I=1 TO N
174 CLS:INPUT "Nombre pto=";N$(I),"Coor X=";X(I),"Coor Y=";Y(I),"Coor Z=";Z(I)
175 NEXT I
176 CLS:INPUT "Nombre pto estacion (ENTER para final)=";B$
177 IF B$="" THEN CLS:CLEAR:END
178 GOSUB 194
179 IF I<=N THEN E=I ELSE 176
180 INPUT "Alt inst=";I,"Descen=";C
181 CLS:INPUT "Nombre pto visado (ENTER para fin)=";B$
182 IF B$=""THEN 176
183 GOSUB 194
184 IF I<=N THEN A=I ELSE 181
185 INPUT "Alt mira=";M:X=X(A)-X(E):Y=Y(A)-Y(E)
186 K=ATN(X/Y)+90*(1-SGN Y)+C
187 IF K>360 THEN K=K-360 ELSE IF K<0 THEN K=K+360
188 D=SQR(X*X+Y*Y):O=90-ATN(Z(A)-Z(E)-I+M-D^2*6.6E-08)/D
189 Q=D/SIN(O)
190 W=J*O/.9+(1-J)*(O-FRAC O+(100*FRAC O-FRAC(100*FRAC O))/60+FRAC(100*FRAC O))/36
191 V=J*K/.9+(1-J)*(K-FRAC K+(100*FRAC K-FRAC(100*FRAC K))/60+FRAC(100*FRAC K))/36
192 PRINT "Ang H=HORIZ, V=VERT","*H=";V,"*V=";W,"DIST=";Q;
193 IF INKEY$="" THEN 193 ELSE 181
194 FOR I=1 TO N
195 IF N$(I)<>B$ THEN NEXT I ELSE RETURN
196 PRINT "  **pto NO existe**";:FOR ESP=0 TO300:NEXT ESP:RETURN
197 '
198 CLS:PRINT "VAR. COORDENADAS"
199 CLEAR :ANGLE 0:INPUT "Sexag o Cent (S/C)";R$
200 IF R$="C" THEN H=1 ELSE IF R$<>"S" THEN 198
201 G=180*(1-H)+H*200:P=PI/180
202 CLS:INPUT "NUM. PTOS DE LA RED:";N
203 DIM N$(N),X(N),Y(N),M(N,N),K(N)
204 FOR I=1 TO N
205 INPUT "NOMBRE PTO:";N$(I),"COOR X=";X(I),"COOR Y=";Y(I):NEXT I:CLS:R=0
206 INPUT "NOM PTO ESTACION:";S$:GOSUB 226
207 V=X:W=Y
208 INPUT "NOM PTO VISADO:";S$
209 GOSUB 226
210 T=X:U=Y:CLS:R=R+1
211 PRINT "Si no hay dist EXE en dist med. Si no hay ang EXE en ang med"
212 INPUT "Dist medida=";DD$,"Lec ang=";LL$:D=VAL(DD$):M=VAL(LL$)
213 L=H*.9*M+(1-H)*(M-FRACM+(100*FRACM-FRAC(100*FRACM))/60+FRAC(100*FRACM)/36
214 IF LL$<>" " THEN INPUT "DESC DEL LIMBO=";K
215 E=H*.9*K+(1-H)*(K-FRACK+(100*FRACK-FRAC(100*FRACK))/60+FRAC(100*FRACK)/36)
216 A=T-V:B=U-W:C=SQR(A^2+B^2):F=ATN(A/B)+180*(1-SGNB)/2:CLS
217 Z=H*F/.9+(1-H)*(F-FRACF+(60*FRACF-FRAC(60*FRACF))/100+.006*FRAC(60*FRACF))
218 PRINT "AX=";A,"AY=";B,"DC=";C,"AZIM=";Z
219 IF DD$=" " THEN 221
220 CLS:PRINT "SIN=";SINF,"COS=";COSF,"KD=";D-C
221 IF LL$=" " THEN 225
222 CLS:PRINT "p*AX/D2=";636620*A/C^2,"p*AY/D2=";636620*B/C^2:O=L+E-F
223 Q=H*O/.9+(1-H)*(O-FRACO+(60*FRACO-FRAC(60*FRACO))/100+.006*FRAC(60*FRACO))
224 PRINT "KO=";Q;"en grados min y seg","KO=";O*P;"en radianes"
225 INPUT "Seguir calculando(S/N)";T$:IF T$="S" THEN 206 ELSE IF T$="N" THEN END ELSE 225
226 FOR I=1 TO N
227 IF S$=N$(I) THEN X=X(I):Y=Y(I):I=N
228 NEXT I:RETURN
266 CLS:CLEAR:DC=0:X2=0:S1=0:Y2=0:XY=0:GR=0:TT$="Sexagesimal"
267 PRINT "POLIGONAL"
268 INPUT "Sexag o Centes(S/C)";AN$:IF AN$=""THEN 268
269 ANGLE 0:IF AN$="C" THEN GR=1:TT$="Centes"
270 G=180*(1-GR)+200*GR:P=PI/180
271 INPUT "Bruj o Taquim (B/T)";B$:IF B$="" THEN 271
272 PRINT "N";CHR$(223);"lados";:INPUT N
273 DIM X(N),Y(N),D(N),V(N),W(N),Z(N)
274 CLS:PRINT TAB(3);"Primer punto",:INPUT "Coord X(0)";X(0),"Coord Y(0)";Y(0)
275 IF B$="T" THEN INPUT "Or visual";Z(0) ELSE 279
276 Z(0)=GR*.9*Z(0)+(1-GR)*(INTZ(0)+INT(100*FRACZ(0))/60+FRAC(100*FRACZ(0))/36
277 Z(0)=Z(0)+180
278 Z(0)=Z(0)-360*INT(Z(0)/360)
279 INPUT "Bessel(S/N)";BS$:IF BS$="" THEN 279
280 FOR I=1 TO N
281 GOSUB 382
282 GOSUB 399
283 NEXT I
284 PRINT
285 FOR I=1 TO N
286 X(I)=X(I-1)+D(I)*SINZ(I)
287 Y(I)=Y(I-1)+D(I)*COSZ(I)
288 PRINT
289 PRINT "Pto No";I;":","X=";X(I),"Y=";Y(I):GOSUB 428
290 NEXT I
291 PRINT
292 INPUT "X ultimo pto";XC
293 INPUT "Y ultimo pto";YC
294 EX=XC-X(N)
295 EY=YC-Y(N)
296 PRINT "Errores:","en X=";EX,"en Y=";EY:GOSUB 428
297 PRINT
298 INPUT "Repasar dato(S/N)";C$:IF C$="" THEN 298
299 IF C$="S" THEN 301
300 IF C$="N" THEN 313 ELSE 298
301 CLS:PRINT "X(0)=";X(0),"Y(0)=";Y(0),"Origen ref";TT$;"=";:H=Z(0):GOSUB 430
302 PRINT H:GOSUB 428
303 CLS:FOR I=1 TO N
304 PRINT "D(";I;")=";D(I),"Vad(";I;")=";:H=V(I):GOSUB 430:PRINT H,"Vat(";I;")=";
305 H=W(I):GOSUB 430:PRINT H,:H=Z(I):GOSUB 430:PRINT "Ang(";I;")=";H;:GOSUB 428
306 CLS:NEXT I
307 INPUT "Modificaciones(S/N)";QW$
308 IF QW$="S" THEN 309 ELSE IF QW$="N" THEN 284 ELSE 308
309 PRINT "N";CHR$(223);"estacion a cambiar";:INPUT I
310 IF I<>0 THEN GOSUB 382 ELSE 284
311 FOR I=1 TO N:GOSUB 399:NEXT I
312 GOTO 303
313 CLS
314 PRINT "Tipo comp.,Expedita o Rigurosa(E/R)";:INPUT C$:IF C$="" THEN 314
315 IF C$="E" THEN 325
316 IF C$="R" THEN 317 ELSE 313
317 FOR I=1 TO N
319 PRINT "**LADO";I;":**"
320 PRINT "Ax=";X(I)-X(I-1),"Ay=";Y(I)-Y(I-1):GOSUB 428
321 PRINT "Sx=";X(N)-X(I-1),"Sy=";Y(N)-Y(I-1):GOSUB 428
322 PRINT "Sen=";SINZ(I),"Cos=";COSZ(I):GOSUB 428
323 NEXT I
324 GOTO 373
325 XD=X(0):YD=Y(0)
326 INPUT "Ptos radiados(S/N)";D$:IF D$="" THEN 326
327 PRINT "Tipo Comp.(R/G/C):",
328 PRINT "Reparto proporc.R,"
329 PRINT "Giro y Homot.G,"
330 PRINT "Crandall.C,";:INPUT C$:IF C$="" THEN 327
332 IF C$="G" THEN 335
333 IF C$="R" THEN 344
334 IF C$="C" THEN 357 ELSE 327
335 IF X(0)=XC THEN IF Y(0)=YC THEN PRINT "No comp cerrada por giro.":GOSUB 428:GOTO 327
336 D1=SQR((X(N)-X(0))^2+(Y(N)-Y(0))^2)
337 ED=((X(N)-X(0))*(XC-X(0))+(Y(N)-Y(0))*(YC-Y(0)))/(D1^2)
338 EO=((Y(N)-Y(0))*(XC-X(0))-(X(N)-X(0))*(YC-Y(0)))/(D1^2)
339 FOR I=1 TO N
340 XT=XD:YT=YD
341 XD=X(0)+(X(I)-X(0))*ED+(Y(I)-Y(0))*EO
342 YD=Y(0)-(X(I)-X(0))*EO+(Y(I)-Y(0))*ED
343 GOSUB 402:NEXT I:GOTO 371
344 DT=0
345 FOR I=1 TO N
346 DT=DT+D(I)
347 NEXT I
348 FX=EX/DT
349 FY=EY/DT
350 FOR I=1 TO N
351 DC=DC+D(I)
352 XT=XD:YT=YD
353 XD=X(I)+FX*DC
354 YD=Y(I)+FY*DC
355 GOSUB 402
356 NEXT I:GOTO 371
357 FOR I=1 TO N
358 GOSUB 376
359 NEXT I
360 A=(EX*XY-EY*X2)/(X2*Y2-XY^2)
361 B=(EY*XY-EX*Y2)/(X2*Y2-XY^2)
362 X2=0:Y2=0:XY=0
363 FOR I=1 TO N
364 GOSUB 376
365 XT=XD:YT=YD
366 YD=Y(I)-A*Y2-B*XY
367 XD=X(I)-A*XY-B*X2
368 GOSUB 402
369 NEXT I
370 PRINT
371 PRINT "Area int. poligonal=";ABS(S1/2):S1=0:GOSUB 428
372 PRINT
373 INPUT "Nueva comp.(S/N)";C$:IF C$="" THEN 373
374 IF C$="S" THEN 314 ELSE IF C$<>"N" THEN 372
375 END
376 DX=X(I)-X(I-1)
377 DY=Y(I)-Y(I-1)
378 X2=X2+DX^2/D(I)
379 Y2=Y2+DY^2/D(I)
380 XY=XY+DX*DY/D(I)
381 RETURN
382 PRINT
383 PRINT "Distan pto";I-1;" al";I;:INPUT D(I)
384 PRINT "Visual directa pto";I-1;" al";I;:INPUT V(I)
385 V=V(I)-G*(SGN(V(I)-G+.000001)):IF BS$="N" GOTO 387
386 PRINT "Visual inversa pto";I-1;" al";I;:INPUT V
387 V(I)=GR*.9*V(I)+(1-GR)*(INTV(I)+INT(100*FRACV(I))/60+FRAC(100*FRACV(I))/36)
388 V(I)=V(I)+GR*.9*V+(1-GR)*(INTV+INT(100*FRACV)/60+FRAC(100*FRACV)/36)
389 V(I)=V(I)-180*SGN(V-G+.000001)
390 V(I)=V(I)/2
391 W(I)=Z(I-1)+180:IF B$="B" THEN 399 ELSE PRINT "Visual directa pto";I-1;" al";I-2;:INPUT W(I)
392 V=W(I)-G*SGN(W(I)-G+.000001):IF BS$="N" THEN 394
393 PRINT "Visual inversa pto";I-1;" al";I-2;:INPUT V
394 W(I)=GR*.9*W(I)+(1-GR)*(INTW(I)+INT(100*FRACW(I))/60+FRAC(100*FRACW(I))/36)
395 W(I)=W(I)+GR*.9*V+(1-GR)*(INTV+INT(100*FRACV)/60+FRAC(100*FRACV)/36)
396 W(I)=W(I)-180*SGN(V-G+.000001)
397 W(I)=W(I)/2
398 RETURN
399 Z(I)=Z(I-1)+180+V(I)-W(I)
400 Z(I)=Z(I)-360*INT(Z(I)/360)
401 RETURN
402 O=ATN((XT-XD)/(YT-YD))+90*(1-SGN(YT-YD))
403 RETURN
404 PRINT "Pto comp";I;":","X(";I;")=";XD,"Y(";I;")=";YD:GOSUB 428
405 IF I<>1 THEN S1=S1+X(0)*YT+XT*YD+XD*Y(0)-XD*YT-XT*Y(0)-X(0)*YD
406 IF D$="S" THEN GOSUB 408
407 RETURN
408 PRINT
409 PRINT "Cuando no desee mas radiados desde";I;"pulse RET a *NOMBRE PTO*";
410 GOSUB 428
411 PRINT
412 INPUT "Nombre pto radiado";N1$
413 IF N1$="" THEN RETURN
414 INPUT "Lect horiz dir al pto radiado";LR
415 V=LR-G*SGN(LR-G+.000001):IF BS$="N" THEN 417
416 INPUT "Lect horiz inv al pto radiado";V
417 LR=GR*.9*LR+(1-GR)*(INTLR+INT(100*FRACLR)/60+FRAC(100*FRACLR)/36)
418 LR=LR+GR*.9*V+(1-GR)*(INTV+INT(100*FRACV)/60+FRAC(100*FRACV)/36)
419 LR=LR-180*SGN(V-G+.000001)
420 LR=LR/2
421 INPUT "Dist al pto radiado";DR
422 O1=O+LR-W(I)
423 X=XD+DR*SIN(O1)
424 Y=YD+DR*COS(O1)
425 PRINT
426 PRINT "Pto ";N1$,"X=";X,"Y=";Y:GOSUB 428
427 GOTO 411
428 IF INKEY$="" THEN 428
429 RETURN
430 HH=60*FRACH-FRAC(60*FRACH)
431 H=GR*H/.9+(1-GR)*(H-FRACH+HH/100+.006*FRAC(60*FRACH)):RETURN
432 '
433 CLEAR :CLS:PRINT "*** MINIMOS CUADRADOS ***"
434 INPUT "N FILAS=";F,"N COLUMNAS=";C
435 IF F<C THEN P=F:Q=C ELSE P=C:Q=F
436 CLS:IF P=F THEN PRINT "SISTEMA CONDICIONADO"ELSE PRINT "SISTEMA INDIRECTO"
437 DIM A(P,P),B(P,P),P(P),F(C),M(F,C),K(F),L(Q),X(C),U(C)
438 FOR I=1 TO F:FOR J=1 TO C
439 PRINT "A(";I;",";J;")=";:INPUT M(I,J):NEXT J:PRINT :NEXT I
440 FOR I=1 TO F:PRINT "K(";I;")=";:INPUT K(I):NEXT I
441 PRINT "RAIZ PESOS DE LAS OBSERVACIONES":IF P=F THEN Y=C ELSE Y=F
442 FOR I=1 TO Y:PRINT "RAIZ PESO OBS.";I;"=";:INPUT L(I):NEXT I:GOSUB 498
443 PRINT "ECUACIONES PONDERADAS":IF P=F THEN FOR I=1 TO F:FOR J=1 TO C:M(I,J)=M(I,J)/L(J):PRINT "A(";I;",";J;")=";M(I,J):NEXT J :NEXT I ELSE FOR I=1 TO F:FOR J=1 TO C:M(I,J)=M(I,J)*L(I):PRINT "A(";I;",";J;")=";M(I,J):NEXT J:NEXT I
444 IF P=C THEN FOR I=1 TO F:K(I)=K(I)*L(I):PRINT "K'(";I;")=";K(I):NEXT I
445 PRINT
446 PRINT "ECUACIONES NORMALES"
447 FOR I=1 TO P:FOR J=1 TO P:FOR K=1 TO Q
448 IF P=F THEN A(I,J)=A(I,J)+M(I,K)*M(J,K) ELSE A(I,J)=A(I,J)+M(K,I)*M(K,J)
449 NEXT K:NEXT J:NEXT I:BEEP
450 IF F>C THEN FOR J=1 TO C:FOR I=1 TO F:U(J)=U(J)+M(I,J)*K(I):NEXT I:PRINT "K''(";J;")=";U(J):NEXT J
451 FOR I=1 TO P:FOR J=1 TO P:PRINT "N(";I;",";J;")=";A(I,J):NEXT J:NEXT I
452 N=P:FOR I=1 TO N:B(I,I)=1:NEXT I:FOR I=1 TO N:IF A(I,I)<>0 THEN 456 ELSE J=1
453 J=J+1:IF J<=N THEN 454 ELSECLS:PRINT "MATRIZ SINGULAR":END
454 IF A(I,J)<>0 THEN 455 ELSE 453
455 FOR K=1 TO N:A(I,K)=A(I,K)+A(K,J):B(I,K)=B(I,K)+B(J,K):NEXT K
456 FOR J=1 TO N
457 IF I=J THEN 460
458 B=-A(J,I)/A(I,I)
459 FOR K=1 TO N:A(J,K)=A(J,K)+B*A(I,K):B(J,K)=B(J,K)+B*B(I,K):NEXT K
460 NEXT J
461 C0=A(I,I):FOR J=1 TO N:B(I,J)=B(I,J)/C0:A(I,J)=A(I,J)/C0:NEXT J
462 NEXT I
463 IF P=F THEN PRINT "MULTIPLICADORES";CHR$(178)
464 FOR I=1 TO P:FOR J=1 TO F
465 IF P=F THEN P(I)=P(I)+B(I,J)*K(J) ELSE P(I)=P(I)+M(J,I)*K(J)
466 NEXT J:NEXT I
467 IF P=F THEN FOR I=1 TO F:PRINT "MULT";I;"=";P(I):NEXT I
468 CLS:PRINT "ERRORES"
469 FOR I=1 TO C:FOR J=1 TO P
470 IF F=Q THEN F(I)=F(I)+B(I,J)*P(J) ELSE F(I)=F(I)+M(J,I)*P(J)
471 NEXT J
472 NEXT I
473 IF F>C THEN FOR I=1 TO C:PRINT "D";I;"=";F(I):NEXT I:GOTO 486
474 FOR I=1 TO C
475 X(I)=F(I)/L(I)
476 PRINT "CORRECCION";I;"=";X(I)
477 NEXT I
478 FOR I=1 TO F
479 S=S+P(I)*K(I)
480 NEXT I:S=SQR(S/F)
481 PRINT "ERROR STANDAR DE LA OBS. DE PESO 1=";S
482 FOR I=1 TO C
483 S=S/L(I)
484 PRINT "ERROR STANDAR DE LA MEDIA";I;"=";S
485 NEXT I
486 PRINT "ERROR STAND/UNID PESO(OJO DIM)"
487 S=0:FOR I=1 TO C:S=S-U(I)*F(I):NEXT I
488 S1=0:FOR I=1 TO F:S1=S1+K(I)*K(I):NEXT I:PRINT "KK=";S1:    W=SQR (ABS((S1+S)/(F-C)))
489 PRINT "M(U.PESO)=";W
490 PRINT "COEF. DE (N)-1=(At*P*A)-1":FOR I=1 TO P:FOR J=1 TO P:PRINT "N-1("I","J")="B(I,J)
491 NEXT J:NEXT I
492 DIM V(P,P):FOR I=1 TO P:FOR J=1 TO P:V(I,J)=W^2*B(I,J):PRINT "E1.(";I;",";J;") DE LA MATRIZ VAR-COV="V(I,J):NEXT J:NEXT I
493 INPUT "MAX-MIN ERROR STANDAR(S/N)";A$:IF A$="S" THEN IF C=2 THEN SX=SQR V(1,1):SY=SQR V(2,2):SXY=V(2,1) ELSE INPUT "ERROR STANDAR EN X=";SX,"ERROR STANDAR EN Y=";SY,"COVARIANZA XY=";SXY ELSE IF A$<>"N" GOTO 493
494 A=SX^2+SY^2:B=SQR((SX^2-SY^2)^2+4*SXY^2)
495 SMAX=SQR(.5*(A+B)):SMIN=SQR(.5*(A-B))
496 PRINT "VALOR MAX DEL ERROR STANDAR=";SMAX,"VALOR MIN DEL ERROR STANDAR=";SMIN
497 END
498 INPUT "ALGUNA EQUIVOCACION(S/N)";A$
499 IF A$="N" THEN RETURN ELSE IF A$<>"S" THEN 498
500 INPUT "EN MATRIZ COEF.,EN TERM. IND. O EN PESOS (C,I,P)";A$
501 IF A$="C" THEN INPUT "I=";I,"J=";J:PRINT "M(";I;",";J;")=";:INPUT Z:M(I,J)=Z:GOTO 498
502 INPUT "I=";I:IF A$="I" THEN PRINT "K(";I;")=";:INPUT Z:K(I)=Z ELSE PRINT "RAIZ PESO";I;"=";:INPUT Z:L(I)=Z
503 GOTO 498
504 '
505 PRINT "NIV TRIGONOMETRICA"
506 CLEAR :PRINT "ENTRADA","*SEXA":INPUT "*CENT ";B$
507 IF B$="C" THEN H=1 ELSE IF B$<>"S" THEN 506
508 G=180*(1-H)+200*H:P=PI/180:R=6370000
509 DIM N$(3),N(3)
510 CLS :PRINT "SELECCIONA","VISUAL RECIPROCA..R","VIS DIRECTA O INVER.U"
511 PRINT "FIN TRABAJO....F";
512 A$=INKEY$:IF A$="" THEN 512
513 IF A$="R" THEN 515 ELSE IF A$="U" THEN 522
514 IF A$="F" THEN CLS :END ELSE 512
515 CLS:GOSUB 530
516 CLS :PRINT "VISUAL DIRECTA":GOSUB 531:D=Z+(M-I)*(SIN(Z)^2)/(C*P)
517 CLS :PRINT "VISUAL RECIPROCA":GOSUB 531:E=Z+(M-I)*(SIN(Z)^2)/(C*P)
518 PRINT " K DE REFRACCION=";.5-R*(D+E-180)*P/(2*C)
519 PRINT "INCREMENTO DE COTA=";C*TAN((E-D)/2);
520 IF INKEY$="" THEN 520
521 CLS :GOTO 510
522 CLS :INPUT "CTE DE REFRACCION K=",K
523 INPUT "vis Direc o Inver";A$:IF A$<>"D" AND A$<>"I" THEN 523
524 CLS :GOSUB 530:GOSUB 531
525 F=C/TANZ+(C*C/R)*(.5-K)+I-M:IF A$="I" THEN F=-F
526 PRINT "DIFERENCIA DE COTA=";F
527 PRINT "Cota pto";:IF A$="D" THEN  INPUT " de estacion";O ELSE INPUT "visado";Q
528 PRINT "COTA PTO";:IF A$="I" THEN PRINT " DE ESTACION=";Q+F ELSE PRINT "VISADO=",O+F
529 IF INKEY$="" THEN 529 ELSE 510
530 INPUT "DIS HORIZ REDUCIDA =";C:RETURN
531 INPUT "ANG VERT ";Z
532 Z=H*.9*Z+(1-H)*(Z-FRACZ+(100*FRACZ-FRAC(100*FRACZ))/60+FRAC(100*FRACZ)/36)
533 INPUT "ALT INST ";I,"ALT MIRA ";M:RETURN
534 CLS:PRINT "INT. INVERSA"
535 CLEAR
536 PRINT "ENTRADA","*Sexag","*Cent";:INPUT W$
537 IF W$="C" THEN Y=1 ELSE IF W$<>"S" THEN 536
538 CLS:PRINT "SALIDA","*Sexag","*Cent";:INPUT W$
539 IF W$="C" THEN F=1 ELSE IF W$<>"S" THEN 538
540 G=180*(1-Y)+200*Y:P=PI/180:DIM N$(3),X(3),Y(3),L(3)
541 '
542 FOR I=1 TO 3:CLS:PRINT "DATOS PTOS CONOCIDOS"
543 INPUT "Nombre pto";N$(I)
544 PRINT "Coor X pto ";N$(I);:INPUT X(I):PRINT "Coor Y pto ";N$(I);:INPUT Y(I)
545 NEXT I
546 CLS:PRINT "DATOS PTO ESTACION":INPUT "Nombre pto";N$
547 CLS:PRINT "Lec azim de ";N$;" al:":FOR I=1 TO 3
548 PRINT "-";N$(I);:INPUT M:NN=100*FRACM
549 L(I)=Y*.9*M+(1-Y)*(M-FRACM+(NN-FRACNN)/60+FRAC(100*FRACM)/36):NEXT I
550 A=ABS(L(1)-L(2)):B=ABS(L(2)-L(3)):C=ABS(L(3)-L(1))
551 IF A>360-A THEN A1=360-A ELSEA1=A
552 IF B>360-B THEN B1=360-B ELSEB1=B
553 IF C>360-C THEN C1=360-C ELSE C1=C
554 C=3
555 IF B1>A1 THEN C=1
556 IF C1>B1 THEN IF C1>A1 THEN C=2
557 FOR H=1 TO 3
558 X=L(H)-L(C):IF ABS(X)<180 THEN  560
559 IF X<0 THEN L(H)=L(H)+360 ELSE L(H)=L(H)-360
560 IF L(H)>L(C) THEN D=H
561 IF L(H)<L(C) THEN I=H
562 NEXT H
563 '
564 '
565 A=SQR((X(I)-X(C))^2+(Y(I)-Y(C))^2)
566 B=SQR((X(D)-X(C))^2+(Y(D)-Y(C))^2)
567 Q=ATN((X(I)-X(C))/(Y(I)-Y(C)))+90*(1-SGN(Y(I)-Y(C)))
568 R=ATN((X(D)-X(C))/(Y(D)-Y(C)))+90*(1-SGN(Y(D)-Y(C)))
569 S=L(D)-L(C):T=L(C)-L(I):U=360-Q+R-S-T
570 K=A*SINS/(B*SINT)
571 V=ATN((K*SINU)/(1+K*COSU)):V=V+90*(1-SGNV)*SGNV
572 O=B*SINV/SINS
573 XP=X(C)+O*SIN(R+180-S-V)
574 YP=Y(C)+O*COS(R+180-S-V)
575 CLS:PRINT "Coor del pto ";N$
576 PRINT "X=";XP,"Y=";YP
577 IF INKEY$="" THEN 577
578 CLS:PRINT "DESEA CALCULAR MAS","Todos los datos de la ult int..T","Fin trabajo....................F"
579 PRINT "Inter a dist ptos..............D"
580 FOR Z=1 TO 200:NEXT Z
581 PRINT "Inter a mismos ptos............N";
582 Z$=INKEY$:IF Z$="" THEN 582
583 IF Z$="T" THEN 586
584 IF Z$="D" THEN 541 ELSE IF Z$="N" THEN  546
585 IF Z$="F" THEN CLS:END ELSE 578
586 FOR  E=1 TO 3:CLS:PRINT "Lado ";N$(E);" =";SQR((X(E)-XP)^2+(Y(E)-YP)^2)
587 M=ATN((X(E)-XP)/(Y(E)-YP))+90*(1-SGN(Y(E)-YP)):GOSUB 601
588 PRINT "Orientacion ";N$;" - ";N$(E);" = ";M:GOSUB 603:NEXT E
589 L=U-V:CLS:PRINT "**Angulos utilizados en la interseccion**"
590 PRINT "-Punto derecho = ";N$(D):GOSUB 603
591 PRINT "-Punto izquierdo = ";N$(I):GOSUB 603
592 PRINT "-Punto central = ";N$(C):GOSUB 603
593 M=S:GOSUB 601:PRINT "Ang ";N$(D);"-";N$;"-";N$(C);" =";M:GOSUB 603
594 M=T:GOSUB 601:PRINT "Ang ";N$(C);"-";N$;"-";N$(I);" =";M:GOSUB 603
595 M=V:GOSUB 601:PRINT "Ang ";N$;"-";N$(D);"-";N$(C);" =";M:GOSUB 603
596 M=360*FRAC(L/360):GOSUB 601:PRINT "Ang ";N$;"-";N$(I);"-";N$(C);" =";M
597 GOSUB 603
598 M=Q:GOSUB 601:PRINT "Orientacion ";N$(C);"-";N$(I);" =";M:GOSUB 603
599 M=R:GOSUB 601:PRINT "Orientacion ";N$(C);"-";N$(D);" =";M:GOSUB 603
600 GOTO 578
601 MM=60*FRACM:M=F*M/.9+(1-F)*(M-FRACM+(MM-FRACMM)/100+.006*FRAC(60*FRACM))
602 RETURN
603 IF INKEY$="" THEN 603 ELSE RETURN
