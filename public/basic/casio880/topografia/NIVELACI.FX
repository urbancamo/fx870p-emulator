1 PRINT "NIV. TRIGONOMETRICA","Si utilizas la FX-850P poner    mode DEG."
2 CLEAR :CLS:PRINT "ENTRADA","*Sexag","*Cent";:INPUT B$
3 IF B$="C" THEN H=1 ELSE IF B$<>"S" THEN 2
4 G=180*(1-H)+200*H:P=PI/180:R=6370000
5 DIM N$(3),N(3)
6 CLS:PRINT "Selecciona","Visual reciproca..R","Vis direc o inver.U"
7 PRINT "Fin trabajo.......F";
8 A$=INKEY$:IF A$="" THEN 8
9 IF A$="R" THEN 11 ELSE IF A$="U" THEN 18
10 IF A$="F" THEN CLS:END ELSE 8
11 CLS:GOSUB 26
12 CLS:PRINT "VISUAL DIRECTA":GOSUB 27:D=Z+(M-I)*(SIN(Z)^2)/(C*P)
13 CLS:PRINT "VISUAL RECIPROCA":GOSUB 27:E=Z+(M-I)*(SIN(Z)^2)/(C*P)
14 PRINT "K DE REFRACCION=";0.5-R*(D+E-180)*P/(2*C)
15 PRINT "INCREMENTO DE COTA=";C*TAN((E-D)/2);
16 IF INKEY$="" THEN 16
17 CLS:GOTO 6
18 CLS:INPUT "Cte. de refraccion K=",K
19 INPUT "vis. Direc. o Inver.";A$:IF A$="D" THEN 20 ELSE IF A$<>"I" THEN 19
20 CLS:GOSUB 26:GOSUB 27
21 F=C/TANZ+(C*C/R)*(0.5-K)+I-M:IF A$="I" THEN F=-F
22 PRINT "DIFERENCIA DE COTA=";F
23 PRINT "Cota pto.";:IF A$="D" THEN INPUT "de estacion";O ELSE INPUT "visado";Q
24 PRINT "COTA PTO.";:IF A$="I" THEN PRINT "DE ESTACION=";Q+F ELSE PRINT "VISADO=";O+F
25 IF INKEY$="" THEN 25 ELSE 6
26 INPUT "Dist. horiz. reducida=";C:RETURN
27 INPUT "Ang. vert.";Z
28 Z=H*0.9*Z+(1-H)*(Z-FRACZ+(100*FRACZ-FRAC(100*FRACZ))/60+FRAC(100*FRACZ)/36)
29 INPUT "Alt.Inst.";I,"Alt. mira";M:RETURN
