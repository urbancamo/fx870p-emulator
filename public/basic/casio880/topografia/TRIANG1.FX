1 CLS:PRINT "TRIANGULACION"
2 PRINT "-ENTRADA","*Sexag (S)","*Cent (C)";:INPUT B$
3 ANGLE 0:IF B$="C" THEN Q=1 ELSE IF B$<>"S" THEN 2
4 CLS:PRINT "-SALIDA","*Sexag (S)","*Cent (C)";:INPUT B$
5 IF B$="C" THEN S=1 ELSE IF B$<>"S"THEN 4
6 G=180*(1-Q)+200*Q:P=PI/180
7 DIM N$(3),N(3)
8 REM
9 CLS :PRINT "-NORMAS PROGRAMA-"
10 PRINT "El pto. a calcular es el VISADO y su nombre es V";:GOSUB 1000
11 CLS:PRINT "Mirando desde el pto. V","Pto. I Izquierda de V"
12 PRINT "Pto. D Derecha de V";:GOSUB 1000
13 CLS:PRINT "Coordenadas":INPUT "X punto D";V,"Y punto D";W
14 CLS:PRINT "Coordenadas":INPUT "X punto I";X,"Y punto I";Y
15 E=SQR((X-V)^2+(Y-W)^2):D=ATN((X-V)/(Y-W))+90*(1-SGN(Y-W))
16 CLS:PRINT "*Interseccion","*triAngulacion","*triLateracion",TAB(6);"(I/A/L)";
17 INPUT A$:IF A$="I" THEN 23
18 IF A$="L" THEN 29 ELSE IF A$="A" THEN 20 ELSE 16
19 REM
20 CLS:PRINT "TRIANGULACION":INPUT "*Angulo D";H,"*Angulo I";I:N=H:GOSUB 1010
21 H=N:N=I:GOSUB 1010:I=N:K=D-H:C=E*SINI/SIN(H+I)
22 CLS:PRINT "TRIANGULACION":GOTO 33
23 CLS:PRINT "INTERSECCION"
24 INPUT "Orientacion del lado DV";K,"Orientacion del lado IV";L:N=K:GOSUB 1010:K=N:N=L:GOSUB 1010
25 L=N:I=L-(D+180):I=I-360*INT(I/360)
26 H=D-K:H=H-360*INT(H/360)
27 C=E*SINI/SIN(H+I):CLS:PRINT "INTERSECCION":GOTO 33
28 REM
29 CLS:PRINT "TRILATERACION"
30 INPUT "*Lado DV";C,"*Lado IV";O
31 H=ACS((C*C+E*E-O*O)/(2*C*E)):I=ACS((E*E+O*O-C*C)/(2*E*O)):K=D-H
32 PRINT "TRILATERACION"
33 REM
34 T=V+C*SINK:U=W+C*COSK
35 PRINT "Coor X";T,"Coor Y";U
36 PRINT "Ver todos los datos",TAB(8);"(S/N)";
37 INPUT A$:IF A$="N" THEN 45 ELSE IF A$<>"S" THEN 36
38 O=C*C+E*E-2*C*E*COSH:SU=C*E*SINH/2
39 N=K:GOSUB 1040:K=N:L=D+180+I:L=L-360*INT(L/360):Z=SINH/SINI:N=D:GOSUB 1040:D=N
40 N=180-H-I:GOSUB 1040:R=N:N=L:GOSUB 1040:L=N:N=H:GOSUB 1040:H=N:N=I:GOSUB 1040:I=N
41 PRINT "-Orientacion lado DV";K,"-Orientacion lado DI";D;:GOSUB 1000:CLS
42 PRINT "-Orientacion lado IV";L:GOSUB 1000:CLS:PRINT "Ang D";H,"Ang I";I
43 PRINT "Ang V";R;:GOSUB 1000:CLS:PRINT "Lado DI";E,"Lado DV";C,"Lado IV";C*Z
44 PRINT "SUPERFICIE";SU;:GOSUB 1000
45 CLS:LOCATE 4,1:PRINT "OTRO PROBLEMA",TAB(8);"(S/N)";
46 A$=INKEY$:IF A$="N" THEN CLS:END ELSE IF A$<>"S" THEN 46 ELSE 8
1000 IF INKEY$="" THEN 1000 ELSE RETURN
1010 F=100*FRACN:N=Q*0.9*N+(1-Q)*(N-FRACN+(F-FRACF)/60+FRAC(100*FRACN)/36)
1011 RETURN
1040 F=60*FRACN:N=S*N/0.9+(1-S)*(N-FRACN+(F-FRACF)/100+0.006*FRAC(60*FRACN))
1041 RETURN
