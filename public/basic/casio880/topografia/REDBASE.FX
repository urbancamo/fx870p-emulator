10 PRINT "RED BASE O PROBLEMA DIRECTO"
25 SETF3:INPUT "INTRODUCE LATITUD=";L
50 GOSUB 2900:LC=L
100 MODE 6
110 GOSUB 4000
120 INPUT "ACIMUT=";Z
125 L=Z:GOSUB 2900:Z=L
130 J=(SINZ)^2:J=J*RO:K=(COSZ)^2:K=K*N:RZ=(RO*N)/(J+K)
140 GOSUB 4000
150 PRINT "RM=";RM;" RZ=";RZ
500 INPUT "RED O BASE 0-PROBL DIR 1 ELIGE=";B
510 IF B=1 THEN 2000
520 IF B<>0 THEN 500
530 INPUT "ZA=";ZA,"ZB=";ZB,"I=";I,"M=";M,"DIST MEDIDA=";DM,"D. CENITAL=";V
532 INPUT "CENT=0 SEXAG=1 ELIGE";B
534 IF B=0 THEN 540
536 IF B<>1 THEN 532
538 L=V:GOSUB 3000:V=L
540 C=ASN(((M-I)*(SINV))/(DM)):C=C*10000
545 CC=C/10000:CC=CC*.9
546 PRINT "CORR. ANG PEND. TERR"
550 PRINT C;"SEG CEN";DMS$(CC);"SEX"
560 VC=V+(C/10000)
570 D1=(DM*SINV)/SIN(200-VC):PRINT D1
580 IH=ZB-ZA
590 CH=-(IH^2)/(D1*2)-(IH^4)/(8*D1^3)
600 PRINT "CH=";CH
605 D22=SQR(D1^2-IH^2)
610 D2=D1+CH:PRINT "D2=";D2;"D22=";D22
620 HM=(ZA+ZB)/2
630 CM=-(D2*HM/RZ)+(D2*(HM^2))/(RZ^2)
640 CM1=-D2*HM/(RZ+HM)
650 PRINT "CM=";CM;"  CM1=";CM1
660 D3=D2+CM:D31=D2+CM1
670 PRINT "D3=";D3;"  D31=";D31
680 CA=(D3^3)/(24*RZ^2)
690 PRINT "CA=";CA:D4=D3+CA
700 PRINT "D4=";D4
710 A32=(D1^2-IM^2)
720 C32=(1+ZA/RZ)*(1+(ZB/RZ))
730 D32=SQR(A32/C32)
740 PRINT "D32=";D32
750 INPUT "FIN 0 PROBL DIR 1 ELIGE";B
760 IF B=0 THEN END
770 IF B<>1 THEN 750
780 GOTO 2010
2000 INPUT "DISTANCIA RED=";D4
2010 INPUT "LOB=";L:GOSUB 2900:LOB=L
2050 EX=.5*(D4^2)*COSZ*SINZ/(N*RO):PRINT "EX=";EX*10000;"SEG C"
2060 Y=D4*COS(Z-((2*EX)/3)):X=D4*SIN(Z-(EX/3)):PRINT "X'=";X;" Y'=";Y
2070 EX=.5*COS(Z-(2/3)*EX)*D4*D4*SIN(Z-EX/3)*SIN(100-EX/3)/(N*RO)
2080 PRINT "EX=";EX*10000;"SEG C"
2090 SN=1/(2*N*RO):X=D4*SINZ*(1-(1/3)*SN*(SIN.0001)*(D4^2)*(COSZ^2)
2100 Y=D4*COSZ*(1+(2/3)*SN*(SIN.0001)*(D4^2)*(SINZ^2)
2110 PRINT "X=";X;" Y=";Y
2120 INFI1=ASN(Y/RO):INFI=(1-(E2*(Y1^2)*(COS(2*LC)))/RO*SIN(0.0001))*Y/(RO*SIN0.0001)
2130 INFI$=DMS$(INFI*.9/10000):PRINT INFI;"SEG C";INFI$
2140 INFI=INFI/10000
2150 L1C=LC+INFI:SET F4
2160 PRINT "FI1=";L1C;DMS$(L1C*.9)
2170 B=LC:LC=L1C:GOSUB 4000:LC=B
2180 W=(X^2)*(TANL1C)/(2*N)
2185 WS=W/(N*SIN.0001)
2190 PRINT "W=";W,"W=";WS;"'' ";DMS$(WS*.9/10000):L1C=L1C-(WS/10000)
2192 PRINT "FIC=";L1C;" ";DMS$(L1C*.9):B=L1C:CO=LC:LC=L1C:GOSUB 4000:L1C=B:LC=CO
2200 J=X/N:K=(1/COSL1C):B=(1/SIN.0001):INLO=J*K*B
2210 PRINT "^LO=";INLO;DMS$(INLO*.9/10000):INLO=INLO/10000
2220 LOP=LOB+INLO:SET F4
2240 PRINT "LOP=";LOP;" "DMS$(LOP*.9)
2250 FIM=(LC+L1C)/2
2270 J=SIN(FIM):K=1/(COS(INFI/2)):B=TAN(INLO/2):J=J*K*B:J=ATNJ:INZ=J*2
2280 PRINT "INZ=";INZ;DMS$(INZ*.9)
2290 ZP=Z+INZ:PRINT "ACI=";ZP;" ";DMS$(ZP*.9)
2800 END
2900 INPUT "CENT=0 SEXA=1 ELIGE=";B
2910 IF B=0 THEN RETURN
2920 IF B<>1THEN 2900
3000 SI=SGNL:L=ABSL:GR=INTL:MI=INT((L-GR)*100):SE=FRAC((L-GR)*100)*100
3010 LS=DEG(GR,MI,SE)
3020 L=SI*LS/.9:RETURN
4000 A=6378388:E2=0.00672267
4005 J=E2*(SINLC)^2:K=(1-J)^0.5:N=A/K:Q=(1-J)^1.5:RO=(A*(1-E2))/Q
4010 SET F3:PRINT "N=";N;" RO=";RO
4020 RM=SQR(N*RO):RETURN
