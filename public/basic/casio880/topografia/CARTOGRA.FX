10 CLEAR
12 ANGLE 1
15 PRINT "CARTOGRAFIA"
16 INPUT "SEMIEJE A";A
18 INPUT "INV APLANAMIENTO W=";W
20 N=1/(2*W-1)
21 E=(2*W-1)/W^2
22 F=E/(1-E)
25 INPUT "UTM=1,LAMB=2,MERC=3 X=";X
27 IF X=2 THEN 750
28 IF X=3 THEN 1150
30 PRINT "COORDENADAS UTM"
51 G=PI*10/648
53 B=A*(1+1/8*N*N)^2/(1+N)
55 H=1-3/8*N*N
65 INPUT "DIRECTO=1,INVERSO=2 X=";X
67 IF X=2 THEN 400
70 INPUT "LATITUD L=";L
85 L=PI/180*L
90 INPUT "LOG M (OESTE-) M=";M
100 INPUT "MERD CENT(OESTE-) MC=";MC
103 M=M-MC
105 M=PI/180*M
140 C=A/SQR(1-E*(SINL)^2)
150 D=A*(1-E)/(1-E*(SINL)^2)^1.5
190 I1=B*(L-H*(1.5*N*SIN(2*L)-15/16*N^2*SIN(4*L)+35/48*N^3*SIN(6*L)))*0.9996
200 I2=B*315/512*N^4*SIN(8*L)*H*0.9996
210 I=I1+I2
220 II=C*SINL*COSL*G^2*0.4998
230 J=C*SINL*(COSL)^3*G^4*0.04165*(5-(TANL)^2+9*F*(COSL)^2+4*F^2*(COSL)^4)
240 IV=C*G*COSL*0.9996
250 V=C*G^3*0.1666*(COSL)^3*(1-(TANL)^2+F*(COSL)^2)
260 P=64.8*M/PI
310 B3=C*(G*(COSL)*P)^5
320 B4=(5-18*(TANL)^2+(TANL)^4+14*F*(COSL)^2-58*F*(SINL)^2)*0.9996/120
330 B5=B3*B4
340 A4=C*SINL*(COSL)^5*(P*G)^6*0.9996/120
350 A5=61-58*(TANL)^2+(TANL)^4+270*F*(COSL)^2-330*F*(SINL)^2
360 A6=A4*A5
370 X=500000+IV*P+V*P^3+B5
375 PRINT "X=";X
380 Y=I+II*P^2+J*P^4+A6
385 PRINT "Y=";Y
386 PRINT "CONVERG MERIDIANO"
388 U1=P*10^4*SINL+P^5*10^4*(G*COSL)^4*SINL*(2-(TANL)^2)/15
390 U2=P^3*10^4*(G*COSL)^2*SINL*(1+3*F*(COSL)^2+2*F^2*(COSL)^4/3
392 U3=(U1+U2)/3600
394 U4$=DMS$(U3)
396 PRINT U4$
398 GOTO 70
400 PRINT "INVERSO UTM"
410 INPUT "MERID CENT(OESTE-)MC=";MC
420 INPUT "X=";X
435 X=X-500000
440 INPUT "Y=";Y
460 K=Y/B/0.9996
470 L=K+(1.5*N-27/32*N^3)*SIN(2*K)+21/16*N^2*SIN(4*K)+151/96*N^3*SIN(6*K)
475 C=A/SQR(1-E*(SINL)^2)
480 V2=TANL*(1+F*(COSL)^2)*10^12/2/(C*0.9996)^2
490 V3=TANL*10^24/24/C^4/0.9996^4
495 V4=5+3*(TANL)^2+6*F*(COSL)^2-6*F*(SINL)^2-3*F^2*(COSL)^4-9*(F*COSL*SINL)^2
500 V8=V3*V4
510 Q=X/10^6
520 D1=(Q*10^6/C/0.9996)^6*TANL/720
522 D2=D1*(61+90*(TANL)^2+45*(TANL)^4+107*F*(COSL)^2)
523 D3=D1*(-162*F*(SINL)^2-45*F*(TANL*SINL)^2)
530 D6=D2+D3
532 E1=10^30*(5+28*(TANL)^2+24*(TANL)^4+6*F*(COSL)^2+8*F*(SINL)^2)*Q^5
533 E2=120*(C*0.9996)^5*COSL
535 E5=E1/E2
540 IX=10^6/C/COSL/0.9996
550 XI=10^18*(1+2*(TANL)^2+F*(COSL)^2)/COSL/6/C^3/0.9996^3
560 L1=(L-Q^2*V2+Q^4*V8-D6)*180/PI
565 LT$=DMS$(L1)
570 PRINT LT$
580 M1=(Q*IX-Q^3*XI+E5)*180/PI
590 MD$=DMS$(M1+MC)
600 PRINT MD$
620 PRINT "CONVER MERID XY UTM"
625 G=PI*10/648
630 U5=Q*10^10*TANL/C/G/0.9996
635 U6=10^22*TANL/G*(Q/(C*0.9996))^3/3*(1+(TANL)^2-F*(COSL)^2-2*F^2*(COSL)^4)
640 U7=10^34*TANL/G*(Q/(C*0.9996))^5/15*(2+5*(TANL)^2+3*(TANL)^4)
650 U8=(U5-U6+U7)/3600
660 U9$=DMS$(U8)
670 PRINT U9$
680 PRINT "ANAMORFOSIS LINEAL"
690 T=0.9996*(1+10^12*Q^2*(1+F*(COSL)^2)/2/C^2/0.9996^2+0.00003*Q^4)
700 PRINT T
710 GOTO 420
750 PRINT "COORDENADAS LAMBERT"
790 INPUT "LATITUD ORIGEN LO=";LO
810 LO=PI*LO/180
820 INPUT "LONG ORIGEN MO=";MO
840 MO=PI*MO/180
850 C=A/SQR(1-E*(SINLO)^2)
860 D=A*(1-E)/(1-E*(SINLO)^2)^1.5
861 R=C/TANLO
862 INPUT "DIRECTO=1,INVERSO=2,X=";X
864 IF X=2 THEN 1000
870 INPUT "LATITUD L=";L
890 L=PI*L/180
900 INPUT "LONGITUD M=";M
920 M=PI*M/180
940 YO=D*(L-LO)+D*D*(L-LO)^3/6/C
950 X=(R-YO)*SIN((M-MO)*SINLO)
960 PRINT "X=";X
970 Y=R-(R-YO)*COS((M-MO)*SINLO)
980 PRINT "Y=";Y
990 GOTO 870
1000 INPUT "X=";X
1020 INPUT "Y=";Y
1040 CM=ATN(X/(R-Y))
1050 M=MO+CM/SINLO
1060 MD$=DMS$(M*180/PI)
1070 PRINT "LONGITUD=";MD$
1080 YO=R-X/SINCM
1090 L=(LO+YO/D*(1-1/(3+6*C*D/YO^2)))*180/PI
1100 LD$=DMS$(L)
1110 PRINT "LATITUD=";LD$
1120 GOTO 1000
1150 PRINT "COORDENADAS MERCATOR"
1160 INPUT "DIRECTO=1,INVERSO=2,X=";X
1180 IF X=2 THEN 1300
1190 INPUT "LATITUD L=";L
1210 L=L*PI/180
1220 INPUT "LONGITUD(OESTE-) M=";M
1230 M=M*PI/180
1250 X=A*M
1260 PRINT "X=";X
1270 Y=A*LOG(TAN(PI/4+L/2))
1280 PRINT "Y=";Y
1290 GOTO 1190
1300 INPUT "X=";X
1320 INPUT "Y=";Y
1340 M=X*180/A/PI
1350 MD$=DMS$(M)
1360 PRINT "LONGITUD=";MD$
1370 L=360*ATN(EXP(Y/A))/PI-90
1380 LD$=DM$(L)
1390 PRINT "LATITUD=";LD$
1400 GOTO 1300
