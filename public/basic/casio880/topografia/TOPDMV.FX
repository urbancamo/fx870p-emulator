10 'OBRAS
20 PRINT "MEGACAM'95    (c) Dani Marti"; CHR$(9)
30 PRINT "1. COORDENADAS":PRINT "2. AZIMUT Y DISTANCIA":PRINT "3. CLOTOIDE"
40 PRINT "4. COORD. RECTA":PRINT "5. COORD. CURVA CIRCULAR"
50 PRINT "6. COORD. CLOT. ENTRADA":PRINT "7. COORD. CLOT. SALIDA"
60 PRINT "8. ALZADO-RASANTE":PRINT "9. ALZADO-ACUERDO"
70 BEEP0:INPUT "ELIGE (1...9)";ELI:ANGLE 2
80 ON ELI GOTO 260,90,380,560,640,750,870,1040,1100
90 PRINT "AZIMUT Y DISTANCIA"
100 PRINT "X1=";X1;
110 INPUT A:X1=A
120 PRINT "Y1=";Y1;
130 INPUT B:Y1=B
140 PRINT "X2=";X2;
150 INPUT C:X2=C
160 PRINT "Y2=";Y2;
170 INPUT D:Y2=D
180 IF A=C THEN IF D>B THEN PRINT "AZIMUT=0":GOTO 240
190 IF A=C THEN PRINT "AZIMUT=200":GOTO 240
200 T=ATN((D-B)/(C-A))
210 IF (C-A)>0 THEN 230
220 PRINT "AZIMUT=";ROUND(300-T,-5):GOTO 240
230 PRINT "AZIMUT=";ROUND(100-T,-5)
240 PRINT "DISTANCIA=";ROUND(SQR((C-A)^2+(D-B)^2),-4)
250 GOTO 90
260 PRINT "COORDENADAS"
270 PRINT "X1=";XA;
280 INPUT E:XA=E
290 PRINT "Y1=";YA;
300 INPUT F:YA=F
310 INPUT "AZIMUT";AZ
320 INPUT "DISTANCIA";DI
330 XB=XA+DI*SINAZ
340 YB=YA+DI*COSAZ
350 PRINT "X2=";XB
360 PRINT "Y2=";YB
370 GOTO 260
380 PRINT "CLOTOIDE":ANGLE 2
390 INPUT "R";R,"L";L,"l";N
400 X=N-N^5/(10*(2*R*L)^2)+N^9/(216*(2*R*L)^4)-N^13/(9360*(2*R*L)^6)
410 Y=N^3/(3*(2*R*L))-N^7/(42*(2*R*L)^3)+N^11/(1320*(2*R*L)^5)-N^15/(75600*(2*R*L)^ 7)
420 A=SQR(R*L):SIG=ATN(Y/X):SL=SQR(X^2+Y^2)
430 TAU=L/2/R:TAUGRAD=TAU*200/PI
440 X0=X-R*SINTAUGRAD:AR=R*COSTAUGRAD+Y-R:Y0=AR+R
450 TC=Y/SINTAUGRAD:TL=X-(Y/TANTAUGRAD)
460 PRINT "A=";A
470 PRINT CHR$(203);"=";TAU;"rad"
480 PRINT CHR$(203);"=";TAUGRAD;"centesimal"
490 PRINT "X=";X,"Y=";Y
500 PRINT "SL=";SL:PRINT CHR$(142);"=";SIG
510 PRINT "X0=";X0:PRINT "Y0=";Y0
520 PRINT CHR$(238);"R=";AR
530 PRINT "Tl=";TL:PRINT "Tc=";TC:GOTO 380
560 PRINT "COORD. RECTA"
570 INPUT "AZ";AZ,"PK inicial";PK,"X";XIN,"Y";YIN,"PK PUNTO 1";PKIN,"INTERVALO";PAS
580 P=PKIN:GOTO 600
590 P=P+PAS
600 XP=XIN+(P-PK)*SINAZ
610 YP=YIN+(P-PK)*COSAZ
620 PRINT "PK";P:PRINT "XP=";ROUND(XP,-4):PRINT "YP=";ROUND(YP,-4)
630 GOTO 590
640 PRINT "COORD.C.CIRCULAR";CHR$(9)
650 INPUT "AZ ENTRADA";AZE,"GIRO (Izq/Der)";GIR$,"R";R,"L";L,"PK inicial";PK,"X";XIN,"Y";YIN,"PK PUNTO 1";PKIN,"INTERVALO";PAS
660 PKF=PKIN +L
670 FOR P=PKIN TO PKF STEP PAS
680 N=P-PK:ALP=N*200/R/PI:BET=ALP/2:C=2*R*SINBET
690 IF GIR$="I" THEN 710
700 AZ=AZE+BET:AT=AZE+ALP:GOTO 720
710 AZ=AZE-BET:AT=AZE-ALP
720 XP=XIN+C*SINAZ:YP=YIN+C*COSAZ
730 PRINT "PK";P:PRINT "AZ=";ROUND(AT,-5):PRINT "XP=";ROUND(XP,-4):PRINT "YP=";ROUND(YP,-4)
740 NEXT P:END
750 PRINT "COORD.CLOT.ENTRADA";CHR$(9)
760 INPUT "AZ ENTRADA";AZE,"GIRO (Izq/Der)";GIR$,"R";R,"L";L,"PK inicial";PK,"X";XIN,"Y";YIN,"PK PUNTO 1";PKIN,"INTERVALO";PAS
770 PKF=PKIN+L:A=SQR(R*L)
780 FOR P=PKIN TO PKF STEP PAS
790 N=P-PK:GOSUB 1010
800 SIG=ATN(Y/X):SL=SQR(X^2+Y^2):RD=A^2/N:TAU=100*N/RD/PI
810 IF GIR$="I" THEN 830
820 AZ=AZE+SIG:AT=AZE+TAU:GOTO 840
830 AZ=AZE-SIG:AT=AZE-TAU
840 XP=XIN+SL*SINAZ:YP=YIN+SL*COSAZ
850 PRINT "PK";P:PRINT "AZ=";ROUND(AT,-5):PRINT "XP=";ROUND(XP,-4):PRINT "YP=";ROUND(YP,-4)
860 NEXT P:END
870 PRINT "COORD.CLOT.SALIDA";CHR$(9)
880 INPUT "AZ ENTRADA";AZE,"GIRO (Izq/Der)";GIR$,"R";R,"L";L:N=L:GOSUB 1010:XF=X:YF=Y
890 INPUT "PK inicial";PK,"X";XIN,"Y";YIN,"PK PUNTO 1";PKIN,"INTERVALO";PAS
900 PKF=PKIN+L:A=SQR(R*L)
910 FOR P=PKIN TO PKF STEP PAS
920 N=PK+L-P:GOSUB 1010
930 LAN=ATN((YF-Y)/(XF-X)):SL=SQR((XF-X)^2+(YF-Y)^2)
940 RD=A^2/N:TAU=L*100/R/PI:SIG=TAU-LAN:TAUP=N*100/RD/PI
950 IF GIR$="I" THEN 970
960 AZ=AZE+SIG:AT=AZE+TAU-TAUP:GOTO 980
970 AZ=AZE-SIG:AT=AZE-TAU+TAUP
980 XP=XIN+SL*SINAZ:YP=YIN+SL*COSAZ
990 PRINT "PK";P:PRINT "AZ=";ROUND(AT,-5):PRINT "XP=";ROUND(XP,-4):PRINT "YP=";ROUND(YP,-4)
1000 NEXT P:END
1010 X=N-N^5/(10*(2*R*L)^2)+N^9/(216*(2*R*L)^4)-N^13/(9360*(2*R*L)^6)
1020 Y=N^3/(3*(2*R*L))-N^7/(42*(2*R*L)^3)+N^11/(1320*(2*R*L)^5)-N^15/(75600*(2*R*L)^7)
1030 RETURN
1040 PRINT "ALZADO-RASANTE"
1050 INPUT "PK inicial";PKIN,"COTA";COTA,"%P";PD,"L";L,"PK PTO 1";PK,"INTERVALO";PAS
1060 FOR LT=PK TO (PKIN+L) STEP PAS
1070 ZP=COTA+(LT-PKIN)*PD/100
1080 PRINT "PK";LT:PRINT "ZP=";ROUND(ZP,-4)
1090 NEXT LT:END
1100 PRINT "ALZADO-ACUERDO";CHR$(9)
1110 INPUT "PK inicial";PKIN,"COTA";COTA,"%P";PD,"K";K,"L";L,"GIRO (I/D)";INK$,"PK PTO 1";PK,"INTERVALO";PAS
1120 FOR LT=PK TO (PKIN+L) STEP PAS
1130 PRR=COTA+(LT-PKIN)*PD/100:Y=(LT-PKIN)^2/2/K:DY=(LT-PKIN)/K
1140 IF INK$="I" THEN 1160
1150 PRC=PRR-Y:PTE=PD-DY*100:GOTO 1170
1160 PRC=PRR+Y:PTE=PD+DY*100
1170 PRINT "PK";LT:PRINT "ZP=";ROUND(PRC,-4):PRINT "%P=";ROUND(PTE,-6)
1180 NEXT LT:END
