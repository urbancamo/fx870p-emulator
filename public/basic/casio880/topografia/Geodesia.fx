1 MODE6:CLEAR :SET N
2 CLS:LOCATE0,1:PRINT"1)Poligonal    3)Nivelacion";
3 LOCATE0,0:PRINT"0)Cartera Alt  2)Conversion Coor";
4 IF INKEY$=""THEN 4
5 X$=INKEY$
6 IFX$="0"THEN100ELSEIFX$="1"THEN3000ELSEIFX$="2"THEN900ELSEIFX$="3"THEN300ELSE5
100 MODE6:CLS:PRINT "      Cartera Altimetrica "
110 CLEAR :CLS
115 INPUT "Nro. Vertices = ";N
120 /2E-7
121 DIM HI(N,2),HJ(N,2),Z(N,2),DI(N,2),DH(N,2),DD(N,2),CO(N),DDP(N),DHP(N),CT(N),CON(N)
122 INPUT "Cota Base = ";CP:CT(O)=CP
137 FOR I=1 TO N
138 FOR J=1 TO 2
139 CLS:LOCATE0,0:IF J=1 THEN PRINT I;")  1 ==> 2";:GOTO 142
140 CLS:LOCATE0,0:IF J=2 THEN PRINT I;")  2 ==> 1";
142 LOCATE0,1:INPUT " Hi = ",HI(I,J)," Hj = ",HJ(I,J),"Zenital=",Z(I,J)," Di = ",DI(I,J)
143 HI=HI(I,J):HJ=HJ(I,J):Z=Z(I,J):DI=DI(I,J)
145 DH(I,J)=DI*SINZ:DH=DH(I,J)
146 DD(I,J)=HI-HJ+(DH/TANZ)+(6.66E-8)*(DH/SINZ)^2:DD=DD(I,J)
147 CLS:PRINT " Dh = ";DH
148 IF J=2 THEN  DDP(I)=((DD(I,J-1))+(DD(I,J)*-1))/2
149 IF J=2 THEN DHP(I)=(DH(I,J)+DH(I,J-1))/2
150 PRINT " Dfn = ";DD
151 CLS:IF J=2 THEN PRINT "Dh prom =";DHP(I):CON(I)=CON+DHP(I):CON=CON(I)
152 IF J=2 THEN PRINT "Dfn prom =";DDP(I)
153 IF J=2 THEN CT(I)=CT(I-1)+DDP(I)
154 IF J=2 THEN PRINT "Cota Pto.=";CT(I)
155 IF J=2 THEN PRINT "Dh acum. =";CON(I)
156 NEXT J
157 NEXT I
160 CLS:PRINT "     Correccion de cotas "
165 DIM CF(N),DG(N):R=6371229
170 CLS:PRINT "Cota Partida <";CP;">";:LOCATE13,1:PRINT ":";:INPUT CP
180 CLS:PRINT "Cota Llegada <";CT(N);">";:LOCATE13,1:PRINT ":";:INPUT CT(N)
185 PRINT "Dh total acum :";CON(N)
186 CLS:PRINT "R. Curv.<";R;">";:INPUT R
190 CT=CP-CT(N)
200 CLS:PRINT "Error =";CT
205 CPC=CT/CON(N)
210 PRINT "Cpc.=";CPC
215 FOR I=1 TO N
220 CLS:PRINT "Dh prom (";I;"):";DHP(I)
230 PRINT "Cota   :";CT(I)
240 CORR=CON(I)*CPC
250 CF(I)=CT(I)+CORR
255 PRINT "Correccion :";CORR
260 PRINT "Cota corr.:";CF(I)
265 NEXT I
268 CLS:FOR I=1 TO N
270 HM=(CF(I)+CF(I-1))/2
280 DG(I)=DHP(I)-(DHP(I)*HM*(1/R))
290 PRINT "Dg (";I;"):";DG(I)
300 NEXT I
350 STOP:GOTO 1
900 CLEAR:CLS:LOCATE0,1:PRINT"2: UTM a Geograficas";
920 LOCATE0,0:PRINT"1: Geograficas a UTM";
940 IFINKEY$=" "THEN940
950 S$=INKEY$:IFS$="1"THEN960ELSEIFS$="2"THEN960ELSE950
960 CLS:PRINT"   1: PSAD-56    2: WGS-84 ";
965 IFINKEY$=" "THEN965
970 W$=INKEY$:IFW$="1"THEN980ELSEIFW$="2"THEN985ELSE970
980  ANGLE1:CLS:a=6378388:b=6356912:E=(a^2-b^2)/a^2:GOTO 990
985  ANGLE1:CLS:a=6378137:b=6356752.31:E=(a^2-b^2)/a^2:GOTO 990
990 IFS$="1"THEN1000ELSEIFS$="2"THEN2000ELSE990
1000 CLS:PRINT" * * Coor.Geograficas a UTM * *";:FORI=1TO400:NEXTI
1004 CLS:LOCATE1,0:PRINT" a :";a;:LOCATE17,0:SET F8:PRINT "e’:";E;
1005 SET N:LOCATE1,1:PRINT" b :";b
1006 CLS:SETF0:LOCATE1,0:PRINT"Lat.pto=";
1008 LOCATE13,0:PRINTCHR$(223);
1010 LOCATE18,0:PRINTCHR$(39);
1012 LOCATE1,1:INPUT"Grados:",Q
1014 LOCATE9,0:PRINTQ;:FORI=1TO10:NEXTI
1016 LOCATE0,1:PRINT"              ";:FORI=1TO10:NEXTI
1018 LOCATE1,1:INPUT"Minutos:",R
1020 LOCATE14,0:PRINTR;:FORI=1TO10:NEXTI
1022 LOCATE0,1:PRINT"              ";:FORI=1TO10:NEXTI
1024 SETF3:LOCATE1,1:INPUT"Segundos:",S
1026 LOCATE19,0:PRINTS;
1028 LOCATE0,1:PRINT"               ";:FORK=1TO300:NEXTK
1030 CLS:SETF0:LOCATE1,0:PRINT"Long.pto=";
1032 LOCATE14,0:PRINTCHR$(223);
1034 LOCATE19,0:PRINTCHR$(39);
1036 LOCATE1,1:INPUT"Grados:",X
1038 LOCATE10,0:PRINTX;:FORI=1TO10:NEXTI
1040 LOCATE0,1:PRINT"              ";:FORI=1TO10:NEXTI
1042 LOCATE1,1:INPUT"Minutos:",Y
1044 LOCATE15,0:PRINTY;:FORI=1TO10:NEXTI
1046 LOCATE0,1:PRINT"              ";:FORI=1TO10:NEXTI
1048 SETF3:LOCATE1,1:INPUT"Segundos:",Z
1050 LOCATE20,0:PRINTZ;
1052 LOCATE0,1:PRINT"               ";:FORK=1TO300:NEXTK
1054 SETF9:Q=DEG(Q,R,S)/(180/PI):R=DEG(X,Y,Z)
1056 CLS:LOCATE8,0:INPUT"Meri.Central=",G
1057 CLS:LOCATE4,0:PRINT"   espere ...... ";:FORk=1TO50:NEXTk
1058 F=E/(1-E):K=.9996:N=a/SQR(1-E*SINQ^2):T=TANQ^2
1060 C=F*COSQ^2:A=COSQ*(R-G)*PI/180
1065 H=(1-E/4-3*E^2/64-5*E^3/256)*Q
1070 I=(3*E/8+3*E^2/32+45*E^3/1024)*SIN(2*Q)
1080 J=(15*E^2/256+45*E^3/1024)*SIN(4*Q)
1090 M=a*(H-I+J-((35*E^3/3072)*SIN(6*Q)))
1100 H=A^2/2+(5-T+9*C+4*C^2)*A^4/24
1110 I=H+(61-58*T+T^2+600*C-330*F)*A^6/720
1120 Y=K*(M+N*TANQ*I):IFQ>0THEN1140
1140 H=A+(1-T+C)*A^3/6+(5-18*T+T^2+72*C-58*F)*A^5/120
1150 D=5*10^5-(K*N*H)
1155 Y=10^7-Y
1157 CLS:LOCATE5,1:PRINT"** Norte y Este UTM **";:FORI=1TO10:NEXTI
1160 SETF3:LOCATE0,0:PRINT"N=";Y;:LOCATE18,0:PRINT"E=";D
1300 CLS:GOTO 1
2000 CLS:PRINT" * * Coor.UTM a Geograficas * *";:FORI=1TO400:NEXTI
2004 CLS:LOCATE1,0:PRINT" a :";a;:LOCATE17,0:SET F8:PRINT "e’:";E;
2005 SET N:LOCATE1,1:PRINT" b :";b
2020 CLS:LOCATE0,0:INPUT"Norte UTM= ",Q:LOCATE1,1:INPUT"Este UTM= ",S
2030 CLS:LOCATE0,0:INPUT"Meridiano Central= ",G:CLS:LOCATE10,0:PRINT"Hemisferio                         N o S ?";
2031 IFINKEY$=" "THEN2031
2032 S$=INKEY$
2033 IFS$="S"THEN2035ELSEIFS$="N"THEN2230ELSE2032
2035 CLS:LOCATE4,0:PRINT"espere ....";:FORk=1TO50:NEXTk
2040 ANGLE1:S=S-5*10^5
2045 Q=Q-10^7
2050 K=.9996:M=Q/K:U=M/(a*(1-E/4-3*E^2/64-5*E^3/256))
2060 B=(1-(1-E)^.5)/(1+(1-E)^.5)
2070 H=U+(3*B/2-27*B^3/32)*SIN(2*U)
2080 I=(21*B^2/16-55*B^4/32)*SIN(4*U)
2090 Q=(H+I+(151*B^3/96)*SIN(6*U))*180/PI
2100 ANGLE0:F=E/(1-E):C=F*COSQ^2:N=a/(1-E*SINQ^2)^.5:T=TANQ^2
2110 R=a*(1-E)/(1-E*SINQ^2)^1.5:D=S/(N*K)
2120 H=D^2/2-(5+3*T+10*C-4*C^2-9*F)*D^4/24
2130 I=(61+90*T+298*C+45*T^2-252*F-3*C^2)*D^6/720
2140 J=Q-(N*TANQ/R)*(H+I)*180/PI
2150 H=5-2*C+28*T-3*C^2+8*F+24*T^2
2160 L=G-(D-(1+2*T+C)*D^3/6+H*D^5/120)/COSQ*180/PI
2170 CLS:SETF3:LOCATE3,0:PRINT"Latitud = ";DMS$(J);:LOCATE3,1:PRINT"Longitud= ";DMS$(L)
2210 GOTO 1
2230 CLS:LOCATE4,0:PRINT " espere .....";:FORk=1TO50:NEXTk
2240 GOTO 2045
3000 CLEAR :SET N
3015 CLS:PRINT " **  Compensacion Poligonal  **"
3016 CLS:INPUT "  Ang ext [1]    Ang int [2]";P
3018 IF P=1 THEN 3020
3019 IF P=2 THEN 3020 ELSE 3016
3020 CLS:INPUT "Nro Vertices:",N
3030 DIM A(N)
3035 CLS
3040 FOR I=1 TO N
3050 PRINT "Ang Horz.(";I;"):";:INPUT A(I)
3060 CO=CO+A(I)
3070 NEXT I
3075 HH=1:UTM=0
3080 CLS:PRINT "„ ang.=";CO
3090 IF P=2 THEN S=200*(N-2):GOTO 3110
3100 IF P=1 THEN S=200*(N+2):GOTO 3110
3110 CT=S-CO
3120 PRINT "Ct.=";CT
3125 CP=CT/N
3130 PRINT "Cp.=";CP
3131 CLS
3135 FOR J=1 TO N
3155 A(J)=A(J)+CP
3160 SET N:PRINT "Ang.Horz.(";J;:SET F6:PRINT "):";A(J)
3165 SS=SS+A(J)
3170 NEXT J
3180 SET N:CLS:PRINT "„ ang.=";SS
3200 CLS:PRINT "    Coordenadas Parciales   "
3205 DIM X(N),Y(N),x(N),y(N),N(N),E(N),DG(N),AZ(N)
3207 CLS:INPUT "Azimut ( 1 )=";AZ(1)
3210 CLS:FOR I=2 TO N
3211 AZ(I)=AZ(I-1)+A(I)
3213 IF AZ(I)<200 THEN 3220
3215 IF AZ(I)>200 THEN 3222
3220 AZ(I)=AZ(I)+200
3221 GOTO 3224
3222 AZ(I)=AZ(I)-200
3223 GOTO 3224
3224 IF AZ(I)>400 THEN AZ(I)=AZ(I)-400
3225 PRINT "Azimut (";I;")=";AZ(I)
3226 NEXT I
3227 CX=0:CY=0:CXX=0:CYY=0
3228 CLS:FOR I=1 TO N
3229 LOCATE0,0:PRINT "Az.(";I;")=<";AZ(I);">";:LOCATE0,1:PRINT "Az.(";I;")=";:INPUT AZ(I)
3230 IF UTM=101 THEN 3231 ELSE IF UTM=0 THEN 3235
3231 PRINT "D. UTM (";I;")=";:INPUT DG(I)
3232 GOTO 3240
3235 PRINT "D. Geod(";I;")=";:INPUT DG(I)
3240 X(I)=DG(I)*COSAZ(I):x(I)=X(I)
3245 CLS:PRINT "x (";I;")=";X(I)
3250 Y(I)=DG(I)*SINAZ(I):y(I)=Y(I)
3255 PRINT "y (";I;")=";Y(I)
3260 CX=CX+X(I):CXX=CXX+ABS(X(I))
3270 CY=CY+Y(I):CYY=CYY+ABS(Y(I))
3280 CPX=CX/CXX:CPY=CY/CYY
3300 CLS:NEXT I
3375 CLS:PRINT "„ x =";CX,"„|x|=";CXX
3376 PRINT "Cpx =";(CPX*-1)
3378 CLS:PRINT "„ y =";CY,"„|y|=";CYY
3390 PRINT "Cpy =";(CPY*-1)
3399 CX=0:CY=0
3400 FOR I=1 TO N
3410 x(I)=x(I)+(ABS(x(I))*(CPX*-1))
3420 y(I)=y(I)+(ABS(y(I))*(CPY*-1))
3430 CLS:PRINT "x'(";I;")=";x(I)
3440 CLS:PRINT "y'(";I;")=";y(I)
3450 CX=CX+x(I)
3460 CY=CY+y(I)
3465 NEXT I
3470 CLS:PRINT "„ x'=";CX,"„ y'=";CY
3500 CLS:INPUT "Norte Base:";N(0):NN=N(0)
3510 INPUT "Este  Base:";E(0):E=E(0)
3520 FOR I=1 TO N
3530 N(I)=x(I)+NN
3540 E(I)=y(I)+E
3550 CLS:PRINT "Norte Pto.(";I;"):";N(I);
3560 LOCATE0,1:PRINT "Este  Pto.(";I;"):";E(I)
3570 E=E(I):NN=N(I)
3580 NEXT I
3590 IF HH=2 THEN RETURN ELSE IF HH=1 THEN 3600
3600 CLS:PRINT "          Factor k "
3610 XV=0.012355:KO=0.9996:EF=500000:DIM EE(N),DU(N)
3612 PRINT "(xviii)<";XV;">=";:INPUT XV
3615 FOR I=1 TO N
3620 EE(I)=(E(I-1)+E(I))/2:EE=EE(I)-EF:q=0.000001*EE
3630 K=KO*(1+XV*q^2+0.00003*q^4)
3640 CLS:PRINT " k (";I;")=";K
3645 DU(I)=K*DG(I)
3647 PRINT "D.UTM (";I;")=";DU(I)
3650 NEXT I
3700 REM t-T
3702 DIM tT12(N),tT21(N),APL(N)
3705 CLS:PRINT "      (t-T) obs.  [seg.]"
3720 CLS
3730 FOR I=1 TO N
3734 DN=0:EF=500000
3735 DN=N(I)-N(I-1)
3736 E1=E(I-1)-EF
3737 E2=E(I)-EF
3740 tT12(I)=(-1*DN*((2*E1)+E2)*XV*(6.8755E-8)*3.0864)/10000
3750 PRINT "(t-T)obs(";I;")=";(tT12(I))*10000
3760 NEXT I
3765 CLS:PRINT "     (t-T) org.[seg.]    "
3770 DN=0:DN=N(N-1)-N(0)
3780 E1=E(0)-EF
3782 E2=E(N-1)-EF
3785 tT21(1)=(-1*DN*((2*E1)+E2)*XV*(6.8755E-8)*3.0864)/10000
3790 PRINT "(t-T)org( 1 )=";tT21(1)*10000
3800 FOR I=2 TO N
3805 DN=N(I-2)-N(I-1)
3810 E1=E(I-1)-EF
3815 E2=E(I-2)-EF
3820 tT21(I)=(-1*DN*((2*E1)+E2)*XV*(6.8755E-8)*3.0864)/10000
3830 PRINT "(t-T)org(";I;")=";tT21(I)*10000
3840 NEXT I
3850 CLS:PRINT "         Angulo Plano":CO=0:CT=0:S=0:CP=0:RR=0:SS=0
3855 FOR I=1 TO N
3860 APL(I)=A(I)-((tT21(I))-(tT12(I)))
3865 PRINT "A. plano(";I;")=";APL(I)
3870 CO=CO+APL(I)
3875 NEXT I
3876 FOR I=1 TO N
3877 A(I)=APL(I)
3878 NEXT I
3880 HH=2
3885 UTM=101
3890 GOSUB 3080
3900 CLS:END
