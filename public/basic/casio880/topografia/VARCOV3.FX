10 'VARIANZAS-COVARIANZAS
20 CLEAR
30 CLS:PRINT "CALCULO DE LAS VARIANZAS Y COVARIANZAS"
40 CLS:INPUT "NUMERO DE VISUALES";N
50 CLS:INPUT "NUMERO DE OBSERVACIONES";M
60 DIM A(M,4*N),C(N),D(N),E(N)
70 DIM B$(N+1):CLS
80 FOR I=1 TO N
90 PRINT "NOMBRE DE "I"VISUAL";:INPUT B$(I)
100 CLS:NEXT:B$(N+1)=B$(1)
110 CLS:PRINT "INTRODUCE OBSERVACIONES"
120 CLS:FOR J=1 TO N
130 FOR I=1 TO M
140 PRINT "INTRODUCE LA OBSERVACION "I"DE LA VISUAL    "B$(J);:INPUT A(I,J):NEXT I:NEXT J
141 INPUT "CORREGIR DATOS(S/N)";F$
142 IF F$="N" THEN 146
143 INPUT "I";I:INPUT "J";J
144 PRINT "A("I","J")=";:INPUT A(I,J)
145 GOTO 141
146 INPUT "VER DATOS (S/N)";W$
147 IF W$="N" THEN 155
148 FOR J=1 TO N:FOR I=1 TO M
149 PRINT "A("I","J")="A(I,J):NEXT I:NEXT J
150 INPUT "ALGUNA EQUIVOCACION (S/N)";F$:GOTO 142
155 CLS:PRINT "CALCULO DE LOS RESIDUOS"
160 S=0: FOR J=1 TO N
170 FOR I=1 TO M
180 S=S+A(I,J):NEXT I
190 C(J)=S:S=0:NEXT J
200 FOR J=1 TO N
210 C(J)=C(J)/M
220 PRINT "MEDIA DE "B$(J)"="C(J)
230 NEXT J
240 FOR J=1 TO N:FOR I=1 TO M
250 A(I,N+J)=C(J)-A(I,J)
260 NEXT I:NEXT J
261 INPUT "QUIERES LOS RESIDUOS EN UNIDADES DIFERENTES (S/N)";P$
262 IF P$="N" THEN 270
263 INPUT "(A)=NUMERO DE CAMBIO;ejemplo 1E4 de gr a dmgr";R
264 FOR J=1 TO N:FOR I=1 TO M
265 A(I,N+J)=A(I,N+J)*R
266 NEXT I:NEXT J
270 FOR J=1 TO N
280 FOR I=1 TO M
290 PRINT "RESIDUO "B$(J)"("I")="A(I,N+J)
300 NEXT I:NEXT J
310 CLS:PRINT "RESIDUOS AL CUADRADO"
320 CLS:FOR J=1 TO N:FOR I=1 TO M
330 A(I,J+2*N)=A(I,J+N)^2
340 NEXT I:NEXT J
350 FOR J=1 TO N:FOR I=1 TO M
360 PRINT "RESIDUO^2 DE "B$(J)" DE LA OBSERV "I" ="A(I,2*N+J)
370 NEXT I:NEXT J
380 S=0:FOR J=1 TO N:FOR I=1 TO M
390 S=S+A(I,J+2*N):NEXT I
400 D(J)=S:S=0:NEXT J
410 FOR J=1 TO N
420 D(J)=SQR(D(J)/(M-1)):NEXT
430 CLS:PRINT "VARIANZAS"
440 FOR J=1 TO N
450 PRINT "VARIANZA "B$(J)"="D(J):NEXT
460 CLS:PRINT "CALCULO DE LAS COVARIANZAS"
470 FOR J=1 TO N-1 :FOR I=1 TO M
480 A(I,3*N+J)=A(I,J+N)*A(I,J+N+1)
490 NEXT I,J
500 FOR I=1 TO M
510 A(I,4*N)=A(I,N+1)*A(I,2*N):NEXT
520 FOR J=1 TO N:FOR I=1 TO M
530 PRINT "RESIDUO DE "B$(J)"*"B$(J+1)"  FILA"I"="A(I,J+3*N)
540 NEXT I,J
550 S=0:FOR J=1 TO N:FOR I=1 TO M
560 S=S+A(I,3*N+J):NEXT I
570 E(J)=S:S=0:NEXT J
580 FOR J=1 TO N
590 E(J)=E(J)/(M-1):NEXT
600 FOR J=1 TO N
610 PRINT "LA COVARIANZA "B$(J)"-"B$(J+1)"="E(J)
620 NEXT
630 CLS:PRINT "FIN"
