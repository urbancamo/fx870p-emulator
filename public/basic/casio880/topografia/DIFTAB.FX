1 'DIFTAB
2 INPUT "*DIFERENCIAS TABULARES(1),VARIANZAS-COVARIANZAS(2)";R$
3 IF R$="1" GOTO 20
4 IF R$="2" GOTO 1010
20 PRINT "*DIFERENCIAS TABULARES*":CLEAR
30 CLS: PRINT "SEX. O CEN.(S/C)";A$
40 A$=INKEY$:IF A$="" THEN 40
50 IF A$="C" THEN GR=1 ELSE IF A$<>"S" THEN 40
60 G=180*(1-GR)+200*GR:P=PI/180
70 CLS :PRINT "LOG.DEC. O NEP.(D/N)";:BB=1
80 A$=INKEY$:IF A$="" THEN 80
90 IF A$="N" THEN 110
100 BB=1/LN(10)
110 CLS:PRINT "INCR. DE RAD/GRD/MIN/SEG.?";
120 B$=INKEY$:IF B$="" THEN 120
130 IF B$="R" THEN A=1: RO=1:GOTO 190
140 IF B$="G" THEN A=2:GOTO 170
150 IF B$="M" THEN A=3:GOTO 170
160 IF B$="S" THEN A=4 ELSE 120
170 X=10^((2-A)*2):GOSUB 280
180 RO=P*HR
190 CLS:INPUT "ANGULO";AN
200 X=AN:GOSUB 280
210 CLS:PRINT "LOG(SIN(";AN;"))=";LN(SIN(HR))*BB
220 PRINT "DIF. TAB.=";RO*BB/TAN(HR)
230 PRINT "REP.NEW.END.(R/N/E)?"
240 C$=INPUT$(1):IF C$="" THEN 240
250 IF C$="N" THEN CLEAR :GOTO 30
260 IF C$="R" THEN 210
270 IF C$="E" THEN CLEAR :END
280 HR=GR*.9*X+(1-GR)*(INTX+INT(100*FRACX)/60+FRAC(100*FRACX)/36)
290 RETURN
1010 'VARIANZAS-COVARIANZAS
1020 CLEAR
1030 CLS:PRINT "CALCULO DE LAS VARIANZAS Y COVARIANZAS"
1040 CLS:INPUT "NUMERO DE VISUALES";N
1050 CLS:INPUT "NUMERO DE OBSERVACIONES";M
1060 DIM A(M,4*N),C(N),D(N),E(N)
1070 DIM B$(N+1):CLS
1080 FOR I=1 TO N
1090 PRINT "NOMBRE DE"I"VISUAL";:INPUT B$(I)
1100 CLS:NEXT :B$(N+1)=B$(1)
1110 CLS:PRINT "INTRODUCE OBSERVACIONES"
1120 CLS:FOR J=1 TO N
1130 FOR I=1 TO M
1140 PRINT "INTRODUCE LA OBSERVACION"I"DE LA VISUAL "B$(J);:INPUT A(I,J):NEXT I:NEXT J
1141 INPUT "CORREGIR DATOS (S/N)";F$
1142 IF F$="N" THEN 1146
1143 INPUT "I";I:INPUT "J";J
1144 PRINT "A("I","J")=";:INPUT A(I,J)
1145 GOTO 1141
1146 INPUT "VER DATOS (S/N)";W$
1147 IF W$="N" THEN 1155
1148 FOR J=1 TO N:FOR I=1 TO M
1149 PRINT "A("I","J")="A(I,J):NEXT I:NEXT J
1150 INPUT "ALGUNA EQUIVOCACION (S/N)";F$:GOTO 1142
1155 CLS:PRINT "CALCULO DE LOS RESIDUOS"
1160 S=0:FOR J=1 TO N
1170 FOR I=1 TO M
1180 S=S+A(I,J):NEXT I
1190 C(J)=S:S=0:NEXT J
1200 FOR J=1 TO N
1210 C(J)=C(J)/M
1220 PRINT "MEDIA DE "B$(J)"="C(J)
1230 NEXT J
1240 FOR J=1 TO N:FOR I=1 TO M
1250 A(I,N+J)=C(J)-A(I,J)
1260 NEXT I:NEXT J
1261 INPUT "QUIERES LOS RESIDUOS EN UNIDADES DIFERENTES (S/N)";P$
1262 IF P$="N" THEN 1270
1263 INPUT "(A)=NUMERO DE CAMBIO;EJEMPLO 1E4 DE GR A DMGR";R
1264 FOR J=1 TO N:FOR I=1 TO M
1265 A(I,N+J)=A(I,N+J)*R
1266 NEXT I:NEXT J
1270 FOR J=1 TO N
1280 FOR I=1 TO M
1290 PRINT "RESIDUO "B$(J)"("I")="A(I,N+J)
1300 NEXT I:NEXT J
1310 CLS:PRINT "RESIDUOS AL CUADRADO"
1320 CLS:FOR J=1 TO N:FOR I=1 TO M
1330 A(I,J+2*N)=A(I,J+N)^2
1340 NEXT I:NEXT J
1350 FOR J=1 TO N:FOR I=1 TO M
1360 PRINT "RESIDUO^2 DE "B$(J)" DE LA OBSEVACION"I"="A(I,2*N+J)
1370 NEXT I:NEXT J
1380 S=0:FOR J=1 TO N:FOR I=1 TO M
1390 S=S+A(I,J+2*N):NEXT I
1400 D(J)=S:S=0:NEXT J
1410 FOR J=1 TO N
1420 D(J)=(D(J)/(M-1)):NEXT
1430 CLS:PRINT "VARIANZAS (ERROR STANDAR=RAIZ VARIANZAS)"
1440 FOR J=1 TO N
1450 PRINT "VARIANZA "B$(J) "="D(J):NEXT
1460 CLS:PRINT "CALCULO DE LAS COVARIANZAS"
1470 FOR J=1 TO N-1:FOR I=1 TO M
1480 A(I,3*N+J)=A(I,J+N)*A(I,J+N+1)
1490 NEXT I,J
1500 FOR I=1 TO M
1510 A(I,4*N)=A(I,N+1)*A(I,2*N):NEXT
1520 FOR J=1 TO N:FOR I=1 TO M
1530 PRINT "RESIDUO DE "B$(J)"*"B$(J+1)" FILA"I"="A(I,J+3*N)
1540 NEXT I,J
1550 S=0:FOR J=1 TO N:FOR I=1 TO M
1560 S=S+A(I,3*N+J):NEXT I
1570 E(J)=S:S=0:NEXT J
1580 FOR J=1 TO N
1590 E(J)=E(J)/(M-1):NEXT
1600 FOR J=1 TO N
1610 PRINT "LA COVARIANZA "B$(J)"-"B$(J+1) "="E(J)
1620 NEXT
1630 CLS:PRINT "FIN"
