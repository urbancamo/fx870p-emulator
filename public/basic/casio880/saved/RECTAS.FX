10 ANGLE2:CLS:PRINT "(1) Recta def. por dos puntos","(2) Interseccion de dos rectas","(3) Min.dist pto y recta(2 ptos)(4) [MAS...]";
20 GOSUB "K$":IF K$="1" GOTO 70
30 IF K$="2" GOTO 160
40 IF K$="3" GOTO 200
50 IF K$="4" GOTO 410
60 GOTO 20
70 CLS:A$="* Recta definida por dos ptos *":PRINT A$
80 INPUT "X1>",X1,"Y1>",Y1,"X2>",X2,"Y2>",Y2
90 M=X2-X1:N=Y1-Y2:S$="":S1$="":MM=X1*Y2-X2*Y1
100 IF SGNM=1 OR SGNM=0 THEN S$="+"
110 IF SGNMM=1 OR SGNMM=0 THEN S1$="+"
120 O$=STR$(N)+"x"+S$+STR$(M)+"y"+S1$+STR$(MM)+"=0"
130 IF LEN(O$)>32 THEN E$=CHR$(13):O$=STR$(N)+"x"+S$+STR$(M)+"y"+E$+"     "+S1$+STR$(MM)+"=0"
140 CLS:PRINT A$,O$
150 GOSUB "K$":GOTO 10
160 CLS:INPUT "X1>",X0,"Y1>",Y0,"X2>",X1,"Y2>",Y1:PRINT "******"
170 INPUT "X3>",x0,"Y3>",y0,"X4>",x1,"Y4>",y1
180 A=Y1-Y0:B=X0-X1:C=(X1*Y0-X0*Y1):a=y1-y0:b=x0-x1:c=(x1*y0-x0*y1):D=(A*b-a*B):X=(-C*b-B*-c)/D:Y=(A*-c+C*a)/D
190 CLS:PRINT "X="X"Y="Y;:GOSUB "K$":CLS:GOTO 10
200 'Minima distancia entre pto y recta definida por dos puntos
210 CLS:INPUT "X1>",X1,"Y1>",Y1,"X2>",X2,"Y2>",Y2:PRINT "Punto:"
220 INPUT "X3>",X3,"Y3>",Y3
230 X=X3-X1:Y=Y3-Y1:GOSUB 380:O1=O:D1=D:X=X2-X1:Y=Y2-Y1:GOSUB 380:O2=O:D2=D:X=X3-X2:Y=Y3-Y2:GOSUB 380:O3=O:D3=D
240 AL=O2-O1+800:AL=AL-400*INT(AL/400):BE=O3-O2+600:BE=BE-400*INT(BE/400)
250 IF AL>200 THEN AL=400-AL
260 IF BE>200 THEN BE=400-BE
270 MD=D1*SINAL:P1=D1*COSAL:P2=D3*COSBE
280 CLS:PRINT "Minima distancia="MD," 1-2 ->"D2," 1-P ->"D1," 2-P ->"D3;:GOSUB "K$"
290 CLS:PRINT "ACIMUTES:"," 1-2 ->"O2," 1-P ->"O1," 2-P ->"O3;:GOSUB "K$"
300 CLS:PRINT "ANGULOS INTERIORES:"," 1~ ->"AL," 2~ ->"BE," P~ ->"200-AL-BE;:GOSUB "K$"
310 CLS:PRINT "PROYECCIONES:"," 1~ ->"P1," 2~ ->"P2;:GOSUB "K$"
320 GOTO 10
330 CLS:A$="*Recta con Pto y acimut*":PRINT A$:INPUT "X1>",X1,"Y1>",Y1
340 A=0:INPUT " Acimut(ñ)>",A
350 IF A<>0 THEN 370
360 INPUT "Pendiente%>",P:PRINT -P"X+Y+"P*X1-Y1"=0":GOSUB "K$":GOTO 10
370 X2=X1+50*SINA:Y2=Y1+50*COSA:PRINT "X2="X2"Y2="Y2:GOSUB "K$":GOTO 90
380 IF Y=0 THEN Y=1E-80
390 D=SQR(X^2+Y^2):O=ATN(X/Y)+100*(1-SGN(Y)):O=O-400*INT(O/400)
400 RETURN
410 CLS:PRINT "(1) Recta:Punto y Acimut","(2) -V- Conoc: 2 Coord y 2 Ptes","(3) Pto medio entre dos","(4) [Mas...]";
420 GOSUB "K$"
430 IF K$="1" GOTO 330
440 IF K$="2" GOTO 480
450 IF K$="3" GOTO 580
460 IF K$="4" GOTO 620
470 GOTO 420
480 ' PUNTO -V- INTERSECCION    DADOS DOS PUNTOS CON COORDENADAS      Y SUS PENDIENTES
490 CLS:INPUT "X1>",X1,"Y1>",Y1,"X2>",X2,"Y2>",Y2
500 CLS:PRINT "ELIGE:","(1) Pendientes","(2) ACIMUTES"
510 GOSUB "K$"
520 IF K$<>"1" AND K$<>"2" THEN 510
530 IF K$="1" THEN INPUT "Pte 1 (%)>",P,"Pte 2 -(%)>",Q
540 IF K$="2" THEN ANGLE2:INPUT "ANGULO 1>",T1 ,"ANGULO 2>",T2:M1=1/TAN T1:M2=1/TAN(400-T2)
550 IF K$="1" THEN X=(Y2-Y1-X1*P/100+X2*Q/100)/((P+Q)/100):Y=Y1+P/100*(X-X1)
560 IF K$="2" THEN X=(Y1-Y2-X1*M1+X2*M2)/(M2-M1):Y=Y1+M1*(X-X1)
570 CLS:PRINT "X="X,"Y="Y:GOSUB "K$":GOTO 10
580 CLS:INPUT "X1>",X1,"Y1>",Y1,"X2>",X2,"Y2>",Y2:X=X2-X1:Y=Y2-Y1:D=SQR(A^2+B^2):XM=(X2+X1)/2:YM=(Y1+Y2)/2:ANGLE1:CLS:PRINT "XM="XM"YM="YM:GOSUB "K$"
590 IF Y=0 THEN O=0:GOTO 610
600 O=ATN(X/Y)+PI/2*(1-SGN(Y))+PI/2:O=O-2*PI*INT(O/(2*PI))
610 O90=O+PI/2:CLS:ANGLE2:PRINT "Az GIRADO 100ñ>"O/PI*200,"Y-"YM+O*XM"-"O"X=0":GOSUB "K$":GOTO 10
620 CLS:PRINT "(1) TANGS DESDE UN PTO A CIRCULO(2) CORTES RECTA Y CIRCULO","(3) CENTRO CIRCULO DADOS 3 PTOS","(4) [INICIO]";
630 GOSUB "K$"
640 IF K$<>"1" AND K$<>"2" AND K$<>"3" AND K$<>"4" THEN 630
650 IF K$="1" THEN 690
660 IF K$="2" THEN 760
670 IF K$="3" THEN 810
680 GOTO 10
690 '   COORDENADAS  DE  LAS      TANGENTES A UN CIRCULO DADO
700 CLS:INPUT "CENTRO:X>",X1,"CENTRO:Y>",Y1,"PUNTO:X>",X2,"PUNTO:Y>",Y2,"RADIO>",R
710 X=X2-X1:Y=Y2-Y1:D=SQR(X^2+Y^2)
720 IF D<R THEN PRINT "NO HAY TANGENCIA":GOSUB "K$":GOTO 10
730 O=ATN(X/Y)+100*(1-SGN(Y)):O=O-400*INT(O/400):AL=ACS(R/D):AL1=O+AL:AL2=O-AL:XT1=X1+R*SINAL1:YT1=Y1+R*COSAL1:XT2=X1+R*SINAL2:YT2=Y1+R*COSAL2:L=SQR((XT1-X2)^2+(YT1-Y2)^2)
740 PRINTREV"1"NORM"X="XT1"Y="YT1,REV"2"NORM"X="XT2"Y="YT2,"Longitud="L;
750 GOSUB "K$":GOTO 620
760 'PTOS DE CORTE:                  CIRCULO (R,Xc,Yc)               RECTA (X1,Y1) (X2,Y2)
770 CLS:INPUT "CENTRO:X>",H,"CENTRO:Y>",K,"RADIO>",R:PRINT "PUNTOS DE RECTA:":INPUT"PUNTO 1:X>",X1,"PUNTO 1:Y>",Y1,"PUNTO 2:X>",X2,"PUNTO 2:Y>",Y2
780 L2=(X2-X1)^2+(Y2-Y1)^2:S=(X1-H)*(X2-X1)+(Y1-K)*(Y2-Y1):C=(X1-H)^2+(Y1-K)^2-R^2:D=SQR(S^2-L2*C):C1=(-S+D)/L2:C2=(-S-D)/L2
790 CLS:XC1=X1+(X2-X1)*C1:YC1=Y1+(Y2-Y1)*C1:XC2=X1+(X2-X1)*C2:YC2=Y1+(Y2-Y1)*C2:PRINT TAB(5);"X             Y",REV"1"NORM;XC1;YC1,REV"2"NORM;XC2;YC2:GOSUB "K$":GOTO 620
800 'CIRCULO PASANDO POR 3 PTOS
810 CLS:INPUT "X1>",X1,"Y1>",Y1,"X2>",X2,"Y2>",Y2,"X3>",X3,"Y3>",Y3
820 N=X2-X1:M=X2-X3:Y=(M*(X1^2+Y1^2-X2^2-Y2^2)-N*(X3^2+Y3^2-X2^2-Y2^2))/(2*(M*Y1-M*Y2+N*Y2-N*Y3)):X=(X2^2+Y2^2-X1^2-Y1^2-2*Y*(Y2-Y1))/(2*N):PRINT "X Centro >"X,"Y Centro >"Y
830 IF SGN(X)=-1 THEN S1$="" ELSE S1$="+"
840 IF SGN(Y)=-1 THEN S2$="" ELSE S2$="+"
850 R=SQR((X-X1)^2+(Y-Y1)^2):PRINT "(X"S1$;ROUND(X,-5)")ý+","(Y"S2$;ROUND(Y,-5)")ý="R"ý";
860 GOSUB "K$":GOTO 620
