5 ANGLE 0
10 CLS:CLEAR:DIM N$(1)
20 INPUT "Coef.reales o complejos (R/C)";N$(1)
50 CLS:INPUT "Numero de incognitas ";S
60 DIM A(2*S,(2*S)+1),R(2*S,(2*S)+1),B(2*S,(2*S)+1)
65 IF N$(1)="C" THEN 710
70 N=S:CLS:PRINT "Introduzca la matriz ampliada;igualada al termino independiente"
80 FOR I=1 TO N
90 FOR J=1 TO N+1
100 PRINT "A(";I;J;")=";:INPUT A(I,J)
110 NEXT J
120 NEXT I
160 C=1
170 FOR P=1 TO N-1
180 IF A(P,P)<>0 THEN 270
190 I=P
200 I=I+1
210 IF A(I,P)=0 AND I<N THEN 200
220 IF A(I,P)=0 THEN PRINT "Sistema indeterminado y determinante nulo":END
230 FOR K=P TO N+1
240 A=A(P,K):A(P,K)=A(I,K):A(I,K)=A
250 NEXT K
260 C=-C
270 X=A(P,P)
280 FOR F=P+1 TO N
290 IF A(F,P)=0 THEN 340
300 G=A(F,P)/X
310 FOR K=P+1 TO N+1
320 A(F,K)=A(F,K)-A(P,K)*G
330 NEXT K
340 NEXT F
350 NEXT P
390 DET=C
400 FOR P=1 TO N:DET=DET*A(P,P):NEXT P
405 CLS
410 PRINT "Determinante=";DET
415 IF INKEY$="" THEN 415
450 DIM Z(N)
460 IF A(N,N)=0 THEN  PRINT "Sistema indeterminado":END
470 FOR F=N TO 1 STEP -1
480 T=0
490 FOR V=F+1 TO N
500 T=T+A(F,V)*Z(V)
510 NEXT V
520 Z(F)=A(F,N+1)/A(F,F)-T/A(F,F)
560 NEXT F
570 IF N$(1)="C" THEN 900
630 FOR V=1 TO N :PRINT "X(";V;")=";Z(V)
635 IF INKEY$="" THEN 635
636 NEXT V
700 END
710 CLS:PRINT "Introduzca la matriz ampliada,igualada al termino independiente"
720 FOR I=1 TO S
730 FOR J=1 TO S+1
740 CLS:PRINT "*A(";I;J;")*":PRINT "Parte real=";:INPUT R(I,J)
745 PRINT "Parte Imaginaria=";:INPUT B(I,J)
750 NEXT J
760 NEXT I
770 W=0 :FOR I=1 TO 2*S STEP 2
775 W=W+1
780 FOR J=1 TO S+1
790 A(I,(2*J)-1)=R(W,J)
810 NEXT J:NEXT I
820 W=0:FOR I=1 TO 2*S STEP 2:W=W+1
830 FOR J=1 TO S
840 A(I,(2*J))=-B(W,J)
850 NEXT J:NEXT I
855 W=0 :FOR I=2 TO 2*S STEP 2:W=W+1
856 FOR J=1 TO S+1
857 A(I,(2*J)-1)=B(W,J)
860 NEXT J:NEXT I
861 W=0 :FOR I=2 TO 2*S STEP 2:W=W+1
862 FOR J=1 TO S
863 A(I,(2*J))=R(W,J)
864 NEXT J:NEXT I
865 N=2*S:GOTO 160
900 FOR V=0 TO (2*S)-2 STEP 2
905 AR=ATN (Z(V+2)/Z(V+1))
910 D=SGN Z(V+1):IF D=-1 THEN A$="-"ELSE A$="+"
920 E=SGN Z(V+2):IF E=-1 THEN B$="-" ELSE B$="+"
921 IF D=-1 AND E=1 THEN ARG=180+AR
922 IF D=-1 AND E=-1 THEN ARG=180+AR
923 IF D=1 AND E=1 OR D=1 AND E=-1 THEN ARG=AR
925 H=H+1
926 CLS:PRINT "X(";H;")=";A$;ABS(Z(V+1));B$;"j";ABS(Z(V+2))
931 IF INKEY$="" THEN 931
932 PRINT "Modulo=";SQR(Z(V+1)^2+Z(V+2)^2)
933 PRINT"Argumento=";ARG
940 IF INKEY$="" THEN 940
950 NEXT V
960 END
