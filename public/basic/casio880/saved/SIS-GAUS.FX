10 CLS :CLEAR 
20 PRINT "GAUSS"
30 INPUT "Dimension N=";N
40 DIM A(N,N+1),B(N)
50 PRINT "Coefientes (por filas)"
60 FOR I=1 TO N
70 FOR J=1 TO N
80 PRINT "A(";I;",";J;")=";:INPUT A(I,J)
90 NEXT J
100 NEXT I
110 PRINT "Termino independiente:"
120 FOR I=1 TO N
130 PRINT "B(";I;")=";:INPUT B(I)
140 NEXT I
150 DIM P(N)
160 FOR I=1 TO N
170 P(I)=I
180 NEXT I
190 DIM Z(N),C(N)
200 FOR K=1 TO N-1
210 FOR I=K TO N
220 S=0
230 FOR J=K TO N
240 S=S+ABS(A(P(I),J))
250 Z(I)=S
260 NEXT J
270 NEXT I
280 FOR L=K TO N
290 C(L)=ABS(A(L,K))/Z(L)
300 NEXT L
310 MX=0:K0=0
320 FOR L=K TO N
330 IF C(L)>=MX THEN MX=C(L):K0=L
340 NEXT L
350 IF P(K)<>P(K0) THEN AX=P(K):P(K)=P(K0):P(K0)=AX
360 FOR J=K+1 TO N
370 A(P(J),K)=A(P(J),K)/A(P(K),K)
380 FOR I=K+1 TO N
390 A(P(J),I)=A(P(J),I)-A(P(J),K)*A(P(K),I)
400 NEXT I
410 NEXT J
420 NEXT K
430 DIM D(N)
440 FOR K=1 TO N
450 S=0
460 FOR J=1 TO K-1
470 S=S+A(P(K),J)*D(J)
480 NEXT J
490 D(K)=B(P(K))-S
500 NEXT K
510 DIM X(N)
520 FOR K=N TO 1 STEP -1
530 S=0
540 FOR J=K+1 TO N:IF J>N THEN 560
550 S=S+A(P(K),J)*X(J)
560 NEXT J
570 X(K)=(D(K)-S)/A(P(K),K)
580 NEXT K
590 CLS
600 FOR I=1 TO N
610 PRINT "X(";I;")=";X(I)
620 GOSUB 650
630 NEXT I
640 END
650 IF INKEY$="" THEN 650 ELSE RETURN
