5 CLEAR
6 ANGLE 0
10 CLS:PRINT "       ASTRONOMIA  :":CLS
11 PRINT "CALCULO DE COORDENADAS (C)"
12 PRINT "CALCULO DEL AZIMUT (A)"
13 PRINT "PUNTEROS MAXIMA DIGRESION (P)"
14 CLS:PRINT "LATITUD POR LA POLAR (L)"
20 INPUT A$
22 IF A$="A" THEN GOTO 30
23 IF A$="P" THEN GOTO 149
24 IF A$="L" THEN GOTO 230
25 IF A$="C" THEN GOTO 300
29 CLS:PRINT "ESCRIBE LA OPCION CORRECTA":GOTO 10
30 CLS:PRINT "POR LA POLAR (P)"
31 PRINT "POR EL SOL (S)"
39 INPUT B$
40 IF B$="P" THEN GOTO 50
41 IF B$="S" THEN GOTO 180
49 CLS:PRINT "ESCRIBE LA OPCION CORRECTA":GOTO 30
50 CLS:PRINT "HORA SIDERIA (S)"
51 PRINT "HORA DE PASO (P)"
52 INPUT C$
53 IF C$="S" THEN GOTO 60
54 IF C$="P" THEN GOTO 130
55 CLS:PRINT "ESCRIBE LA OPCION CORRECTA":GOTO 50
60 CLS:INPUT "HORAS ADELANTADAS A T.U.";J:CLS
61 CLS:INPUT "DECLINACION (DEG) ";D:CLS
62 INPUT "ASCENSION RECTA (Hor) ";AR:CLS
63 INPUT "LATITUD LUGAR (DEG) ";L:CLS
64 INPUT "LONGITUD LUGAR (Hor) ";LON:CLS
65 INPUT "CORRECCION RELOJ (Seg) ";R:CLS
66 INPUT "HORA SIDERIA A 0h T.U. ";HSG:CLS
67 INPUT "NUMERO DE OBSERVACIONES ";N:CLS
68 DIM H(N),O(N)
69 FOR I=1 TO N
70 PRINT "HORA OBSERVACION";I;" ";:INPUT HO:CLS
71 HCG=HO-J+DEG(0,0,R) :IF HCG<0 THEN HCG=24+HCG
72 HCSG=HCG*1.00273791
73 HS=HCSG+HSG
74 HSL=HS-LON
75 IF HSL>24 THEN HSL=HSL-24
76 H(I)=HSL-AR :IF H(I)<0 THEN H(I)=24+H(I)
77 C=H(I)*15:K=ATN((SINC)/(SINL*COSC-TAND*COSL))
78 PRINT "LECTURA A LA POLAR ";I;"(GRA) ";:INPUT LP:CLS
79 A=(K*400)/360 :IF A>100 THEN A=200-A
80 O(I)=LP-A : G=200+A :IF  O(I)<0 THEN O(I)=400+O(I)
90 PRINT "ANGULO HORARIO ";DMS$(H(I)):CLS:PRINT "AZIMUT GEO (GRA)";G:CLS:PRINT "ACIMUT TOPO (GRA) ";A:CLS:PRINT "DESORIENTACION (GRA)";O(I):CLS
95 IF C$="P" THEN GOTO 146
100 NEXT I
105 S=0:M=0:RES=0
110 FOR I=1 TO N
111 S=S+O(I)
112 NEXT I
113 M=S/N
114 FOR I=1 TO N
115 RES=RES+(M-O(I))^2
116 NEXT I
117 EC=SQR(RES/(N-1))
118 PRINT "LA MEDIA ES";M
119 PRINT "ERROR CUADRATICO ES";EC
125 CLS
129 GOTO 2000
130 CLS:INPUT "HORAS ADELANTADAS A T.U.";J:CLS
131 CLS:INPUT "DECLINACION (DEG) ";D:CLS
132 INPUT "LATITUD LUGAR (DEG) ";L:CLS
133 INPUT "LONGITUD LUGAR (Hor) ";LON:CLS
134 INPUT "CORRECCION DEL RELOJ (Seg) ";R:CLS
135 INPUT "HORA DE PASO";HP:CLS
136 INPUT "NUMERO DE OBSERVACIONES ";N:CLS
137 DIM H(N),O(N)
138 FOR I=1 TO N
139 PRINT "HORA DE OBSERVACION";I;" ";:INPUT HO
141 HCG=HO-J+DEG(0,0,R) :IF HCG<0 THEN HCG=24+HCG
142 HCL=HCG-LON
143 HPV=HP
144 HC=HCL-HPV:H(I)=HC*1.00273791:IF H(I)<0 THEN H(I)=24+H(I)
145 GOTO 77
146 NEXT I
147 GOTO 110
149 CLS:PRINT "  PUNTERO M.D."
150 CLS:INPUT "HORAS ADELANTADAS A T.C.G.";HA
151 CLS:INPUT "LATITUD DEL L. (DEG)";L
152 CLS:INPUT "LONGITUD DEL L. (DEG)";LON
153 CLS:INPUT "HORA SIDERIA A 0h T.U.";HSG
154 LON=LON/15
155 CLS:INPUT "PUNTEROS A CALCULAR";N
156 CLS:FOR I=1 TO N
157 CLS:INPUT "DECLINACION E. DEG)";DE
158 CLS:INPUT "ASCENSION R. E. (hor)";AR:CLS
159 IF DE<L THEN PRINT "NO  DECLINACION MENOR LATITUD":CLS:GOTO 157
160 h=ASN(SINL/SINDE)
161 HOC=ACS(TANL/TANDE):HOR=360-HOC:HOC=HOC/15:HOR=HOR/15
162 A=ASN(COSDE/COSL):AOC=360-A:AOR=A
163 HSLOC=HOC+AR:IF HSLOC>24 THEN HSLOC=HSLOC-24
164 HSLOR=HOR+AR :IF HSLOR>24 THEN HSLOR=HSLOR-24
165 HSGOC=HSLOC+LON:HSGOR=HSLOR+LON
166 HTCOC=HSGOC-HSG:IF HTCOC<0 THEN HTCOC=HTCOC+24
167 HTCOR=HSGOR-HSG:IF HTCOR<0 THEN HTCOR=HTCOR+24
168 HTCOC=HTCOC/1.00273791:HTCOR=HTCOR/1.00273791:PRINT "   MAXIMA D. OCCIDENTAL "
169 PRINT "HORA HOFICIAL";DMS$(HTCOC+HA)
170 PRINT "AZIMUT";DMS$(AOC);"=";((AOC*400)/360)
171 CLS:PRINT "   MAXIMA D. ORIENTAL "
172 PRINT "HORA OFICIAL";DMS$(HTCOR+HA)
173 PRINT "AZIMUT";DMS$(AOR);"=";((AOR*400)/360)
174 CLS:Z=90-h
175 PRINT "      LA ALTURA CENITAL :":PRINT "   ";DMS$(Z);"=";((Z*400)/360)
176 NEXT I
177 GOTO 2000
180 CLS:INPUT "HORA MEDIA OBSERVACION T.U.";HO
181 CLS:INPUT "LATITUD LUGAR (DEG)";L
182 CLS:INPUT "Cor. refraccion (DEG)";CR
184 CLS:INPUT "Cor. sem. zen. (DEG)";CC
185 CLS:INPUT "DECLINACION SOL (DEG)";D
189 CLS:INPUT "NUMERO DE OBSERVACIONES";N:CLS:DIM W(N),A(N),hm(N),Z(N),hr(N),hs(N)
190 FOR I=1 TO N
191 PRINT "L. AZIMUTAL";I;"(GRA)";:INPUT A(I)
192 PRINT "L. ZENITAL";I;"(GRA)";:INPUT V:CLS
193 V=(V*360)/400:hm(I)=90-V:hr(I)=hm(I)-CR:hs(I)=hm(I)+CC:h=hm(I)-CR+CC
194 Z=ACS((SIND-SINL*SINh)/(COSL*COSh))
195 Z(I)=(Z*400)/360
196 IF HO>12 THEN Z(I)=400-Z(I)
197 NEXT I:FOR I=1 TO N:S=S+hm(I):NEXT I:S=S/N:CA=CC/COSS:PRINT "Altura media =";DMS$(S):CLS:PRINT "semi. acimutal =";DMS$(CA):CLS:CA=(CA*400)/360
198 FOR I=1 TO N:A(I)=A(I)+CA
199 W(I)=Z(I)-A(I) :IF W(I)<0 THEN W(I)=400+W(I)
200 PRINT "ALTURA C.ref";DMS$(hr(I))
201 CLS:PRINT "ALTURA C.sem.";DMS$(hs(I))
202 CLS:PRINT "L. AZIMUTAL C.sem.";A(I);"g"
203 CLS:PRINT "AZIMUT SOL";Z(I);"g"
204 CLS:PRINT "C.  W  =";W(I);"g":CLS
205 NEXT I
210 FOR I=1 TO N
211 M=M+W(I)
212 NEXT I
213 M=M/N
214 FOR I=1 TO N
215 R=R+(M-W(I))^2
216 NEXT I
217 EC=SQR(R/(N-1))
218 PRINT "MEDIA DE W";M;"g"
219 CLS:PRINT "E. CUADRATICO";EC:CLS:PRINT "E. CUA MEDIA";(EC/SQR(N)
220 CLS:INPUT "CUANTAS L. REFERENCIA";Q:CLS
221 FOR I=1 TO Q
222 PRINT "LECTURA";I;"(GRA)";:INPUT LEC:CLS
223 K=K+LEC
224 NEXT I
225 LECM=K/Q
226 AZIM=LECM+M :IF  AZIM>400  THEN AZIM=AZIM-400
227 PRINT "  AZIMUT A LA REFERENCIA ES ;"
228 PRINT "       ";AZIM;" g"
229 GOTO 2000
230 CLS:INPUT "HORAS ADELANTADAS A T.U.";J:CLS:INPUT "DECLINACION (DEG)";D:CLS
231 INPUT "ASCENSION RECTA (Hor)";AR:CLS
232 INPUT "LONGITUD LUGAR (Hor)";LON:CLS
233 INPUT "CORRECCION RELOJ (Seg)";R:CLS
234 INPUT "HORA SIDERIA A 0h T.U.";HSG:CLS
235 INPUT "PRESION MEDIA";B:CLS
236 INPUT "TEMPERATURA MEDIA";T:CLS
237 INPUT "NUMERO DE OBSERVACIONES";N:DIM LAT(N):CLS
240 FOR I=1 TO N
241 PRINT "HORA DE OBSERVACION";I;" ";:INPUT HO:CLS
242 PRINT "L. VERTICAL";I;"(GRA)";:INPUT LV:CLS
243 HCG=HO-J+DEG(0,0,R):IF HCG<0 THEN HCG=24+HCG
244 HCSG=HCG*1.00273791:IF HCSG>24 THEN HCSG=HCSG-24
245 HS=HCSG+HSG:IF HS>24 THEN HS=HS-24
246 HSL=HS-LON
247 IF HSL<0 THEN HSL=HSL+24
248 H=HSL-AR:IF H<0 THEN H=24+H
250 HF=H*15
251 LV=(LV*360)/400:CR=DEG(0,0,57.8)*TAN(LV-DEG(0,0,230)*TANLV)*((287*(1-0.00016*T))/(753.5*(268+T)))*B:LVC=LV+CR
253 n=ATN(TAND/COSHF):nl=ACS((COSLVC*SINn)/SIND):IF n<0 THEN n=SQR(n^2)
255 LAT(I)=n-nl:IF LAT(I)<0 THEN LAT(I)=SQR(LAT(I)^2)
256 PRINT "L.V. cor. refr.";DMS$(LVC):CLS
257 PRINT "HORA T.U.";DMS$(HCG):CLS
258 PRINT "INCREMENTO SIDERIO";DMS$(HCSG):CLS
259 PRINT "HORA SIDERIA G.";DMS$(HS):CLS
260 PRINT "HORA SIDERIA L.";DMS$(HSL):CLS
261 PRINT "H=S-AR=";DMS$(H):CLS
262 PRINT "LATITUD =";DMS$(LAT(I)):CLS
263 NEXT I
264 FOR I=1 TO N
265 M=M+LAT(I)
266 NEXT I
267 M=M/N :R=0
268 FOR I=1 TO N
269 R=R+(M-LAT(I))^2
270 NEXT I
271 EC=SQR(R/(N-1))
272 ECM=EC/SQRN
273 PRINT "LATITUD PROBABLE";DMS$(M):CLS:PRINT "E.C. UNA OBSERVACION";DMS$(EC):CLS:PRINT "E.C. DE LA MEDIA";DMS$(ECM):CLS
275 GOTO 2000
300 CLS:PRINT "    CAMBIOS DE COORDENADAS"
2000 INPUT "QUIERES VOLVER A EMPEZAR (S/N)";E$:IF E$="S" THEN GOTO 5 :CLS
