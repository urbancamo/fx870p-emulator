5 TIT$="Programa para la resolucion de n ecuaciones con n incognitas con coeficientes y soluciones COMPLEJOS - V1.0 - FEBRERO 1990"
10 CLEAR:CLS
20 PRINT "","Introduce la forma del complejo:";"    1.-Polar","    2.-Cartesiana";
30 DEC=VAL(INKEY$):IF DEC<>1 AND DEC<>2 THEN 30
40 CLS:INPUT"Numero de ecuaciones: ",NEC
50 IF NEC<2 THEN PRINT "IMPOSIBLE CALCULAR":END
60 DIM A(NEC*2,(NEC+1)*2),B(NEC*2,1)
70 FOR F=1 TO NEC
80 CLS:LOCATE 0,0:PRINT"Ecuacion ";F
90 FOR C=1 TO NEC+1
100 IF C=NEC+1 THEN LOCATE 13,0:PRINT"T. indep.      ":GOTO 120
110 LOCATE 13,0:PRINT"Coeficiente ";C;"     ";
120 LOCATE 0,2:IF DEC=1 THEN PRINT"Modulo                        " ELSE PRINT"Real                          "
130 LOCATE 0,3:IF DEC=1 THEN PRINT"Argumento                     "; ELSE PRINT"Imaginaria                    ";
140 LOCATE 10,2:INPUT A(F*2,C*2)
150 LOCATE 10,3:INPUT A(F*2,C*2-1)
160 NEXT C,F
170 CLS:PRINT "ESPERA -CALCULANDO-"
180 IF DEC=1 THEN FOR F=2 TO NEC*2 STEP 2:FOR C=2 TO (NEC+1)*2 STEP 2:MO=A(F,C):ARG=A(F,C-1):GOSUB 450:A(F,C)=RCART:A(F,C-1)=ICART:NEXT C,F
190 FOR F=2 TO NEC*2 STEP 2:FOR C=2 TO (NEC+1)*2 STEP 2:A(F-1,C)=-A(F,C-1):A(F-1,C-1)=A(F,C):NEXT C,F
200 N=NEC*2
210 FOR F=1 TO N
220 FOR C=F TO N
230 IF A(F,C)=0 THEN NEXT C:CLS:PRINT"SISTEMA INDETERMINADO":END
240 FOR K=1 TO N+1:X=A(F,K):A(F,K)=A(C,K):A(C,K)=X:NEXT K
250 IF A(F,F)=0 THEN A(F,F)=1E-90
260 Y=1/A(F,F)
270 FOR K=1 TO N+1:A(F,K)=A(F,K)*Y:NEXT K
280 FOR C=1 TO N
290 IF C=F GOTO 340
300 Y=-A(C,F)
310 FOR K=1 TO N+1
320 A(C,K)=A(C,K)+A(F,K)*Y
330 NEXT K
340 NEXT C,F
350 FOR F=1 TO N:B(F,1)=INT(A(F,N+1)*10000+.5)/10000:NEXT F
360 IF DEC=1 THEN FOR F=1 TO NEC*2 STEP 2:RCART=B(F,1):ICART=B(F+1,1):GOSUB 470:B(F,1)=MO:B(F+1,1)=ARG:NEXT F
370 CLS:BEEP
380 FOR F=1 TO NEC STEP 4:CLS:FOR C=0 TO 3 :'STEP 2
390 IF C+F<=NEC THEN LOCATE 1,C:PRINT"X";C+F;"=";B((C+F)*2-1,1);",";B((C+F)*2,1);
400 NEXT C
410 IF INKEY$="" THEN 410 ELSE NEXT F
420 IF NEC>3 THEN MENS=1:PRINT "","[CONT]                    [FIN]";
430 GOSUB"DEC.SUB":GOTO 10
440 'PASA POLARES A CARTESIANAS
450 RCART=MO*COS ARG:ICART=MO*SIN ARG:RETURN
460 'PASA CARTESIANAS A POLARES
470 MO=SQR(RCART^2+ICART^2)
480 IF RCART=0 THEN RCART=1E-90
490 ARG=ATN(ICART/RCART)
500 IF RCART<0 THEN ARG=ARG+180
510 RETURN
