10 CLS:PRINT "********* PROGRAMAS DE ******************  ASTRONOMIA *********":SET F6:ANGLE 0
30 PRINT "OPCIONES: 1,2,3,4,5,6,7,8,9,10,           11,12,13,14 "
40 CLS:PRINT "1:HORIZONTALES==>ECUAT.HORARIAS"
50 CLS:PRINT "2:ECUAT.HORARIAS==>HORIZONTALES"
60 CLS:PRINT "3:HORIZONTALES==>ECUAT.ABSOLUTAS"
70 CLS:PRINT "4:ECUAT.ABSOLUTAS==>HORIZONTALES"
80 CLS:PRINT "5:ECLIPTICAS==>ECUAT.ABSOLUTAS"
90 CLS:PRINT "6:ECUAT.ABSOLUTAS==>ECLIPTICAS"
91 CLS:PRINT "7:MAXIMAS DIGRESIONES"
92 CLS:PRINT "8:ORTO Y OCASO"
93 CLS:PRINT "9:PRIMER VERTICAL"
94 CLS:PRINT "10:PASO DE HORA CIVIL              A HORA SIDEREA"
95 CLS:PRINT "11:PASO DE HORA SIDEREA            A HORA CIVIL"
96 CLS:PRINT "12:PASO DE HORA CIVIL              A HORA VERDADERA"
97 CLS:PRINT "13:PASO DE HORA VERDADERA          A HORA CIVIL"
98 CLS:PRINT "14:DURACION DE LAS ESTACIONES"
100 CLS:INPUT "QUE OPCION DESEAS: 1,2,3,4,5,6, 7,8,9,10,11,12,13,14 ";A
150 ON A GOTO 180,320,460,630,740,870,1000,1200,1400,1600,1800,2000,2200,2400
180 CLS:PRINT "HORIZONTALES==>ECUAT.HORARIAS"
183 INPUT "FORMULARIO (S/N) ";F$
184 IF F$<>"S" THEN 190
185 PRINT "tgM=cosA/tgh    tg";CHR$(142);"=cosH*tg(";CHR$(143);"-M)      tgH=tgA*senM/cos(";CHR$(143);"-M)
190 CLS:INPUT "LATITUD ";LAT
200 INPUT "ACIMUT ";ACI
210 INPUT "ALTURA ";ALT
220 M=ATN(COS(ACI)/TAN(ALT))
230 H=ATN(((TAN(ACI)*SIN(M))/(COS(LAT-M)))
240 IF ACI>0 AND ACI<180 AND TAN(H)<0 THEN H=H+180
250 IF ACI>180 AND TAN(H)>0 THEN H=H+180
260 IF H<0 THEN H=H+360
270 DEC=ATN(COS(H)*TAN(LAT-M))
280 IF DEC>90 THEN DEC=DEC-180
285 CLS:PRINT "M= ";M;"=";DMS$(M)
290 PRINT "EL ANGULO HORARIO ES            ";H;"=";DMS$(H)
300 PRINT "LA DECLINACION ES               ";DEC;"=";DMS$(DEC)
310 GOTO 190
320 CLS:PRINT "ECUAT.HORARIAS==>HORIZONTALES"
323 INPUT "FORMULARIO (S/N)";F$
324 IF F$<>"S" THEN 330
325 PRINT "tgM=tg";CHR$(142);"/cosH    tgh=cosA/tg(";CHR$(143);"-M)      tgA=tgH*cosM/sen(";CHR$(143);"-M)"
330 CLS:INPUT "LATITUD ";LAT
340 INPUT "ANG.HORARIO ";H
350 INPUT "DECLINACION ";DEC
360 M=ATN(TAN(DEC)/COS(H))
370 ACI=ATN((TAN(H)*COS(M))/SIN(LAT-M))
390 IF H>0 AND H<180 AND TAN(ACI)<0 THEN ACI=ACI+180
400 IF H>180 AND ATN(ACI)>0 THEN ACI=ACI+180
410 IF ACI<0 THEN ACI=ACI+360
415 ALT=90-ATN((TAN(LAT-M))/COS(ACI))
420 IF ALT>90 THEN ALT=ALT-180
425 CLS:PRINT "M= ";M;"=";DMS$(M)
430 PRINT "EL ACIMUT ES                    ";ACI;"=";DMS$(ACI)
440 PRINT "LA ALTURA ES                    ";ALT;"=";DMS$(ALT)
450 GOTO 330
460 CLS:PRINT "HORIZONTALES==>ECUAT.ABSOLUTAS"
463 INPUT "FORMULARIO (S/N) ";F$
464 IF F$<>"S" THEN 470
465 PRINT "tgM=cosA/tgH    tg";CHR$(142);"=cosH*tg(";CHR$(143);"-M) tgH=tgA*senM/cos(";CHR$(143);"-M)   ";CHR$(140);"=H+";CHR$(136)
470 CLS:INPUT "HORA SIDEREA ";V
480 INPUT "LATITUD ";LAT
490 INPUT "ACIMUT ASTRONOMICO              ";ACI
500 INPUT "ALTURA ";ALT
510 M=ATN(COS(ACI)/TAN(ALT))
520 H=ATN(TAN(ACI)*SIN(M)/COS(LAT-M))
530 IF ACI>0 AND ACI<180 AND TAN(H)<0 THEN H=H+180
540 IF ACI>180 AND TAN(H)>0 THEN H=H+180
550 IF H<0 THEN H=H+360
560 DEC=ATN(COS(H)*TAN(LAT-M))
570 IF DEC>90 THEN DEC=DEC-180
580 H1=H/15
590 R=V-H1:IF R<0 THEN R=R+24
595 CLS:PRINT "M= ";M;" = ";DMS$(M)
597 CLS:PRINT "EL ANGULO HORARIO ES            ";H;" = ";DMS$(H)
600 PRINT "LA DECLINACION ES               ";DEC;" = ";DMS$(DEC)
610 PRINT "LA ASCENSION RECTA ES           ";R;"=";DMS$(R);" HORAS"
620 GOTO 470
630 CLS:PRINT "ECUAT.ABSOLUTAS==>HORIZONTALES"
633 PRINT "FORMULARIO (S/N) ";F$
634 IF F$<>"S" THEN 640
635 PRINT "tgM=tg";CHR$(142);"/cosH    tgh=cosA/tg(";CHR$(143);"-M)  tgA=tgH*cosM/sen(";CHR$(143);"-M)   H=";CHR$(140);"-";CHR$(136)
640 CLS:INPUT "LATITUD ";LAT
650 INPUT "HORA SIDERA ";V
660 INPUT "DECLINACION ";DEC
670 INPUT "ASCENSION RECTA (en horas)      ";R
680 H=V-R:IF H<0 THEN H=H+24
690 H=H*15:M=ATN(TAN(DEC)/COS(H))
700 ACI=ATN(TAN(H)*COS(M)/SIN(LAT-M)):IF ACI<0 THEN ACI=ACI+360
703 IF H>0 AND H<180 AND TAN(ACI)<0 THEN H=H+180
704 IF H>180 AND TAN(ACI)>0 THEN ACI=ACI+180
705 IF ACI<0 THEN ACI=ACI+360
707 ALT=90-ATN(TAN(LAT-M)/COS(ACI)):IF ALT>90 THEN ALT=ALT-180
708 CLS:PRINT "EL ANGULO HORARIO ES            ";H;" = ";DMS$(H)
709 CLS:PRINT "M= ";M;" = ";DMS$(M)
710 PRINT "EL ACIMUT ASTRONOMICO ES        ";ACI;" = ";DMS$(ACI)
720 PRINT "LA ALTURA ES                    ";ALT;" = ";DMS$(ALT)
730 GOTO 640
740 CLS:PRINT "ECLIPTICAS==>ECUAT.ABSOLUTAS"
743 INPUT "FORMULARIO (S/N) ";F$
744 IF F$<>"S" THEN 750
745 PRINT "tgM=tg";CHR$(137);"/sen";CHR$(138);"   tg";CHR$(142);"=sen";CHR$(136);"*tg(M+";CHR$(139);")    tg";CHR$(136);"=tg";CHR$(138);"*cos(M+";CHR$(139);")/cosM"
750 CLS:PRINT "LONGITUD (";CHR$(138);") ";:INPUT LAN
760 PRINT "LATITUD (";CHR$(137);") ";:INPUT BET
770 PRINT "EPSILON (";CHR$(139);") ";:INPUT EE
780 M=ATN(TAN(BET)/SIN(LAN))
790 R=ATN(TAN(LAN)*COS(M+EE)/COS(M))
800 IF COS(LAN)<0 AND TAN(R)>0 AND R<180 THEN R=R+180:IF COS(LAN)>0 AND TAN(R)<0 AND R<180 THEN R=R+180
810 DEC=ATN(SIN(R)*TAN(M+EE))
830 IF DEC>90 THEN DEC=DEC-180
835 IF R<0 THEN R=R+360
837 CLS:PRINT "M= ";M;"=";DMS$(M)
840 PRINT "LA ASCENSION RECTA ES           ";R;"=";DMS$(R)
850 PRINT "LA DECLINACION ES               ";DEC;"=";DMS$(DEC)
860 GOTO 750
870 CLS:PRINT "ECUAT.ABSOLUTAS==>ECLIPTICAS"
873 INPUT "FORMULARIO (S/N) ";F$
874 IF F$<>"S" THEN 880
875 PRINT "tgM=tg";CHR$(142);"/sen";CHR$(136);"   tg";CHR$(137);"=sen";CHR$(138);"*tg(M-";CHR$(139);")    tg";CHR$(138);"=tg";CHR$(136);"*cos(M-";CHR$(139);")/cosM"
880 CLS:PRINT "ASCENSION RECTA (";CHR$(136);") (en grados) ";:INPUT R
890 CLS:PRINT "DECLINACION (";CHR$(142);") ";:INPUT DEC
900 PRINT "EPSILON (";CHR$(139);") ";:INPUT EE
910 M=ATN(TAN(DEC)/SIN(R))
920 LAN=ATN(TAN(R)*COS(M-EE)/COS(M))
930 IF LAN<0 THEN LAN=LAN+360
940 IF COS(R)>0 AND TAN(LAN)<0 AND LAN<180 THEN LAN=LAN+180
950 IF COS(R)<0 AND TAN(LAN)>0 AND LAN<180 THEN LAN=LAN+180
960 LAT=ATN(SIN(LAN)*TAN(M-EE))
965 CLS:PRINT "M= ";M;"=";DMS$(M)
970 PRINT "LA LONGITUD ECLIPTICA (";CHR$(138);") ES     ";LAN;"=";DMS$(LAN)
980 PRINT "LA LATITUD ECLIPTICA (";CHR$(137);") ES      ";LAT;"=";DMS$(LAT)
990 GOTO 880
1000 CLS:PRINT "MAXIMAS DIGRESIONES"
1003 INPUT "FORMULARIO (S/N) ";F$
1004 IF F$<>"S" THEN 1010
1005 PRINT "senh=sen";CHR$(143);"/sen";CHR$(142);"   cosH=tg";CHR$(143);"/tg";CHR$(142);"     senA=cos";CHR$(142);"/cos";CHR$(143);"   ";CHR$(140);"=H+";CHR$(136)
1010 CLS:PRINT "ASCENSION RECTA (";CHR$(136);") (en horas)  ";:INPUT R
1020 CLS:PRINT "DECLINACION (";CHR$(142);") ";:INPUT DEC
1030 PRINT "LATITUD (";CHR$(143);") ";:INPUT LAT
1040 ALT=ASN(SIN(LAT)/SIN(DEC))
1050 ACI=ASN(COS(DEC)/COS(LAT))
1053 IF LAT<0 THEN ACI=180-ACI
1055 IF ACI<90 THEN ACI1=180-ACI
1057 ACI2=360-ACI1
1060 H1=ACS(TAN(LAT)*(1/TAN(DEC)))
1065 H2=360-H1
1070 HS1=R+H1/15:IF HS1>24 THEN HS1=HS2-24
1075 HS2=R+H2/15:IF HS2>24 THEN HS2=HS2-24
1090 PRINT "LA ALTURA ES                    ";ALT;" = ";DMS$(ALT)
1100 PRINT "EL ACIMUT OCCIDENTAL ES         ";ACI1;" = ";DMS$(ACI1)
1105 PRINT "EL ACIMUT ORIENTAL ES           ";ACI2;" = ";DMS$(ACI2)
1110 PRINT "EL ANGULO HORARIO OCCIDENTAL ES ";H1;" = ";DMS$(H1)
1115 PRINT "EL ANGULO HORARIO ORIENTAL ES   ";H2;" = ";DMS$(H2)
1120 PRINT "LA HORA SIDEREA (max.digr.occ)ES";HS1;DMS$(HS1);"(horas)"
1125 PRINT "LA HORA SIDEREA (max.digr.or) ES";HS2;DMS$(HS2);"(horas)"
1190 GOTO 1010
1200 CLS:PRINT "ORTO Y OCASO"
1203 INPUT "FORMULARIO (S/N) ";F$
1204 IF F$<>"S" THEN 1210
1205 PRINT "cosH=-tg";CHR$(142);"*tg";CHR$(143);"                   cosA=-sen";CHR$(142);"/sen";CHR$(143)
1210 CLS:PRINT "LATITUD (";CHR$(143);") ";:INPUT LAT
1220 PRINT "DECLINACION (";CHR$(142);") ";:INPUT DEC
1225 PRINT "ASCENSION RECTA (";CHR$(136);") (en horas)  ";:INPUT R
1230 ACI=ACS(-SIN(DEC)/COS(LAT))
1235 ACI2=360-ACI
1240 H=ACS(-TAN(LAT)*TAN(DEC))
1245 H2=360-H
1247 HS1=R+H/15:IF HS1>24 THEN HS1=HS1-24
1248 HS2=R+H2/15:IF HS2>24 THEN HS2=HS2-24
1250 PRINT "EL ACIMUT (en ocaso) ES         ";ACI;" = ";DMS$(ACI)
1255 PRINT "EL ACIMUT (en orto) ES          ";ACI2;" = ";DMS$(ACI2)
1260 PRINT "EL ANGULO HORARIO (en ocaso) ES ";H;" = ";DMS$(H)
1265 PRINT "EL ANGULO HORARIO (en orto) ES  ";H2;" = ";DMS$(H2)
1270 PRINT "LA HORA SIDEREA (en ocaso) ES  ";HS1;DMS$(HS1);" (horas)"
1280 PRINT "LA HORA SIDEREA (en orto) ES   ";HS2;DMS$(HS2);" (horas)"
1290 GOTO 1210
1400 CLS:PRINT "PRIMER VERTICAL"
1403 INPUT "FORMULARIO (S/N) ";F$
1404 IF F$<>"S" THEN 1410
1405 PRINT "cosH=tg";CHR$(142);"/tg";CHR$(143);"                    senh=sen";CHR$(142);"/sen";CHR$(143)
1410 CLS:PRINT "ASCENSION RECTA (";CHR$(136);") (en horas)  ";:INPUT R
1420 PRINT "DECLINACION (";CHR$(142);") ";:INPUT DEC
1430 PRINT "LATITUD (";CHR$(143);") ";:INPUT LAT
1440 H=ACS(TAN(DEC)/TAN(LAT))
1450 ALT=ASN(SIN(DEC)/SIN(LAT))
1460 HS1=R+H/15:IF HS1>24 THEN HS1=HS1-24
1470 HS2=R-H/15:IF HS2<0 THEN HS2=HS2+24
1475 CLS:PRINT "EL ANGULO HORARIO ES            ";H;" = ";DMS$(H)
1480 PRINT "LA ALTURA ES                     ";ALT;" = ";DMS$(ALT)
1490 PRINT "LA HORA SIDEREA (oeste) ES      ";HS1;"=";DMS$(HS1);"(horas)"
1500 PRINT "LA HORA SIDEREA (este) ES       ";HS2;"=";DMS$(HS2);"(horas)"
1510 GOTO 1410
1600 CLS:PRINT "PASO DE HORA CIVIL A SIDEREA"
1605 INPUT "PASANDO POR GREENWICH (S/N) ";G$
1610 CLS:INPUT "HORA CIVIL EN CIUDAD ORIGEN     ";HCO
1620 INPUT "LONGITUD CIUDAD ORIGEN en horas ";LCO
1630 INPUT "LONGITUD CIUDAD DESTINO en horas";LCD
1635 INPUT "HORA SIDEREA 0 horas T.U. tablas";HS0
1640 K=1.00273791
1645 IF G$<>"S" THEN 1710
1650 HCG=HCO-LCO
1655 CLS:PRINT "Tc Greenwich=Tc origen-";CHR$(138);"destino Tc Greenwich = ";DMS$(HCG)
1660 IS=HCG*K
1665 CLS:PRINT "Is=Tc Greenwich * 1.00273791    Is=";DMS$(IS)
1670 HSG=IS+HS0
1675 CLS:PRINT "Ts Greenwich=Is+Hs 0 horas T.U  Ts Greenwich=";DMS$(HSG)
1680 HSD=HSG+LCD
1685 CLS:PRINT "Ts destino=Ts Greenwich-";CHR$(138);"destinoTs destino= ";DMS$(HSD)
1700 CLS:GOTO 1605
1710 X=ABS(LCO)*6.5556E-2/24:CLS:PRINT "x= ";CHR$(138);" origen *";DMS$(6.5556E-2);"/24 horas   x=";DMS$(X)
1720 HSO0=HS0-X:CLS:PRINT "Hs 0h T.C.origen = Hs 0h T.U.-x Hs 0h T.C.=";DMS$(HSO0)
1730 ISO=HCO*K:CLS:PRINT "Is origen = Tc origen*1.00273791Is origen = ";DMS$(ISO)
1740 HSO=ISO+HSO0:CLS:PRINT "Hs origen= Is origen+ Hs 0h T.C.origen; Hs origen=";DMS$(HSO)
1750 HSD=HSO+(LCD-LCO):IF HSD>24 THEN HSD=HSD-24
1760 PRINT "Hs destino= Hs origen+ ";CHR$(238);CHR$(138);"       Hs destino=";DMS$(HSD)
1770 CLS:GOTO 1605
1800 CLS:PRINT "PASO DE HORA SIDEREA A CIVIL"
1805 INPUT "PASANDO POR GREENWICH (S/N) ";G$
1810 CLS:INPUT "HORA SIDEREA CIUDAD ORIGEN      ";HSO
1820 INPUT "LONGITUD CIUDAD ORIGEN          ";LCO
1830 INPUT "LONGITUD CIUDAD DESTINO         ";LCD
1840 INPUT "HORA SIDEREA A 0 horas T.U.     ";HS0
1850 K=1.00273791
1855 IF G$<>"S" THEN 1920
1860 HSG=HSO-LCO
1865 CLS:PRINT "Ts Greenwich= Ts origen+";CHR$(138);" origenTs Greenwich= ";DMS$(HSG)
1870 IS=HSG-HS0
1875 CLS:PRINT "Is= Ts Greenwich- Hs 0h T.U.    Is= ";DMS$(IS)
1880 HCG=IS/K
1885 CLS:PRINT "Tc Greenwich= Is/1.00273791     Tc= ";DMS$(HCG)
1890 HCD=HCG+LCD
1895 CLS:PRINT "Tc destino=Tc Greenwich+";CHR$(238);CHR$(138);"origenTc destino= ";DMS$(HCD)
1910 CLS:GOTO 1805
1920 X=(ABS(LCO)*6.5556E-2)/24:CLS:PRINT "x= (";CHR$(138);" origen *";DMS$(6.5556E-2);")/24       x=";DMS$(X)
1930 HSO0=HS0+X:PRINT "Hs 0h T.C.origen= Hs 0h T.U.+ x Hs 0h T.C.origen= ";DMS$(HSO0)
1940 ISO=HSO-HSO0:PRINT "Is= Hs origen- Hs 0h T.C. origenIs= ";DMS$(ISO)
1950 HCO=ISO/K:PRINT "Tc origen= Is/1.00273791        Tc origen= ";DMS$(HCO)
1960 HCD=HCO+(LCD-LCO):IF HCD>24 THEN HCD=HCD-24
1970 PRINT "Tc destino= Tc origen + ";CHR$(238);CHR$(138);"      Tc destino= ";DMS$(HCD)
1980 CLS:GOTO 1805
2000 CLS:PRINT "PASO DE TIEMPO CIVIL A TIEMPO   VERDADERO"
2010 CLS:INPUT "LONGITUD CIUDAD ORIGEN ";LCO
2020 CLS:INPUT "LONGITUD CIUDAD DESTINO ";LCD
2030 INPUT "TIEMPO CIVIL CIUDAD ORIGEN      ";TCO
2040 INPUT "ECUACION DE TIEMPO A 0 horas T.U";ET
2045 INPUT "ECUACION DE TIEMPO A 0 horas T.U (del dia siguiente) ";ETS
2050 TCG=TCO-LCO
2060 ETT=ETS-ET
2070 X=TCG*ETT/24
2080 ET=ET+X
2090 TVG=TCG-ET+12:IF TVG>24 THEN TVG=TVG-24
2100 TV=TVG+LCD
2110 PRINT "EL TIEMPO VERDADERO ES          ";TV;" = ";DMS$(TV)
2120 GOTO 2010
2200 CLS:PRINT "PASO DE TIEMPO VERDADERO A      TIEMPO CIVIL"
2210 CLS:INPUT "LONGITUD CIUDAD ORIGEN          ";LCO
2220 CLS:INPUT "LONGITUD CIUDAD DESTINO         ";LCD
2225 INPUT "TIEMPO VERDADERO CIUDAD ORIGEN  ";TVO
2230 INPUT "ECUACION DE TIEMPO A 0 horas    ";ET
2240 INPUT "ECUACION DE TIEMPO A 0 horas (del dia siguiente) ";ETS
2250 ETT=ETS-ET
2260 TVG=TVO-LCO
2270 TVG1=TVG+12:IF TVG1>24 THEN TVG1=TVG1-24
2280 X=TVG1*ETT/24
2290 ET=ET+X
2300 TCG=TVG+ET+12:IF TCG>24 THEN TCG=TCG-24
2310 TC=TCG+LCD
2320 PRINT "EL TIEMPO CIVIL ES              ";TC;" = ";DMS$(TC)
2330 GOTO 2030
2400 CLS:PRINT "DURACION DE LAS ESTACIONES"
2410 CLS:CLEAR
2420 INPUT "LONGITUD DEL PERIGEO            ";L:CLS:INPUT "EXCENTRICIDAD ";E
2425 VP=360-L
2427 CLS:PRINT "Vp=360- longitud perigeo        Vp= ";DMS$(VP)
2430 CLS:DIM A(9)
2440 A(0)=VP
2450 FOR F=1 TO 8
2460 A(F)=VP-(2*E*SIN(A(F-1))+(5/4)*(E^2)*SIN(2*A(F-1)))*(180/PI)
2465 IF A(F)=A(F-1) THEN 2485
2470 PRINT "nt";F;"= ";DMS$(A(F))
2480 NEXT F
2485 A=A(F)
2490 PRINT "COMIENZO DE LA PRIMAVERA        ";DMS$(A)
2500 VV=VP+90:PRINT "Vv=Vp+90                        Vv= ";DMS$(VV)
2502 CLS:DIM B(9)
2503 B(0)=VV
2505 FOR F=1 TO 8
2510 B(F)=VV-(2*E*SIN(B(F-1))+(5/4)*(E^2)*SIN(2*B(F-1)))*(180/PI)
2520 IF B(F)=B(F-1) THEN 2580
2540 PRINT "nt";F;"= ";DMS$(B(F))
2550 NEXT F
2580 B=B(F)
2590 PRINT "COMIENZO DEL VERANO             ";DMS$(B)
2600 VO=VV+90:PRINT "Vo=Vv+90                        Vo= ";DMS$(VO)
2602 CLS:DIM C(9)
2603 C(0)=VO
2605 FOR F=1 TO 8
2610 C(F)=VO-(2*E*SIN(C(F-1))+(5/4)*(E^2)*SIN(2*C(F-1)))*(180/PI)
2620 IF C(F)=C(F-1) THEN 2680
2640 PRINT "nt";F;"= ";DMS$(C(F))
2650 NEXT F
2680 C=C(F)
2690 PRINT "COMIENZO DEL OTONO              ";DMS$(C)
2700 VI=VO+90:PRINT "Vi=Vo+90                        Vi= ";DMS$(VI)
2702 CLS:DIM D(9)
2703 D(0)=VI
2705 FOR F=1 TO 8
2710 D(F)=VI-(2*E*SIN(D(F-1))+(5/4)*(E^2)*SIN(2*D(F-1)))*(180/PI)
2720 IF D(F)=D(F-1) THEN 2780
2740 PRINT "nt";F;"= ";DMS$(D(F))
2750 NEXT F
2780 D=D(F)
2790 PRINT "COMIENZO DEL INVIERNO           ";DMS$(D)
2810 DP=B-A:G1=365.2422*DP/360:H1=INT(G1):J=FRAC(G1):Z1=J*24
2820 CLS:PRINT "LA DURACION DE LA PRIMAVERA ES ";DMS$(DP);" ";H1;"d.";DMS$(Z1);"h."
2830 DV=C-B:G2=365.2422*DV/360:H2=INT(G2):J=FRAC(G2):Z2=J*24
2840 CLS:PRINT "LA DURACION DEL VERANO ES      ";DMS$(DV);" ";H2;"d.";DMS$(Z2);"h."
2850 DO=D-C:G3=365.2422*DO/360:H3=INT(G3):J=FRAC(G3):Z3=J*24
2860 CLS:PRINT "LA DURACION DEL OTONO ES       ";DMS$(DO);" ";H3;"d.";DMS$(Z3);"h."
2870 DI=360+A-D:G4=365.2422*DI/360:H4=INT(G4):J=FRAC(G4):Z4=J*24
2880 CLS:PRINT "LA DURACION DEL INVIERNO ES    ";DMS$(DI);" ";H4;"d.";DMS$(Z4);"h."
2890 CLS:PRINT "COMPROBACION (error de calculo)";360-(DP+DV+DO+DI);" ";365.2422-(G1+G2+G3+G4)
