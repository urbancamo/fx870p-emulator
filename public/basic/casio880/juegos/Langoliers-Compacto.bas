10 F$=" ******* LANGOLIERS 2015 ******  ******* MANUELITO LOPEZ ******"
20 GOSUB 850
30 GOSUB 830:F$="DESEA VER INSTRUCCIONES (S/N)?":GOSUB 840
40 IF N$="S" OR N$="s" THEN 780
50 IF N$="N" OR N$="n" THEN 70
60 GOTO 30
70 CLS:PRINT "ESPERE POR FAVOR...";
80 IF A$="" THEN A$=" "
90 IF T$="" THEN T$=" "
100 C$=CHR$(252):D$="":K$="":L$="":M$=CHR$(253):N$=""
110 A=0:C=0:D=0:E=0:M=0:N=0:L=0:P=0:Q=0:T=0:V=5:X=0
120 GOSUB 600:GOSUB 920
130 GOSUB 830
140 LET F$="NOVATO, MEDIO, EXPERTO (N/M/E)?":GOSUB 840
150 IF N$="N" OR N$="n" THEN LET A=1:GOTO 190
160 IF N$="M" OR N$="m" THEN LET A=2:GOTO 190
170 IF N$="E" OR N$="e" THEN LET A=4:GOTO 190
180 GOTO 130
190 GOSUB 830
200 F$="FACIL O DIFICIL (F/D)?":GOSUB 840
210 IF N$="F" OR N$="f" THEN N=20:T=1:GOTO 240
220 IF N$="D" OR N$="d" THEN N=10:T=2:GOTO 240
230 GOTO 190
240 GOSUB 830
250 PRINT CHR$(134);"0123456789";CHR$(134);"PUNTOS";P;
260 LOCATE 23,0:PRINT CHR$(134);R$;
270 LOCATE 0,1:PRINT C$;"          ";CHR$(134);"VIDAS ";V;
280 LOCATE 23,1:PRINT CHR$(134);A$;T$;R;
290 K$=INKEY$
300 IF K$="" THEN 340
310 IF K$>="0" AND K$<="9" THEN 400
320 IF K$="P" OR K$="p" THEN 630
330 IF K$="S" OR K$="s" THEN IF P>R THEN 670 ELSE 730
340 LOCATE X,1:PRINT M$;
350 M=M+1:IF M=2 THEN GOSUB 380
360 D=D+1:IF D=N THEN GOSUB 500
370 GOTO 290
380 M=O:IF M$=CHR$(253) THEN M$=CHR$(254) ELSE M$=CHR$(253)
390 RETURN
400 BEEP:L=VAL(K$)+1:GOSUB 430
410 IF L=X THEN GOSUB 460 ELSE GOSUB 500
420 GOTO 290
430 L$="-":GOSUB 450:L$=" ":GOSUB 450
440 RETURN
450 FOR Q=1 TO L:LOCATE Q,1:PRINT L$;:NEXT Q:RETURN
460 BEEP:BEEP 1:LOCATE X,1:PRINT "*";:GOSUB 590
470 LOCATE X,1:PRINT " ";:GOSUB 600:D=0:P=P+1
480 LOCATE 18,0:PRINT P;:C=C+A:IF C=100 THEN GOSUB 610
490 RETURN
500 M$=CHR$(255):LOCATE X,1:PRINT M$;
510 L$="-":GOSUB 570:L$=" ":GOSUB 570
520 BEEP 1:BEEP:LOCATE 0,1:PRINT "*";:GOSUB 590
530 LOCATE 0,1:PRINT C$;:D=0:V=V-1
540 LOCATE 18,1:PRINT V;:IF V=0 THEN IF P>R THEN 670 ELSE 730
550 LOCATE X,1:PRINT " ";:GOSUB 600:M$=CHR$(253)
560 RETURN
570 FOR Q=X-1 TO 0 STEP -1:LOCATE Q,1:PRINT L$;:NEXT Q
580 RETURN
590 FOR E=1 TO 50:NEXT E:RETURN
600 X=INT(RAN#*10)+1:RETURN
610 C=0:N=N-2:IF N=0 THEN N=2
620 GOSUB 820:RETURN
630 BEEP 1:GOSUB 900:LOCATE 0,1:PRINT "   PAUSA   ";
640 GOSUB 880:LOCATE 3,1:PRINT "     ";
650 LOCATE 0,1:PRINT C$;:LOCATE X,1:PRINT M$;
660 GOTO 290
670 GOSUB 820:GOSUB 830
680 F$="INTRODUZCA SU NOMBRE:":GOSUB 840:R$=LEFT$(N$,8):R=P
690 IF A=1 THEN A$="N"
700 IF A=2 THEN A$="M"
710 IF A=4 THEN A$="E"
720 IF T=1 THEN T$="F" ELSE T$="D"
730 GOSUB 830
740 F$="DESEA VOLVER A JUGAR (S/N)?":GOSUB 840
750 IF N$="S" OR N$="s" THEN 70
760 IF N$="N" OR N$="n" THEN 950
770 GOTO 730
780 GOSUB 900:GOSUB 830:LOCATE 0,0:PRINT "USE LAS TECLAS DEL (0) AL (9)";
790 LOCATE 0,1:PRINT "PARA DISPARAR AL LOS LANGOLIERS";:GOSUB 880
800 GOSUB 830:LOCATE 0,0:PRINT "LA TECLA (P) PAUSA EL JUEGO";
810 LOCATE 0,1:PRINT "LA TECLA (S) SALE DEL JUEGO";:GOSUB 880:GOTO 70
820 FOR Q=1 TO 5:BEEP 1:NEXT Q:RETURN
830 BEEP:CLS:RETURN
840 GOSUB 900:PRINT F$;:LOCATE 0,1:INPUT "",N$:RETURN
850 CLS:FOR Q=1 TO LEN(F$):PRINT MID$(F$,Q,1);
860 IF MID$(F$,Q,1)<>" " THEN BEEP
870 NEXT Q
880 K$=INKEY$:IF K$="" THEN 880
890 BEEP 1
900 K$=INKEY$:IF K$<>"" THEN 900
910 RETURN
920 RESTORE:FOR Q=252 TO 255:READ D$:DEFCHR$(Q)=D$:NEXT Q
930 RETURN
940 DATA "28FF7C3810","76D8F0D876","78D6F0D678","3810387CFE"
950 GOSUB 830:PRINT "Ready Px";:END
