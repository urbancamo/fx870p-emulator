10 REM LISTADO DE PROGRAMA PARA LAS CASIO FX-850P Y FX-880P

20 REM DESORDEN ALFABETICO!
30 REM MANUEL LOPEZ 2016

40 REM INSTRUCCIONES DEL JUEGO
50 REM USE LAS TECLAS (4) Y (6) PARA MOVER EL TRANSPORTADOR
60 REM LA TECLA (5) TRANSPORTA LA LETRA AL CESTO DE ALMACENAJE
70 REM INTERCAMBIANDO LAS LETRAS DEBE LOGRAR ORDENARLAS
80 REM ALFABETICAMENTE DEJANDO ARREGLADO EL SALON DE JUEGO
90 REM EN EL MENOR TIEMPO POSIBLE
100 REM LA TECLA (P) PONE EL JUEGO EN PAUSA
110 REM LA TECLA (S) SALE DEL JUEGO

120 REM DEFINIR EL USO DE LAS VARIABLES
130 REM C$=CESTO DE ALMACENAJE, D$=MATRIZ DESORDENADA, F$=GUARDAR FRASE, G$=GUARDAR RECORD
140 REM I$=INTERCAMBIO DE LETRA, K$=CAPTURAR TECLADO, L$=RECEPTACULO LLENO, O$=MATRIZ ORDENADA
150 REM P$=CAPTURAR PALABRAS, S$=SALON DE JUEGO, U$=TIEMPO TRANSCURRIDO, V$=RECEPTACULO VACIO
160 REM C=CICLOS DE CONTEO, G=GUARDAR RECORD, P=PUNTERO ALEATORIO, T=TIEMPO TRANSCURRIDO
170 REM X=POSICION DEL TRANSPORTADOR

180 REM PRESENTACION
190 LET F$=" #### DESORDEN ALFABETICO! ####  ######## MANUEL LOPEZ ########"
200 GOSUB 1330

210 REM PREGUNTAR SI SE DESEA VER INSTRUCIONES
220 GOSUB 1250
230 LET F$="DESEA VER INSTRUCCIONES (S/N)?"
240 GOSUB 1280
250 IF P$="S" OR P$="s" THEN GOTO 1110
260 IF P$="N" OR P$="n" THEN GOTO 290
270 GOTO 220

280 REM MOSTRAR RECORD ACTUAL
290 GOSUB 1250
300 LOCATE 7,0:PRINT "MEJOR TIEMPO:";G;
310 LOCATE 7,1:PRINT "CAMPEON: ";G$;
320 GOSUB 1200
330 GOSUB 1390

340 REM DEFINIR VARIABLES
350 LET C=0:LET P=0:LET T=0:LET X=12
360 LET C$=" ":LET F$="":LET I$="":LET K$="":LET P$="":LET S$="":LET U$=""
370 LET L$="abcdefghijklmnopqrstuvwxyz"
380 LET V$="                          "
390 DIM D$(26),O$(26)

400 REM MOSTRAR MENSAJE DE ESPERA MIENTRAS SE REVUELVEN LAS LETRAS
410 GOSUB 1250
420 LOCATE 0,0:PRINT "ESPERE POR FAVOR...";
430 GOSUB 740

440 REM CREAR PANTALLA DE JUEGO
450 BEEP 0:BEEP 1:GOSUB 1250
460 LOCATE 0,0:PRINT "ABCDEFGHIJKLMNOPQRSTUVWXYZ";CHR$(134);U$;
470 LOCATE X,1:PRINT CHR$(135);
480 LOCATE 0,1:PRINT S$;CHR$(134);"  ";C$;

490 REM CAPTURAR DESDE EL TECLADO
500 LET K$=INKEY$
510 IF K$="" THEN GOTO 580
520 IF K$="4" THEN BEEP 0:LET X=X-1:IF X<0 THEN LET X=25
530 IF K$="6" THEN BEEP 0:LET X=X+1:IF X>25 THEN LET X=0
540 IF K$="5" THEN BEEP 1:GOSUB 640
550 IF K$="P" OR K$="p" THEN GOTO 690
560 IF K$="S" OR K$="s" THEN GOTO 1040

570 REM CONTAR TIEMPO
580 LET T=T+1:LET U$=STR$(T):LET U$=RIGHT$(U$,4)
590 LOCATE 27,0:PRINT U$;

600 REM VERIFICAR ORDEN
610 IF S$<>"abcdefghijklmnopqrstuvwxyz" THEN GOTO 470
620 GOTO 880

630 REM INTERCAMBIAR DE LETRAS
640 IF X=0 THEN LET I$=MID$(S$,1,1):LET S$=C$+MID$(S$,2):GOTO 660
650 LET I$=MID$(S$,X+1,1):LET S$=MID$(S$,1,X)+C$+MID$(S$,X+2)
660 LET C$=I$
670 RETURN

680 REM PONER EL JUEGO EN PAUSA
690 BEEP 1:GOSUB 1430
700 LOCATE 0,1:PRINT "       (( pausa ))        ";
710 GOSUB 1390
720 GOTO 470

730 REM REVOLVER LETRAS
740 FOR C=1 TO 26
750 LET O$(C)=MID$(L$,C,1)
760 LET D$(C)=MID$(V$,C,1)
770 NEXT C
780 FOR C=1 TO 26
790 LET P=INT(RAN#*26)+1
800 IF D$(P)<>" " THEN GOTO 790
810 LET D$(P)=O$(C)
820 NEXT C
830 FOR C=1 TO 26
840 LET S$=S$+D$(C)
850 NEXT C
860 RETURN

870 REM MOSTRAR ORDEN CORECTO PARPADEANDO
880 LOCATE 29,1:PRINT " ";
890 FOR C=1 TO 5
900 LOCATE 0,1:PRINT V$;:BEEP 0
910 LOCATE 0,1:PRINT L$;:BEEP 0
920 NEXT C

930 REM VERIFICAR SI SE MEJORO EL TIEMPO
940 IF G=0 OR G>T THEN GOTO 970
950 GOTO 1040

960 REM GUARDAR NOMBRE RECORD
970 GOSUB 1200
980 GOSUB 1250
990 LET F$="INTRODUZCA SU NOMBRE:"
1000 GOSUB 1280
1010 LET G$=LEFT$(P$,9)
1020 LET G=T

1030 REM PREGUNTAR SI SE DESEA VOLVER A JUGAR
1040 GOSUB 1250
1050 LET F$="DESEA VOLVER A JUGAR (S/N)?"
1060 GOSUB 1280
1070 IF P$="S" OR P$="s" THEN GOTO 350
1080 IF P$="N" OR P$="n" THEN GOTO 1470
1090 GOTO 1040

1100 REM MOSTRAR LAS INSTRUCCIONES DEL JUEGO
1110 GOSUB 1430:GOSUB 1250
1120 LOCATE 0,0:PRINT "(4) Y (6) MUEVEN EL CURSOR, (5)";
1130 LOCATE 0,1:PRINT "INTERCAMBIA LA LETRA DEL CESTO";
1140 GOSUB 1390:GOSUB 1250
1150 LOCATE 0,0:PRINT "LA TECLA (P) PAUSA EL JUEGO";
1160 LOCATE 0,1:PRINT "LA TECLA (S) SALE DEL JUEGO";
1170 GOSUB 1390
1180 GOTO 290

1190 REM EMITIR SONIDO DE CELEBRACION 
1200 FOR C=1 TO 5
1210 BEEP 1
1220 NEXT C
1230 RETURN

1240 REM PREPARAR PANTALLA PARA ENTRADA
1250 BEEP 0:CLS
1260 RETURN

1270 REM MOSTRAR UNA FRASE Y CAPTURAR REPUESTA
1280 GOSUB 1430
1290 LOCATE 0,0:PRINT F$;
1300 LOCATE 0,1:INPUT "",P$
1310 RETURN

1320 REM AMIMAR UNA FRASE
1330 CLS
1340 FOR C=1 TO LEN(F$)
1350 PRINT MID$(F$,C,1);
1360 IF MID$(F$,C,1)<>" " THEN BEEP 0
1370 NEXT C

1380 REM ESPERAR PULSACION DE UNA TECLA
1390 LET K$=INKEY$
1400 IF K$="" THEN GOTO 1390
1410 BEEP 1

1420 REM EVITAR PULSACION DOBLE
1430 LET K$=INKEY$
1440 IF K$<>"" THEN GOTO 1430
1450 RETURN

1460 REM FINALIZAR EL JUEGO
1470 GOSUB 1250
1480 LOCATE 0,0:PRINT "Ready Px";
1490 END
