10 REM LISTADO DE PROGRAMA PARA LAS CASIO FX-850P Y FX-880P

20 REM LANGOLIERS 2015 POR MANUELITO LOPEZ

30 REM INSTRUCCIONES DEL JUEGO
40 REM USE LAS TECLAS DEL (0) AL (9) PARA DISPARAR AL LANGOLIERS
50 REM LA TECLA (P) PONE EL JUEGO EN PAUSA
60 REM LA TECLA (S) SALE DEL JUEGO

70 REM PRESENTACION
80 LET F$=" ******* LANGOLIERS 2015 ******  ******* MANUELITO LOPEZ ******"
90 GOSUB 1800

100 REM DEFINIR EL USO DE LAS VARIABLES
110 REM A$=TIPO DE EXPERIENCIA, C$=FIGURA DEL CAÑON, D$=DEFINICION DE CARACTER, F$=GUARDAR FRASE
120 REM K$=CAPTURAR TECLADO, L$=FORMA DEL LASER, M$=FIGURA DEL LANGOLIERS, N$=CAPTURAR PALABRAS
130 REM R$=NOMBRE RECORD, T$=TIPO DE DIFICULTAD, A=EXPERIENCIA, C=CONTADOR, D=DIFICULTAD, E=ESPERA
140 REM M=MOVIMIENTO, N=NIVEL, L=LASER, P=PUNTOS, Q=CILOS, R=RECORD, T=TIPO, V=VIDAS, X=POCICION

150 REM PREGUNTAR SI SE DESEA VER INSTRUCIONES
160 GOSUB 1720
170 LET F$="DESEA VER INSTRUCCIONES (S/N)?"
180 GOSUB 1750
190 IF N$="S" OR N$="s" THEN GOTO 1580
200 IF N$="N" OR N$="n" THEN GOTO 230
210 GOTO 160

220 REM MOSTRAR MENSAJE DE ESPERA MIENTRAS SE DEFINEN PARAMETROS
230 CLS :LOCATE 0,0:PRINT "ESPERE POR FAVOR...";

240 REM DEFINIR VARIABLES
250 IF A$="" THEN LET A$=" "
260 IF T$="" THEN LET T$=" "
270 LET C$=CHR$(252):LET D$="":LET K$=""
280 LET L$="":LET M$=CHR$(253):LET N$=""
290 LET A=0:LET C=0:LET D=0:LET E=0:LET M=0:LET N=0
300 LET L=0:LET P=0:LET Q=0:LET T=0:LET V=5:LET X=0
310 GOSUB 1240

320 REM CARGAR CARACTERES DEFINIDOS
330 GOSUB 1940

340 REM ESCOGER NIVEL DE EXPERIENCIA
350 GOSUB 1720
360 LET F$="NOVATO, MEDIO, EXPERTO (N/M/E)?"
370 GOSUB 1750
380 IF N$="N" OR N$="n" THEN LET A=1:GOTO 430
390 IF N$="M" OR N$="m" THEN LET A=2:GOTO 430
400 IF N$="E" OR N$="e" THEN LET A=4:GOTO 430
410 GOTO 350

420 REM ESCOGER NIVEL DE DIFICULTAD
430 GOSUB 1720
440 LET F$="FACIL O DIFICIL (F/D)?"
450 GOSUB 1750
460 IF N$="F" OR N$="f" THEN LET N=20:LET T=1:GOTO 500
470 IF N$="D" OR N$="d" THEN LET N=10:LET T=2:GOTO 500
480 GOTO 430

490 REM CREAR PANTALLA DE JUEGO
500 GOSUB 1720
510 LOCATE 0,0:PRINT CHR$(134);"0123456789";CHR$(134);"PUNTOS";P;
520 LOCATE 23,0:PRINT CHR$(134);R$;
530 LOCATE 0,1:PRINT C$;"          ";CHR$(134);"VIDAS ";V;
540 LOCATE 23,1:PRINT CHR$(134);A$;T$;R;

550 REM CAPTURAR DESDE EL TECLADO
560 LET K$=INKEY$
570 IF K$="" THEN GOTO 620
580 IF K$>="0" AND K$<="9" THEN GOTO 710
590 IF K$="P" OR K$="p" THEN GOTO 1320
600 IF K$="S" OR K$="s" THEN IF P>R THEN GOTO 1400 ELSE GOTO 1510

610 REM CREAR MOVIMIENTO
620 LOCATE X,1:PRINT M$;
630 LET M=M+1:IF M=2 THEN GOSUB 670
640 LET D=D+1:IF D=N THEN GOSUB 980
650 GOTO 560

660 REM CREAR ANIMACION DEL LANGOLIERS
670 LET M=O
680 IF M$=CHR$(253) THEN LET M$=CHR$(254) ELSE LET M$=CHR$(253)
690 RETURN

700 REM DEFINIR ACCION AL PULSAR UNA TECLA
710 BEEP 0
720 LET L=VAL(K$)+1
730 GOSUB 770
740 IF L=X THEN GOSUB 860 ELSE GOSUB 980
750 GOTO 560

760 REM CREAR DISPARO DEL CAÑON
770 LET L$="-":GOSUB 810
780 LET L$=" ":GOSUB 810
790 RETURN

800 REM DIBUJAR RAYO LASER HACIA LA DERECHA
810 FOR Q=1 TO L
820 LOCATE Q,1:PRINT L$;
830 NEXT Q
840 RETURN

850 REM MATAR AL LANGOLIERS
860 BEEP 0:BEEP 1
870 LOCATE X,1:PRINT "*";
880 GOSUB 1210
890 LOCATE X,1:PRINT " ";
900 GOSUB 1240

910 REM AUMENTAR PUNTAJE
920 LET D=0:LET P=P+1
930 LOCATE 18,0:PRINT P;
940 LET C=C+A
950 IF C=100 THEN GOSUB 1270
960 RETURN

970 REM CREAR DISPARO DEL LANGOLIERS
980 LET M$=CHR$(255)
990 LOCATE X,1:PRINT M$;
1000 LET L$="-":GOSUB 1160
1010 LET L$=" ":GOSUB 1160

1020 REM DESTRUIR EL CAÑON
1030 BEEP 1:BEEP 0
1040 LOCATE 0,1:PRINT "*";
1050 GOSUB 1210
1060 LOCATE 0,1:PRINT C$;

1070 REM PERDER VIDA
1080 LET D=0:LET V=V-1
1090 LOCATE 18,1:PRINT V;
1100 IF V=0 THEN IF P>R THEN GOTO 1400 ELSE GOTO 1510
1110 LOCATE X,1:PRINT " ";
1120 GOSUB 1240
1130 LET M$=CHR$(253)
1140 RETURN

1150 REM DIBUJAR RAYO LASER HACIA LA IzQUIERDA
1160 FOR Q=X-1 TO 0 STEP -1
1170 LOCATE Q,1:PRINT L$;
1180 NEXT Q
1190 RETURN

1200 REM PAUTAR UN TIEMPO DE ESPERA PARA LA EXPLOSION
1210 FOR E=1 TO 50:NEXT E
1220 RETURN

1230 REM CREAR UNA POSICION ALEATORIA PARA EL LANGOLIERS
1240 LET X=INT(RAN#*10)+1
1250 RETURN

1260 REM PASAR DE NIVEL
1270 LET C=0:LET N=N-2
1280 IF N=0 THEN LET N=2
1290 GOSUB 1670
1300 RETURN

1310 REM PONER EL JUEGO EN PAUSA
1320 BEEP 1:GOSUB 1900
1330 LOCATE 0,1:PRINT "   PAUSA   ";
1340 GOSUB 1860
1350 LOCATE 3,1:PRINT "     ";
1360 LOCATE 0,1:PRINT C$;
1370 LOCATE X,1:PRINT M$;
1380 GOTO 560

1390 REM GUARDAR NOMBRE RECORD
1400 GOSUB 1670
1410 GOSUB 1720
1420 LET F$="INTRODUZCA SU NOMBRE:"
1430 GOSUB 1750
1440 LET R$=LEFT$(N$,8)
1450 LET R=P
1460 IF A=1 THEN LET A$="N"
1470 IF A=2 THEN LET A$="M"
1480 IF A=4 THEN LET A$="E"
1490 IF T=1 THEN LET T$="F" ELSE LET T$="D"

1500 REM PREGUNTAR SI SE DESEA VOLVER A JUGAR
1510 GOSUB 1720
1520 LET F$="DESEA VOLVER A JUGAR (S/N)?"
1530 GOSUB 1750
1540 IF N$="S" OR N$="s" THEN GOTO 230
1550 IF N$="N" OR N$="n" THEN GOTO 2100
1560 GOTO 1510

1570 REM MOSTRAR LAS INSTRUCCIONES DEL JUEGO
1580 GOSUB 1900:GOSUB 1720
1590 LOCATE 0,0:PRINT "USE LAS TECLAS DEL (0) AL (9)";
1600 LOCATE 0,1:PRINT "PARA DISPARAR AL LOS LANGOLIERS";
1610 GOSUB 1860:GOSUB 1720
1620 LOCATE 0,0:PRINT "LA TECLA (P) PAUSA EL JUEGO";
1630 LOCATE 0,1:PRINT "LA TECLA (S) SALE DEL JUEGO";
1640 GOSUB 1860
1650 GOTO 230

1660 REM EMITIR SONIDO DE CELEBRACION 
1670 FOR Q=1 TO 5
1680 BEEP 1
1690 NEXT Q
1700 RETURN

1710 REM PREPARAR PANTALLA PARA ENTRADA
1720 BEEP 0:CLS
1730 RETURN

1740 REM MOSTRAR UNA FRASE Y CAPTURAR REPUESTA
1750 GOSUB 1900
1760 LOCATE 0,0:PRINT F$;
1770 LOCATE 0,1:INPUT "",N$
1780 RETURN

1790 REM AMIMAR UNA FRASE
1800 CLS
1810 FOR Q=1 TO LEN(F$)
1820 PRINT MID$(F$,Q,1);
1830 IF MID$(F$,Q,1)<>" " THEN BEEP 0
1840 NEXT Q

1850 REM ESPERAR PULSACION DE UNA TECLA
1860 LET K$=INKEY$
1870 IF K$="" THEN GOTO 1860
1880 BEEP 1

1890 REM EVITAR PULSACION DOBLE
1900 LET K$=INKEY$
1910 IF K$<>"" THEN GOTO 1900
1920 RETURN

1930 REM DEFINIR CARACTERES
1940 RESTORE
1950 FOR Q=252 TO 255
1960 READ D$
1970 DEFCHR$(Q)=D$
1980 NEXT Q
1990 RETURN

2000 REM CODIGOS DE CARACTERES DEFINIDOS
2010 DATA "28FF7C3810","76D8F0D876","78D6F0D678","3810387CFE"
2020 REM    X            XXX          XXX             X
2030 REM    XX          XXXXX        XXXXX           XX
2040 REM   XXXX         X X X        X X X        X XXX
2050 REM    XXXX        XXXXX        XXXXX        XXXXX
2060 REM   XXXX          X X         X   X        X XXX
2070 REM    XX          X   X         X X            XX
2080 REM    X           X   X         X X             X

2090 REM FINALIZAR EL JUEGO
2100 GOSUB 1720
2110 LOCATE 0,0:PRINT "Ready Px";
2120 END
