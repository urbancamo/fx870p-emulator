10 F$=" #### DESORDEN ALFABETICO! ####  ######## MANUEL LOPEZ ########"
20 GOSUB 570
30 GOSUB 550:F$="DESEA VER INSTRUCCIONES (S/N)?":GOSUB 560
40 IF P$="S" OR P$="s" THEN 470
50 IF P$="N" OR P$="n" THEN 70
60 GOTO 30
70 GOSUB 550
80 LOCATE 7,0:PRINT "MEJOR TIEMPO:";G;:LOCATE 7,1:PRINT "CAMPEON: ";G$;
90 GOSUB 540:GOSUB 600
100 C=0:P=0:T=0:X=12:C$=" ":F$="":I$="":K$="":P$="":S$="":U$=""
110 L$="abcdefghijklmnopqrstuvwxyz":V$="                          "
120 DIM D$(26),O$(26):GOSUB 550:PRINT "ESPERE POR FAVOR...";:GOSUB 290
130 BEEP:BEEP 1:GOSUB 550:PRINT "ABCDEFGHIJKLMNOPQRSTUVWXYZ";CHR$(134);U$;
140 LOCATE X,1:PRINT CHR$(135);:LOCATE 0,1:PRINT S$;CHR$(134);"  ";C$;
150 K$=INKEY$:IF K$="" THEN 210
160 IF K$="4" THEN BEEP:X=X-1:IF X<0 THEN X=25
170 IF K$="6" THEN BEEP:X=X+1:IF X>25 THEN X=0
180 IF K$="5" THEN BEEP 1:GOSUB 240
190 IF K$="P" OR K$="p" THEN 270
200 IF K$="S" OR K$="s" THEN 430
210 T=T+1:U$=STR$(T):U$=RIGHT$(U$,4):LOCATE 27,0:PRINT U$;
220 IF S$<>"abcdefghijklmnopqrstuvwxyz" THEN 140
230 GOTO 350
240 IF X=0 THEN I$=MID$(S$,1,1):S$=C$+MID$(S$,2):GOTO 260
250 I$=MID$(S$,X+1,1):S$=MID$(S$,1,X)+C$+MID$(S$,X+2)
260 C$=I$:RETURN
270 BEEP 1:GOSUB 620:LOCATE 0,1:PRINT "       (( pausa ))        ";
280 GOSUB 600:GOTO 140
290 FOR C=1 TO 26:O$(C)=MID$(L$,C,1):D$(C)=MID$(V$,C,1):NEXT C
300 FOR C=1 TO 26
310 P=INT(RAN#*26)+1
320 IF D$(P)<>" " THEN 310
330 D$(P)=O$(C):NEXT C
340 FOR C=1 TO 26:S$=S$+D$(C):NEXT C:RETURN
350 LOCATE 29,1:PRINT " ";
360 FOR C=1 TO 5:LOCATE 0,1:PRINT V$;:BEEP
370 LOCATE 0,1:PRINT L$;:BEEP:NEXT C
380 IF G=0 OR G>T THEN 400
390 GOTO 430
400 GOSUB 540:GOSUB 550
410 F$="INTRODUZCA SU NOMBRE:"
420 GOSUB 560:G$=LEFT$(P$,9):G=T
430 GOSUB 550:F$="DESEA VOLVER A JUGAR (S/N)?":GOSUB 560
440 IF P$="S" OR P$="s" THEN 100
450 IF P$="N" OR P$="n" THEN 640
460 GOTO 430
470 GOSUB 620:GOSUB 550
480 LOCATE 0,0:PRINT "(4) Y (6) MUEVEN EL CURSOR, (5)";
490 LOCATE 0,1:PRINT "INTERCAMBIA LA LETRA DEL CESTO";
500 GOSUB 600:GOSUB 550
510 LOCATE 0,0:PRINT "LA TECLA (P) PAUSA EL JUEGO";
520 LOCATE 0,1:PRINT "LA TECLA (S) SALE DEL JUEGO";
530 GOSUB 600:GOTO 70
540 FOR C=1 TO 5:BEEP 1:NEXT C:RETURN
550 BEEP:CLS:RETURN
560 GOSUB 620:PRINT F$;:LOCATE 0,1:INPUT "",P$:RETURN
570 CLS:FOR C=1 TO LEN(F$):PRINT MID$(F$,C,1);
580 IF MID$(F$,C,1)<>" " THEN BEEP
590 NEXT C
600 K$=INKEY$:IF K$="" THEN 600
610 BEEP 1
620 K$=INKEY$:IF K$<>"" THEN 620
630 RETURN
640 GOSUB 550:PRINT "Ready Px";:END
