5 'COLE-PRE
6 PRINT "*COLEBROOK o U. PRESION (C/P)*";:INPUT ZA$
7 IF ZA$="P" GOTO 1020 ELSE GOTO 10
10 CLEAR :CLS
20 PRINT " ****** COLEBROOK-WHITE ******";
30 LOCATE 0,1
40 PRINT " *****************************"
50 CLS:PRINT "Casos: 1.-Re,D,K--->f";
60 LOCATE 7,1
70 PRINT "2.-Re,D,f--->K";
80 LOCATE 25,1:INPUT "Cual";E
90 IF E<1 OR E>2 THEN 50
95 CLS
100 INPUT "Diametro (m)";D
110 INPUT "Numero Reynolds";Re
120 ON E GOTO 130,330
130 INPUT "Rugosidad (mm)";K
140 K=K/1000
150 X=1
160 A=2*LOG(D/K)+1.14
170 B=9.35*D/Re/K
180 Y=A-2*LOG(1+B*X)-X
190 I=1
200 IF ABS Y<1E-6 THEN 290
210 X=X+I
220 Z=A-2*LOG(1+B*X)-X
230 IF SGN Y=SGN Z THEN Y=Z:GOTO 210
240 IF ABS Z<1E-6 THEN Y=Z:GOTO 290
250 IF ABS Y<1E-6 THEN 290
260 X=X-I
270 I=I/2
280 GOTO 210
290 CLS:BEEP0:BEEP1
300 F=1/X/X
310 PRINT "Solucion f=";ROUND(F,-6)
320 CLS:END
330 '*****CASO B *****
340 INPUT "f";F
350 K=3.715*D*(10^(-1/(2*SQR F))-2.51/Re/SQR F)
360 PRINT "K=";ROUND(1E3*K,-5);"mm"
370 CLS:END
1010 'PRESION
1020 CLEAR:CLS
1030 PRINT " ****** UNIDADES PRESION ******";
1035 LOCATE 0,1
1040 PRINT " ******************************"
1050 INPUT "Densidad (k/m^3)";D
1060 CLS
1070 PRINT "Opciones:  1      2     3";
1080 LOCATE 0,1
1090 PRINT "         m.c.a  N/m^2  Atm";:INPUT A
10100 ON A GOTO 10110,10170,10220
10110 CLS
10120 INPUT "Carga en m.c.a";H
10130 H=H*D/1000
10140 P1=H*9.81*1000
10150 P2=P1/1.013E5
10160 GOTO 10260
10170 CLS
10180 INPUT "Carga en N/m^2";P1
10190 H=P1/1000/9.81
10200 P2=P1/1.013E5
10210 GOTO 10260
10220 CLS
10230 INPUT "Carga en atm";P2
10240 P1=P2*1.013E5
10250 H=P1/1000/9.81
10260 CLS
10270 PRINT " m.c.a     N/m^2        Atm";
10280 LOCATE 0,1:PRINT ROUND(H,-4);
10285 LOCATE 10,1:PRINT ROUND(P1,-4);
10290 LOCATE 23,1:PRINT ROUND(P2,-4)
10310 INPUT "Mas conversiones S/N";A$
10320 IF A$="S" THEN 1060 ELSE IF A$<>"N" THEN 10310
10330 END
