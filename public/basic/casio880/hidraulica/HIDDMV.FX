10 PRINT "SUPERHIDRAULICA'95              (c) Dani Marti"; CHR$(9): CLEAR
20 PRINT "1.- OSCILACION EN MASA"
30 PRINT "2.- TOP-CHANNEL":PRINT "3.- RESALTO HIDRAULICO":PRINT "4.- MANNING"
40 BEEP0:INPUT "ELIGE (1...4)";ELI
50 ON ELI GOTO 60,230,1000,1060
60 PRINT "MAXIMOS Y MINIMOS DE LA          OSCILACION EN MASA"
70 PRINT "ESCRIBE F=F(V,Z,T) EN LAS LINEAS 120 Y 160"
80 INPUT "Z0";Z,"V0";V
90 PRINT CHR$(238);
100 INPUT "T";AT
110 FOR T=AT TO 1000 STEP AT:PRINT CHR$(236);
120 F=((V^2/2/9.8-29-810*V*ABS(V))/(918.3673+Z/9.8))*AT
130 A=Z
140 Z=Z+(V+F/2)*AT:B=Z
150 V=V+F
160 G=((V^2/2/9.8-29-810*V*ABS(V))/(918.3673+(Z)/9.8))*AT
170 IF B>A AND B>(Z+(V+G/2)*AT) THEN 200
180 IF B<A AND B<(Z+(V+G/2)*AT) THEN 200
190 NEXT T:END
200 PRINT "T="T
210 PRINT "Z="Z:PRINT "V="V
220 GOTO 190
230 CLEAR:PRINT "TOP-CHANNEL"
240 INPUT "n";N,"I0";I0,"Q";Q,"X0";X0,"Y";Y1
250 PRINT CHR$(238);"Y";:INPUT DY
260 CLS:PRINT "SECCION:(+,-)"
270 DIM A$(5)
280 GOSUB 360:C=1:MAX=5
290 LOCATE 0,1:FOR T=1 TO 10:PRINT CHR$(234);
300 NEXT T:LOCATE 11,1:PRINT A$(C);
310 T$=INKEY$:IF T$="" THEN 310
320 IF T$="+" THEN C=C+1:IF C>MAX THEN C=1
330 IF T$="-" THEN C=C-1:IF C<1 THEN C=MAX
340 IF T$=CHR$(13) THEN CLS:ON C GOTO 420,450,490,530,580
350 GOTO 290
360 A$(1)=" RECTANGULAR"
370 A$(2)=" TRAPEZOIDAL"
380 A$(3)=" TRIANGULAR "
390 A$(4)=" CIRCULAR   "
400 A$(5)=" OTROS CASOS"
410 RETURN
420 PRINT "S. RECTANGULAR"
430 INPUT "B";B
440 S=B*Y1:PM=B+2*Y1:PH=Y1:GOTO 600
450 PRINT "S. TRAPEZOIDAL"
460 INPUT "Base,B";B
470 INPUT "TALUD HORIZ.,Z";Z
480 S=(B+Z*Y1)*Y1:PM=B+2*Y1*SQR(1+Z^2):TW=B+2*Z*Y1:PH=S/TW:GOTO 600
490 PRINT "S. TRIANGULAR"
500 INPUT "B";B
510 INPUT "TALUD HORIZ.,Z";Z
520 S=Z*Y1^2:PM=2*Y1*SQR(1+Z^2):PH=.5*Y1:GOTO 600
530 PRINT "S. CIRCULAR":ANGLE 1
540 INPUT "D";D
550 IF Y1<(D/2) THEN 570
560 ANG=2*ACS((Y1-D/2)/D*2):S=PI*D^2/4-ANG*D^2/8+D/2*SIN(ANG/2)*(Y1-D/2):PM=(2*PI-ANG+2*SIN(ANG/2))*D/2:GOTO 600
570 ANG=2*ACS((D/2-Y1)/D*2):S=ANG*D^2/8-D/2*SIN(ANG/2)*(D/2-Y1):PM=(ANG+2*SIN(ANG/2))*D/2:GOTO 600
580 PRINT "S. COMPLEJA"
590 INPUT "S";S,"Pm";PM
600 PRINT "SECCION INICIAL":RH=S/PM
610 V1=Q/S:H1=Y1+V1^2/2/9.8
620 I1=(Q*N/(S*RH^(2/3)))^2
630 IF C>3 THEN 670
640 F=V1^2/9.8 /PH
650 IF C>1 THEN 670
660 YCONJ=Y1/2*(SQR(1+8*F)-1)
670 PRINT "V1="V1:PRINT "S1="S:PRINT "Pm1="PM:PRINT "H1="H1
680 PRINT "Rh1="RH:PRINT "I1="I1
690 IF C=<3 THEN PRINT "F^2="F
700 IF C=1 THEN PRINT "Yconj="YCONJ
710 INPUT "TODOS LOS PARAMETROS (S/N)";PRM$
720 Y2=Y1:M=1
730 Y2=Y2+DY:ON C GOSUB 930,940,950,960,980
740 RH=S/PM:V2=Q/S:I2=(Q*N/(S*RH^(2/3)))^2
750 IM=(I1+I2)/2
760 DX=((Y2+V2^2/(2*9.8))-(Y1+V1^2/(2*9.8)))/(I0-IM)
770 X=X0+DX:X0=X:PRINT CHR$(219);
780 Y1=Y2:V1=V2:I1=I2
790 IF C>3 THEN 830
800 F=V2^2/9.8/PH
810 IF C>1 THEN 830
820 YCONJ=Y2/2*(SQR(1+8*F)-1)
830 M=M+1:PRINT "Y"M"="Y2:PRINT "X"M"="X
840 PRINT "V"M"="V2
850 IF C=<3 THEN PRINT "F^2"M"="F
860 IF C=1 THEN PRINT "Yconj"M"="YCONJ
870 IF PRM$="N" THEN 730
880 PRINT "I"M"="I2:PRINT "Imed="IM
890 PRINT "Rh"M"="RH
900 H0=Y2+V2^2/2/9.8:PRINT "H0="H0
910 PRINT "S"M"="S
920 PRINT "Pm"M"="PM:GOTO 730
930 S=B*Y2:PM=B+2*Y2:PH=Y2:RETURN
940 S=(B+Z*Y2)*Y2:PM=B+2*Y2*SQR(1+Z^2):TW=B+2*Z*Y2:PH=S/TW:RETURN
950 S=Z*Y2^2:PM=2*Y2*SQR(1+Z^2):PH=.5*Y2:RETURN
960 IF Y2<(D/2) THEN 980
970 ANG=2*ACS((Y2-D/2)/D*2):S=PI*D^2/4-ANG*D^2/8+D/2*SIN(ANG/2)*(Y2-D/2):PM=(2*PI-ANG+2*SIN(ANG/2))*D/2:RETURN
980 ANG=2*ACS((D/2-Y2)/D*2):S=ANG*D^2/8-D/2*SIN(ANG/2)*(D/2-Y2):PM=(ANG+2*SIN(ANG/2))*D/2:RETURN
990 PRINT "FIN":END
1000 PRINT "RESALTO HIDRAULICO"
1010 INPUT "Y";Y
1020 INPUT "F^2";F
1030 YCONJ=Y/2*(SQR(1+8*F)-1)
1040 PRINT "Yconj="YCONJ
1050 GOTO 1010
1060 PRINT "MANNING"
1070 INPUT "I0";I0:INPUT "n";N:INPUT "B";B
1080 Q=0
1090 PRINT "IF EXE POR TANTEOS.... ";:INPUT "Q";Q
1100 IF Q=0 THEN 1240
1110 IF I0<=0 THEN GOTO 1190
1120 T=((Q*N)/I0^.5)^3
1130 Y1=0
1140 Z=(2*Y1+B)^2*T
1150 Y2=Z^.2/B
1160 IF Y2-Y1<.001 THEN 1190
1170 Y1=Y2
1180 GOTO 1140
1190 YC=(Q^2/9.8/B^2)^(1/3):IF I0<=0 THEN GOTO 1210
1200 PRINT "Y NORMAL="Y2
1210 PRINT "Y CRITICO="YC:IF I0<=0 THEN GOTO 1230
1220 F=Q^2/(9.8*B^2*Y2^3):PRINT "F^2="F:YCONJ=Y2/2*(SQR(1+8*F)-1):PRINT "YCONJ="YCONJ
1230 IF INKEY$="" THEN 1230 ELSE 1080
1240 PRINT "POR TANTEOS"
1250 Y1=0
1260 INPUT "CALADO";Y1
1270 Q=N^(-1)*B*Y1*I0^.5*((B*Y1)/(2*Y1+B))^(2/3)
1280 V1=Q/B/Y1
1290 VC=(9.8*Y1)^.5
1300 IF ABS(VC-V1)<.005 THEN PRINT "****R. CRITICO****":GOTO 1360
1310 PRINT "YC="(Q^2/9.8/B^2)^(1/3):PRINT "Q="Q:IF V1=VC THEN 1330
1320 IF V1<VC THEN PRINT "R.LENTO" ELSE PRINT "R.RAPIDO"
1330 IF INKEY$="" THEN 1330
1340 PRINT "V1="V1:PRINT "VC="VC
1350 GOTO 1250
1360 PRINT "YC="Y1
1370 PRINT "QC="Q
1380 IF INKEY$="" THEN 1380 ELSE 1080
