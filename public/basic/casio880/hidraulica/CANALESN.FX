10 CLEAR:CLS:PRINT "*******  C A N A L E S  ******* *******************************":INPUT "Rectangular[R]-Trapezoidal[T]   Circular[C]-TriAngular[TA]  ";NACHO$
20 IFNACHO$="R"THEN 100
30 IFNACHO$="C"THEN 1000
40 IFNACHO$="T"THEN 2000
50 IFNACHO$="TA"THEN 3000
60 GOTO 10
100 CLEAR:CLS:PRINT "CANAL DE SECCION RECTANGULAR"
111 INPUT "OBTENER CALADOS [C]  o  ENERGIA PARA UN CALADO DADO [E] ";JISR$
112 IF JISR$="C" THEN GOTO 119
113 IF JISR$="E" THEN GOTO 940
114 IF JISR$="" THEN GOTO 111
119 CLS:INPUT"PENDIENTE SOLERA=";I0
120 INPUT "NUM. MANNING";N
130 INPUT"ANCHO";B
140 CLS
150 INPUT"MANNING [M] - ENERGIAS [E]      DIF. FINITAS [D] - FIN [ ] ";A$
160 IFA$="D"THEN 190
170 IFA$="M"THEN 420
180 IF A$="E"THEN 760 ELSE END
190 INPUT "CALADO EN SECCION 1=";Y1
200 INPUT"CAUDAL(m^3/seg)=";Q:V1=Q/B/Y1
210 X=0
220 INPUT"NUM.ITERACIONES";P
230 INPUT "CALADO FINAL";YF
240 IF YF=0 THEN 140
250 K=(YF-Y1)/P
260 Y2=Y1
270 FOR J=1 TO P
280 Y2=Y2+K:V2=V1*Y1/Y2
290 H1=Y1+V1^2/2/9.810001:H2=Y2+V2^2/2/9.810001
300 I2=V2^2*N^2/((B*Y2)/(2*Y2+B))^(4/3)
310 I1=V1^2*N^2/((B*Y1)/(2*Y1+B))^(4/3)
320 IM=(I1+I2)/2
330 AX=(H2-H1)/(I0-IM)
340 X=X+AX
350 Y1=Y2:V1=V2
360 NEXT J
370 CLS
380 PRINT"Y2=";Y2:LOCATE16,0:PRINT"V2=";ROUND(V1,-4)
390 PRINT"X=";X
400 PRINT"NUM. FROUDE^2=";V2^2/9.810001/Y2
410 YF=0:GOTO190
420 CLS
430 PRINT"ECUACION DE MANNING"
440 Q=0
450 INPUT"si Q=0 por tanteos: Q =";Q
460 IF Q=0 THEN 590
465 IF I0=0 THEN I0=0.0000000001
470 T=((Q*N)/I0^.5)^3
480 Y1=0
490 Z=(2*Y1+B)^2*T
500 Y2=Z^.2/B
510 IF ABS(Y2-Y1)<.0001 THEN 540
520 Y1=Y2
530 GOTO 490
540 YC=(Q^2/9.810001/B^2)^(1/3)
550 PRINT "CALADO NORMAL  ---> Yo =";ROUND(Y2,-4);" CALADO CRITICO ---> Yc =";ROUND(YC,-4)
570 PRINT"NUM.FROUDE^2=";Q^2/(9.810001*B^2*Y2^3)
580 IF INKEY$=""THEN 580 ELSE 140
590 CLS
600 PRINT"POR TANTEOS"
610 Y1=0
620 INPUT"CALADO=";Y1
630 IF Y1=0 THEN 140
640 Q=N^(-1)*B*Y1*I0^.5*((B*Y1)/(2*Y1+B))^(2/3)
650 V1=Q/B/Y1
660 VC=(9.810001*Y1)^.5
670 IF ABS(VC-V1)<.005 THEN PRINT"****Reg. Critico********":GOTO640
680 CLS:PRINT"YC=";(Q^2/9.810001/B^2)^(1/3):PRINT"Q=";Q:IF V1=VCTHEN 610
690 IF V1<VC THEN PRINT"Reg. Lento" ELSE PRINT"Reg. Rapido"
700 IFINKEY$=""THEN 700
710 PRINT "V1=";V1:PRINT"VC=";VC
720 GOTO530
730 PRINT"CALADO CRIT.=";Y1
740 PRINT"CAUDAL CRIT.=";Q
750 IF INKEY$=""THEN 750 ELSE 140
760 CLS
770 INPUT"ENERGIA CONOCIDA (E en m.)";H
780 INPUT"CAUDAL (Q en m^3/seg.)";Q
790 Z=0:Y2=0:YR=0:YL=0
800 Y1=((Q^2/B^2+Z)/(2*9.810001*H))^.5
810 Z=2*9.810001*Y1^3
820 IF Y1-Y2<.0001 THEN 850
830 Y2=Y1
840 GOTO 800
850 YR=Y1
860 Y1=(Q^2/9.810001/B^2)^(1/3)
870 Z=2*9.810001*H*Y1^2-Q^2/B^2
880 Y1=(Z/2/9.810001)^(1/3)
890 IFY1-Y2<.0001 THEN 910 ELSE Y2=Y1
900 GOTO  870
910 PRINT"CALADO RAPIDO=";YR
920 PRINT"CALADO LENTO=";Y1
930 IFINKEY$=""THEN 930 ELSE 140
940 CLS:PRINT "CALCULO DE LA ENERGIA":INPUT "Q(M^3/S)=";Q:INPUT "ANCHO=";B:INPUT "CALADO Y=";Y
950 A=B*Y:E=Y+Q*Q/(2*9.8*A*A)
960 PRINT "A =";A:PRINT "E =";E:END
1000 MODE5:CLEAR:CLS:PRINT "CANAL DE SECCION CIRCULAR"
1001 INPUT "OBTENER CALADOS [C] o DIAM. MIN.PARA QUE NO SE OBTURE [O] ";PPP$
1002 IF PPP$="O" THEN GOTO 1500
1010 INPUT "CALADO NORMAL [N] o             CALADO CRITICO [C] ";R$
1020 IF R$="N" THEN GOTO 4000 ELSE 4400
1030 N=.015:I=.003:D=3
1040 CLS
1050 PRINT "CALADO NORMAL  -------->  CAMBIAEL VALOR DE N,DyI EN LINEA 1030"
1060 PRINT "PARA TERMINAR TECLEA 100"
1070 T=SQR(I)/N
1080 INPUT "ANGULO(rad) ";A
1090 IF A=100 THEN GOTO 1000
1100 B=(SIN(2*A)/(2*A))
1110 RH=.25*D*(1-B)
1120 C=RH^(2/3)
1130 E=.5*SIN(2*A)
1140 AR=.25*D^2*(A-E)
1150 Q=T*C*AR
1160 PRINT "Q= ";Q
1170 Y=.5*D*(1+SIN(A-(PI/2)))
1180 PRINT "Yo= ";Y
1190 GOTO 1080
1200 Q=700:D=3.5
1210 PRINT "CALADO CRITICO  ------->  CAMBIAEL VALOR DE QyD EN LINEA 1200"
1220 INPUT "ANGULO (rad) = ";A
1230  K=Q^2*D*SINA
1240 L=(A-.5*SIN(2*A))
1250 M=9.810001*(.25*D^2*L)^3
1260 PRINT "UNO= ";K/M
1270 Y=D*.5*(1+SIN(A-(PI/2)))
1280 PRINT "YC = ";Y
1290 GOTO 1220
1500 CLS:INPUT "E1 =";E1:INPUT "Q =";Q
1510 PRINT "ANGULO(rad) [";A;"] ";:INPUT A
1520 D=(6.5306*Q*Q*SIN(A)/(A-0.5*SIN(2*A))^3)^(1/5)
1530 CERO=(D*(1-COS(A))/2)+Q*Q/(2*9.8*((D^2)*(A-0.5*SIN(2*A))/4)^2)-E1
1540 PRINT "CERO =";ROUND(CERO,-6);"   Dmin =";ROUND(D,-3):GOTO 1510
2000 CLEAR:CLS
2010 PRINT "CANAL DE SECCION TRAPEZOIDAL"
2011 INPUT "OBTENER CALADOS [C]  o  ENERGIA PARA UN CLADO DADO [E]";JISR$:CLS
2012 IF JISR$="C" THEN GOTO 2040
2013 IF JISR$="E" THEN GOTO 2400
2014 IF JISR$="" THEN GOTO 2011
2040 PRINT "DAME EL VALOR DE LA PENDIENTE DELOS TALUDES (H:V)";
2050 INPUT " --> H ";H:INPUT "                  --> V ";V
2060 INPUT"Q(M^3/S) ";Q
2070 INPUT "NO. MANING ";N
2080 INPUT "ANCHURA ";B
2090 INPUT "PENDIENTE ";I
2100 IF I=0 THEN I=0.0000000001
2241 F=(Q*N/SQR(I))^(3/5):T=H/V
2242 Y1=0
2243 Y2=((F*((B+2*Y1*SQR(1+T^2))^(2/3))^(3/5))/(Y1*T+B)
2244 IF ABS(Y2-Y1)<0.0001 THEN 2247
2245 Y1=Y2
2246 GOTO 2243
2247 Y0=Y2
2251 FF=Q*Q/9.810001
2252 Y1=0
2253 Y2=(FF*(2*Y1*T+B)/((Y1*T+B)^3))^(1/3)
2254 IF ABS(Y2-Y1)<0.0001 THEN 2257
2255 Y1=Y2
2256 GOTO 2253
2257 YC=Y2
2258 CLS:PRINT "CALADO NORMAL  ---> Yo =";ROUND(Y0,-4);" CALADO CRITICO ---> Yc =";ROUND(YC,-4)
2360 END
2400 CLS:PRINT "CALCULO DE LA ENERGIA":PRINT "DAME EL VALOR DE LA PENDIENTE DELOS TALUDES (H:V)";
2410 INPUT " --> H ";H:INPUT "                  --> V ";V
2413 INPUT "ANCHURA ";B
2420 INPUT"Q(M^3/S) ";Q
2430 INPUT "CALADO Y ";Y
2440 T=H/V:A=B*Y+Y*Y*T
2450 E=Y+Q*Q/(2*9.8*A*A)
2460 PRINT "A (";Y;") =";A
2470 PRINT "E =";E:END
3000 CLEAR:CLS
3010 PRINT "CANAL DE SECCION TRIANGULAR"
3011 INPUT "OBTENER CALADOS [C]  o  ENERGIA PARA UN CLADO DADO [E]";JISR$
3012 IF JISR$="C" THEN GOTO 3020
3013 IF JISR$="E" THEN GOTO 3400
3014 IF JISR$="" THEN GOTO 3011
3020 PRINT "DAME EL VALOR DE LA PENDIENTE DELOS TALUDES (H:V)";
3030 INPUT " --> H ";H:INPUT "                  --> V ";V
3040 INPUT"Q(M^3/S) ";Q
3050 INPUT "NO. MANING ";N
3060 INPUT "PENDIENTE ";I
3070 T=H/V
3075 IF I=0 THEN I=0.0000000001
3080 Y0=((Q*N*(2*SQR(1+T^2))^(2/3))/(SQR(I)*T^(5/3)))^(3/8)
3100 YC=(2*Q*Q/(9.8*T*T))^(1/5)
3110 CLS:PRINT "CALADO NORMAL  ---> Yo =";ROUND(Y0,-4);" CALADO CRITICO ---> Yc =";ROUND(YC,-4)
3120 END
3400 CLS:PRINT "CALCULO DE LA ENERGIA":PRINT "DAME EL VALOR DE LA PENDIENTE DELOS TALUDES (H:V)";
3410 INPUT " --> H ";H:INPUT "                  --> V ";V
3420 INPUT"Q(M^3/S) ";Q
3430 INPUT "CALADO Y ";Y
3440 T=H/V:A=Y*Y*T
3450 E=Y+Q*Q/(2*9.8*A*A)
3460 PRINT "A (";Y;") =";A
3470 PRINT "E =";E:END
4000 CLS:INPUT "Q(M^3/S) ";Q
4010 INPUT "DIAMETRO ";D
4020 INPUT "No.MANING ";N
4030 INPUT "PENDIENTE ";I
4040 CTE=SQR(I)*(D/4)^(2/3)*(D*D/4)/N
4045 O1=0.001:O2=3.14
4047 CLS:PRINT "calculando Yo ";
4050 O=(O1+O2)/2
4055 PRINT ".";
4060 F1=CTE*(O1-SIN(2*O1)/2)*(1-SIN(2*O1)/(2*O1))^(2/3)-Q
4070 F2=CTE*(O2-SIN(2*O2)/2)*(1-SIN(2*O2)/(2*O2))^(2/3)-Q
4080 FF=CTE*(O-SIN(2*O)/2)*(1-SIN(2*O)/(2*O))^(2/3)-Q
4090 IF FF<0 THEN GOTO 4130
4100 IF F1>F2 THEN O1=O2
4110 O2=O
4120 GOTO 4150
4130 IF F1>F2 THEN O2=O1
4140 O1=O
4150 IF ABS(F1)<0.000001 THEN GOTO 4170
4160 GOTO 4050
4170 CLS:PRINT "ANGULO =";ROUND(O1,-6);"rad. -------->     Yo =";ROUND((1-COS(O1))*D/2,-4)
4180 GOTO 1010
4400 CLS:INPUT "Q(M^3/S) ";Q
4410 INPUT "DIAMETRO ";D
4440 CTE=Q*Q*D/(9.810001*(D*D/4)^3)
4445 O1=0.001:O2=3.14
4447 CLS:PRINT "calculando Yc ";
4450 O=(O1+O2)/2
4455 PRINT ".";
4460 F1=CTE*SIN(O1)/(O1-SIN(2*O1)/2)^(3)-1
4470 F2=CTE*SIN(O2)/(O2-SIN(2*O2)/2)^(3)-1
4480 FF=CTE*SIN(O)/(O-SIN(2*O)/2)^(3)-1
4490 IF FF<0 THEN GOTO 4530
4500 IF F1>F2 THEN O1=O2
4510 O2=O
4520 GOTO 4550
4530 IF F1>F2 THEN O2=O1
4540 O1=O
4550 IF ABS(F1)<0.000001 THEN GOTO 4570
4560 GOTO 4450
4570 CLS:PRINT "ANGULO =";ROUND(O1,-6);"rad. -------->     Yc =";ROUND((1-COS(O1))*D/2,-4)
4580 GOTO 1010
