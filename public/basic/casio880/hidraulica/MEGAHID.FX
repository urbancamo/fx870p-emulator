5 CLEAR:DIM A$(10)
10 CLS
20 GOSUB 5998:' No te recomiendo que modifiques este programa, y el que avisa no es traidor.
30 GOSUB 90:C=1:MAX=10
40 LOCATE 0,1:PRINT A$(C);
45 T$=INKEY$:IF T$="" THEN 45
50 IF T$="+" THEN C=C+1:IF C>MAX THEN C=1
55 IF T$="-" THEN C=C-1:IF C<1 THEN C=MAX
60 IF T$=CHR$(13) THEN CLS:ON C GOTO 100,300,400,600,700,800,900,200,2000,6000
65 GOTO 40
90 A$(1)="..... Golpe de Ariete ....."
91 A$(2)="........ Colebrook ........"
92 A$(3)="......... Canales ........."
93 A$(4)=".... Curvas de Remanso ...."
94 A$(5)="..Chimenea de Equilibrio .."
95 A$(6)=". Depositos Concurrentes .."
96 A$(7)="..... Canal Circular ......":A$(8)=".... Calado Conjugado ....."
97 A$(9)="...... Runge - Kutta ......"
98 A$(10)=".......... LEER ..........."
99 RETURN
100 'g-ariete.bas
101 CLEAR:CLS:PRINT"** GOLPE DE ARIETE **"
102 INPUT"CELERIDAD DE ONDA = ";A:Y=A/9.810001
103 INPUT"LONGITUD DE LA CONDUCCION = ";L
104 INPUT"TIEMPO DE CIERRE = ";TC:N=INTTC+1
105 T0=(2*L)/A:IF TC<T0 THEN 128
106 DIMH(N),V(N),P(N),F(N)
107 PRINT"CARGA INICIAL...H";CHR$(161);"= ";:INPUT H0
108 PRINT"VELOCIDAD INICIAL...V";CHR$(161);"= ";:INPUT V0:V0=V0^2
109 FOR I=1 TO 2*N
110 INPUT"At/A. =";C:C=C^2:IF C=0 THEN V(I)=0:J=1
111 IF QQ=1 THEN 116
112 INPUT"CARGA ANTERIOR =";H(I-1)
113 INPUT"VELOCIDAD ANTERIOR =";V(I-1)
114 IF H(I-1)=0 AND V(I-1)=0 THEN H(I-1)=H0:V(I-1)=SQRV0
115 IF T0<>2 THEN INPUT"PERDIDAS =";P(I)
116 IF I-2<=0 THEN P(I)=0 ELSE P(I)=(-1)*P(I-2)-2*F(I-2)
117 IF J=1 THEN 122
118 X=H0/(V0*C):Z=-H(I-1)-(Y*V(I-1))-P(I)
119 C=SQRC
120 V(I)=(-Y*SQR(Y^2-(4*X*Z)))/(2*X)
121 PRINT"*....V = ";V(I)
122 H(I)=H(I-1)+(Y*(V(I-1)-V(I)))+P(I)
123 PRINT"*....H = ";H(I)
124 J=0
125 F(I)=Y*(V(I-1)-V(I))
126 PRINT F(I)
127 QQ=1:NEXT I
128 '** CIERRE RAPIDO **
129 CLS:PRINT
130 PRINT "CIERRA MAS DESPACIO"
131 IF INKEY$="" THEN 131 ELSE GOTO 101
200 'calado.bas
201 PRINT "Calculo calado conjugado"
202 INPUT "Q=";Q
203 INPUT "y1=";Y1
204 INPUT "b=";B:G=9.810001
205 Y2=(Y1/2)*(((1+((8*Q*Q)/(B*B*Y1*Y1*G*Y1)))^.5)-1)
206 PRINT "EL CONJUGADO  ES y2=";Y2
300 'COLE-PRE
301 PRINT "*COLEBROOK o U. PRESION (C/P)*";:INPUT ZA$
302 IF ZA$="P" GOTO 342 ELSE GOTO 303
303 CLEAR :CLS
304 PRINT " ****** COLEBROOK-WHITE ******";
305 LOCATE 0,1
306 PRINT " *****************************"
307 CLS:PRINT "Casos: 1.-Re,D,K--->f";
308 LOCATE 7,1
309 PRINT "2.-Re,D,f--->K";
310 LOCATE 25,1:INPUT "Cual";E
311 IF E<1 OR E>2 THEN 307
312 CLS
313 INPUT "Diametro (m)";D
314 INPUT "Numero Reynolds";RE
315 ON E GOTO 316,336
316 INPUT "Rugosidad (mm)";K
317 K=K/1000
318 X=1
319 A=2*LOG(D/K)+1.14
320 B=9.350001*D/RE/K
321 Y=A-2*LOG(1+B*X)-X
322 I=1
323 IF ABS Y<.000001 THEN 332
324 X=X+I
325 Z=A-2*LOG(1+B*X)-X
326 IF SGN Y=SGN Z THEN Y=Z:GOTO 324
327 IF ABS Z<.000001 THEN Y=Z:GOTO 332
328 IF ABS Y<.000001 THEN 332
329 X=X-I
330 I=I/2
331 GOTO 324
332 CLS:BEEP0:BEEP1
333 F=1/X/X
334 PRINT "Solucion f=";ROUND(F,-6)
335 CLS:END
336 '*****CASO B *****
337 INPUT "f";F
338 K=3.715*D*(10^(-1/(2*SQR F))-2.51/RE/SQR F)
339 PRINT "K=";ROUND(1000*K,-5);"mm"
340 CLS:END
341 'PRESION
342 CLEAR:CLS
343 PRINT " ****** UNIDADES PRESION ******";
344 LOCATE 0,1
345 PRINT " ******************************"
346 INPUT "Densidad (k/m^3)";D
347 CLS
348 PRINT "Opciones:  1      2     3";
349 LOCATE 0,1
350 PRINT "         m.c.a  N/m^2  Atm";:INPUT A
351 ON A GOTO 352,358,363
352 CLS
353 INPUT "Carga en m.c.a";H
354 H=H*D/1000
355 P1=H*9.810001*1000
356 P2=P1/101300
357 GOTO 367
358 CLS
359 INPUT "Carga en N/m^2";P1
360 H=P1/1000/9.810001
361 P2=P1/101300
362 GOTO 367
363 CLS
364 INPUT "Carga en atm";P2
365 P1=P2*101300
366 H=P1/1000/9.810001
367 CLS
368 PRINT " m.c.a     N/m^2        Atm";
369 LOCATE 0,1:PRINT ROUND(H,-4);
370 LOCATE 10,1:PRINT ROUND(P1,-4);
371 LOCATE 23,1:PRINT ROUND(P2,-4)
372 INPUT "Mas conversiones S/N";A$
373 IF A$="S" THEN 347 ELSE IF A$<>"N" THEN 372
374 END
400 CLEAR:CLS:REM (C) Fernando Martinez Perez-1992.
401 PRINT " ********** CANALES ***********": PRINT "OPCIONES:"
402 PRINT "(1) MANNING   ";
403 PRINT "(2) ENERGIAS"
404 INPUT "OPCION ";Z$
405 IF Z$="1" THEN GOTO 407
406 IF Z$="2" THEN GOTO 488 ELSE 401
407 INPUT " CANAL RECTANGULAR(R) O TRAPEZOIDAL(T) ";T$
408 CLS:PRINT "OPCIONES":PRINT "(1) CALCULO DE Yo,Yc":PRINT "(2) CALCULO DE Yc":INPUT "OPCION"; RRR
409 IF T$="R" THEN 414
410 IF T$="T" THEN 411 ELSE 407
411 IF RRR=2 THEN GOTO 412:PRINT "CALCULO COEF. DEL POLINOMIO DE GRADO 10, SOLUCION DE EC. MANING"
412 PRINT "DAME EL VALOR DE LA PENDIENTE H:V"
413 INPUT "H ";H:INPUT "V ";V:GOTO 415
414 IF RRR=2 THEN GOTO 415:PRINT "CALCULO COEF. DEL POLINOMIO DE GRADO 5, SOLUCION DE EC. MANING"
415 INPUT"Q(M^3/S) ";Q
416 INPUT "ANCHURA ";B
417 IF T$="T" AND RRR=2 THEN GOTO 461
418 IF RRR=2 THEN GOTO 438
419 INPUT "NO. MANING ";NM
420 INPUT "PENDIENTE ";I
421 IF I=0 AND T$="R" THEN GOTO 486
422 IF I<0 THEN PRINT "VALOR DEL CALADO NORMAL ES IRREAL ---> PENDIENTE ADVERSA ":GOTO 487
423 I=SQR(I)
424 IF T$="R" THEN GOTO 425 ELSE 441
425 A=B^2
426 M=4*(Q*NM/(B*I))^3
427 C=B*M
428 D=B^2*M/4
429 PRINT"A*Y^5+B*Y^2+C*Y+D=0"
430 N=5:DIM A(N),B(N),C(N)
431 PRINT "A= ";A/A:A(0)=1
432 A(1)=0:A(2)=0
433 PRINT "B= ";-M/A:A(3)=-M/A
434 PRINT "C= ";-C/A:A(4)=-C/A
435 PRINT "D= ";-D/A:A(5)=-D/A
436 CLS:PRINT "CALCULANDO...";
437 GOSUB 553
438 YC=(Q^2/(B^2*9.810001))^(1/3)
439 CLS:PRINT "CALADO CRITICO, CANAL RECTANGULAR= ";YC
440 END
441 IF I=0 AND T$="T" THEN GOTO 485
442 IF RRR=2 THEN GOTO 461
443 T=H*V:S=2*SQR(H^2+V^2):C=NM*Q/I
444 A=1:E=(5*B)/T:F=(10*B^2)/T^2:G=(10*B^3)/T^3:J=(5*B^4)/T^4:K=B^5/T^5:L=(S^2*C^3)/T^5:M=(2*S*B*C^3)/T^5:NT=(B^2*C^3)/T^5
445 BEEP0
446 N=10:DIM A(N),B(N),C(N)
447 PRINT "LOS COEF. SON:"
448 PRINT "A10= ";A:A(0)=A
449 PRINT "A9= ";E:A(1)=E
450 PRINT "A8= ";F:A(2)=F
451 PRINT "A7= ";G:A(3)=G
452 PRINT "A6= ";J:A(4)=J
453 PRINT "A5= ";K:A(5)=K
454 PRINT "A4= 0 ":A(6)=0
455 PRINT "A3= 0 ":A(7)=0
456 PRINT "A2= ";-L:A(8)=-L
457 PRINT "A1= ";-M:A(9)=-M
458 PRINT "A0= ";-NT:A(10)=-NT
459 CLS:PRINT "CALCULANDO...";
460 GOSUB 553
461 BEEP0
462 CLS:PRINT "CALADO CRITICO"
463 P=9.810001/Q^2
464 PRINT "COEF. DEL POLINOMIO DE GRADO 6, SOLUCION"
465 Z=H^3*V^3*P
466 IF RRR=1 THEN GOTO 468
467 N=6:DIM A(N),B(N),C(N)
468 N=6
469 PRINT "A6= ";H^3*V^3*P/Z
470 A(0)=H^3*V^3*P/Z
471 PRINT "A5= ";P*3*V^2*H^2*B/Z
472 A(1)=P*3*V^2*H^2*B/Z
473 PRINT "A4= ";P*3*V*H*B^2/Z
474 A(2)=P*3*V*H*B^2/Z
475 PRINT "A3= ";P*B^3/Z
476 A(3)=P*B^3/Z:A(4)=0
477 PRINT "A2= 0 "
478 PRINT "A1= ";-2*H*V/Z
479 A(5)=-2*H*V/Z
480 PRINT "A0= ";-B/Z
481 A(6)=-B/Z
482 CLS:PRINT "CALCULANDO...";
483 GOSUB 553
484 GOTO 440
485 PRINT "PENDIENTE HORIZONTAL, CALADO NORMAL INFINITO":GOTO 462
486 PRINT "PENDIENTE HORIZONTAL, CALADO NORMAL INFINITO":GOTO 438
487 IF T$ ="R" THEN GOTO 438 ELSE 462
488 CLS:INPUT"ENERGIA CONOCIDA";E
489 INPUT " CANAL RECTANGULAR(R) O TRAPEZOIDAL(T) ";T$
490 INPUT "ANCHO";B
491 INPUT "CAUDAL";Q
492 IF T$="R" THEN 494
493 IF T$="T" THEN 525 ELSE 489
494 K=2*9.810001*B^2
495 A=1:B=E:D=Q^2/K
496 PRINT "ECUACION A RESOLVER:"
497 BEEP0
498 PRINT "Y^3 - ";B;"*Y^2 + ";D;" = 0"
499 CLS:PRINT "CALCULANDO...";
500 SS=.1:PP=.1:N=3
501 DIM A(N),B(N),C(N)
502 A(0)=1:A(1)=-B:A(2)=0:A(3)=D
503 B(0)=A(0):C(0)=A(0):MM=1E-10:K=1
504 B(1)=A(1)+SS*A(0):C(1)=B(1)+SS*A(0)
505 FOR I=2 TO N
506 B(I)=A(I)+SS*B(I-1)-PP*B(I-2)
507 IF I=N THEN GOTO 511
508 IF I=N-1 THEN C(I)=SS*C(I-1)-PP*C(I-2):GOTO 510
509 C(I)=B(I)+SS*C(I-1)-PP*C(I-2)
510 NEXT I
511 T=B(N)*C(N-3)-B(N-1)*C(N-2)
512 Q=B(N)*C(N-2)-B(N-1)*C(N-1)
513 D=C(N-2)^2-C(N-1)*C(N-3)
514 SS=SS+T/D:PP=PP+Q/D
515 IF (ABS(B(N))+ABS(B(N-1)))/(ABS(A(N))+ABS(A(N-1)))<MM THEN GOTO 519
516 IF K<>15 THEN K=K+1:GOTO 504
517 IF MM<>1 THEN K=1:MM=MM*10:GOTO 504
518 PRINT "IMPOSIBLE RESOLVER"
519 T=SS^2-4*PP
520 CLS:PRINT " *********** CALADOS *********"
521 PRINT "Y=";(SS+SQR(T))/2
522 PRINT "Y=";(SS-SQR(T))/2
523 PRINT "Y=";-B(1)/A(0)
524 END
525 PRINT "PENDIENTE H:V"
526 INPUT "H ";H
527 INPUT "V ";V
528 P=H/V
529 A1=2*9.810001*P^2*E
530 A2=4*9.810001*P*B*E
531 A3=2*9.810001*B^2*E
532 B1=2*9.810001*P^2
533 B2=4*9.810001*P*B
534 B3=2*9.810001*B^2
535 BEEP0
536 N=5:DIM A(N),B(N),C(N)
537 PRINT "RESOLVER LA EC. DE GRADO 5 CON COEF. DE MAYOR A MENOR"
538 PRINT "A5 =";B1/B1:A(0)=1
539 PRINT "A4 =";(B2-A1)/B1
540 A(1)=(B2-A1)/B1
541 PRINT "A3 =";(B3-A2)/B1
542 A(2)=(B3-A2)/B1
543 PRINT "A2 =";(-A3)/B1
544 A(3)=(-A3)/B1
545 PRINT "A1 = 0"
546 A(4)=0
547 PRINT "A0 =";Q^2/B1
548 A(5)=Q^2/B1
549 CLS:PRINT"CALCULANDO...";
550 GOSUB 553
551 END
552 GOSUB 553
553 CLS
554 SS=.1:PP=.1
555 B(0)=A(0):C(0)=A(0)
556 MM=1E-10:KK=1
557 B(1)=A(1)+SS*A(0)
558 C(1)=B(1)+SS*A(0)
559 FOR I=2 TO N
560 B(I)=A(I)+SS*B(I-1)-PP*B(I-2)
561 IF I=N THEN GOTO 565
562 IF I=N-1 THEN C(I)=SS*C(I-1)-PP*C(I-2):GOTO 564
563 C(I)=B(I)+SS*C(I-1)-PP*C(I-2)
564 NEXT I
565 TT=B(N)*C(N-3)-B(N-1)*C(N-2)
566 QQ=B(N)*C(N-2)-B(N-1)*C(N-1)
567 DD=C(N-2)^2-C(N-1)*C(N-3)
568 SS=SS+TT/DD:PP=PP+QQ/DD
569 PRINT CHR$(134);
570 ST=(ABS(B(N))+ABS(B(N-1)))/(ABS(A(N))+ABS(A(N-1))):IF ST<MM THEN GOTO 575
571 IF KK<>15 THEN KK=KK+1:GOTO 557
572 IF MM<>1 THEN KK=1:MM=MM*10:GOTO 557
573 PRINT "IMPOSIBLE RESOLVER"
574 GOTO 594
575 TT=SS^2-4*PP
576 BEEP0:CLS:PRINT "CALADOS :                       ";
577 IF TT<0 THEN GOTO 581
578 PRINT "Y= ";(SS+SQR(TT))/2
579 PRINT "Y= ";(SS-SQR(TT))/2
580 GOTO 583
581 PRINT "Y="SS/2;"+";SQR(ABS(TT))/2;"*i"
582 PRINT "Y="SS/2;"-";SQR(ABS(TT))/2;"*i"
583 N=N-2
584 IF N=2 THEN GOTO 591
585 IF N=1 THEN PRINT "Y=";-B(1)/A(0):GOTO 594
586 IF N=0 GOTO 594
587 FOR I=1 TO N
588 A(I)=B(I)
589 NEXT I
590 GOTO 556
591 SS=-B(1)/B(0)
592 PP=B(2)/B(0)
593 GOTO 575
594 RETURN
595 END
600 CLEAR:CLS
601 O=0:CLS:PRINT"- CURVAS DE REMANSO -":GOTO 630
602 G=19.62:D=.1
603 PRINT"CARACTERISTICAS DEL CANAL :"
604 INPUT"ANCHO =";BB$:IF BB$="" THEN 607 ELSE INPUT"LONGITUD= ";LL$:L=VAL(LL$):B=VAL(BB$)
605 PRINT"PENDIENTE I";CHR$(161);:INPUT"= ";I,"COEF.MANNING n= ";N:CLS
606 PRINT"CARACTERISTICAS DEL FLUIDO ":INPUT"CAUDAL Q= ";Q
607 CLS:PRINT"CONDICIONES INICIALES "
608 INPUT"COORDENADA Y= ";Y
609 INPUT"FIJO Ax O Ay (X/Y) ";A$
610 IF A$="X" THEN INPUT"Ax= ";T:Z=+1 ELSE INPUT"Ay= ";Z
611 CLS:IF A$="X" THEN 623
612 R=B*Y/(B+2*Y):V=Q/(B*Y):J=N*N*V*V/R^(4/3):H=Y+V*V/19.6
613 Y=Y+Z:S=B*Y/(B+2*Y):W=Q/(B*Y):K=N*N*W*W/S^(4/3):F=Y+W*W/19.6
614 T=(F-H)/(I-(J+K)/2):X=X+I
615 IF L=0 THEN KK=KK+T:PRINT"Ax= ";ROUND(T,-4);"KK= ";ROUND(KK,-4),"X= ";ROUND(X,-4),"Y= ";Y:GOTO 622
616 IF T<0 THEN SE=L+ROUND(X,-2):IF SE<0 THEN T=-(SE-T):Y=Y-Z:GOTO 623
617 IF T>0 THEN IF X>L THEN T=L-X+T:Y=Y-Z:GOTO 623
618 KK=KK+T : PRINT"Ax= ";ROUND(T,-4);"KK= ";ROUND(KK,-4)
619 IF A$="X" THEN SE=T ELSE IF O<>0 THEN IF T<0 THEN SE=0 ELSE X=L
620 IF T<0 THEN PRINT"X= ";ROUND(SE,-4) ELSE PRINT "X= ";ROUND(X,-4)
621 PRINT"Y= ";ROUND(Y,-4)
622 C$=INKEY$:IF C$=""THEN 622 ELSE IF O=0 THEN 611 ELSE 601
623 O=1:R=B*Y/(B+2*Y):V=Q/(B*Y):J=N*N*V*V/R^(4/3):H=Y+V*V/19.6
624 C=T*(I-J/2)+H:Y=Y-Y/5
625 R=B*Y/(B+2*Y):V=Q/(B*Y):J=N*N*V*V/R^(4/3):H=Y+V*V/19.6
626 V1=H+T*J/2-C:IF ABSV1<1E-08 THEN X=X+T:GOTO 618
627 Y=Y+Z*D:S=B*Y/(B+2*Y):W=Q/(B*Y):K=N*N*W*W/S^(4/3):F=Y+W*W/19.62
628 V2=F+T*K/2-C:IF ABSV2<1E-08 THEN X=X+T:GOTO 618
629 IF SGNV1=SGNV2 THEN V1=V2:GOTO 627 ELSE Y=Y-Z*D:D=D/2:GOTO 626
630 PRINT"COORD. X INIC. X";CHR$(161);"= ";:INPUT KK
631 GOTO 602
700 CLS:PRINT"**CHIMENEA DE EQUILIBRIO**"
701 INPUT "DIAM. TUBERIA ";DT:INPUT "DIAM. CHIMENEA ";DC:INPUT "LONGITUD TUB. EN CARGA ";L:INPUT "COEF. PERDIDAS(R) ";R:INPUT "CAUDAL(Q) ";Q
702 INPUT "COTA INICIAL(ZO) ";Z:INPUT "VELOCIDAD INICIAL(WO) ";W:PRINT "INTERVALO DE TIEMPO(";CHR$(238);"T)";:INPUT T
703 F1=PI*DT^2/4:F2=PI*DC^2/4
704 I=-T
705 I=I+T
706 AZ=(F1*W-Q)*T/F2:AW=-9.810001/L*(Z+R*W*ABS(W))*T
707 PRINT "********> PARA T= ";I
708 PRINT "Z= ";Z:PRINT "W= ";W:PRINT CHR$(238);"Z= ";AZ:PRINT CHR$(238);"W= ";AW
709 Z=Z+AZ:W=W+AW
710 GOTO 705
800 CLEAR :CLS
801 REM'(C) Fernando Martinez Perez
802 PRINT "*****DEPOSITOS CONCURRENTES ************** A UN NUDO **********"
803 INPUT"NUMERO DE TUBERIAS ";N
804 DIM Z(N),L(N),D(N),F(N),HE(N),Q(N),T(N),K(N)
805 CLS:PRINT "$$$ ENTRADA DE DATOS $$$"
806 FOR I=1 TO N
807 PRINT " -----> TUBERIA   ";I
808 INPUT" COTA ";Z(I)
809 INPUT "LONGITUD ";L(I)
810 INPUT "DIAMETRO ";D(I)
811 INPUT "F ";F(I)
812 NEXT I
813 L=0
814 GOTO 843
815 INPUT "VALOR INICIAL";Z
816 L=L+1
817 BEEP1:BEEP0
818 PRINT "*******  TANTEO ";L;"*********"
819 FOR I=1 TO N
820 PRINT "$$$ TUBERIA $$$ ";I
821 HE(I)=Z(I)-Z
822 PRINT "H EST= "; HE(I)
823 Q(I)=SQR(ABS(HE(I)/K(I))
824 IF HE(I)<0 THEN Q(I)=-1*Q(I)
825 PRINT "Q= ";Q(I)
826 T(I)=Q(I)/HE(I)
827 PRINT "Q/H= ";T(I)
828 NEXT I
829 AQ=0
830 SQ=0
831 FOR I=1 TO N
832 AQ=AQ+Q(I)
833 SQ=SQ+T(I)
834 NEXT I
835 PRINT "AQ= ";AQ
836 PRINT "S Q/H= ";SQ
837 DH=2*AQ/SQ
838 PRINT "DH= ";DH
839 PRINT "Z= ";Z+DH
840 INPUT "OTRO TANTEO (S/N)";M$
841 IF M$="S" THEN Z=Z+DH ELSE 848
842 GOTO 816
843 FOR J=1 TO N
844 K(J)=(F(J)*L(J)*8)/(D(J)^5*PI*PI*9.810001)
845 PRINT "K(";J;")=";K(J)
846 NEXT J
847 GOTO 815
848 IF M$="N" THEN GOTO 849 ELSE GOTO 840
849 END
900 '(C) Fernando Martinez Perez -1992 -
901 GOTO 910
902 INPUT"W";W
903  A=(.8*W+9.600001*W^2)/(1+.053*W)
904 B=SQR(1+(300/(W+.053*W^2)))
905 C=.4*W*B-1.2*W:C=C^3
906 D=1.28*W^2*B-1.28*W^2
907 E=1.5*CUR(19.2*W^2/(1+.035*W))
908 F=A-(C/D)-2.75-E
909 PRINT "F=";F:GOTO 900
910 CLEAR:CLS:MODE 5
911 PRINT "CALCULO CALADOS  CANAL CIRCULAR          POR ITERACIONES"
912 INPUT "CALADO NORMAL (N) O CALADO CRITICO (C) ";R$
913 IF R$="N" THEN GOTO 914 ELSE 932
914 N=.015:I=.002:D=2.96
915 PRINT "CALADO NORMAL SECCION CIRCULAR "
916 PRINT "CAMBIA VALOR DE N-D-I EN LINEA 914"
917 PRINT "PARA TERMINAR TECLEA 100"
918 T=SQR(I)/N
919 INPUT "ANGULO(rad) ";A
920 IF A=100 THEN GOTO 944
921 B=(SIN(2*A)/(2*A))
922 RH=.25*D*(1-B)
923 C=RH^(2/3)
924 E=.5*SIN(2*A)
925 AR=.25*D^2*(A-E)
926 BEEP0
927 Q=T*C*AR
928 PRINT "Q= ";Q
929 Y=.5*D*(1+SIN(A-(PI/2)))
930 PRINT "Yo= ";Y
931 GOTO 919
932 Q=4:D=2.96
933 PRINT "ESCRIBE EL VALOR DE Q Y D EN LA LINEA 932"
934 INPUT "ANGULO (rad) = ";A
935 IF A=100 THEN GOTO 944
936  K=Q^2*D*SINA
937 L=(A-.5*SIN(2*A))
938 M=9.810001*(.25*D^2*L)^3
939 BEEP0
940 PRINT "UNO= ";K/M
941 Y=D*.5*(1+SIN(A-(PI/2)))
942 PRINT "YC = ";Y
943 GOTO 934
944 MODE 4:END
2000 CLEAR :CLS:PRINT "** RUNGE - KUTTA (CHIMENEAS) **"
2005 '(C) Fernando Martinez-1992
2010 PRINT "ESCRIBE EC. F1(V,Z,T) Y F2(V,Z,T) EN LINEA 5000"
2020 PRINT "CONDICIONES INICIALES:"
2030 INPUT "T";T0
2040 INPUT "Z";Z0
2050 INPUT "V";V0
2060 INPUT "AT";H
2900 V=V0:Z=Z0:T=T0
3000 GOSUB 5000
3010 U11=H*F1
3020 U12=H*F2
3030 V=V0+(U11/3)
3040 Z=Z0+(U12/3)
3050 T=T0+H/3
3060 GOSUB 5000
3070 U21=H*F1
3080 U22=H*F2
3090 V=V0+(2*U21/3)
3100 Z=Z0+(2*U22/3)
3110 T=T0+(2*H/3)
3120 GOSUB 5000
3130 U31=H*F1
3140 U32=H*F2
3200 V0=V0+(U11/4)+(3*U31/4)
3300 Z0=Z0+(U12/4)+(3*U32/4)
3400 T0=T0+H
3500 PRINT "------> T=";T0
3600 PRINT "V=";V0
3700 PRINT "Z=";Z0
3800 GOTO 2900
5000 F1=(-29.4*Z+6.5758-173.245*V*ABS(V))/(1502.9-3*Z)
5010 'F1=DV/DT
5100 F2=V
5110 'F2=DZ/DT
5998 GOSUB 6037:LOCATE 0,0:RETURN
5999 IF INKEY$="" THEN 5999 ELSE RETURN
6000 CLS
6010 A$="................................ Programa suministrado por la Delegacion de Alumnos de Caminos, programado por JuanJu Navarro y Fernando Martinez ....................":D$=A$:GOSUB 6040
6020 B$="................................ Esta delegacion no se hace responsable de los problemas que pueda ocasionar la utilizacion de este software...................":D$=B$:GOSUB 6040
6030 C$="................................ Si tuvieses algun problema comunicanoslo de forma precisa de tal forma que podamos subsanarlo, gracias.......................":D$=C$:GOSUB 6040
6035 GOTO 5
6037 PRINT "MEGAHIDRAULICA v1.3, (C) Caminos 1993";:LOCATE 0,1:PRINT "Fernando & JuanJu Research Co.";:GOSUB 5999:LOCATE 0,1:PRINT "                                               ";:RETURN
6040 L=LEN(D$):FOR A=1 TO L-30:D$=RIGHT$(D$,L-1)+LEFT$(D$,1):LOCATE 1,1:PRINT MID$(D$,1,30);:NEXT A:RETURN
