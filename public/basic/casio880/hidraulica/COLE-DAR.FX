5 PRINT "Colebrook-White;Darcy-Weisbach"
7 PRINT "Obtener D, dados J y Q (1)      Obtener J, dados D y Q (2)";
8 TC$=INKEY$:IF TC$="" THEN GOTO8
9 IF TC$="2" THEN CLS:GOTO 200 ELSE IF TC$<>"1" THEN GOTO 8
10 CLS
12 INPUT "nu[m2/sg]=";NU
15 INPUT "K[mm]=";K
20 INPUT "Q[l/sg]=";Q:Q=Q/1000
30 INPUT "J=";J
40 F0=0.2
50 D0=((8*Q^2*F0)/(9.81*PI^2*J))^0.2
60 F1=1/(-2*LOG(K*1E-3/3.71/D0+2.51*PI*D0*NU/(4*Q*SQRF0)))^2
70 IF ABS((F0-F1)/F0)>0.000001 THEN F0=F1: GOTO 60
80 D1=((8*Q^2*F1)/(9.81*PI^2*J))^0.2
90 IF ABS((D0-D1)/D0)>0.000001 THEN D0=D1: GOTO 60
100 BEEP
110 PRINT "D=";D1*1000;"mm"
120 GOTO 20
200 INPUT "nu[m2/sg]=";NU
215 INPUT "K[mm]=";K
220 INPUT "Q[l/sg]=";Q:Q=Q/1000
230 INPUT "D[mm]=";D:D=D/1000
240 F0=0.2
250 F1=1/(-2*LOG(K*1E-3/3.71/D+2.51*PI*D*NU/(4*Q*SQRF0)))^2
260 IF ABS((F0-F1)/F0)>0.000001 THEN F0=F1: GOTO 250
270 J=(F1*8*Q^2)/(PI^2*9.81*D^5)
280 BEEP
290 PRINT "J=";J
300 GOTO 220
