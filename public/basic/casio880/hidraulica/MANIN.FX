1 CLEAR:CLS:REM (C) Fernando Martinez Perez-1992.
2 PRINT "OPCIONES:"
3 PRINT "(1) MANNING"
4 PRINT "(2) ENERGIAS"
5 INPUT "OPCION ";Z$
6 IF Z$="1" THEN GOTO 12
7 IF Z$="2" THEN GOTO 2000 ELSE 2
12 INPUT " CANAL RECTANGULAR(R) O TRAPEZOIDAL(T) ";T$
14 IF T$="R" THEN 20
15 IF T$="T" THEN 16 ELSE 12
16 PRINT "CALCULO COEF. DEL POLINOMIO DE GRADO 10, SOLUCION DE EC. MANING"
18 PRINT "DAME EL VALOR DE LA PENDIENTE H:V"
19 INPUT "H ";H:INPUT "V ";V:GOTO 30
20 PRINT "CALCULO COEF. DEL POLINOMIO DE GRADO 5, SOLUCION DE EC. MANING"
30 INPUT"Q(M^3/S) ";Q
40 INPUT "NO. MANING ";NM
50 INPUT "ANCHURA ";B
60 INPUT "PENDIENTE ";I
65 IF I=0 AND T$="R" THEN GOTO 700
67 IF I<0 THEN PRINT "VALOR DEL CALADO NORMAL ES IRREAL ---> PENDIENTE ADVERSA ":GOTO 1000
70 I=SQR(I)
75 IF T$="R" THEN GOTO 80 ELSE 250
80 A=B^2
90 M=4*(Q*NM/(B*I))^3
100 C=B*M
110 D=B^2*M/4
120 PRINT"A*Y^5+B*Y^2+C*Y+D=0"
125 N=5:DIM A(N),B(N),C(N)
130 PRINT "A= ";A/A:A(0)=1
135 A(1)=0:A(2)=0
140 PRINT "B= ";-M/A:A(3)=-M/A
150 PRINT "C= ";-C/A:A(4)=-C/A
160 PRINT "D= ";-D/A:A(5)=-D/A
162 CLS:PRINT "CALCULANDO...";
165 GOSUB 5000
170 YC=(Q^2/(B^2*9.81))^(1/3)
180 PRINT "CALADO CRITICO, CANAL RECTANGULAR= ";YC
200 END
250 IF I=0 AND T$="T" THEN GOTO 600
300 T=H*V:S=2*SQR(H^2+V^2):C=NM*Q/I
310 A=1:E=(5*B)/T:F=(10*B^2)/T^2:G=(10*B^3)/T^3:J=(5*B^4)/T^4:K=B^5/T^5:L=(S^2*C^3)/T^5:M=(2*S*B*C^3)/T^5:NT=(B^2*C^3)/T^5
315 BEEP0
316 N=10:DIM A(N),B(N),C(N)
320 PRINT "LOS COEF. SON:"
330 PRINT "A10= ";A:A(0)=A
340 PRINT "A9= ";E:A(1)=E
350 PRINT "A8= ";F:A(2)=F
360 PRINT "A7= ";G:A(3)=G
370 PRINT "A6= ";J:A(4)=J
380 PRINT "A5= ";K:A(5)=K
390 PRINT "A4= 0 ":A(6)=0
400 PRINT "A3= 0 ":A(7)=0
410 PRINT "A2= ";-L:A(8)=-L
420 PRINT "A1= ";-M:A(9)=-M
430 PRINT "A0= ";-NT:A(10)=-NT
431 CLS:PRINT "CALCULANDO...";
432 GOSUB 5000
435 BEEP0
440 PRINT "CALADO CRITICO"
450 P=9.81/Q^2
460 PRINT "COEF. DEL POLINOMIO DE GRADO 6, SOLUCION"
465 Z=H^3*V^3*P
467 N=6
470 PRINT "A6= ";H^3*V^3*P/Z
475 A(0)=H^3*V^3*P/Z
480 PRINT "A5= ";P*3*V^2*H^2*B/Z
485 A(1)=P*3*V^2*H^2*B/Z
490 PRINT "A4= ";P*3*V*H*B^2/Z
495 A(2)=P*3*V*H*B^2/Z
500 PRINT "A3= ";P*B^3/Z
505 A(3)=P*B^3/Z:A(4)=0
510 PRINT "A2= 0 "
520 PRINT "A1= ";-2*H*V/Z
525 A(5)=-2*H*V/Z
530 PRINT "A0= ";-B/Z
535 A(6)=-B/Z
536 CLS:PRINT "CALCULANDO...";
537 GOSUB 5000
540 GOTO 200
600 PRINT "PENDIENTE HORIZONTAL, CALADO NORMAL INFINITO":GOTO 440
700 PRINT "PENDIENTE HORIZONTAL, CALADO NORMAL INFINITO":GOTO 170
1000 IF T$ ="R" THEN GOTO 170 ELSE 440
2000 CLS:INPUT"ENERGIA CONOCIDA";E
2010 INPUT " CANAL RECTANGULAR(R) O TRAPEZOIDAL(T) ";T$
2020 INPUT "ANCHO";B
2030 INPUT "CAUDAL";Q
2040 IF T$="R" THEN 2100
2050 IF T$="T" THEN 3000 ELSE 2010
2100 K=2*9.81*B^2
2110 A=1:B=E:D=Q^2/K
2120 PRINT "ECUACION A RESOLVER:"
2125 BEEP0
2130 PRINT "Y^3 - ";B;"*Y^2 + ";D;" = 0"
2135 CLS:PRINT "CALCULANDO...";
2140 SS=0.1:PP=0.1:N=3
2145 DIM A(N),B(N),C(N)
2150 A(0)=1:A(1)=-B:A(2)=0:A(3)=D
2170 B(0)=A(0):C(0)=A(0):MM=1E-10:K=1
2180 B(1)=A(1)+SS*A(0):C(1)=B(1)+SS*A(0)
2210 FOR I=2 TO N
2220 B(I)=A(I)+SS*B(I-1)-PP*B(I-2)
2230 IF I=N THEN GOTO 2270
2240 IF I=N-1 THEN C(I)=SS*C(I-1)-PP*C(I-2):GOTO 2260
2250 C(I)=B(I)+SS*C(I-1)-PP*C(I-2)
2260 NEXT I
2270 T=B(N)*C(N-3)-B(N-1)*C(N-2)
2280 Q=B(N)*C(N-2)-B(N-1)*C(N-1)
2290 D=C(N-2)^2-C(N-1)*C(N-3)
2300 SS=SS+T/D:PP=PP+Q/D
2320 IF (ABS(B(N))+ABS(B(N-1)))/(ABS(A(N))+ABS(A(N-1)))<MM THEN GOTO 2370
2330 IF K<>15 THEN K=K+1:GOTO 2180
2340 IF MM<>1 THEN K=1:MM=MM*10:GOTO 2180
2350 PRINT "IMPOSIBLE RESOLVER"
2370 T=SS^2-4*PP
2380 CLS:PRINT " *********** CALADOS *********"
2390 PRINT "Y=";(SS+SQR(T))/2
2400 PRINT "Y=";(SS-SQR(T))/2
2410 PRINT "Y=";-B(1)/A(0)
2800 END
3000 PRINT "PENDIENTE H:V"
3010 INPUT "H ";H
3020 INPUT "V ";V
3030 P=H/V
3040 A1=2*9.81*P^2*E
3045 A2=4*9.81*P*B*E
3050 A3=2*9.81*B^2*E
3052 B1=2*9.81*P^2
3054 B2=4*9.81*P*B
3056 B3=2*9.81*B^2
3058 BEEP0
3059 N=5:DIM A(N),B(N),C(N)
3060 PRINT "RESOLVER LA EC. DE GRADO 5 CON COEF. DE MAYOR A MENOR"
3070 PRINT "A5 =";B1/B1:A(0)=1
3080 PRINT "A4 =";(B2-A1)/B1
3085 A(1)=(B2-A1)/B1
3090 PRINT "A3 =";(B3-A2)/B1
3095 A(2)=(B3-A2)/B1
3100 PRINT "A2 =";(-A3)/B1
3105 A(3)=(-A3)/B1
3110 PRINT "A1 = 0"
3115 A(4)=0
3120 PRINT "A0 =";Q^2/B1
3125 A(5)=Q^2/B1
3126 CLS:PRINT"CALCULANDO...";
3127 GOSUB 5000
3130 END
3217 GOSUB 5000
5000 CLS
5110 SS=0.1:PP=0.1
5170 B(0)=A(0):C(0)=A(0)
5180 MM=1E-10:KK=1
5190 B(1)=A(1)+SS*A(0)
5195 C(1)=B(1)+SS*A(0)
5210 FOR I=2 TO N
5220 B(I)=A(I)+SS*B(I-1)-PP*B(I-2)
5230 IF I=N THEN GOTO 5270
5240 IF I=N-1 THEN C(I)=SS*C(I-1)-PP*C(I-2):GOTO 5260
5250 C(I)=B(I)+SS*C(I-1)-PP*C(I-2)
5260 NEXT I
5270 TT=B(N)*C(N-3)-B(N-1)*C(N-2)
5280 QQ=B(N)*C(N-2)-B(N-1)*C(N-1)
5290 DD=C(N-2)^2-C(N-1)*C(N-3)
5300 SS=SS+TT/DD:PP=PP+QQ/DD
5310 PRINT CHR$(134);
5320 ST=(ABS(B(N))+ABS(B(N-1)))/(ABS(A(N))+ABS(A(N-1))):IF ST<MM THEN GOTO 5370
5330 IF KK<>15 THEN KK=KK+1:GOTO 5190
5340 IF MM<>1 THEN KK=1:MM=MM*10:GOTO 5190
5350 PRINT "IMPOSIBLE RESOLVER"
5360 GOTO 5600
5370 TT=SS^2-4*PP
5375 BEEP0:CLS:PRINT "CALADOS :                       ";
5380 IF TT<0 THEN GOTO 5420
5390 PRINT "Y= ";(SS+SQR(TT))/2
5400 PRINT "Y= ";(SS-SQR(TT))/2
5410 GOTO 5440
5420 PRINT "Y="SS/2;"+";SQR(ABS(TT))/2;"*i"
5430 PRINT "Y="SS/2;"-";SQR(ABS(TT))/2;"*i"
5440 N=N-2
5450 IF N=2 THEN GOTO 5510
5460 IF N=1 THEN PRINT "Y=";-B(1)/A(0):GOTO 5600
5465 IF N=0 GOTO 5600
5470 FOR I=1 TO N
5480 A(I)=B(I)
5490 NEXT I
5500 GOTO 5180
5510 SS=-B(1)/B(0)
5520 PP=B(2)/B(0)
5530 GOTO 5370
5600 RETURN
20000 END
