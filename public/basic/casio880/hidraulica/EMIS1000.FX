5 ANGLE 1
10 PRINT "Diseno hidraulico"
20 INPUT "Z(1)[m] =";ZFE
30 INPUT "Q.em[m3/s] =";QE
40 INPUT "D.em.[mm]=";DE
50 DE=DE/1000
60 INPUT "K.abs.em.[mm] =";KE
70 KE=KE/1000
80 INPUT "D.dif.[mm]";DD
90 DD=DD/1000
100 INPUT "K.abs.dif.[mm] =";KD
110 KD=KD/1000
120 INPUT "dist. entre dif. [m]=";SD
130 INPUT "No de dif. =";ND
140 INPUT "Pendiente =";PE
150 INPUT "Viscosidad [m2/s] =";NU
160 INPUT "Angulo estrechamientos [DEG] =";AE
165 ERASE DE,DD,VE,VD,RQB,KBE,KBD,ZFE,HE,QE,QD
170 DIM DE(ND),DD(ND),VE(ND),VD(ND),RQB(12),KBE(12),KBD(12),ZFE(ND),HE(ND),QE(ND),QD(ND)
180 DATA 0,0.1,0.2,0.3,0.4,0.5,0.6,0.7,0.8,0.9,1
190 DATA 1,1,1.01,1.03,1.05,1.09,1.15,1.22,1.32,1.38,1.45
200 DATA 0,0.004,0.02,0.04,0.06,0.1,0.15,0.2,0.26,0.32,0.4
210 FOR I=1 TO 11:READ RQB(I):NEXT I
220 FOR I=1 TO 11:READ KBD(I):NEXT I
230 FOR I=1 TO 11:READ KBE(I):NEXT I
240 REM DIAMETRO INICIAL
245 SE=PI*DE^2/4
250 VE(1)=QE/SE
260 DE(1)=DE
270 QE(1)=QE
280 QD=QE/ND
290 AD=PI*DD^2/4
300 VD(1)=QD/AD
310 DD(1)=DD
320 ZFE(1)=ZFE
330 REM ENERGIA
340 ZD=ZFE(1)-DE(1)/2-3.5*DD(1)
350 RQB=QD/QE(1)
360 F=0.04
370 F0=F
380 F=1/(-0.86*LOG(KD/3.71/DD(1)+2.51*NU/VD(1)/DD(1)/SQR(F)))^2
390 IF ABS((F0-F)/F0)>0.000001 THEN GOTO 370
400 KPD=F*(3+1.5*PI/2)+0.18
410 FOR I=1 TO 11
420 IF RQB>RQB(I) THEN KBD=KBD(I)+(KBD(I+1)-KBD(I))*(RQB-RQB(I))/(RQB(I+1)-RQB(I))
430 IF RQB>RQB(I) THEN KBE=KBE(I)+(KBE(I+1)-KBE(I))*(RQB-RQB(I))/(RQB(I+1)-RQB(I))
440 NEXT I
450 HE(1)=ZD*0.026+VD(1)^2*(1+KPD)/2/9.81+VE(1)^2*(KBD-1)/2/9.81
460 FOR I=2 TO ND :PRINT I;
470 SEA=SE:KBDA=KBD:KBEA=KBE
480 ZFE(I)=ZFE(I-1)+PE*SD
490 QE(I)=QE(I-1)-QD
500 RQB=QD/QE(I)
505 FOR J=1 TO 11
510 IF RQB>RQB(J) THEN KBD=KBD(J)+(KBD(J+1)-KBD(J))*(RQB-RQB(J))/(RQB(J+1)-RQB(J))
520 IF RQB>RQB(J) THEN KBE=KBE(J)+(KBE(J+1)-KBE(J))*(RQB-RQB(J))/(RQB(J+1)-RQB(J))
525 NEXT J
530 DE(I)=DE(I-1)
540 SE=PI*DE(I)^2/4
550 VE(I)=QE(I)/SE
560 IF VE(I)<0.8 THEN PRINT "Hay que cambiar diametro" ELSE GOTO 590
570 PRINT "Nuevo diametro emisario <";DE(I)*1000;"[mm]";:INPUT DE(I):DE(I)=DE(I)/1000
580 GOTO 540
590 F0=F
600 F=1/(-0.86*LOG(KE/3.71/DE(I)+2.51*NU/VE(I)/DE(I)/SQR(F)))^2
610 IF ABS ((F0-F)/F0)>0.000001 THEN GOTO 590
620 LE=0
630 REM PERDIDA POR ESTR
640 RDE=DE(I)/DE(I-1)
650 IF DE(I)=DE(I-1) THEN GOTO 730
660 IF AE=90 THEN LE=0 ELSE  LE=(DE(I-1)-DE(I))/TAN(PI*AE/180)/2
665 PRINT "d/D=";RDE
670 INPUT "Ke=";KEE
680 VEA=QE(I)/SEA
690 FA=F
700 F0=FA
710 FA=1/(-0.86*LOG(KE/3.71/DE(I-1)+2.51*NU/VEA/DE(I-1)/SQR(FA)))^2
720 IF ABS((F0-FA)/F0)>0.000001 THEN GOTO 700
730 RDE=1/RDE
740 IF RDE<>1 THEN X=RDE*(RDE^4-1)/4/(RDE-1)
750 HE(I)=HE(I-1)+(VE(I-1)^2*(1-KBEA)-VE(I)^2*(1+F*(SD-LE)/DE(I)+KEE)-X*FA*LE*VEA^2/DE(I-1))/2/9.81
760 DD=DD(I-1)
770 VD=VD(I-1)
780 DD0=DD
790 F0=F
800 F=1/(-0.86*LOG(KD/3.71/DD+2.51*NU/VD/DD/SQR(F)))^2
810 IF ABS((F0-F)/F0)>0.000001 THEN GOTO 780
820 KPD=F*(3+1.5*PI/2)+0.18
830 ZD=ZFE(I)-DE(I)/2-3.5*DD
840 VD=SQR((HE(I)+VE(I)^2*(1-KBD)/2/9.81-ZD*0.026)*2*9.81/(1+KPD))
850 DD=SQR(4*QD/VD/PI)
860 IF (ABS(DD-DD0)/DD0)>0.000001 THEN GOTO 780
870 DD(I)=DD
880 VD(I)=VD
890 NEXT I
900 PRINT "Situacion con diametros teoricos"
910 FOR I=1 TO ND
920 PRINT "Z(";I;")=";ZFE(I)
930 PRINT "Eemis(";I;")=";HE(I)+VE(I)^2/2/9.81
932 GOSUB 10000
935 PRINT "P.emis.(";I;")=";HE(I)+ZFE(I)
940 PRINT "D.emis.(";I;")=";DE(I)*1000
945 GOSUB 10000
950 PRINT "Q.emis.(";I;")=";QE(I)
960 PRINT "V.emis.(";I;")=";VE(I)
965 GOSUB 10000
970 PRINT "D.difu.(";I;")=";DD(I)*1000
980 PRINT "V.difu.(";I;")=";VD(I)
985 GOSUB 10000
1000 NEXT I
1010 REM DIAMETROS COMERCIALES
1020 QD(1)=QD
1030 FOR I=1 TO ND
1035 QD(I)=QD
1040 PRINT "Diametro comercial mas proximo a";DD(I)*1000;"[mm]=";:INPUT DD(I):DD(I)=DD(I)/1000
1050 NEXT I
1052 HES=HE(1)*1.1:HEI=HE(1)*0.9
1055 HE=(HES+HEI)/2
1057 HE(1)=HE
1060 DD=DD(1)
1070 VD=VD(1)
1080 QD=QD(1)
1090 QD0=QD
1100 F0=F
1110 F=1/(-0.86*LN(KD/3.71/DD+2.51*NU/VD/DD/SQR(F)))^2
1120 IF ABS((F0-F)/F0)>0.000001 THEN GOTO 1090
1130 KPD=F*(3+1.5*PI/2)+0.18
1140 ZD=ZFE(1)-DE(1)/2-3.5*DD
1150 VD=SQR((HE(1)+VE(1)^2*(1-KBD)/2/9.81-ZD*0.026)*2*9.81/(1+KPD))
1160 QD=VD*PI*DD^2/4
1170 RQB=QD/QE(1)
1175 FOR J=1 TO 11
1180 IF RQB>RQB(J) THEN KBD=KBD(J)+(KBD(J+1)-KBD(J))*(RQB-RQB(J))/(RQB(J+1)-RQB(J))
1190 IF RQB>RQB(J) THEN KBE=KBE(J)+(KBE(J+1)-KBE(J))*(RQB-RQB(J))/(RQB(J+1)-RQB(J))
1200 NEXT J
1210 IF (ABS(QD-QD0)/QD0)>0.000001 THEN GOTO 1090
1220 QD(1)=QD
1230 VD(1)=VD
1240 SE=PI*DE(1)^2/4
1250 FOR I= 2 TO ND
1260 SEA=SE
1270 KBDA=KBD
1280 KBEA=KBE
1300 QE(I)=QE(I-1)-QD(I-1)
1310 QD00=QD(I)
1312 RQB=QD(I)/QE(I)
1315 FOR J=1 TO 11
1320 IF RQB>RQB(J) THEN KBD=KBD(J)+(KBD(J+1)-KBD(J))*(RQB-RQB(J))/(RQB(J+1)-RQB(J))
1330 IF RQB>RQB(J) THEN KBE=KBE(J)+(KBE(J+1)-KBE(J))*(RQB-RQB(J))/(RQB(J+1)-RQB(J))
1340 NEXT J
1350 SE=PI*DE(I)^2/4
1360 VE(I)=QE(I)/SE
1370 F0=F
1380 F=1/(-0.86*LOG(KE/3.71/DE(I)+2.51*NU/VE(I)/DE(I)/SQR(F)))^2
1390 IF ABS((F0-F)/F0)>0.000001 THEN GOTO 1370
1400 LE=0
1410 RDE=DE(I)/DE(I-1)
1420 IF DE(I)=DE(I-1) THEN GOTO 1510
1430 IF AE=90 THEN LE=0 ELSE LE=(DE(I-1)-DE(I))/TAN(PI*AE/180)/2
1440 PRINT "d/D=";RDE
1450 INPUT "Ke=";KEE
1460 VEA=QE(I)/SEA
1470 FA=F
1480 F0=FA
1490 FA=1/(-0.86*LOG(KE/3.71/DE(I-1)+2.51*NU/VEA/DE(I-1)/SQR(FA)))^2
1500 IF ABS((F0-FA)/F0)>0.000001 THEN GOTO 1480
1510 RDE=1/RDE
1520 IF RDE<>1 THEN X=RDE*(RDE^4-1)/4/(RDE-1)
1530 HE(I)=HE(I-1)+(VE(I-1)^2*(1-KBEA)-VE(I)^2*(1+F*(SD-LE)/DE(I)+KEE)-X*FA*LE*VEA^2/DE(I-1))/2/9.81
1540 DD=DD(I)
1550 VD=VD(I)
1560 QD=QD(1)
1570 QD0=QD
1580 F0=F
1590 F=1/(-0.86*LOG(KD/3.71/DD+2.51*NU/VD/DD/SQR(F)))^2
1600 IF ABS((F0-F)/F0)>0.000001 THEN GOTO 1570
1610 KPD=F*(3*1.5*PI/2)+0.18
1620 ZD=ZFE(I)-DE(I)/2-3.5*DD
1630 VD=SQR((HE(I)+VE(I)^2*(1-KBD)/2/9.81-ZD*0.026)*2*9.81/(1+KPD))
1640 QD=VD*PI*DD^2/4
1650 IF (ABS(QD-QD0)/QD0)>0.000001 THEN GOTO 1570
1660 QD(I)=QD
1670 VD(I)=VD
1680 IF (ABS(QD-QD00)/QD00)>0.000001 THEN GOTO 1310
1690 NEXT I
1700 IF (ABS(QE(ND)-QD(ND))/QD(ND))<0.0001 THEN GOTO 1740
1710 IF QE(ND)>QD(ND) THEN HEI=HE
1720 IF QE(ND)<QD(ND) THEN HES=HE
1730 GOTO 1055
1740 FOR I=1 TO ND
1750 PRINT "Z(";I;")=";ZFE(I)
1755 GOSUB 10000
1760 PRINT "E.emis.(";I;")=";HE(I)+VE(I)^2/2/9.81
1770 PRINT "P.emis.(";I;")=";HE(I)+ZFE(I)
1775 GOSUB 10000
1780 PRINT "D.emis.(";I;")=";DE(I)*1000
1790 PRINT "Q.emis.(";I;")=";QE(I)
1795 GOSUB 10000
1800 PRINT "V.emis.(";I;")=";VE(I)
1810 PRINT "D.difus.(";I;")=";DD(I)*1000
1815 GOSUB 10000
1820 PRINT "V.difus.(";I;")=";VD(I)
1830 PRINT "Q.difus.(";I;")=";QD(I)
1835 GOSUB 10000
1850 NEXT I
10000 IF INKEY$<>" " THEN 10000
11000 RETURN
