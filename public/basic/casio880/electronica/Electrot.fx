1 '(C)1996 FuP v2.3  ‰ final
5 CLS:CLEAR:MODE 4
10 DEFCHR$(252)="0408700804"
12 DEFCHR$(253)="3844384438"
15 B$=CHR$(8):PR=-4:RA=1E8:ra=1E8:CC=1E-8:cc=1E-8
20 DIM X(5),Y(5),Q(5),R(5),S(5),T(5),U(3,2),I(4,2),J(3,2),V(4,2)
30 PRINT "     ** ELECTROTECNIA 3ß **          1996 Fermin U.P. -OTM-";:FOR I=1 TO 300:NEXT
40 CLS:PRINT"Ope Zeq  ü  ün  î îæü ‹ Fin  i   1   2   3   4  5  6  7  8   .";:M$=INPUT$(1)
50 IF M$=CHR$(46) THEN GOSUB 2400
60 M=VAL(M$)
90 IF M=0 THEN GOTO 40
93 CLS:ON M GOSUB 140,230,560,890,1270,1700,3000,3500
99 GOTO 40
140 N=2:J=1
150 GOSUB 2000
160 FOR I=1 TO N
170 R(I)=X(I):T(I)=Y(I)
180 NEXT I
185 CLS:PRINT "Tipo de operacion(+,-,*,/):";:L$=INPUT$(1):IF L$="+" THEN L=1:GOTO 190
186 IF L$="-" THEN L=2:GOTO 190
187 IF L$="*" THEN L=3:GOTO 190
188 IF L$="/" THEN L=4:GOTO 190
189 GOTO 185
190 ON L GOSUB 2100,2500,2600,2700
200 CLS:PRINT "Z1";L$;"Z2 ="ROUND(X,PR);"(";ROUND(Y,PR);B$;CHR$(223)
220 X(1)=X:Y(1)=Y
225 RETURN
230 'Hallar la Zeq
235 CLS:PRINT "1)Admitancias";:LOCATE 0,1:PRINT "2)Impedancias:";:G$=INPUT$(1):G=VAL(G$):IF G<>1 AND G<>2 THEN 235
240 P=0:J=1
250 CLS:PRINT "1)En serie";:LOCATE 0,1:PRINT "2)En paralelo:";:F$=INPUT$(1):F=VAL(F$):IF F<>1 AND F<>2 THEN 250
260 N=0
265 CLS:IF P=1 THEN LOCATE 0,1:PRINT "(sin contar el Zeq/Yeq anterior)";
266 LOCATE 0,0:INPUT "Numero de elementos:",N
267 IF N=0 THEN GOTO 260
270 ON F GOSUB 330,440
275 IF G=1 THEN CLS:PRINT "Yeq =";ROUND(X,PR);:GOTO 290
280 CLS:PRINT "Zeq =";ROUND(X,PR);
290 PRINT "(";ROUND(Y,PR);B$;CHR$(223)
300 X(1)=X:Y(1)=Y:PRINT "Mas derivaciones (S/N)";:A$=INPUT$(1)
310 IF A$="S" OR A$="s" THEN J=2:GOTO 250
320 RETURN
330 'Elementos en serie
360 IF P=1 THEN N=N+1
370 GOSUB 2000
380 FOR I=1 TO N
390 R(I)=X(I):T(I)=Y(I)
400 NEXT I
410 GOSUB 2100
430 P=1:RETURN
440 'Elem.en paralelo
460 IF P=1 THEN N=N+1
480 GOSUB 2000
490 FOR I=1 TO N
500 R(I)=1/X(I):T(I)=-Y(I)
510 NEXT I
520 GOSUB 2100
530 X=1/X:Y=-Y
550 P=1:RETURN
560 'Estrella
565 N=3:J=1:H=9
580 GOSUB 2200
640 GOSUB 2000
645 GOSUB 4000
650 R(1)=X(1)*X(2):T(1)=Y(1)+Y(2)
660 R(2)=X(2)*X(3):T(2)=Y(2)+Y(3)
670 R(3)=X(3)*X(1):T(3)=Y(3)+Y(1):GOSUB 4040
680 GOSUB 2100:GOSUB 4040
690 D=X:E=Y
700 R(1)=U(1,1)*X(3):T(1)=U(1,2)+Y(3)
710 R(2)=U(3,1)*X(2):T(2)=U(3,2)+Y(2)+180
720 N=2
730 GOSUB 4040:GOSUB 2100:GOSUB 4040
735 K=I+2:IF K>3 THEN K=K-3
740 I(1,1)=X/D:I(1,2)=Y-E
745 GOSUB 4040
750 R(1)=U(2,1)*X(1):T(1)=U(2,2)+Y(1)
760 R(2)=U(1,1)*X(3):T(2)=U(1,2)+Y(3)+180
770 GOSUB 4040:GOSUB 2100:GOSUB 4040
780 I(2,1)=X/D:I(2,2)=Y-E
790 R(1)=U(3,1)*X(2):T(1)=U(3,2)+Y(2)
800 R(2)=U(2,1)*X(1):T(2)=U(2,2)+Y(1)+180
810 GOSUB 4040:GOSUB 2100:GOSUB 4040
820 I(3,1)=X/D:I(3,2)=Y-E
830 GOSUB 2300
832 FOR I=1 TO 3
834 R(I)=1/X(I):T(I)=-Y(I)
836 NEXT I
837 N=3
838 GOSUB 2100
840 D=X:E=Y
842 FOR I=1 TO 3
844 R(I)=U(I,1)*R(I)/SQR3
845 T(I)=T(I)+U(I,2)-30
846 NEXT I
848 GOSUB 2100
850 D=X/D:E=Y-E
852 CLS:PRINT "VnN:Mod=";ROUND(D,PR);
854 LOCATE 4,1:PRINT "Arg=";ROUND(E,PR)
860 RETURN
880 'Estrella con neutro
890 J=1:N=3:H=10
900 GOSUB 2200
910 V(1,1)=U(1,1)/SQR3
920 FOR I=1 TO 3
930 V(I,1)=V(1,1)
940 V(I,2)=U(I,2)-30
950 NEXT I
960 GOSUB 2000
970 CLS:PRINT "Zneutro:";
972 LOCATE 0,1:PRINT "     CC æ Z=0  ;  RA æ Z=ý";
975 LOCATE 8,0:PRINT "Mod=";ROUND(X(4),PR);:INPUT X(4)
976 IF X(4)=RA OR X(4)=CC THEN Y(4)=0:GOTO 982
977 LOCATE 0,1:PRINT "                             ";
980 LOCATE 8,1:PRINT "Arg=";ROUND(Y(4),PR);:INPUT Y(4)
982 GOSUB 4000
985 N=4:GOSUB 4040
990 R(1)=X(1)*X(2)*X(3):T(1)=Y(2)+Y(3)+Y(1)
1000 R(2)=X(1)*X(2)*X(4):T(2)=Y(1)+Y(2)+Y(4)
1010 R(3)=X(1)+X(3)+X(4):T(3)=Y(1)+Y(3)+Y(4)
1020 R(4)=X(2)*X(3)*X(4):T(4)=Y(2)+Y(3)+Y(4)
1030 GOSUB 4040:GOSUB 2100
1040 D=X:E=Y
1050 N=3:GOSUB 4040
1060 R(1)=V(1,1)*X(2)*X(3):T(1)=V(1,2)+Y(2)+Y(3)
1070 R(2)=V(2,1)*X(1)*X(3):T(2)=V(2,2)+Y(1)+Y(3)
1080 R(3)=V(3,1)*X(1)*X(2):T(3)=V(3,2)+Y(1)+Y(2)
1090 GOSUB 4040:GOSUB 2100:GOSUB 4040
1100 I(4,1)=X/D:I(4,2)=Y-E
1110 V(4,1)=I(4,1)*X(4):V(4,2)=I(4,2)+Y(4)
1120 R(2)=V(4,1):T(2)=V(4,2)+180
1125 N=2:GOSUB 4040
1130 FOR I=1 TO 3
1140 R(1)=V(I,1):T(1)=V(I,2)
1150 GOSUB 2100:GOSUB 4040
1160 I(I,1)=X/X(I):I(I,2)=Y-Y(I)
1170 NEXT I
1180 GOSUB 4040:GOSUB 2300
1230 CLS:PRINT "Ineutro:";
1240 PRINT ROUND(I(4,1),PR);
1250 PRINT "(";ROUND(I(4,2),PR);B$;CHR$(223)
1255 CLS:PRINT "VnN:";
1256 PRINT ROUND(V(4,1),PR);
1257 PRINT "(";ROUND(V(4,2),PR);B$;CHR$(223)
1260 RETURN
1270 'Triangulo
1272 J=1:N=3:H=9
1275 CLS:PRINT "       Carga 1  î  Carga 2";:LOCATE 13,1:PRINT "Carga 3";:FOR CN=1 TO 400:NEXT
1280 GOSUB 2200
1340 GOSUB 2000
1350 GOSUB 4000
1360 FOR I=1 TO 3
1370 J(I,1)=U(I,1)/X(I):J(I,2)=U(I,2)-Y(I)
1380 GOSUB 4040:NEXT I
1385 N=N-1
1390 R(1)=J(1,1):T(1)=J(1,2)
1400 R(2)=J(3,1):T(2)=J(3,2)+180
1410 GOSUB 4040:GOSUB 2100:GOSUB 4040
1420 I(1,1)=X:I(1,2)=Y
1430 R(1)=J(2,1):T(1)=J(2,2)
1440 R(2)=J(1,1):T(2)=J(1,2)+180
1450 GOSUB 4040:GOSUB 2100:GOSUB 4040
1460 I(2,1)=X:I(2,2)=Y
1470 R(1)=J(3,1):T(1)=J(3,2)
1480 R(2)=J(2,1):T(2)=J(2,2)+180
1490 GOSUB 4040:GOSUB 2100:GOSUB 4040
1500 I(3,1)=X:I(3,2)=Y
1510 GOSUB 2300
1560 FOR I=1 TO 3
1570 CLS:PRINT "J(";CHR$(48+I);") =";
1580 PRINT ROUND(J(I,1),PR);
1590 PRINT "(";ROUND(J(I,2),PR);B$;CHR$(223)
1600 NEXT I
1610 RETURN
1700 'Formulas de Kenelly
1705 CLS:PRINT "1)îæü    (Sentido horario)      2)üæî    (Carga 1:izda)   ";:R$=INPUT$(1):R=VAL(R$):IF R<>1 AND R<>2 THEN 1705
1710 J=1:N=3:H=9
1720 GOSUB 2000:GOSUB 4000
1740 FOR I=1 TO 3
1750 R(I)=X(I):T(I)=Y(I)
1760 GOSUB 4040:NEXT I
1765 IF R=2 THEN GOSUB 1900
1770 GOSUB 4040:GOSUB 2100:GOSUB 4040
1775 IF R=2 THEN X=(1/X):Y=-Y
1780 I(1,1)=X(1)*X(3)/X:I(1,2)=Y(3)+Y(1)-Y:GOSUB 4040
1790 I(2,1)=X(1)*X(2)/X:I(2,2)=Y(2)+Y(1)-Y:GOSUB 4040
1800 I(3,1)=X(2)*X(3)/X:I(3,2)=Y(2)+Y(3)-Y:GOSUB 4040
1810 IF R=2 THEN GOTO 1820
1815 GOSUB 4040:GOTO 1850
1820 AUX1=I(1,1):AUX2=I(1,2):I(1,1)=I(2,1):I(1,2)=I(2,2):I(2,1)=I(3,1):I(2,2)=I(3,2):I(3,1)=AUX1:I(3,2)=AUX2:GOSUB 4040
1850 FOR I=1 TO 3
1860 CLS:PRINT "Z(";CHR$(48+I);") =";
1870 PRINT ROUND(I(I,1),PR);
1880 PRINT "(";ROUND(I(I,2),PR);B$;CHR$(223)
1890 NEXT I
1895 RETURN
1900 FOR  K=J TO N
1910 R(K)=1/R(K):T(K)=-T(K)
1920 NEXT K
1930 RETURN
2000 'Valores de impedancias
2001 FOR K=J TO N
2005 CLS
2010 PRINT "Z(";CHR$(48+K);"):";
2020 IF M=3 OR M=4 OR M=5 THEN 2030 ELSE 2040
2030 LOCATE 0,1:PRINT "     CC æ Z=0  ;  RA æ Z=ý";
2040 LOCATE 5,0:PRINT "Mod=";ROUND(X(K),PR);:INPUT X(K)
2050 IF X(K)=RA OR X(K)=CC THEN Y(K)=0:GOTO 2070
2055 LOCATE 0,1:PRINT "                             ";
2060 LOCATE 5,1:PRINT "Arg=";ROUND(Y(K),PR);:INPUT Y(K)
2070 NEXT K
2080 RETURN
2100 'Suma de num.compl.
2105 A=0:B=0
2110 FOR K=1 TO N
2120 X=REC(R(K),T(K))
2130 A=A+X
2140 B=B+Y
2150 NEXT K
2160 X=POL(A,B)
2170 RETURN
2200 CLS:PRINT "Tension compuesta entre lineas  ";
2210 PRINT "U12:Mod=";ROUND(U(1,1),PR);:INPUT U(1,1)
2220 PRINT "    Arg=";ROUND(U(1,2),PR);:INPUT U(1,2)
2230 FOR I=2 TO 3
2240 U(I,1)=U(1,1)
2250 U(I,2)=U(I-1,2)-120
2260 NEXT I
2270 RETURN
2300 FOR I=1 TO 3
2310 CLS:PRINT "I(";CHR$(48+I);") =";
2320 PRINT ROUND(I(I,1),PR);
2330 PRINT "(";ROUND(I(I,2),PR);B$;CHR$(223)
2340 NEXT I
2350 RETURN
2400 'Informacion
2410 CLS:PRINT "Ope:operaciones (+,-,*,/)           con numeros complejos"
2420 CLS:PRINT "Zeq:hallar la impedancia o          admitancia equivalente"
2430 CLS:PRINT " ü :valores para una estrella"
2440 CLS:PRINT " ün:valores para una estrella       con neutro"
2450 CLS:PRINT " î :valores para un triangulo"
2460 CLS:PRINT "îæü:formulas de Kenelly             para pasar de îæü o üæî"
2470 CLS:PRINT " ‹ :precision en el resultado,      por defecto ‹=10›“"
2480 CLS:PRINT "Fin:finalizar este ridiculo         programa inmediatamente"
2490 CLS:PRINT " i :informacion                  -- (C)Fermin U.P.1996 ETSII --":RETURN
2500 'Resta de num.compl.
2505 X=REC(R(1),T(1)):A=X:B=Y
2510 X=REC(R(2),T(2)):A=A-X:B=B-Y
2520 X=POL(A,B)
2530 RETURN
2600 'Prod.de num.compl.
2605 X=R(1)*R(2):Y=T(1)+T(2)
2610 RETURN
2700 'Div.de num.compl.
2705 X=R(1)/R(2):Y=T(1)-T(2)
2710 RETURN
3000 'Precision
3010 CLS:PRINT "Grado de precision actual: 10";"›";CHR$(144-PR-1);:LOCATE 0,1:PRINT "Nueva precision(1-9):";:L$=INPUT$(1):PE=VAL(L$)
3020 IF PE=0 THEN 3010
3030 PR=-PE-1:RETURN
3500 CLEAR :END
4000 'Grafico del proceso
4010 L=0:CLS
4020 PRINT "       0% [";:FOR F=1 TO H:PRINT " ";:NEXT:PRINT "] 100%";
4030 RETURN
4040 LOCATE 11+L,0:PRINT "‡";
4050 L=L+1
4060 RETURN
