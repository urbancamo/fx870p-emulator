1 REM HANGMAN
2 REM FOR CASIO VX-4
3 REM 100 ROT13 WORDS
10 CLEAR:CLS
11 PRINT "*** HANGMAN ***";
12 LOCATE 0,1:PRINT "GUESS THE WORD A-Z";
13 LOCATE 0,2:PRINT "7 WRONG = GAME OVER";
14 LOCATE 0,3:PRINT "PRESS ANY KEY...";
15 K$=INPUT$(1)
20 N=100:R=INT(RAN#*N)+1
30 RESTORE
40 FOR I=1 TO R:READ E$:NEXT I
50 REM DECODE ROT13
60 W$="":L=LEN(E$)
70 FOR I=1 TO L
80 C=ASC(MID$(E$,I,1))-65
90 W$=W$+CHR$((C+13) MOD 26+65)
100 NEXT I
110 DIM G(15)
120 M=7:NG=0:U$=""
200 REM DISPLAY
210 CLS
220 T$=""
230 FOR I=1 TO L
240 IF G(I)=1 THEN T$=T$+MID$(W$,I,1) ELSE T$=T$+"_"
250 IF I<L THEN T$=T$+" "
260 NEXT I
270 PRINT T$
280 H$=""
290 FOR I=1 TO M-NG:H$=H$+CHR$(135):NEXT I
300 FOR I=1 TO NG:H$=H$+" ":NEXT I
310 LOCATE 0,1:PRINT H$;" ";NG;"/";M
320 IF LEN(U$)>0 THEN LOCATE 0,2:PRINT U$;
330 REM CHECK WIN
340 F=1
350 FOR I=1 TO L
360 IF G(I)=0 THEN F=0
370 NEXT I
380 IF F=1 THEN LOCATE 0,3:PRINT "YOU WIN!  ";W$;:GOTO 600
390 IF NG>=M THEN LOCATE 0,3:PRINT "ANSWER: ";W$;:GOTO 600
400 REM GET LETTER
410 LOCATE 0,3:PRINT "GUESS: ";
420 A$=INPUT$(1)
425 IF ASC(A$)>=97 AND ASC(A$)<=122 THEN A$=CHR$(ASC(A$)-32)
430 IF ASC(A$)<65 THEN 420
440 IF ASC(A$)>90 THEN 420
450 PRINT A$;
460 REM CHECK DUPLICATE
470 FOR I=1 TO LEN(U$)
480 IF MID$(U$,I,1)=A$ THEN 200
490 NEXT I
500 U$=U$+A$
510 REM CHECK IN WORD
520 H=0
530 FOR I=1 TO L
540 IF MID$(W$,I,1)=A$ THEN G(I)=1:H=1
550 NEXT I
560 IF H=0 THEN NG=NG+1
570 GOTO 200
600 REM PLAY AGAIN
610 K$=INPUT$(1)
620 GOTO 10
700 DATA CLGUBA,FPERRA,ZRZBEL,FLFGRZ,OVANEL
710 DATA CYNARG,EBPXRG,OEVQTR,PNFGYR,QENTBA
720 DATA JVMNEQ,CHMMYR,SBERFG,VFYNAQ,QRFREG
730 DATA FGERNZ,JVAGRE,FCEVAT,FHZZRE,OERRMR
740 DATA PBCCRE,FVYIRE,ZNTARG,ZVEEBE,CEVFZ
750 DATA YNFRE,ENQNE,EBOBG,RATVAR,NAPUBE
760 DATA ORNPBA,SNYPBA,UNZZRE,CVENGR,GUEBAR
770 DATA FUVRYQ,GRZCYR,PNALBA,GUHAQRE,PELFGNY
780 DATA QVNZBAQ,YNAGREA,ZBAFGRE,CRATHVA,ENVAOBJ
790 DATA FCUVAK,GHAARY,IVBYVA,JNYEHF,DHNEGM
800 DATA ZNEOYR,ONFXRG,PNAQYR,QBAXRL,CNEEBG
810 DATA BLFGRE,CVPXYR,PNPGHF,TRAGYR,UNEOBE
820 DATA WVTFNJ,XRGGYR,YHZORE,ZHSSVA,ANCXVA
830 DATA BEPUVQ,CRCCRE,EVOOBA,FNQQYR,GHEONA
840 DATA INYYRL,JNSSYR,LRYYBJ,MVTMNT,OBGGYR
850 DATA PBSSRR,QVAARE,SVATRE,TNEQRA,UVQQRA
860 DATA VAFRPG,WHATYR,XVGGRA,YBPXRG,ZNEXRG
870 DATA ARRQYR,CNQQYR,ENOOVG,FNYZBA,GNOYRG
880 DATA JVAQBJ,OHGGBA,SNOEVP,URYZRG,ZNATB
890 DATA OHPXRG,SBFFVY,CROOYR,GHEXRL,IBLNTR
