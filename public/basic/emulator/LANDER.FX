1 REM LUNAR LANDER
2 REM AFTER MARK WICKENS 2010
3 REM FOR CASIO VX-4
5 CLEAR
10 CLS:PRINT "** LUNAR LANDER **";
11 LOCATE 0,1:PRINT "BY MARK WICKENS (2010)";
12 LOCATE 0,2:PRINT "I=INFO S=START *=KEYS";
13 K$=INPUT$(1)
14 IF K$="I" OR K$="i" THEN GOSUB 900:GOTO 10
15 IF K$="*" THEN GOSUB 940:GOTO 10
16 IF K$<>"S" AND K$<>"s" THEN GOTO 13
100 REM === INITIALISE ===
101 Y=10000:X=-30000:DX=500:DY=0
102 F=25000:BR=0:LA=60:VT=0:HT=0
103 G=10/3:ST=0:F9=0
104 K9$=""
200 REM === MAIN LOOP ===
201 K9$="":K$=INKEY$:IF K$<>"" THEN K9$=K$
202 GOSUB 300
203 K$=INKEY$:IF K$<>"" THEN K9$=K$
204 GOSUB 500
205 K$=INKEY$:IF K$<>"" THEN K9$=K$
206 GOSUB 600
207 IF Y<=0 AND BR=0 THEN GOTO 700
208 ST=ST+1:F9=1-F9
209 GOTO 200
300 REM === DISPLAY ===
301 CLS
302 IF Y<=75 THEN GOTO 340
303 REM --- CRUISE MODE ---
304 LOCATE 0,0:PRINT "ALT:";INT(Y);
305 LOCATE 11,0:PRINT "VSI:";INT(DY);
306 LOCATE 22,0:PRINT "FUL:";INT(F);
307 LOCATE 0,1:PRINT "RNG:";INT(X);
308 LOCATE 11,1:PRINT "HSP:";INT(DX);
309 LOCATE 22,1:PRINT "THR:";BR;
310 LOCATE 0,2:PRINT "TLT:";LA;
311 LOCATE 9,2:PRINT "VTH:";INT(VT);
312 LOCATE 19,2:PRINT "HTH:";INT(HT);
313 GOSUB 380
314 RETURN
340 REM --- RADAR MODE ---
341 LOCATE 0,0:PRINT "ALT:";INT(Y);
342 LOCATE 11,0:PRINT "VSI:";INT(DY);
343 LOCATE 22,0:PRINT "FUL:";INT(F);
344 LOCATE 0,1:PRINT "RNG:";INT(X);
345 LOCATE 10,1:PRINT "HSP:";INT(DX);
346 LOCATE 19,1:PRINT "BR:";BR;
347 LOCATE 26,1:PRINT "LA:";LA;
348 REM ALT TAPE (0-75M)
349 P=INT(Y/75*13):IF P<0 THEN P=0
350 IF P>13 THEN P=13
351 LOCATE 0,2:PRINT "A:--------------";
352 LOCATE P+2,2:PRINT ">";
353 REM VSI TAPE (0 TO -10 M/S)
354 Q=INT(ABS(DY)/10*12):IF Q<0 THEN Q=0
355 IF Q>12 THEN Q=12
356 LOCATE 17,2:PRINT "V:-------------";
357 LOCATE Q+19,2:PRINT ">";
358 GOSUB 380
359 RETURN
380 REM --- STATUS INDICATORS ---
381 IF F<3000 AND F9=1 THEN LOCATE 0,3:PRINT "FUEL";
382 IF DY>0 THEN LOCATE 5,3:PRINT "ASC";
383 IF Y<3 THEN LOCATE 9,3:PRINT "CONTACT";:GOTO 386
384 IF Y<1000 THEN LOCATE 9,3:PRINT "RADAR";
386 IF Y<=0 AND BR>0 THEN LOCATE 17,3:PRINT "CUT!";:GOTO 389
387 IF BR=0 AND F=0 THEN LOCATE 17,3:PRINT "ENG";
389 IF ABS(X)<=500 THEN LOCATE 24,3:PRINT "RANGE";
390 RETURN
500 REM === PHYSICS ===
501 HT=LA/90*BR/10
502 VT=(1-ABS(LA)/90)*BR/10
503 DY=DY-G+VT
504 Y=Y+DY
505 DX=DX-HT
506 X=X+DX
507 IF F-BR<0 THEN BR=F
508 IF F>0 THEN F=F-BR
509 IF Y<0 THEN Y=0
510 RETURN
600 REM === INPUT ===
601 IF K9$="" THEN RETURN
602 IF K9$="0" THEN BR=0:RETURN
603 IF K9$="1" THEN BR=20:RETURN
604 IF K9$="2" THEN BR=40:RETURN
605 IF K9$="3" THEN BR=60:RETURN
606 IF K9$="." THEN BR=100:RETURN
607 IF K9$="+" AND BR<100 THEN BR=BR+10
608 IF K9$="-" AND BR>0 THEN BR=BR-10
609 IF K9$="4" AND LA>-60 THEN LA=LA-10
610 IF K9$="6" AND LA<60 THEN LA=LA+10
611 IF K9$="7" AND LA>-60 THEN LA=LA-5
612 IF K9$="9" AND LA<60 THEN LA=LA+5
613 IF K9$="8" OR K9$="5" THEN LA=0
614 IF K9$="R" OR K9$="r" THEN GOTO 100
615 IF K9$="Q" OR K9$="q" THEN GOTO 750
616 IF K9$="*" THEN GOSUB 940
617 RETURN
700 REM === LANDED ===
701 IF DX<0 THEN DX=-DX
702 IF X<0 THEN X=-X
703 CLS
704 IF DY<-6 OR DX>6 THEN GOTO 730
705 IF DY<-3 THEN PRINT "HARD LANDING, NO GOING HOME!":GOTO 710
706 PRINT "THE EAGLE HAS LANDED!";
710 LOCATE 0,1:PRINT "HSP:";INT(DX);" VSI:";INT(DY);
711 IF X>500 THEN LOCATE 0,2:PRINT INT(X);"M FROM SITE";:GOTO 740
712 IF DX<0.1 THEN DX=0.1
713 IF ABS(DY)<0.1 THEN DY=0.1
714 SC=INT(100/(DX*ABS(DY)))
715 LOCATE 0,2:PRINT "YOUR SCORE IS:";SC;
716 GOTO 740
730 PRINT "CRASHED! YOU MADE A CRATER!";
731 LOCATE 0,1:PRINT "HSP:";INT(DX);" VSI:";INT(DY);
732 IF ABS(DY)>6 THEN LOCATE 0,2:PRINT "CRATER IS:";INT(ABS(DY)*2.5);"M WIDE!";
740 LOCATE 0,3:PRINT "PLAY AGAIN? Y/N";
741 K$=INPUT$(1)
742 IF K$="Y" OR K$="y" THEN GOTO 10
743 END
750 REM === QUIT ===
751 CLS:PRINT "SIMULATION ENDED";
752 LOCATE 0,2:PRINT "PLAY AGAIN? Y/N";
753 K$=INPUT$(1)
754 IF K$="Y" OR K$="y" THEN GOTO 10
755 END
900 REM === INSTRUCTIONS ===
901 CLS:PRINT "LAND THE LEM ON THE";
902 LOCATE 0,1:PRINT "MOON. TILT AND BURN TO";
903 LOCATE 0,2:PRINT "SLOW DOWN, THEN DESCEND";
904 LOCATE 0,3:PRINT "TO THE LANDING SITE.";
905 K$=INPUT$(1)
906 CLS:PRINT "LAND WITHIN 500M OF SITE";
907 LOCATE 0,1:PRINT "WITH DX AND DY BELOW 6";
908 LOCATE 0,2:PRINT "CUT ENGINE ON SURFACE";
909 LOCATE 0,3:PRINT "TO COMPLETE. GOOD LUCK!";
910 K$=INPUT$(1)
911 CLS:PRINT "TIP: AT 60DEG AND 100%";
912 LOCATE 0,1:PRINT "BURN, DESCENT IS STEADY";
913 LOCATE 0,2:PRINT "33% VERTICAL HOLDS ALT";
914 LOCATE 0,3:PRINT "PRESS ANY KEY...";
915 K$=INPUT$(1)
916 CLS:PRINT "TIP: HOLD A KEY DOWN";
917 LOCATE 0,1:PRINT "UNTIL YOU SEE THE";
918 LOCATE 0,2:PRINT "DISPLAY RESPOND.";
919 LOCATE 0,3:PRINT "PRESS ANY KEY...";
920 K$=INPUT$(1)
921 RETURN
940 REM === KEYS ===
941 CLS:PRINT "4/6:TILT10  7/9:TILT5";
942 LOCATE 0,1:PRINT "0:CUT 1-3:SET .:MAX";
943 LOCATE 0,2:PRINT "5/8:VERT +:UP -:DOWN";
944 LOCATE 0,3:PRINT "R:RESET Q:QUIT *:KEYS";
945 K$=INPUT$(1)
946 RETURN
