1 REM BINARY SEARCH
2 REM FOR CASIO VX-4
3 REM TANDY 100 CONVERT
10 CLEAR:CLS
11 DIM A(10),B(10)
12 FOR I=1 TO 10:READ A(I):B(I)=I:NEXT I
20 REM BUBBLE SORT BY VALUE
21 FOR I=1 TO 9
22 FOR J=1 TO 10-I
23 IF A(J)>A(J+1) THEN GOSUB 300
24 NEXT J
25 NEXT I
100 REM SHOW SORTED DATA
101 CLS
102 T$=""
103 FOR I=1 TO 10
104 T$=T$+STR$(A(I))
105 IF I<10 THEN T$=T$+" "
106 NEXT I
107 PRINT T$;
110 LOCATE 0,1:PRINT "SEARCH FOR? (0=END)";
111 LOCATE 0,2:INPUT X
112 IF X=0 THEN CLS:PRINT "GOODBYE!";:END
120 REM BINARY SEARCH
121 L=1:H=10
130 IF L>H THEN 160
131 M=INT((L+H)/2)
132 IF X=A(M) THEN 150
133 IF X<A(M) THEN H=M-1:GOTO 130
134 L=M+1:GOTO 130
150 LOCATE 0,3:PRINT "FOUND AT POSITION";M;
151 K$=INPUT$(1):GOTO 100
160 LOCATE 0,3:PRINT "NOT FOUND!          ";
161 K$=INPUT$(1):GOTO 100
300 REM SWAP
301 T=A(J):A(J)=A(J+1):A(J+1)=T
302 T=B(J):B(J)=B(J+1):B(J+1)=T
303 RETURN
400 DATA 44,15,68,37,101,23,89,51,72,94
