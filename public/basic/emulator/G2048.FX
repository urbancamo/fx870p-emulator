1 REM 2048
2 REM FOR CASIO VX-4
10 CLEAR:CLS
11 DIM G(16),T(4),O(16)
12 PRINT "*** 2048 ***";
13 LOCATE 0,1:PRINT "4=LEFT 6=RIGHT";
14 LOCATE 0,2:PRINT "8=UP   2=DOWN";
15 LOCATE 0,3:PRINT "Q=QUIT  ANY KEY...";
16 K$=INPUT$(1)
17 SC=0
18 FOR I=1 TO 16:G(I)=0:NEXT I
19 GOSUB 700:GOSUB 700
100 REM MAIN LOOP
101 GOSUB 500
102 K$=INPUT$(1)
103 IF K$="Q" OR K$="q" THEN CLS:PRINT "SCORE:";SC;:END
104 FOR I=1 TO 16:O(I)=G(I):NEXT I
110 IF K$="4" THEN GOSUB 300
111 IF K$="6" THEN GOSUB 310
112 IF K$="8" THEN GOSUB 320
113 IF K$="2" THEN GOSUB 330
115 MV=0
116 FOR I=1 TO 16
117 IF G(I)<>O(I) THEN MV=1
118 NEXT I
119 IF MV=0 THEN 100
120 GOSUB 700
121 W=0
122 FOR I=1 TO 16
123 IF G(I)>=2048 THEN W=1
124 NEXT I
125 IF W=1 THEN GOSUB 500:LOCATE 22,1:PRINT "YOU WIN!";:LOCATE 22,2:PRINT "ANY KEY";:K$=INPUT$(1):GOTO 17
126 GOSUB 900
127 IF GM=1 THEN GOSUB 500:LOCATE 22,1:PRINT "GAME";:LOCATE 22,2:PRINT "OVER!";:LOCATE 22,3:PRINT "ANY KEY";:K$=INPUT$(1):GOTO 17
128 GOTO 100
300 REM LEFT
301 FOR R=1 TO 4:P=(R-1)*4
302 FOR I=1 TO 4:T(I)=G(P+I):NEXT I
303 GOSUB 800
304 FOR I=1 TO 4:G(P+I)=T(I):NEXT I
305 NEXT R:RETURN
310 REM RIGHT
311 FOR R=1 TO 4:P=(R-1)*4
312 FOR I=1 TO 4:T(I)=G(P+5-I):NEXT I
313 GOSUB 800
314 FOR I=1 TO 4:G(P+5-I)=T(I):NEXT I
315 NEXT R:RETURN
320 REM UP
321 FOR C=1 TO 4
322 FOR I=1 TO 4:T(I)=G((I-1)*4+C):NEXT I
323 GOSUB 800
324 FOR I=1 TO 4:G((I-1)*4+C)=T(I):NEXT I
325 NEXT C:RETURN
330 REM DOWN
331 FOR C=1 TO 4
332 FOR I=1 TO 4:T(I)=G((4-I)*4+C):NEXT I
333 GOSUB 800
334 FOR I=1 TO 4:G((4-I)*4+C)=T(I):NEXT I
335 NEXT C:RETURN
500 REM DISPLAY
501 CLS
502 FOR R=0 TO 3
503 FOR C=0 TO 3
504 V=G(R*4+C+1)
505 LOCATE C*5,R
506 IF V=0 THEN PRINT "."; ELSE PRINT V;
507 NEXT C
508 NEXT R
509 LOCATE 22,0:PRINT SC;
510 RETURN
700 REM ADD TILE
701 E=0
702 FOR I=1 TO 16:IF G(I)=0 THEN E=E+1
703 NEXT I
704 IF E=0 THEN RETURN
705 P=INT(RAN#*E)+1:E=0
706 FOR I=1 TO 16
707 IF G(I)<>0 THEN 711
708 E=E+1
709 IF E<>P THEN 711
710 G(I)=2:IF RAN#<0.1 THEN G(I)=4
711 NEXT I
712 RETURN
800 REM MERGE LEFT
801 J=1
802 FOR I=1 TO 4
803 IF T(I)=0 THEN 806
804 IF I>J THEN T(J)=T(I):T(I)=0
805 J=J+1
806 NEXT I
807 FOR I=1 TO 3
808 IF T(I)<>0 AND T(I)=T(I+1) THEN T(I)=T(I)*2:SC=SC+T(I):T(I+1)=0
809 NEXT I
810 J=1
811 FOR I=1 TO 4
812 IF T(I)=0 THEN 815
813 IF I>J THEN T(J)=T(I):T(I)=0
814 J=J+1
815 NEXT I
816 RETURN
900 REM GAME OVER CHECK
901 GM=1
902 FOR I=1 TO 16:IF G(I)=0 THEN GM=0
903 NEXT I
904 IF GM=0 THEN RETURN
905 FOR R=1 TO 4:FOR C=1 TO 4
906 I=(R-1)*4+C
907 IF C<4 THEN IF G(I)=G(I+1) THEN GM=0
908 IF R<4 THEN IF G(I)=G(I+4) THEN GM=0
909 NEXT C:NEXT R
910 RETURN
