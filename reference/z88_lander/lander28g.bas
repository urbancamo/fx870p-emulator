100 REM LUNAR LANDER, by Mark Wickens
110 REM developed for Summer 2010 Retrochallenge
120 REM V30A, 30-JUL-2010
130 REM http://hecnet.eu/rc2010sc
140 LANDED=0: REM 1 when down
150 RESET=0
160 DONE=0
170 ST%=0: REM Step count
180 TS=100
190 PROC_SPLASH
200 PROC_INITIAL
210 PROC_SIMLOOP
220 END
230 DEF PROC_SIMLOOP
240 CLS
250 REPEAT
260 TIME=0
270 PROC_DISPLAY
280 PROC_GETKEY
290 PROC_UPDSIM
300 PROC_INPUT
310 PROC_TESTLAND
320 PROC_DELAY
330 IF RESET=1 THEN PROC_RESET
340 ST%=ST%+1
350 UNTIL LANDED=1 OR DONE=1
360 IF DONE=1 THEN PRINT TAB(0,5) "DONE"
370 ENDPROC
380 DEF PROC_INITIAL
390 VSI=0: REM Vertical Speed Indicator Position
400 PVSI=3: REM Previous VSI indicator
410 ALT=0: REM Altitude indicator
420 PALT=3: REM Previous altitude indicator
430 HPI=0: REM Horizontal position indicator
440 LA=60: REM Lander angle (0=vertical)
450 BR=0: REM Burn rate/%
460 F=25000: REM Total fuel/kg
470 X=-30000: REM Range to landing site
480 Y=10000: REM Initial height/metres
490 DX=500: REM Initial horizontal velocity/ms
500 DY=0: REM Initial vertical velocity/ms
510 G=10/3: REM Gravity, metres/second
520 VT=0: REM Vertical thrust component
530 HT=0: REM Horizontal thrust component
540 ENDPROC
550 DEF PROC_DISPLAY
560 PROC_NAVDISPLAY
570 PROC_TENW2DP
580 PRINT TAB(1,0) "F=" F
590 PRINT TAB(15,0) "X=" X
600 PRINT TAB(29,0) "Y=" Y
610 PRINT TAB(0,1) "DX=" DX
620 PRINT TAB(14,1) "DY=" DY
630 PRINT TAB(28,1) "BR=" BR
640 PRINT TAB(0,2) "LA=" LA
650 PRINT TAB(14,2) "VT=" VT
660 PRINT TAB(28,2) "HT=" HT
670 IF F<3000 AND ST% MOD 2=0 THEN PRINT TAB(0,3) "FUEL" ELSE PRINT TAB(0,3) "    "
680 IF DY>0 THEN PRINT TAB(5,3) "ASC" ELSE PRINT TAB(5,3) "   "
690 IF Y<1000 THEN PRINT TAB(9,3) "RADAR"
700 IF Y<3 THEN PRINT TAB(9,3) "CONTACT" ELSE IF Y>1000 PRINT TAB(9,3) "      "
710 IF BR=0 AND F=0 THEN PRINT TAB(17,3) "ENGINE"
720 IF X>=-500 AND X<=500 THEN PRINT TAB(24,3) "RANGE" ELSE PRINT TAB(24,3) "     "
730 PROC_VSI
740 PROC_ALT
750 ENDPROC
760 DEF PROC_VSI
770 VSI=(0-DY)*6/15
780 IF VSI<>PVSI THEN PROC_UPDTAPE(59,VSI,PVSI)
790 PVSI=VSI
800 IF PVSI<0 THEN PVSI=0
810 IF PVSI>6 THEN PVSI=6
820 ENDPROC
830 DEF PROC_ALT
840 IF Y>75 THEN ALT=-1 ELSE ALT=6-(Y/75*6)
850 IF ALT<>PALT THEN PROC_UPDTAPE(50,ALT,PALT)
860 PALT=ALT
870 IF PALT<0 THEN PALT=0
880 IF PALT>6 THEN PALT=6
890 ENDPROC
900 DEF PROC_UPDTAPE(COL,CURR,PREV): REM -- Update tape display
910 PRINT TAB(COL,PREV) " ": REM clear previous indicator position
920 IF CURR<0 THEN PRINT TAB(COL,0) "^"
930 IF CURR>6 THEN PRINT TAB(COL,6) "V"
940 IF CURR>=0 AND CURR<=6 THEN PRINT TAB(COL,CURR) ">"
950 ENDPROC
960 DEF PROC_UPDSIM: REM --- Update simulation parameters
970 HT=LA/90*BR/10: REM horizontal component of lander angle multiplied by burn rate
980 VT=(1-(ABS(LA)/90))*BR/10
990 DY=DY-G+VT: REM vert. speed affected by gravity and vert. thrust
1000 Y=Y+DY
1010 DX=DX-HT
1020 X = X + DX
1030 IF F-BR<0 THEN BR=F
1040 IF F>0 THEN F=F-BR
1050 IF Y<0 THEN Y=0
1060 ENDPROC
1070 DEF PROC_INPUT
1080 IF C$="]" AND BR<100 THEN BR=BR+10
1090 IF C$="[" AND BR>0 THEN BR=BR-10
1100 IF C$="-" AND LA>-60 THEN LA=LA-5
1110 IF C$="=" AND LA<60 THEN LA=LA+5
1120 IF C$="R" OR C$="r" THEN RESET=1
1130 IF C$="I" OR C$="i" THEN PROC_INSTR
1140 IF C$=";" AND BR>=2 THEN BR=BR-2
1150 IF C$="'" AND BR<=98 THEN BR=BR+2
1160 IF C$="V" OR C$="v" THEN LA=0
1170 IF C$="C" OR C$="c" THEN LA=-60
1180 IF C$="B" OR C$="b" THEN LA=60
1190 IF C$="1" THEN BR=20
1200 IF C$="2" THEN BR=40
1210 IF C$="3" THEN BR=60
1220 IF C$="4" THEN BR=80
1230 IF C$="5" THEN BR=100
1240 IF C$="0" THEN BR=0
1250 IF C$="P" OR C$="p" THEN PROC_PAUSE
1260 IF C$="I" OR C$="i" THEN PROC_INSTR
1270 IF C$="K" OR C$="k" THEN PROC_KEYS
1280 IF C$="Q" OR C$="q" THEN DONE=1
1290 ENDPROC
1300 DEF PROC_TESTLAND
1310 IF Y<=0 AND BR=0 THEN LANDED=1: PROC_LANDED
1320 ENDPROC
1330 DEF PROC_LANDED
1340 IF DX<0 THEN DX=-DX
1350 IF X<0 THEN X=-X
1360 PROC_DISPLAY
1370 IF DX<=6 AND DY>=-6 THEN PRINT TAB(0,4) "You have landed successfully!"
1380 IF DX<6 AND DY<-3 AND DY>-6 THEN PRINT TAB(0,4) "You have landed with a bounce!"
1390 IF DY<-6 OR DX>6 THEN PRINT TAB(0,4) "You have CRASHED!"
1400 @%=&00000000
1410 IF X>500 THEN PRINT X " metres from the landing site"
1420 IF DY<-6.0 THEN PRINT "You left a crater " (ABS(DY)*2.5) " metres deep"
1430 IF DX=0.0 THEN DX=0.1
1440 IF DY=0.0 THEN DY=0.1
1450 SCORE%=100/(ABS(DX)*ABS(DY))
1460 IF ABS(X)<500 AND ABS(DX)<6.0 AND ABS(DY)<6.0 THEN PRINT "YOUR SCORE IS: " SCORE%
1470 ENDPROC
1480 DEF PROC_DELAY
1490 REPEAT T=TIME
1500 UNTIL T>TS
1510 REM PRINT TAB(0,5) "T=" T, "TS=" TS
1520 ENDPROC
1530 DEF PROC_PAUSE
1540 PRINT TAB(0,4) "PAUSED - Press 'P' to continue..."
1550 REPEAT
1560 C$=GET$
1570 UNTIL C$="P"
1580 PRINT TAB(0,4) "                                 "
1590 ENDPROC
1600 DEF PROC_NAVDISPLAY
1610 PRINT TAB(46,0) "ALT"
1620 PROC_TAPE(51,75,-25)
1630 PRINT TAB(55,0) "VSI"
1640 PROC_TAPE(60,0,5)
1650 ENDPROC
1660 DEF PROC_TAPE(COL,MIN,TICK)
1670 PROC_MINW0DP
1680 PRINT TAB(COL,0) "-" MIN
1690 PRINT TAB(COL,1) "."
1700 PRINT TAB(COL,2) "-" (MIN+TICK)
1710 PRINT TAB(COL,3) "."
1720 PRINT TAB(COL,4) "-" (MIN+(TICK*2))
1730 PRINT TAB(COL,5) "."
1740 PRINT TAB(COL,6) "-" (MIN+(TICK*3))
1750 ENDPROC
1760 DEF PROC_GETKEY
1770 C$=INKEY$(TS*0.1)
1780 ENDPROC
1790 DEF PROC_SPLASH
1800 REPEAT
1810 CLS
1820 PRINT "Lunar Lander, by Mark Wickens"
1830 PRINT ""
1840 PRINT "Press 'I' for instructions, 'S' to start"
1850 PRINT "      'K' for keys"
1860 C$=GET$
1870 IF C$="I" OR C$="i" THEN PROC_INSTR
1880 IF C$="K" OR C$="k" THEN PROC_KEYS
1890 UNTIL C$="S" OR C$="s"
1900 ENDPROC
1910 DEF PROC_INSTR
1920 CLS
1930 PRINT "Whilst listing pressing Q will QUIT, any other key to scroll"
1940 PRINT "Press any key to display instructions"
1950 T$=GET$
1960 CLS
1970 PROC_MORE("/mcp/lander.txt")
1980 ENDPROC
1990 DEF PROC_MORE(F$)
2000 FIN%=OPENIN(F$)
2010 IF FIN%<>0 THEN PROC_READ(FIN%) ELSE PRINT "Cannot read: " F$ " any key...":T$=GET$
2020 ENDPROC
2030 DEF PROC_READ(I%)
2040 T$=""
2050 REPEAT
2060 REPEAT
2070 C%=BGET#I%
2080 IF C%<>13 AND C%<>10 THEN PRINT CHR$(C%);
2090 UNTIL C%=13 OR C%=10
2100 PRINT ""
2110 LC%=LC%+1
2120 IF LC% MOD 6=0 THEN T$=GET$
2130 UNTIL EOF#I% OR T$="Q" OR T$="q"
2140 CLOSE#I%
2150 PRINT "EOF...": T$=GET$
2160 ENDPROC
2170 DEF PROC_RESET
2180 RESET=0
2190 PROC_INITIAL
2200 ENDPROC
2210 DEF PROC_TENW2DP
2220 @%=&0102020A
2230 ENDPROC
2240 DEF PROC_MINW0DP
2250 @%=&01000002
2260 ENDPROC
2270 DEF PROC_KEYS
2280 CLS
2290 PRINT "Keys:"
2300 PRINT "Thrust: 1:20%, 2:40%, 3:60%, 4:80%, 5:100%, 0:Engine Cut"
2310 PRINT "        [:-10%, ]:+10%, ;:-2%, ':+2%"
2320 PRINT "Attitude: C:-60 degrees, V:vertical, B:+60 degrees"
2330 PRINT "          -:-5 degrees, +:+5 degrees"
2340 PRINT "Simulation: P:Pause, R:Restart, I:Instructions, K:Keys"
2350 T$=GET$
2355 CLS
2360 ENDPROC
